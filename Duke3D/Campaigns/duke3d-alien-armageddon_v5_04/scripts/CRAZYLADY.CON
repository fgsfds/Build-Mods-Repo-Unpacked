// CRAZYLADY
// transforms from some female civilians

action ACRAZTRANSFORM1  85  2  1  1  24
action ACRAZTRANSFORM2  87  2  1  1  24
action ACRAZTRANSFORM3  91  2  1  1  24
action ACRAZSTAND	    -5  1  5  1  10
action ACRAZWALK  0  4  5  1  12
action ACRAZRUN   20  4  5  1  8
action ACRAZHIT1   40 2  5  1  14 // melee begin
action ACRAZHIT2   50 3  5  1  14 // 1st hit when this starts
action ACRAZHIT3   65 1  5  1  20 // 2nd hit when this starts
action ACRAZBITE  45 1  5  1  10 // when biting player
action ACRAZLEAP1  70 2  5  1  20
action ACRAZLEAP2  80 1  5  1  20
action ACRAZFROZEN -5  1  5  1  10
action ACRAZDIE   108 5  1  1  16
action ACRAZDECAP 103 5  1  1  16
action ACRAZDECAPDEAD 107 1  1  1  10
action ACRAZSTUN	113 1  5  1  8
action ACRAZDEAD  112 1  1  1  10

move CRAZYWALKVEL 96
move CRAZYRUNVEL 256
move CRAZYLEAPVEL 320 -90

ai AICRAZTRANSFORM ACRAZTRANSFORM1 STOPPED faceplayer
ai AICRAZWAIT ACRAZSTAND STOPPED geth
ai AICRAZLEAP ACRAZLEAP1 CRAZYLEAPVEL geth getv
ai AICRAZWANDER ACRAZWALK CRAZYWALKVEL randomangle
ai AICRAZSEEK ACRAZWALK CRAZYWALKVEL seekplayer
ai AICRAZCHARGE ACRAZRUN CRAZYRUNVEL faceplayer
ai AICRAZSHRUNK ACRAZRUN CORRSHRUNKVEL fleeenemy
ai AICRAZFROZEN ACRAZFROZEN STOPPED geth
ai AICRAZGROW ACRAZFROZEN STOPPED geth
ai AICRAZDYING ACRAZDIE STOPPED geth
ai AICRAZATTK ACRAZHIT1 CRAZYWALKVEL geth
ai AICRAZBITE ACRAZBITE STOPPED faceplayer
ai AICRAZSTUN ACRAZSTUN STOPPED geth

state crazystunstate

	ifactioncount 2
	ife stun 0 ai AICRAZSEEK

ends

state crazyleapstate

	ifaction ACRAZLEAP1
	{
		state turntotarget
		ifactioncount 1 
		{
			ifmove CRAZYLEAPVEL move SITHATTACKVEL2 geth
			
			state hitscan_targetprep
			zshoot zdist KNIFEPROJ
			
		}
		ifactioncount 2
		{
			action ACRAZLEAP2
			move CRAZYWALKVEL geth
		}
	}
	ifaction ACRAZLEAP2
	{
		ifactioncount 1
		ai AICRAZSEEK
	}

ends

state crazygrowstate state genericgrowcode ends

state crazyshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AICRAZSEEK
  else
    ifcount SHRUNKCOUNT
      sizeto 28 26
  else
    state genericshrunkcode

ends

state crazyfrozenstate

    ifcount THAWTIME
    {
      ai AICRAZSEEK
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state crazydyingstate

	ifactorsound THISACTOR CORRUPT_DIE1 setactorsoundpitch THISACTOR CORRUPT_DIE1 512
	ifactorsound THISACTOR CORRUPT_DIE2 setactorsoundpitch THISACTOR CORRUPT_DIE2 512
	strength 0
	state deadsquished
	set temp NO
	ifaction ACRAZDIE set temp YES
	ifaction ACRAZDECAP set temp YES
	ife temp YES
	{
		ifrnd 128 guts JIBS6 1
		ifhitweapon state random_wall_jibs
		ifactioncount 4
		{
			cstat 0
			ifaction ACRAZDIE action ACRAZDEAD else
			action ACRAZDECAPDEAD
			iffloordistl 8 sound THUD
			break
		}
	}
	
	ifaction ACRAZDEAD set temp YES
	ifaction ACRAZDECAPDEAD set temp YES
	ife temp YES
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				state standard_jibs
				killit
			}
		}
	}
	
ends

state crazybitestate

	ifp palive nullop else ife zombieonplayer THISACTOR { set zombieonplayer -1 set zombiecount -30 }
	ifn zombieonplayer THISACTOR
	{
		ai AICRAZSEEK
		cstat 257
		break
	}
	else
	{
		cstator 256
		
		getp[].posz z
		add z 9216
		
		getp[].oang angvar
		
		set x2 player[].posx
		add x2 512
		rotatepoint player[].posx player[].posy x2 player[].posy angvar x y
		
		setsprite THISACTOR x y z
		setp[].posxv 0
		setp[].posyv 0
		setp[].jumping_counter 0
		setp[].ang player[].oang
		setp[].horiz 100
	}

ends

state crazyattkstate

	state turntotarget
	ifaction ACRAZHIT1
	{
		ifactioncount 2
		{
			action ACRAZHIT2
			state zombiescratch
		}
	}
	else ifaction ACRAZHIT2
	{
		ifactioncount 3
		{
			action ACRAZHIT3
			state zombiescratch
			ifrnd 128 state zombiescratch
			else ifrnd 128
			{
				ifpdistl 1280
				ife slidekick 0
				ife pdown NO
				ife sprite[].sectnum player[].cursectnum
				ife brutalcount 0
				ifn team 1
				{
					ife zombieonplayer -1
					ife zombiecount 0
					{
						ai AICRAZBITE
						globalsound Z_BITESTART
						set zombieonplayer THISACTOR
						set zombietile CRAZYBITE
						ifrnd 128 set zombieflip YES
						geta[].x x2
						geta[].y y2
						sub x2 player[].posx
						sub y2 player[].posy
						getangle angvar x2 y2
						setp[].ang angvar
						setp[].oang angvar
						setp[].weapon_pos -9
						cstat 33024
						break
					}
				}
			}
		}
	}
	else ifaction ACRAZHIT3
	{
		ifactioncount 1
		{
			set target bottarget
			ife target -1 ife team 0 set target player[].i
			
			ife target -1 { ai AICRAZSEEK break }
			
			ifn target player[].i
			{
				set spriteid target
				state facesprite
				
				ifg targetdist 1560 ai AICRAZSEEK
					else ai AICRAZATTK
				ife targetalive NO ai AICRAZSEEK
				
			}
			else
			{
				set spriteid player[].i
				state facesprite
				
				ifpdistg 1560 ai AICRAZSEEK
				else ife seeplayer YES ifp palive
				{
					ifpdistl 1280
					ife slidekick 0
					ife pdown NO
					ife sprite[].sectnum player[].cursectnum
					ife brutalcount 0
					{
						ife zombieonplayer -1
						ife zombiecount 0
						{
							ai AICRAZBITE
							globalsound Z_BITESTART
							set zombieonplayer THISACTOR
							set zombietile CRAZYBITE
							ifrnd 128 set zombieflip YES
							geta[].x x2
							geta[].y y2
							sub x2 player[].posx
							sub y2 player[].posy
							getangle angvar x2 y2
							setp[].ang angvar
							setp[].oang angvar
							setp[].weapon_pos -9
							cstat 33024
							break
						}
					}
					else
					ai AICRAZATTK
				}
				else ai AICRAZSEEK
				
			}
		}
	}

ends

state crazyroamsounds

	ifrnd 1
	ifrnd 128
	{
		ifactorsound THISACTOR CORRUPT_ROAM1 break
		ifactorsound THISACTOR CORRUPT_ROAM2 break
		rand temp 1
		ife temp 0 sound CORRUPT_ROAM1 else
		ife temp 1 sound CORRUPT_ROAM2
		ai AICRAZWAIT
	}
	
ends

state crazycheckmelee
	
	ifg dodgetime 2 set myspawner -1
	ifn myspawner -1
	{
		state incoming_eval
		set dodgetime 8
		set crumbwait 9
		ai AICRAZCHARGE
		set myspawner -1
		seta[].ang angvel
		break
	}
	ifn dodgetime 0 break
	// ife team 0
	// ife seeplayer YES ifpdistl 1408 
	// ifp palive
	// {
		// ife zombieonplayer -1
		// ife zombiecount 0
		// ifcount 15
		// ife slidekick 0
		// ife pdown NO
		// ife sprite[].sectnum player[].cursectnum
		// ife brutalcount 0
		// {
			// ai AICRAZBITE
			// sound Z_BITESTART
			// set zombieonplayer THISACTOR
			// set zombietile CRAZYBITE
			// ifrnd 128 set zombieflip YES
			// geta[].x x2
			// geta[].y y2
			// sub x2 player[].posx
			// sub y2 player[].posy
			// getangle angvar x2 y2
			// setp[].ang angvar
			// setp[].oang angvar
			// setp[].weapon_pos -9
			// cstat 33024
		// }
		// else
		// ai AICRAZATTK
	// }
	// else
	ifn bottarget -1
	{
		ife seemytarget YES
		ife targetalive YES
		{
			ifl targetdist 1408
				ai AICRAZATTK 
			else
			ifl targetdist 2176 ifrnd 12 
			{
				ai AICRAZLEAP
				sound CORRUPT_ATTACK2
			}
		
		}
	}
		
	
ends

state checkcrazyhit

	set initsprite YES
	ife zombieonplayer THISACTOR 
	{
		set zombieonplayer -1
		set zombiecount -30
		cstat 257
		ai AICRAZSEEK
	}
	ifai AICRAZWANDER ai AICRAZSEEK
	
	  ifwasweapon HEADJIB1 // HEADSHOT!
	  {
		sound SQUISHED
		ifrnd 128 cstat 0 else cstat 4
		ai AICRAZDYING
		move STOPPED
		action ACRAZDECAP

		  rand zdist 1024
		  sub zdist 3500
		  ezshoot zdist CRAZYHEADPROJ
		  rand angvar 512
		  sub angvar 256
		  add angvar sprite[].htang
		  seta[RETURN].ang angvar
		  
		addkills 1 state enemy_death
		break
	  }
	
	ifwasweapon SHRINKSPARK
	{	
	  sound ACTOR_SHRINKING
	  ai AICRAZSHRUNK
	  set shrunken 1
	  break
	}
	else
		ifwasweapon GROWSPARK
		  sound SPARKLESOUND
	state random_wall_jibs
	
	ifg sprite[].extra 0
	{
		ifactorsound THISACTOR Z_PAIN1 nullop else
		ifactorsound THISACTOR Z_PAIN2 nullop else
		ifrnd 128 sound Z_PAIN1 else sound Z_PAIN2
	}
	

	ifdead
	{
		ifwasweapon FREEZEBLAST
		{
		  sound NEWFREEZE
		  spritepal 1
		  ai AICRAZFROZEN
		  strength 0
		  state freezeme
		  break
		}
		ifwasweapon GROWSPARK
		{
		  cstat 0
		  sound ACTOR_GROWING
		  ai AICRAZGROW
		  break
		}
		addkills 1
		state enemy_death
		ai AICRAZDYING
		ifrnd 128 cstator 4
		
		
		ifrnd 128 sound CORRUPT_DIE1 else sound CORRUPT_DIE2
		
		
		ifwasweapon RPG
		{
			sound SQUISHED
			state standard_jibs
			killit
		}
		ifwasweapon RADIUSEXPLOSION
		{
			sound SQUISHED
			state standard_jibs
			killit
		}
		break
	}
	ifai AICRAZSTUN nullop else
	ifg stun 0 ai AICRAZSTUN else
	ifrnd 16 ai AICRAZSTUN

ends

state crazychargestate

	state crazycheckmelee
	ifai AICRAZATTK break
	ifai AICRAZBITE break

ifcount 70 state crazyroamsounds

ifcount 90
	ai AICRAZSEEK
	
	// ife seeplayer YES getp[].i target
		// else set target -1
	// ifn target -1
	// {
		// set spriteid target
		// state facesprite
	// }
	// else 
	ifnotmoving { operate ai AICRAZSEEK }

ends

state crazywalkstate

	ifcount 10
	state crazycheckmelee
	
	ifai AICRAZATTK break
	ifai AICRAZBITE break
	
	ifn bottarget -1 
	{
		set target bottarget 
		set initsprite YES
	}
	else set target -1

	ifai AICRAZWANDER
	{
		ifrnd 12
		ifcount 20 ifn target -1
			ai AICRAZSEEK
	}
	else
	ifai AICRAZSEEK
	{
		ifrnd 12
		ifcount 16 ife target -1
			ai AICRAZWANDER
	}
	
	ifnotmoving
	{
		ife target -1
		ifcount 8
		{
			ifn team 1 set crumbwait 150
			ai AICRAZWANDER
		}
		ifrnd 64 operate
	}
	
	ifn target -1
	ifrnd 4
	{
		ifl targetdist 8192
		{
			set crumbwait 0
			ai AICRAZCHARGE
			set spriteid target
			state facesprite
			ifrnd 128 sound CORRUPT_CHARGE1
			else sound CORRUPT_CHARGE2
			break
		}	
		
	}
	state crazyroamsounds
	

ends

useractor enemy CRAZYLADY CORRUPTSTRENGTH

	fall
	
	ifai 0
	{
		sizeat 28 26
		cstator 257
		clipdist 40
		ai AICRAZWAIT
		set team 0
		ifspritepal 0 spritepal 21
	}
	ifactorsound THISACTOR CORRUPT_ROAM1 setactorsoundpitch THISACTOR CORRUPT_ROAM1 512
	ifactorsound THISACTOR CORRUPT_ROAM2 setactorsoundpitch THISACTOR CORRUPT_ROAM2 512
	ifactorsound THISACTOR CORRUPT_CHARGE1 setactorsoundpitch THISACTOR CORRUPT_CHARGE1 512
	ifactorsound THISACTOR CORRUPT_CHARGE2 setactorsoundpitch THISACTOR CORRUPT_CHARGE2 512
	ifactorsound THISACTOR CORRUPT_ATTACK2 setactorsoundpitch THISACTOR CORRUPT_ATTACK2 512
	ifactorsound THISACTOR Z_BITESTART setactorsoundpitch THISACTOR Z_BITESTART 512
	ifactorsound THISACTOR Z_PAIN1 setactorsoundpitch THISACTOR Z_PAIN1 512
	ifactorsound THISACTOR Z_PAIN2 setactorsoundpitch THISACTOR Z_PAIN2 512
	ifactorsound THISACTOR Z_HITP1 setactorsoundpitch THISACTOR Z_HITP1 512
	ifactorsound THISACTOR Z_HITP2 setactorsoundpitch THISACTOR Z_HITP2 512
	
	state monsterai
	ifai AICRAZTRANSFORM
	{
		ifcount 10 nullop else ifcount 9
		{
			guts JIBS6 4
			guts JIBS3 2
		}
		ifactioncount 2 { ai AICRAZWAIT set team 0 }
	}
	ifai AICRAZWAIT
	{
		set temp inithp
		mul temp 2
		ifl sprite[].extra temp
		addstrength 1
		ifcount 20 
		{ 
			ifn attmode 0 
			{
				set temp plevel
				mul temp 3
				ifl mlevel temp
				add mlevel 1
			}
			ai AICRAZSEEK 
			set countvar 0
			set angvel 0
			set startx sprite[].x
			
			whilevarn countvar 9
			{
				set startx sprite[].x
				add startx 512
				rand x 512
				add startx x
				ifrnd 84 espawn BLOODSPLASH1 else ifrnd 128 espawn BLOODSPLASH2 else espawn BLOODSPLASH3
				rotatepoint sprite[].x sprite[].y startx sprite[].y angvel newx newy
				seta[RETURN].x newx
				seta[RETURN].htbposx newx
				seta[RETURN].y newy
				seta[RETURN].htbposy newy
				
				set savz sprite[].z
				sub savz 1024
				rand z 4096
				sub savz z
				seta[RETURN].z savz
				seta[RETURN].htbposz savz
				
				add countvar 1
				add angvel 256
			}
		}
	}
	
	ifn bottarget -1 ife actorvar[bottarget].monstatus 2 set bottarget -1
	ife initsprite NO set crumbwait 10
	
	ifai AICRAZDYING { state crazydyingstate break }
	ifai AICRAZFROZEN { state crazyfrozenstate break }
	ifai AICRAZSHRUNK { state crazyshrunkstate break }
	ifai AICRAZGROW { state crazygrowstate break }

	ifhitweapon 
		state checkcrazyhit
	else state checksquished
	ifai AICRAZSTUN state crazystunstate else
	ifai AICRAZWANDER state crazywalkstate else
	ifai AICRAZSEEK state crazywalkstate else
	ifai AICRAZATTK state crazyattkstate else
	ifai AICRAZCHARGE state crazychargestate else
	ifai AICRAZBITE state crazybitestate else
	ifai AICRAZLEAP state crazyleapstate
	
	set myspawner -1
	
enda

