// EDF SNIPER

state snipershootbot

	state hitscan_targetprep
	setprojectile[RAILPROJ].trail -1
	setprojectile[RAILPROJ].spawns PART_EXP
	zshoot zdist RAILPROJ
	setprojectile[RAILPROJ].trail RANGERTRAIL
	setprojectile[RAILPROJ].spawns RAILIMPACT
	
ends

action ASNIPERWALK		0  4  5  1  16
action ASNIPERRUN		0  4  5  1  8
action ASNIPERJUMP1		30 1  5  1  20
action ASNIPERJUMP2		35 1  5  1  20
action ASNIPERSTAND 	40 1  5  1  10
action ASNIPERSHOOT1	40 1  5  1  40
action ASNIPERSHOOT2	45 1  5  1  20
action ASNIPERCROUCH1	0 1  5  1  35 // only used by EDFSNIPERCROUCH
action ASNIPERCROUCH2	5 1  5  1  20 // firing

action ASNIPERKICK		20 2  5  1  20
action ASNIPERKICK2		20 1  5  1  20
action ASNIPERFLINCH	60 1  1  1  18
action ASNIPERDIE		60 5  1  1  18
action ASNIPERDEAD		65 1  1  1  10
action ASNIPERFROZEN 	40 1  5  1  10
// action ASNIPERDECAP	    1316  5  1  1  18
// action ASNIPERDECAPDEAD 1320  1  1  1  10

action ASNIPERRECOVER	100  2  5  1  20

move SNIPERFIRED
move SNIPERKICKED
move SNIPERSHRUNKVELS 40
move SNIPERWALKVELS 140
move SNIPERRUNVELS 320

ai AISNIPERFROZEN ASNIPERFROZEN STOPPED geth
ai AISNIPERSHRUNK ASNIPERRUN SNIPERSHRUNKVELS fleeenemy
ai AISNIPERWAIT ASNIPERSTAND STOPPED geth
ai AISNIPERSEEK ASNIPERWALK SNIPERWALKVELS geth
ai AISNIPERDODGE ASNIPERRUN SNIPERRUNVELS dodgebullet
ai AISNIPERSHOOT ASNIPERSHOOT1 STOPPED geth
ai AISNIPERHURT ASNIPERFLINCH STOPPED geth
ai AISNIPERDYING ASNIPERDIE STOPPED geth
ai AISNIPERGROW ASNIPERFLINCH STOPPED geth
ai AISNIPERKICK ASNIPERKICK STOPPED geth
ai AISNIPERCROUCH ASNIPERCROUCH1 STOPPED geth
ai AISNIPERJUMP ASNIPERJUMP1 EDFJUMPVEL geth getv

state sniperkickcheck
	ifrnd 160
	ai AISNIPERKICK
	else
	{
		ifrnd 128
		{
			ai AISNIPERDODGE
			set dodgetime 8
		}
		else
		{
			cactor EDFSNIPERCROUCH
			ai AISNIPERCROUCH
		}
	}
ends

state sniperjumpstate

	seta[].ang mtype
	ifmove EDFJUMPVEL
	{
		ifcount 8 
		{
			move EDFWALKVEL geth
			action ASNIPERJUMP2
		}
	}
	ifmove EDFWALKVEL
	{
		iffloordistl 8
		{
			cactor EDFSNIPERCROUCH
			ai AISNIPERCROUCH
		}
	}

ends

state sniperattackcheck
	ife seemytarget YES
	ifcanshoottarget
	ife targetalive YES
	{
		ifl targetdist 1280 state sniperkickcheck
		else
		{
			state lowlineofsight
			ifrnd 128 ife B YES 
			{ 
				cactor EDFSNIPERCROUCH 
				ai AISNIPERCROUCH 
				ifn bottarget -1
				{
					set spriteid bottarget
					state facesprite
				}
			}
			else 
				ai AISNIPERSHOOT
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT nullop else
			sound RIFLEREADY
			
		}
	}
	else ai AISNIPERSEEK
ends

state snipercheckdodge

	ifcount 6 nullop else break
	
	ifp pfacing
	ife seemytarget YES
	ifl targetdist 8192
	ife targetalive YES
	ifn player[].kickback_pic 0
	ifn player[].curr_weapon 5
	{
		cactor EDFSNIPER
		set dodgetime 8
		ai AISNIPERDODGE
		break
	}
	
	ifg targetdist 3072
	ifbulletnear
	{
		cactor EDFSNIPER
		set dodgetime 8
		ai AISNIPERDODGE
	}
ends

state sniperchat

	ifg countvarc 0 { ifg countvarc 10 set myvictim -1 break }

	ifg myvictim -1
	{
		set temp NO
		ife actorvar[myvictim].monstatus 2 set temp YES
		ife sprite[myvictim].statnum 1024 set temp YES
		ife temp YES
		ifn myvictim -2
		{
			ife sprite[myvictim].htpicnum HEADJIB1
			{
				ife sprite[myvictim].picnum PIGCOP ifrnd 128 sound F1SQUEEL else
				{
				ifrnd 128 sound F1BOOM else
				sound F1SORRY
				}
				set countvarc 70
			}
			else
			{
				switch sprite[myvictim].picnum
				case PIGCOP case PIGCOPDIVE
				case EDFPIG
					ifrnd 128
					sound F1SQUEEL
					else
					{
						rand temp 7
						ife temp 0 sound F1DESTROY else
						ife temp 1 sound F1DIEBITCH else
						ife temp 2 sound F1LOSER else
						ife temp 3 sound F1OHYEAH else
						ife temp 4 sound F1STAYDOWN else
						ife temp 5 sound F1SUCKITDOWN else
						ife temp 6 sound F1WANTSOME else
						ife temp 7 sound F1YOUSUCK
					}
					set countvarc 70
				break
				default
					rand temp 7
					ife temp 0 sound F1DESTROY else
					ife temp 1 sound F1DIEBITCH else
					ife temp 2 sound F1LOSER else
					ife temp 3 sound F1OHYEAH else
					ife temp 4 sound F1STAYDOWN else
					ife temp 5 sound F1SUCKITDOWN else
					ife temp 6 sound F1WANTSOME else
					ife temp 7 sound F1YOUSUCK
					set countvarc 70
				break
				endswitch
			}
		}
		set myvictim -1
	}

ends

state sniperroamsounds

	ifactorsound THISACTOR F1DESTROY break

	ife gametype CTF
	{
		ife team 1 ifn bluecarrier -1 { soundonce F1GETFLAG break }
		else
		ife team 0 ifn redcarrier -1 { soundonce F1GETFLAG break }
	}
	ifrnd 128
	soundonce F1DESTROY
	
	
ends

state sniperpainsounds

	ifg countvarc 0 break
	
	ifn sprite[].htowner -1
	ife team actorvar[sprite[].htowner].team
	ifrnd 128
	{
		ifrnd 128 sound F1IDIOT else sound F1SAMETEAM
		set countvarc 50
		break
	}
	
	
	ifrnd 84 { sound SHELLYPAIN1 setactorsoundpitch THISACTOR SHELLYPAIN1 -128 }
	else ifrnd 128 { sound SHELLYPAIN2 setactorsoundpitch THISACTOR SHELLYPAIN2 -128 }
	else { sound SHELLYPAIN3 setactorsoundpitch THISACTOR SHELLYPAIN3 -128 }
	set countvarc 30
	
ends

state sniperdodgestate

	ifl dodgetime 2 set dodgetime 2
	ifrnd 1 ifrnd 64 state sniperroamsounds
	ifcount 10
		state sniperattackcheck
	else
	ifnotmoving
		state sniperattackcheck
		
		
ends

state snipergrowstate state genericgrowcode ends

state snipershrunkstate

  ifcount SHRUNKDONECOUNT
    ai AISNIPERSEEK
  else
    ifcount SHRUNKCOUNT
      sizeto 28 26
  else
    state genericshrunkcode

ends

state sniperfrozenstate

    ifcount THAWTIME
    {
      ai AISNIPERSEEK
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  ifrnd SPAWNAMMOODDS ife gametype 0 spawn RAILGUNSPRITE
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
	  ifn team actorvar[player[].i].team
        state pkick_check
  
ends

state sniperdyingstate

	
	// ifaction ASNIPERDECAP
	// {
		// ifhitweapon state random_wall_jibs
		// ifactioncount 5
		// {
			// cstat 0
			// action ASNIPERDECAPDEAD
			// iffloordistl 8 sound THUD
			// ifrnd SPAWNAMMOODDS spawn RAILGUNSPRITE
			// ifrnd 128 spawn BLOODPOOL
			// break
		// }
	// }
	// ifaction ASNIPERDECAPDEAD
	// {
		// ifhitweapon
		// {
			// ifwasweapon RPG
			// {
				// sound SQUISHED
				// spawn BLOODPOOL
				// state standard_jibs
				// killit
			// }
			// ifwasweapon RADIUSEXPLOSION
			// {
				// sound SQUISHED
				// spawn BLOODPOOL
				// state standard_jibs
				// killit
			// }
		// }
		// ifcount RESPAWNACTORTIME
		// ifrespawn
		// {
		  // addkills -1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength SNIPERSTRENGTH
		  // ai AISNIPERSEEK
		// }
	// }
	
	ifaction ASNIPERRECOVER
	{
		ifhitweapon 
		{
			state random_wall_jibs
			ifdead
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs	
				killit
			}
		}
		ifactioncount 5
		{
			strength SNIPERSTRENGTH
			addstrength -10
			set ikicked 0
			set monstatus 1
			ai AISNIPERWAIT
			seta[].htextra -1
		}
		break
	}
	strength 0
	state deadsquished
	ifaction ASNIPERDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action ASNIPERDEAD
			iffloordistl 8 sound THUD
			ifrnd SPAWNAMMOODDS spawn RAILGUNSPRITE
			ifrnd 128 spawn BLOODPOOL
			break
		}
	}
	ifaction ASNIPERDEAD
	{
		ife ikicked -2
		{
			action ASNIPERRECOVER
			strength 50
			cstat 257
			seta[].htextra -1
			break
		}
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				// state sniper_body_jibs
				// state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				// state sniper_body_jibs
				// state lite_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn
		// {
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength SNIPERSTRENGTH
		  // ai AISNIPERSEEK
		// }
	}
	
ends

state sniperwaitstate

	ife gametype SURVIVAL ifl player[].player_par 150 break
	ifn thiscam -1 break
	ifrnd 1 ifrnd 64 state sniperroamsounds

	ife shootmytarget YES
	ife targetalive YES
	{
		ifcount 8
		{
			ifl targetdist 1280 state sniperkickcheck
			else
			ifl targetdist 3072 
			{
				ai AISNIPERDODGE
				set dodgetime 8
			}
			else
			{
				state lowlineofsight
				ifrnd 128 ife B YES
				{ 
					cactor EDFSNIPERCROUCH 
					ai AISNIPERCROUCH 
					ifn bottarget -1
					{
						set spriteid bottarget
						state facesprite
					}
				}
				else 
				ai AISNIPERSHOOT
				// sound RIFLEREADY
				// ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR RIFLEREADY SHRUNKPITCH
			}
		}
	}
	else 
	{
		ife team actorvar[player[].i].team ifpdistl 1560 break
		ifcount 8
		ai AISNIPERSEEK
	}

ends

state sniperseekstate

	ifrnd 1 ifrnd 64 state sniperroamsounds

	ifl targetdist 1280 
		ife seemytarget YES
		  ife targetalive YES
	{
		state sniperkickcheck
		break
	}
		
	state snipercheckdodge
	ifai AISNIPERSEEK
	{
		ifn sprite[].htactorstayput -1 // stayput actor
		{
			ifawayfromwall nullop else
			{
				ife bottarget -1
				{
					state facestartpos
					set dodgetime 30
					break
				}
			}
		}
		ifnotmoving 
		{
			ifl navpoint 0 ifrnd 32 operate
			ifrnd 128 { ai AISNIPERJUMP geta[].ang mtype } else
			ai AISNIPERWAIT 
			break
		}
		ife shootmytarget YES
		ifcount 30 ai AISNIPERWAIT
		
		ifl navpoint 0
		ifcount 50 ifrnd 16 ai AISNIPERWAIT
		
		ifactornotstayput nullop else
		ifawayfromwall nullop else
		ifl navpoint 0
		ifrnd 12 ai AISNIPERWAIT
		
		ife team actorvar[player[].i].team ifpdistl 1560
			ai AISNIPERWAIT
	}
	
ends

state sniperkickstate

	ifaction ASNIPERKICK
	{
		ifmove STOPPED
		ifactioncount 1
		{
			move SNIPERKICKED
			ife shrunken 0
			ife seemytarget YES ifl targetdist 1408
			ife targetalive YES
			{
				sound KICK_HIT
				ifn bottarget -1 set target bottarget
				else set target player[].i
				ife target player[].i
				{
					palfrom 40 63
					wackplayer
					getp[].ang bossang
					add bossang 1024
					cos newx bossang
					sin newy bossang
					shiftvarl newx 8
					shiftvarl newy 8
					setplayer[THISACTOR].posxv newx
					setplayer[THISACTOR].posyv newy
					// push player backwards
				}
				else
				{
					seta[target].xvel -512
					getav[target].stun temp
					add temp 10
					setav[target].stun temp
					geta[target].z z
					sub z 512
					seta[target].z z
					geta[target].zvel z
					sub z 512
					seta[target].zvel z
				}
				
				seta[target].htextra 10
				seta[target].htpicnum SHOTSPARK1
				seta[target].htowner THISACTOR
				seta[target].htang sprite[THISACTOR].ang
			}
		}
		ifactioncount 2 action ASNIPERKICK2
	}
	ifaction ASNIPERKICK2
	{
		ifactioncount 1 ai AISNIPERSEEK
	}

ends

state snipercrouchstate

	ifaction ASNIPERCROUCH1
	{
		ifactioncount 2
		{
			ifmove STOPPED
			{
				ife targetalive YES
				{
					action ASNIPERCROUCH2
					ife seemytarget YES ifrnd 128 nullop else
					move SNIPERFIRED geth
					sound SNIPERSHOT
					ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SNIPERSHOT SHRUNKPITCH
					geta[].z bossy
					sub bossy 1536
					seta[].z bossy
					ifn bottarget -1
					state snipershootbot
					else
					{
						setprojectile[RAILPROJ].trail -1
						setprojectile[RAILPROJ].spawns PART_EXP
						shoot RAILPROJ
						setprojectile[RAILPROJ].trail RANGERTRAIL
						setprojectile[RAILPROJ].spawns RAILIMPACT
					}
					add bossy 1536
					seta[].z bossy
				}
				else
				{
					ifrnd 192 resetactioncount
					else
					{
						cactor EDFSNIPER
						ai AISNIPERSEEK
					}
				}
			}
			else { cactor EDFSNIPER ai AISNIPERSEEK }
		}
		else 
		ifmove SNIPERFIRED
		state snipercheckdodge
	}
	ifaction ASNIPERCROUCH2
	ifactioncount 1
		action ASNIPERCROUCH1
		
ends

state snipershootstate
	ifaction ASNIPERSHOOT1
	{
		ifactioncount 1
		{
			ifmove STOPPED
			{
				ife shootmytarget YES
					ife targetalive YES
				{
					action ASNIPERSHOOT2
					move SNIPERFIRED geth
					sound SNIPERSHOT
					ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SNIPERSHOT SHRUNKPITCH
					geta[].z bossy
					sub bossy 4096
					seta[].z bossy
					ifn bottarget -1
					state snipershootbot
					else
					{
						setprojectile[RAILPROJ].trail -1
						setprojectile[RAILPROJ].spawns PART_EXP
						shoot RAILPROJ
						setprojectile[RAILPROJ].trail RANGERTRAIL
						setprojectile[RAILPROJ].spawns RAILIMPACT
					}
					add bossy 4096
					seta[].z bossy
				}
				else ai AISNIPERWAIT
			}
			else 
			{
				ife targetalive YES
				{
					ifl targetdist 2048
					{
						set dodgetime 8
						ai AISNIPERDODGE
					}
					else
					ife shootmytarget YES // can see target
					{
						ifactornotstayput ifg targetdist 8192
						ai AISNIPERSEEK
						else
						ai AISNIPERWAIT
					}
					else ai AISNIPERSEEK
				}
				else
				ai AISNIPERSEEK
			}
		}
		else
		ifmove SNIPERFIRED
			state snipercheckdodge
		
		
	}
	ifaction ASNIPERSHOOT2
	ifactioncount 1
		action ASNIPERSHOOT1
	
ends

state sniperhurtstate
	ifg stun 0 nullop else
	ifcount 10 ai AISNIPERSEEK
ends

state edfsnipercode

	fall
	ifai 0
	{
		ifactor EDFSNIPERCROUCH ai AISNIPERCROUCH
		else
		ai AISNIPERWAIT
		cstat 257
		sizeat 28 26
		clipdist 36
		set team 1
		ifspritepal 42 { addstrength 50 set team 0 }
		set navmode 3
		geta[].x startx
		geta[].y starty
		ife gametype SURVIVAL addstrength SNIPERSTRENGTH
		sound F1LETSROCK
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		geta[].htflags temp
		orvar temp 8192
		seta[].htflags temp
	}

	ifai AISNIPERGROW { state snipergrowstate break }
	ifai AISNIPERFROZEN { state sniperfrozenstate break }
	ifai AISNIPERDYING { state sniperdyingstate break }
	state avoidcliffs
	state monsterai 
	ife team 1 
	{
		state summondespawn
		// state botblocking
	}
	ife bottarget -1 add FEMFALLDMG 1 else
	set FEMFALLDMG 0
	ifg FEMFALLDMG 300
	ifp palive
	ifg FEMKILLCOUNT 0
	{
		set FEMFALLDMG 0
		sound TELEPORTER
		spawn GLARESTAR
		state spawnoutline
		setsprite THISACTOR player[].posx player[].posy player[].posz
		sound TELEPORTER
		spawn GLARESTAR
		state spawnoutline
	}
	ifg countvarc 0 sub countvarc 1 
	state sniperchat


	ifai AISNIPERWAIT state sniperwaitstate else
	ifai AISNIPERJUMP state sniperjumpstate else
	ifai AISNIPERSHRUNK state snipershrunkstate else
	ifai AISNIPERSEEK state sniperseekstate else
	ifai AISNIPERDODGE state sniperdodgestate else
	ifai AISNIPERSHOOT state snipershootstate else
	ifai AISNIPERKICK state sniperkickstate else
	ifai AISNIPERCROUCH state snipercrouchstate else
	ifai AISNIPERDODGE state sniperdodgestate else
	ifai AISNIPERHURT state sniperhurtstate

	ife monstatus -1 break
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{	
		  cactor EDFSNIPER
		  sound ACTOR_SHRINKING
		  ai AISNIPERSHRUNK
		  //set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		// ifwasweapon HEADJIB1
		// {
			// addkills 1
			// state enemy_death
			// cactor EDFSNIPER
			// ai AISNIPERDYING
			// action ASNIPERDECAP
			// strength 0
			// cstat 0
			// guts JIBS6 4
			// rand zdist 1024
			// sub zdist 3500
			// ezshoot zdist SNIPERHEADPROJ
			// rand angvar 512
			// sub angvar 256
			// add angvar sprite[].htang
			// seta[RETURN].ang angvar
			// seta[RETURN].pal sprite[].pal
			// sound SQUISHED
			// break
		// }
		ifdead
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor EDFSNIPER
			  ai AISNIPERFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  cactor EDFSNIPER
			  ai AISNIPERGROW
			  break
			}
			state enemy_death
			cactor EDFSNIPER
			ai AISNIPERDYING
			ifrnd 128 { sound SHELLYDIE1 setactorsoundpitch THISACTOR SHELLYDIE1 -128 } else
			{ sound SHELLYDIE2 setactorsoundpitch THISACTOR SHELLYDIE2 -128 }
			guts JIBS6 4
			
			ifwasweapon RPG
			{
				// state spawnrifle
				cactor EDFSNIPER
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				// state sniper_body_jibs
				// state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				// state spawnrifle
				cactor EDFSNIPER
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				// state sniper_body_jibs
				// state lite_jibs
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		ifai AISNIPERHURT nullop else
		ifai AISNIPERSHRUNK nullop else
		{
			ifg stun 0 { cactor EDFSNIPER ai AISNIPERHURT break }
			state sniperpainsounds
			ifrnd 32
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			{ 
				cactor EDFSNIPER
				ai AISNIPERHURT 
				break 
			}
		}
	}
	else state checksquished

ends

useractor enemy EDFSNIPER SNIPERSTRENGTH state edfsnipercode enda

useractor enemystayput EDFSNIPERSTAYPUT SNIPERSTRENGTH cactor EDFSNIPER enda

useractor enemy EDFSNIPERCROUCH SNIPERSTRENGTH state edfsnipercode enda