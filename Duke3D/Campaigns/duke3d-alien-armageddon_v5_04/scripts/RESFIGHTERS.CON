// RESISTANCE FIGHTERS
// A and B refer to weapon A (rifle) and weapon B (pistol)
action AFIGHTSTAND  0  1  5  1  10
action AFIGHTWALK  5  4  5  1  10
action AFIGHTCROUCH  0  1  5  1  10 // CROUCHING ACTOR
action AFIGHTAIMA  30  1  5  1  5
action AFIGHTCROUCHSHOOTA 10  1  5  1  5
action AFIGHTSHOOTA 40  1  5  1  5
action AFIGHTWALKA  45  4  5  1  10
action AFTWALKABACK 60  4  5  -1  10
action AFIGHTKICK1	65  2  5  1  20
action AFIGHTKICK2	70  1  5  1  20
action AFIGHTJUMP1  75  1  5  1  18
action AFIGHTJUMP2	80  1  5  1  18
action AFIGHTCROUCHAIMA 60  1  5  1  5
action AFIGHTDOWN	90  2  5  1  50 // NORMAL ACTOR
action AFIGHTHIT	90  2  5  1  20
action AFIGHTRECOVER	90  2  5  1  50
action AFIGHTAIMB	100  1  5  1  14
action AFIGHTSHOOTB  105  1  5  1  8
action AFIGHTWALKB  110  4  5  1  10
action AFTWALKBBACK 125  4  5  -1  10
action AFIGHTDYING  130  6  1  1  18
action AFIGHTDEAD   135  1  1  1  10

// move EDFSHRUNKVEL 32
// move EDFWALKVEL 128
move RESJUMPVEL 160 -96
move RESMOVEBACK -128
ai AIFIGHTSTAND AFIGHTSTAND STOPPED geth
ai AIFIGHTCROUCH AFIGHTCROUCH STOPPED geth

ai AIFIGHTJUMP AFIGHTJUMP1 RESJUMPVEL geth getv
ai AIFIGHTWALK AFIGHTWALK EDFWALKVEL geth
ai AIFIGHTSHOOTA AFIGHTAIMA STOPPED geth
ai AIFIGHTSHOOTB AFIGHTAIMB STOPPED geth
ai AIFIGHTKICK AFIGHTKICK1 STOPPED geth
ai AIFIGHTDOWN AFIGHTDOWN STOPPED geth
ai AIFIGHTHIT AFIGHTHIT STOPPED geth
ai AIFIGHTFROZEN AFIGHTSTAND STOPPED geth
ai AIFIGHTGROW AFIGHTSTAND STOPPED geth
ai AIFIGHTDODGE AFIGHTWALKA STOPPED geth

ai AIFIGHTDYING AFIGHTDYING STOPPED geth
ai AIFIGHTSHRUNK AFIGHTWALK EDFSHRUNKVEL fleeenemy


defstate fightcactorstand

	ife mtype 1 cactor FIGHTMALE else cactor FIGHTFEM

ends

defstate fightcactorcrouch

	ife mtype 1 cactor FIGHTMALECROUCH else
	cactor FIGHTFEMCROUCH

ends

defstate fighterpainsounds
	ife mtype 1
	{
		ifactorsound THISACTOR FTRM_PAIN1 break
		ifactorsound THISACTOR FTRM_PAIN2 break
		ifrnd 128 sound FTRM_PAIN1 else sound FTRM_PAIN2
	}
	else
	{
		ifactorsound THISACTOR FTRF_PAIN1 break
		ifactorsound THISACTOR FTRF_PAIN2 break
		ifrnd 128 sound FTRF_PAIN1 else sound FTRF_PAIN2
	}

ends

defstate fighterdeathsounds
	ife mtype 1
	{
		ifactorsound THISACTOR FTRM_DEATH1 break
		ifactorsound THISACTOR FTRM_DEATH2 break
		ifrnd 128 sound FTRM_DEATH1 else sound FTRM_DEATH2
	}
	else
	{
		ifactorsound THISACTOR FTRF_DEATH1 break
		ifactorsound THISACTOR FTRF_DEATH2 break
		ifrnd 128 sound FTRF_DEATH1 else sound FTRF_DEATH2
	}
ends

defstate ftr_reload

	ifl botclip 0
	{
		add botclip 1
		ife mtype 0 // has smg
		{
			ife botclip -45 
			{
				sound SHELLYCLIPOUT
				espawn SHELL
				setav[RETURN].mtype CLIPFALL
			}
			ife botclip -15 sound SHELLYCLIPIN
			ife botclip 0 set botclip 30
		}
		else // has shotgun
		{
			ife botclip -15
			{
				sound NEWSHOTCOCK
				spawn SHOTGUNSHELL
			}
			ife botclip 0 set botclip 1
		}
	}
	else
	ifl initsprite 0
	{
		add initsprite 1
		ife initsprite -30 sound GLOCKMAGOUT
		ife initsprite -15 sound GLOCKMAGIN
		ife initsprite 0 set initsprite 10
	}

ends

defstate ftr_dodge

	state fightcactorstand
	geta[].ang angvel
	ifrnd 128 add angvel 512 else sub angvel 512
	ai AIFIGHTDODGE
	ife thirdbaseval 1 action AFIGHTWALKB
	set dodgetime 10
	
ends

defstate ftr_combatwalk

	ai AIFIGHTWALK
	ife thirdbaseval 0
		action AFIGHTWALKA
	else
		action AFIGHTWALKB // PISTOL

ends

defstate ftr_walkback

	ifmove RESMOVEBACK break
	
	state fightcactorstand

	ai AIFIGHTWALK
	move RESMOVEBACK geth

	ife thirdbaseval 0
		action AFTWALKABACK
	else
		action AFTWALKBBACK // PISTOL
ends

defstate ftrgunselect

	ife mtype 1 // male with shotgun
		set botclip 1
		
	state lowlineofsight
	ifrnd 128 ife B YES
	ifn sector[].lotag 1
	ife thirdbaseval 0
	{
		state fightcactorcrouch
		ai AIFIGHTSHOOTA
		action AFIGHTCROUCHAIMA
	}
	else
	{
		state fightcactorstand
		ife thirdbaseval 0
			ai AIFIGHTSHOOTA
		else
			ai AIFIGHTSHOOTB
	}
ends

defstate ftr_combat_choice
	
	state avoidmines
	ife B YES
	{
		state ftr_walkback
		break
	}
	
	ifl targetdist 1280
	ifrnd 160
	{
		state fightcactorstand
		ai AIFIGHTKICK
		break
	}
	ifrnd 64
	{
		ife thirdbaseval 1 set thirdbaseval 0 else ifrnd 192 set thirdbaseval 1
	}
	ifnotmoving state ftrgunselect else
	ifl targetdist 4096
	ifrnd 96
		state ftr_walkback
	else
	ifl targetdist 8192
	ifrnd 192
		state ftrgunselect
	else
	ifl targetdist 12288
	ifrnd 64
		state ftrgunselect
	else
		state ftr_combatwalk
ends
			
defstate ftr_roam_choice

	ifn gametype 0 
	{	state fightcactorstand
		ai AIFIGHTWALK
		break
	}
	
	ifai AIFIGHTSTAND 
	{
		ifactioncount 6
		{
			state fightcactorstand
			ai AIFIGHTWALK
			
			ifcansee
			ifpdistg 2560
			ifpdistl 10240
			{
				move EDFWALKVEL seekplayer
				orvar initflags 1
			}
			else
			{
				state setfarang
				ifvarand initflags 1 xorvar initflags 1
			}
				
		}
	}
	else
	{
		state fightcactorstand
		ai AIFIGHTSTAND
	}

ends

defstate ftr_choice

	ifn bottarget -1 state ftr_combat_choice
	else state ftr_roam_choice

ends

defstate fighterkickstate

	ifaction AFIGHTKICK1
	{
		ifmove STOPPED
		ifactioncount 1
		{
			move SNIPERKICKED
			ife shrunken 0
			ife seemytarget YES ifl targetdist 1408
			ife targetalive YES
			{
				sound KICK_HIT
				ifn bottarget -1 set target bottarget
				else set target player[].i
				ife target player[].i
				{
					palfrom 40 63
					wackplayer
					getp[].ang bossang
					add bossang 1024
					cos newx bossang
					sin newy bossang
					shiftvarl newx 8
					shiftvarl newy 8
					setplayer[THISACTOR].posxv newx
					setplayer[THISACTOR].posyv newy
					// push player backwards
				}
				else
				{
					seta[target].xvel -512
					getav[target].stun temp
					add temp 10
					setav[target].stun temp
					geta[target].z z
					sub z 512
					seta[target].z z
					geta[target].zvel z
					sub z 512
					seta[target].zvel z
				}
				ifspritepal 17 seta[target].htextra 20 else
				seta[target].htextra 10
				seta[target].htpicnum SHOTSPARK1
				seta[target].htowner THISACTOR
				seta[target].htang sprite[THISACTOR].ang
			}
		}
		ifactioncount 2 action AFIGHTKICK2
	}
	ifaction AFIGHTKICK2
	{
		ifactioncount 1
		state ftr_choice
	}

ends

defstate ftr_shootsmg


	ifspritepal 17
	{
		sound RIFLEFIRE
		state calczdist
		setprojectile[RANGERPROJ].workslike 36866
		mulvar zdist 844
		div zdist xydist
		ezshoot zdist RANGERPROJ
		setprojectile[RANGERPROJ].workslike 32770
		sub botclip 5
	}
	else
	ifspritepal 24
	{
		sound RIFLEFIRE
		state calczdist
		setprojectile[RANGERPROJ].workslike 36866
		mulvar zdist 844
		div zdist xydist
		ezshoot zdist RANGERPROJ
		setprojectile[RANGERPROJ].workslike 32770
		sub botclip 3
	}
	else
	{
		spawn SHELL		
		sound SHELLYFIRE
		state hitscan_targetprep
		zshoot zdist SHOTSPARK1
	}
	
	
	sub botclip 1
	ifle botclip 0
		set botclip -60 // reloading
		

ends

defstate ftr_shootpistol

	// initsprite is mag, max 8
	
	sub initsprite 1
	spawn SHELL

	ifspritepal 17
	{
		sound DEAGLE_FIRE
		state hitscan_targetprep
		state randzshoot
		zshoot tempb EXPBULLET
	}
	else
	ifspritepal 24
	{
		sound DEAGLE_FIRE
		state hitscan_targetprep
		state randzshoot
		zshoot tempb EXPBULLET
	}
	else
	{
		sound NEW_PISTOL_FIRE
		state hitscan_targetprep
		state randzshoot
		zshoot tempb SHOTGUN
	}
	
	ifle initsprite 0 set initsprite -45

ends

defstate ftr_shootshottie

	spawn SHOTGUNSHELL	
	ifspritepal 17
	{
		set temp sprite[].cstat
		ifvarand temp 1 xorvar temp 1
		seta[].cstat temp
		
		sound PLASFIRE
		set xvel 844
		state rpg_targetprep
		setprojectile[GENERICBOLT].pal 2
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		setprojectile[GENERICBOLT].pal 0
	}
	else
	ifspritepal 24
	{
		set temp sprite[].cstat
		ifvarand temp 1 xorvar temp 1
		seta[].cstat temp
		
		sound PLASFIRE
		set xvel 844
		state rpg_targetprep
		setprojectile[GENERICBOLT].pal 2
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		ezshoot zdist GENERICBOLT
		setprojectile[GENERICBOLT].pal 0
	}
	else
	{
		sound NEWSHOTFIRE
		state hitscan_targetprep
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
		state randzshoot
		zshoot tempb SHOTSPARK1
	}
	
	set botclip -30 // reloading
		

ends


defstate fightercode

	ifai 0
	{
		// mtype == 1 means male, 0 is female
		// pal 24  is heavy
		
		ifactor FIGHTFEM 
		{
			set botclip 30 
			ifrnd 160 ai AIFIGHTSTAND
		} 
		else
		ifactor FIGHTMALE { set botclip 1 set mtype 1 ai AIFIGHTSTAND } else
		{
			ifactor FIGHTMALECROUCH { set mtype 1 set botclip 1 }
			else set botclip 30
			ai AIFIGHTCROUCH
		}
		ife droptile -1 // dialog NPC
		{
			move STOPPEDFORPLAYER faceplayerslow
			break
		}
		set initsprite 10 // pistol clip
		// ifrnd 96 set thirdbaseval 1 // 0 = main gun; 1 = pistol
		cstat 257
		ifspritepal 24
		{
			sizeat 24 21
			clipdist 36
			addstrength EDFTROOPSTRENGTH
		}
		else
		{
			sizeat 22 20
			clipdist 32
		}
		
		ifspritepal 17 
		{ 
			sizeat 24 21
			clipdist 36
			addstrength EDFTROOPSTRENGTH
		} 
		else
		{
			set team 1
			// getp[].max_actors_killed temp
			// sub temp 1
			// setp[].max_actors_killed temp
			set navmode 3
		}
		
		
		geta[].htflags temp
		orvar temp 8192
		seta[].htflags temp
	}
	fall
	ife droptile -1 break
	
	ifvarand monstflags 131072 set team 0
	state monsterai
	
	ifai AIFIGHTDYING
	{
		ifaction AFIGHTRECOVER
		{
			ifhitweapon 
			{
				state random_wall_jibs
				ifdead
				{
					sound SQUISHED
					spawn BLOODPOOL
					state standard_jibs	
					killit
				}
			}
			ifactioncount 5
			{
				strength EDFTROOPSTRENGTH
				seta[].extra inithp
				set ikicked 0
				set monstatus 1
				ifspritepal 22 set ikicked -1
				ai AIFIGHTSTAND
				seta[].htextra -1
			}
			break
		}
		state deadsquished
		strength 0
		ifaction AFIGHTDYING
		{
			ifhitweapon state random_wall_jibs
			ifactioncount 5
			{
				cstat 0 ifrnd 128 cstator 4
				action AFIGHTDEAD
				iffloordistl 8 sound THUD
				ifrnd 128 spawn BLOODPOOL
				break
			}
		}
		ifaction AFIGHTDEAD
		{
			ifhitweapon
			{
				ifwasweapon RPG
				{
					sound SQUISHED
					spawn BLOODPOOL
					state standard_jibs	
					killit
				}
				ifwasweapon RADIUSEXPLOSION
				{
					sound SQUISHED
					spawn BLOODPOOL
					state standard_jibs
					killit
				}
			}
			ife ikicked -2
			{
				action AFIGHTRECOVER
				strength 20
				cstat 257
				seta[].htextra -1
				ife mtype 1
				sound FTRM_DAMMIT
				else 
				sound FTRF_BACKBITCHES
			}
		}
		break
	}
	else
	ifai AIFIGHTFROZEN
	{
		set myspawner -1
		ifcount THAWTIME
		{
		  ai AIFIGHTSTAND
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			resetcount resetactioncount
			strength 0
			break
		  }
		  
		  state enemy_death

		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  state lite_jibs
		  sound GLASS_BREAKING
		  killit
		}
		ifn team 1
		ifp pfacing
		  ifpdistl FROZENQUICKKICKDIST
			state pkick_check
			
		break
	}
	else 
	ifai AIFIGHTGROW
	{
		state genericgrowcode
		break
	}
	else
	ifai AIFIGHTSHRUNK
	{
	  set myspawner -1
	  ifcount SHRUNKDONECOUNT
		ai AIFIGHTSTAND
	  else
		ifcount SHRUNKCOUNT
		{
		  ifspritepal 24 sizeto 24 21
		  else
		  ifspritepal 17 sizeto 24 21
		  else
		  sizeto 22 20
		}
	  else
		state genericshrunkcode
		break
	}
	
	ifn myspawner -1 // incoming projectile
	{
		ifn sprite[myspawner].statnum 4 set myspawner -1
		ifn dodgetime 0 set myspawner -1
		ifn myspawner -1
		{
			set temp NO
			ifai AIFIGHTSTAND set temp YES
			ifai AIFIGHTWALK set temp YES
			ifl botclip -1 set temp YES
			ifl initsprite -1 set temp YES
			ifai AIFIGHTHIT set temp NO
			ifai AIFIGHTKICK set temp NO
			ifai AIFIGHTJUMP set temp NO
			
			ife temp YES
			{
				state fightcactorstand
				geta[].ang angvel
				ifrnd 128 add angvel 512 else sub angvel 512
				ai AIFIGHTDODGE
				ife thirdbaseval 1 action AFIGHTWALKB
				set dodgetime 10
			}
		}
		set myspawner -1
	}
	
	ifai AIFIGHTHIT
	{
		ifg stun 0 resetactioncount
		ifactioncount 2 ai AIFIGHTSTAND
	}
	else
	ifai AIFIGHTSTAND
	{
		ifactioncount 2
		{
			// ifspritepal 22 assess allies for healing
			state ftr_choice
		}
	}
	else
	ifai AIFIGHTSHOOTA
	{
		ife mtype 0
		{
			ifaction AFIGHTAIMA
			{
				ifactioncount 1
				{
					ife botclip -30 state ftr_choice else
					ifl botclip 1 { ifg sprite[].htextra 0 state ftr_walkback } else
					ife bottarget -1 state ftr_choice else
					{
						ifspritepal 24
						{
							ifactioncount 3 nullop else break
						}
						set target bottarget
						ife sprite[].htextra -1
						state friendlyfirecheck
						ife target -1 { state ftr_dodge break }
						action AFIGHTSHOOTA
						state ftr_shootsmg
					}
				}
			}
			else ifaction AFIGHTSHOOTA
			{
				ifactioncount 1
					action AFIGHTAIMA
			}
			else
			ifaction AFIGHTCROUCHAIMA
			{
				ifactioncount 1
				{
					ife botclip -30 state ftr_choice else
					ifl botclip 1 { ifg sprite[].htextra 0 state ftr_walkback } else
					ife bottarget -1 state ftr_choice else
					{
						ifspritepal 24
						{
							ifactioncount 3 nullop else break
						}
						set target bottarget
						ife sprite[].htextra -1
						state friendlyfirecheck
						ife target -1 { state ftr_dodge break }
						action AFIGHTCROUCHSHOOTA
						state ftr_shootsmg
					}
				}
			}
			else ifaction AFIGHTCROUCHSHOOTA
			{
				ifactioncount 1
					action AFIGHTCROUCHAIMA
			}
		}
		else
		{
			ifaction AFIGHTAIMA
			{
				ifactioncount 1
				{
					ifl botclip -1 { ifg sprite[].htextra 0 state ftr_walkback } else
					ife botclip -1 state ftr_choice else
					ife bottarget -1 state ftr_choice else
					{
						set target bottarget
						ife sprite[].htextra -1
						state friendlyfirecheck
						ife target -1 { state ftr_dodge break }
						action AFIGHTSHOOTA
						state ftr_shootshottie
					}
				}
			}
			else ifaction AFIGHTSHOOTA
			{
				ifactioncount 1
					action AFIGHTAIMA
			}
			else
			ifaction AFIGHTCROUCHAIMA
			{
				ifactioncount 1
				{
					ifl botclip -1 
					{
						ifg sprite[].htextra 0 { state fightcactorstand state ftr_walkback break } 
					}
					else
					ife botclip -1 state ftr_choice else
					ife bottarget -1 state ftr_choice else
					{
						set target bottarget
						ife sprite[].htextra -1
						state friendlyfirecheck
						ife target -1 { state ftr_dodge break }
						action AFIGHTCROUCHSHOOTA
						state ftr_shootshottie
					}
				}
			}
			else ifaction AFIGHTCROUCHSHOOTA
			{
				ifactioncount 1
					action AFIGHTCROUCHAIMA
			}
		}
	}
	else
	ifai AIFIGHTSHOOTB
	{
		ifaction AFIGHTAIMB
		{
			ifactioncount 1
			{
				ifl initsprite -1 { ifg sprite[].htextra 0 state ftr_walkback } else
				ife initsprite -1 state ftr_choice else
				ife bottarget -1 state ftr_choice else
				{
					set target bottarget
					ife sprite[].htextra -1
					state friendlyfirecheck
					ife target -1 { state ftr_dodge break }
					action AFIGHTSHOOTB
					state ftr_shootpistol
				}
			}
		}
		else ifaction AFIGHTSHOOTB
		{
			ifactioncount 1
				action AFIGHTAIMB
		}
	}
	ifai AIFIGHTWALK
	{
		ifceilingdistl 96 nullop else
		ifmove EDFWALKVEL ifactioncount 2
		{
			geta[].z z sub z 4096
			cos mycos sprite[].ang
			sin mysin sprite[].ang
			state jumpheightcheck
			ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
			ife hitsprite -1
			{
				ifl starty 384
				{
					sub z 8192
					state jumpheightcheck
					
					ifg starty 2048 { state fightcactorstand ai AIFIGHTJUMP break }
				}
			}
		}
		ifn bottarget -1
		{
			ifg sprite[].htextra 0 ifrnd 128 { state ftr_combat_choice break }
			
			ifnotmoving { state ftrgunselect break }
			else ifl targetdist 4096 ifmove EDFWALKVEL state ftr_combat_choice
			else ifactioncount 4 
			{
				ifl targetdist 4096 ifrnd 8 state ftr_combat_choice
				ifl targetdist 8192 ifrnd 3 state ftr_combat_choice
			}
				
			
		}
		else
		ifactioncount 2
		{
			ifnotmoving ai AIFIGHTSTAND
			else ifaction AFTWALKABACK ai AIFIGHTSTAND
			else ifaction AFTWALKBBACK ai AIFIGHTSTAND
			else
			ifvarand initflags 1 
			ifpdistl 1280
			{
				state setfarang
				xorvar initflags 1
				move EDFWALKVEL geth
			}
		}
	}
	else
	ifai AIFIGHTDODGE
	{
		cos xvel angvel
		sin tempb angvel
		shiftr xvel 7
		shiftr tempb 7
		movesprite THISACTOR xvel tempb 0 CLIPMASK0 RETURN
		ifn RETURN 0 set dodgetime 0
		ife dodgetime 0
			state ftr_choice
	}
	else
	ifai AIFIGHTJUMP
	{
		ifmove RESJUMPVEL
		{
			ifcount 8 
			{
				move EDFWALKVEL geth
				action AFIGHTJUMP2
			}
		}
		ifmove EDFWALKVEL
		{
			iffloordistl 8
			{
				ifn bottarget -1 state ftr_combatwalk else
				ai AIFIGHTWALK
				
			}
			
		}
	}
	else
	ifai AIFIGHTKICK state fighterkickstate
	
	state ftr_reload
	ifg countvarb 0 sub countvarb 1 // voice counter
	ifn myvictim -1
	ifn myvictim -2
	{
		set temp NO
		ife actorvar[myvictim].monstatus 2 set temp YES
		ife sprite[myvictim].statnum 1024 set temp YES
		ife temp YES
		ife countvarb 0
		{
			ife mtype 0 // female
			{
				rand temp 5
				switch temp
				case 0 sound FTRF_GOODBYE break
				case 1 sound FTRF_LAUGH break
				case 2 sound FTRF_MESSY break
				case 3 sound FTRF_STAYDOWN break
				case 4 sound FTRF_SUCKER break
				case 5 sound FTRF_WHOISNEXT break
				endswitch
				set countvarb 60
			}
			else // male
			{
				rand temp 5
				switch temp
				case 0 sound FTRM_LAUGH break
				case 1 sound FTRM_KABOOM break
				case 2 sound FTRM_BOOMBABY break
				case 3 sound FTRM_GONNADONOW break
				case 4 sound FTRM_HEARTHEMSCREAM break
				case 5 sound FTRM_STAYDOWN break
				endswitch
				set countvarb 60
			}
		}
		set myvictim -1
	}
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{
		  state fightcactorstand
		  sound ACTOR_SHRINKING
		  ai AIFIGHTSHRUNK
		  set countvar NO
		  //set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		ifdead
		{
			set countvar NO
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  state fightcactorstand
			  ai AIFIGHTFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  state fightcactorstand
			  ai AIFIGHTGROW
			  break
			}
			// ifn team 1 ifn team 3 addkills 1
			getp[].player_par ikicked
			state enemy_death
			state fightcactorstand
			ifrnd 128 cstator 4
			ai AIFIGHTDYING
			
			state fighterdeathsounds
			
			guts JIBS6 4
			
			ifwasweapon RPG
			{
				state fightcactorstand
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				state fightcactorstand
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		ifai AIFIGHTHIT nullop else
		ifai AIFIGHTSHRUNK nullop else
		{
			ifg stun 0 { state fightcactorstand ai AIFIGHTHIT break }
			
			state fighterpainsounds
			
			ifrnd 32 
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			{
				state fightcactorstand
				ai AIFIGHTHIT
				break 
			}
		}
	}
	else state checksquished
	

ends

useractor enemy FIGHTFEM EDFTROOPSTRENGTH AFIGHTSTAND state fightercode enda
useractor enemy FIGHTFEMCROUCH EDFTROOPSTRENGTH state fightercode enda
useractor enemy FIGHTMALE EDFTROOPSTRENGTH AFIGHTSTAND state fightercode enda
useractor enemy FIGHTMALECROUCH EDFTROOPSTRENGTH state fightercode enda

action AFFNPCSTAND -5  1  5  1  10
useractor notenemy FIGHTFEMNPC 0 AFFNPCSTAND
	fall
	ifmove 0
	{
		move STOPPEDFORPLAYER faceplayerslow
		cstator 257
	}
enda

