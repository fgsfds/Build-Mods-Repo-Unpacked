// WESBOT

spriteflags WESBOT 4202497
spriteflags WESBOTCROUCH 4202497

// action AWESBOTSTAND			0  1  5  1  10
// action AWESBOTSTANDGUN		5  1  5  1  10
action AWESBOTRUN			10 4  5  1  8
action AWESBOTRUNBACK		25 4  5  -1 8

action AWESBOTJUMP1			30 1  5  1  18
action AWESBOTJUMP2			35 1  5  1  18
action AWESBOTFALL			35 1  5  1  20

// from crouching actor
action AWESBOTCROUCH		0  1  5  1  10
action AWESBOTCRAWL			5  4  5  1  14

action AWESBOTSWIM		   65  4  5  1  13
action AWESBOTGLIDE		   80  1  5  1  10
action AWESBOTSWORDSTAND   85  1  5  1  20
action AWESBOTKICKING	   85  5  5  1  8 // hits on actioncount 3+
action AWESBOTKICKOUT	   105  1  5  1  18 // no hit
action AWESBOTKICKEND	   110  2  5  1  16
action AWESBOTKICKPOSE		115  1  5  1  20
action AWESBOTSWORD1		120  4  5  1  7 // hits on actioncount 2+ 
action AWESBOTSWORD2		140  2  5  1  10 // no hits
action AWESBOTSWORD3	    150  3  5  1  7 // hits on actioncount 1+
action AWESBOTFLINTCH	   165 1  1  1  10
action AWESBOTDIE		   165 7  1  1  18
action AWESBOTDEAD		   171 1  1  1  10
action AWESBOTDOWN		   132 2  5  1  50
action AWESSHOTTY1		   182 1  5  1  10
action AWESSHOTTY2		   187 1  5  1  10
action AWESBLOCK		   468 1  5  1  10
action AWESKNEE			   454 1  5  1  40

// move WESVELS
move WESJUMPVELS 300
move WESJUMPCTF 300 -108
move WESWANDERVELS 256
move WESSHRUNKVELS 38
move WESKNEEVELS 256 -64

// ai AIWESBOTSTAND AWESBOTSTAND WESVELS geth

ai AIWESBOTFLINTCH AWESBOTFLINTCH BOTNOMOVE geth
ai AIWESBOTRUN AWESBOTRUN STOPPED geth
ai AIWESBOTWANDER AWESBOTRUN STOPPED geth
ai AIWESBOTCROUCH AWESBOTCROUCH STOPPED geth
ai AIWESBOTCRAWL AWESBOTCRAWL STOPPED geth
ai AIWESBOTJUMP AWESBOTJUMP1 WESJUMPVELS jumptoplayer
ai AIWESBOTDIE AWESBOTDIE STOPPED geth
ai AIWESBOTSWIM AWESBOTSWIM STOPPED geth
ai AIWESBOTSHRUNK AWESBOTRUN WESSHRUNKVELS fleeenemy
ai AIWESBOTFROZEN AWESBOTSTAND STOPPED geth
ai AIWESBOTDOWN AWESBOTDOWN STOPPED geth
ai AIWESBOTKICK AWESBOTSWORDSTAND STOPPED geth
ai AIWESBOTSWORD AWESBOTSWORDSTAND STOPPED geth
ai AIWESBOTBLOCK AWESBLOCK DEABACK geth
ai AIWESBOTKNEE AWESKNEE WESKNEEVELS geth getv

defstate wesbotkneecode

	ifactioncount 1
	{
		ai AIWESBOTJUMP
		set countvar 0
		
		action AWESBOTJUMP2
		move WESKNEEVELS geth
	}
	else
	ifn bottarget -1
	ifl targetdist 1280
	{
		ifl actorvar[bottarget].stun 15 setav[bottarget].stun 15
		seta[bottarget].zvel -512
		set z sprite[].z
		sub z 4096
		set x sprite[].x
		add x 644
		rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
		setsprite bottarget x2 y2 z
	}

ends

defstate wesbotflintch

	ifcount 8
	ife stun 0
	{
		cactor WESBOT
		ai AIWESBOTRUN
	}

ends

defstate wesbotpainsounds

		ifstrength SHELLYBIGHURT
        {
          rand temp 1
		  ife temp 0 sound WESPAIN1 else
		  ife temp 1 sound WESPAIN2
		  
        }
        else
		ifstrength SHELLYHURT
        {
          rand temp 2
		  ife temp 0 sound WESPAIN3 else
		  ife temp 1 sound WESPAIN4 else
		  ife temp 2 sound WESIMHIT
        }
ends

defstate wesblockstate

	set stun 0
	ife sprite[].htpicnum GROWSPARK seta[].htpicnum SHOTSPARK1
	ifn bottarget -1
	{
		set spriteid bottarget
		state facesprite
	}
	
	ifcount 6 ifg saberpos 0 ifpdistl 1560 ifp pfacing
	ife blockang 6666 ifrnd 128 count 3

	ifcount 8
	{
		ifn bottarget -1
		{
			ifl targetdist 1792
			{
				ifrnd 64
				{
					set botclip 1
					ai AIWESBOTSTAND
					action AWESSHOTTY1
					set float 20
					set thirdbaseval 2
					move BOTNOMOVE geth
				}
				// else
				// {
					// ai AIWESBOTKNEE
					// setav[bottarget].stun 15
					// seta[bottarget].htpicnum KNEE
					// seta[bottarget].htowner THISACTOR
					// seta[bottarget].htang sprite[].ang
					// seta[bottarget].htextra 50
					// sound KICKIMPACT
				// }
				else
				ifrnd 128
				ifl targetdist 1560
					ai AIWESBOTSWORD
				else
					ai AIWESBOTKICK
			}
			else ai AIWESBOTRUN
				
		}
		else
			ai AIWESBOTRUN
	}
	else
	ifg sprite[].htextra 0
	ife sprite[].htpicnum SHOTSPARK1
	{
		seta[].htextra -1
		set damflash 0
		ifactorsound THISACTOR BLADEHITMET nullop else sound BLADEHITMET
		ai AIWESBOTBLOCK
		count 3
	}

	ife sprite[].htpicnum KNEE seta[].htextra -1

	ifg sprite[].htextra 1
	{
		geta[].htextra temp
		shiftr temp 1
		seta[].htextra temp
		ifactorsound THISACTOR BLADEHITMET nullop else sound BLADEHITMET
	}

ends

defstate wesbotweap
	
	// 1 is pistol
	// 2 is shotgun (both types)
	// 3 is kick (but not controlled in this state)
	// 4 is swords
	// 5 is ice bombs
	
	set TMP_A thirdbaseval // save the current value
	
	ifn target -1
	ifl xydist 1280
	ife sprite[target].sectnum sprite[].sectnum
	{
		set thirdbaseval 4 
		cactor WESBOT
		ai AIWESBOTSWORD
	}
	else
	{
		ifl plevel 6 set temp 9216 else set temp 20480
		
		ifl xydist temp
		ifg xydist 2048
		ifrnd 40
		ifn thirdbaseval 5
			set thirdbaseval 5
		else
		{
			rand temp 9
			ife thirdbaseval 2 set temp 0
			switch temp
			case 0 case 1 case 2 case 3 case 4 case 5
				set tempb healthbuff
				div tempb 2
				ifle sprite[].extra tempb set thirdbaseval 6 else
				set thirdbaseval 1 // pistol
				ifmove BOTNOMOVE move STOPPED geth
			break
			
			case 6 case 7 case 8 case 9
				set thirdbaseval 2 // shotgun variants
				ifinwater nullop else
				{
					cactor WESBOT
					ai AIWESBOTSTAND
					action AWESSHOTTY1
					set float 20
				}
			break
			
			endswitch
		}
		
	}
	
	ifn bottarget -1 ife sprite[bottarget].pal 1 set thirdbaseval 1
	ifn bottarget -1 ife sprite[bottarget].picnum GREENSLIME set thirdbaseval 1
	
	switch thirdbaseval
	case 1
		set botclip 16
	break
	case 2
		rand botclip 2 add botclip 1
	break
	case 3
		set botclip 1
	break
	case 4
		set botclip 2
	break
	case 5
		set botclip 1
	break
	case 6
		set botclip 10
	break
	endswitch
	
	ife TMP_A thirdbaseval set float 0 else
	set float -10
	
ends

defstate wesbotreviveplayer
	ifai AIWESBOTRUN { cactor WESBOT ai AIWESBOTCROUCH }
	ifai AIWESBOTSTAND { cactor WESBOT ai AIWESBOTCROUCH }
	set bottarget -1
	set spriteid player[].i
	state facesprite
	addphealth 1
	// ifg sprite[player[].i].extra 50 addphealth 1
	addstrength -1
	ife sprite[].extra 30 setav[spriteid].ikicked -96
	ifstrength 1 strength 1
	ife sprite[player[].i].extra 10
	{
		rand temp 2
		ife temp 0 globalsound WESALREADYDIDTHIS
		ife temp 1 globalsound WESALREADYLOSING
		ife temp 2 globalsound WESGOTYOURBACK				
	}
	palfrom 16 0 10 63
	espawn CAPTUREPLUS
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	seta[RETURN].z sprite[player[].i].z
	seta[RETURN].pal 1
	rand x 512
	sub x 256
	add x sprite[].x
	seta[RETURN].x x
	rand y 512
	sub y 256
	add y sprite[].y
	seta[RETURN].y y
ends

defstate wesbotcheckorders

	ifg rage 0 { set cmode 0 break }
	switch cmode
		case 1 // awaiting command
		break
		case 2 // player pressed forward
			ifhitspace
			{
				quote 142
				ifrnd 84 sound WESAFFIRMATIVE else
				ifrnd 128 sound WESGOTIT else sound WESINPOSITION
				set countvarc 0
				set monstatus 70
				set countvarb 0
				seta[].htactorstayput sprite[].sectnum 
				set cmode 0
			}
		break
		case 3 // player pressed left
			ifhitspace
			{
				ifl sprite[].extra healthbuff
				{
					state healmybot
				}
				else quote 1135

				set cmode 0
			}
		break
		case 4 // player pressed right
			ifhitspace
			{
				ifn player[].cursectnum -1
				ifn sprite[myshelly].sectnum -1
				ife shellysquish NO
				ifp palive
				ifg sprite[myshelly].xrepeat 22
				ifg sprite[player[].i].xrepeat 32
				{
					set x sprite[myshelly].x
					set y sprite[myshelly].y
					set z sprite[myshelly].z
					set angvar sprite[myshelly].ang
					set mysector sprite[myshelly].sectnum
					
					setav[myshelly].monstatus 60 // trigger to switch characters
					
					ife switchmode 0
					{
						getp[].posz zdist add zdist 8192
						setsprite myshelly player[].posx player[].posy z
						changespritesect myshelly player[].cursectnum
						setsprite player[].i x y z
						changespritesect player[].i mysector
						setp[].posx x
						setp[].posy y
						sub z 8192
						setp[].posz z
						setp[].ang angvar
					}
					
					
					globalsound WESAWWYEAH set pchar 2
					ifge wescharge FULLBOTCHARGE
					{
						set wescharge 0
						set switchboost SWITCHBOOSTTIME
					}
					
				}
				set cmode 0
			}
		break
	endswitch
	
	ifn cmode 0 
	{ 
		ife cmode -1 set cmode 0 
		ifpdistg 4096 set cmode 0
		// ifp pfacing nullop else set cmode 0
		ife pdown YES set cmode 0
		break 
	}

	add countvarb 1

	ife monstatus 70
	{
		ife pdown YES
		{
			set monstatus 30
			seta[].htactorstayput -1
			break
		}
		
		iffloordistl 32
		ifpdistl 1280
		ifp pfacing
		ifp palive
		ifg countvarb 30
		ife seeplayer YES
		{
			ifl player[].fta 30
			quote 144
			ifhitspace
			{
				ifrnd 84 sound WESGOTYOURBACK else
				ifrnd 128 sound WESIMONIT else
				sound WESAFFIRMATIVE
				set monstatus 30
				seta[].htactorstayput -1
				quote 146
				set countvarb 0
			}
		}
		else ife player[].ftq 144 setp[].fta 0
		break
	}
	else seta[].htactorstayput -1
	
	ifn vendor_screen 0 break
	
	ife subtitle_time 0
	ife pdown NO
	ife monstatus 30
	iffloordistl 32
	ifpdistl 1280
	ifp pfacing
	ifp palive
	ifg countvarb 30
	ifn gametype CTF ifn gametype CONTROL
	ife seeplayer YES
	{
		ifinwater ifl sprite[player[].i].z sprite[].z nullop else
		{
			ifl player[].fta 60
			quote 139
			ifhitspace
			{
				ifl player[].fta 60
				{ quote 139 setp[].fta 88 }
				ifhitspace	
				ife player[].newowner -1
				ifl player[].fta 90
				set cmode 1
			}
		}
	}
	else ife player[].ftq 139 setp[].fta 0
ends

defstate resetwesbotmultiplayer
	cactor WESBOT
	set monstatus 30
	// strength DUKESTRENGTH
	seta[].extra healthbuff
	ai AIWESBOTSTAND
	ife gametype DM
	ifg pstarts 0
	{
		set tempb pstarts
		sub tempb 1
		rand temp tempb
		set mysector 0
		updatesectorz loadx[temp] loady[temp] loadz[temp] mysector
		ifn temp -1
		{
			seta[].x loadx[temp]
			seta[].y loady[temp]
			seta[].z loadz[temp]
			seta[].sectnum mysector
		}
	}
	else
	setsprite THISACTOR loadx[LEVEL] loady[LEVEL] loadz[LEVEL]
	set bottarget -1
	set navpoint -1
ends

defstate wesbotshrunkstate

  ifcount SHRUNKDONECOUNT
  {
    seta[].htextra -1
    ai AIWESBOTSTAND
	cactor WESBOT
	ifl sprite[].extra 1 strength 1
  }
  else
    ifcount SHRUNKCOUNT
	{
      sizeto 22 20
	  ifgapzl 48 
	  {
		cactor WESBOTCROUCH
		action AWESBOTCROUCH	
	  }
	}
  else
    state genericshrunkcode

ends

defstate wesbotfrozenstate

	ife ikicked -2 count 1000
	ifcount THAWTIME
    {
	  ife team 1
	  set monstatus 30
	  else set monstatus 1
	  set ikicked 0
      ai AIWESBOTSTAND
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  ife team 0
	  {
		state lite_jibs
		set droptile 0 state enemy_death
        sound GLASS_BREAKING
        killit
	  }
	  else
	  sound GLASS_BREAKING
	  
	  cactor WESBOTCROUCH
	  ifrnd 84 sound WESDEATH1 else
	  ifrnd 128 sound WESDEATH2 else sound WESDEATH3
	  ai AIWESBOTDOWN
	  quote 1193
	  set countvarc 0
	  strength 5
	  getlastpal
    }

ends

defstate movewesbot

	ifn padmove 0 { set angvel padang break }
	ifmove BOTNOMOVE break

	getincangle temp lastangvel angvel
	abs temp
	shiftr temp 4
	add SPRITELOTAG temp
	sub SPRITELOTAG 8
	
	ifg SPRITELOTAG 128 set SPRITELOTAG 128
	ifl SPRITELOTAG 56 set SPRITELOTAG 56
	
	ifn dodgetime 0
	{
		mul SPRITELOTAG	3 div SPRITELOTAG 4
		espawn SPEEDBLUR
		setav[RETURN].mtype sprite[].htdispicnum
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].alpha 80
		seta[RETURN].pal sprite[].pal
		seta[RETURN].mdflags 16
	}
	
	ife pdown NO ife bottarget -1 
	ifpdistl 2560
	ifpdistg 844
	ife seeplayer YES
	ifn angvel player[].ang
	ifn player[].cursectnum -1
	ife sector[player[].cursectnum].lotag 0
	ife mtype 0
	{
		set B YES
		headspritesect spriteid player[].cursectnum
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum SECTOREFFECTOR
			ife sprite[spriteid].lotag 14
				set B NO
			nextspritesect spriteid spriteid
		}
		ife B YES
		{
			geta[].x x2
			geta[].y y2
			sub x2 player[].posx
			sub y2 player[].posy
			getangle angvar x2 y2
			
			getincangle tempb angvar player[].ang
			ifge tempb 0 add angvel 256 else sub angvel 256
		}
	}
	
	// hack to force bot to go towards player when down in matches
	ifg gametype 0
	ife pdown YES
	ifai AIWESBOTRUN
	ifpdistl 5120
	ife seeplayer YES
	{
		geta[player[].i].x x2
		geta[player[].i].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvel x2 y2
	}
	
	cos xvel angvel
	sin tempb angvel
	
	div xvel SPRITELOTAG
	div tempb SPRITELOTAG
	ifaction AWESBOTCRAWL { shiftr xvel 1 shiftr tempb 1 }
	geta[].ang TMP_A
	movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
	seta[].ang TMP_A
	ifn mtype 0 set dodgetime 0
	
	set temp mtype
	ifn temp 0
	// ifg navpoint -1
	{
		// bumping something
		sub temp 49152
		ifl temp 16384 ifg temp -1 // bumping a sprite
		{
			set B NO
			ife actorvar[temp].monstatus 1
			ife actorvar[temp].team team set B YES
			ifg gametype 0 ifvarand sprite[temp].cstat 16 set B YES
			ife B YES
			{
				set x2 sprite[].x
				add x2 256
				rotatepoint sprite[].x sprite[].y x2 sprite[].y sprite[].ang x y
				setsprite THISACTOR x y sprite[].z
				seta[].htmovflag 0
			}
		}
	}

	set lastangvel angvel
ends

defstate wesbotstartwander

	ifaction AWESBOTFALL break
	ifaction AWESBOTGLIDE break
	
	set B 0
	set xydist 2048
	whilevarn B 32
	{
		add B 1
		geta[].z z
		sub z 4096
		rand angvar 2047
		cos mycos angvar
		sin mysin angvar
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz CLIPMASK1
		
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		
		ifg startx xydist set B 32
		ife B 32 set angvel angvar
		
	}
	cactor WESBOT
	ai AIWESBOTWANDER
	// ifgapzl 48 { cactor WESBOTCROUCH action AWESBOTCRAWL }
	ifl gametype 1
	{
		ifl countvarc 150
		set crumbwait 30
	}
	else { set crumbwait 6 set dodgetime 6 }

ends

defstate wesbotwander

	ifg gametype 0
	{
		state movewesbot
		ife crumbwait 0
		{
			cactor WESBOT
			ai AIWESBOTRUN
		}
		break
	}
	
	ife seeplayer YES { cactor WESBOT ai AIWESBOTRUN }
	
	ifai AIWESBOTRUN break
	state movewesbot
	ifl crumbwait 1 sub crumbwait 1
	
	ifl crumbwait 0 
	{
	    ifpdistl 1280 { cactor WESBOT ai AIWESBOTSTAND set SPRITELOTAG 128 break }
		ifrnd 2 state wesbotstartwander else
		ifn mtype 0 { ifn gametype -1 set navpoint -1 state wesbotstartwander }
	}
	

ends

defstate wesbothitscan

	geta[].sectnum mysector
	ife mysector -1 break
	getceilzofslope mysector sprite[].x sprite[].y z
	sub z sprite[].z
	ifg z -16384 break
	
	ifai AIWESBOTCRAWL nullop else
	ifai AIWESBOTRUN nullop else break

	// we want to scan at waist heigh in the direction angvel
	// then another scan much higher if there is an obstacle

	geta[].z z
	sub z 4096
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz CLIPMASK1

	set startx sprite[].x
	sub startx hitx
	mul startx startx
	set y2 sprite[].y
	sub y2 hity
	mul y2 y2
	add startx y2
	sqrt startx startx

	set temp NO
	ifl startx JUMPDIST set temp YES
	ifn mtype 0 { set startx 256 set temp YES }
	ifn hitsprite -1 
	{
		ife actorvar[hitsprite].monstatus 1 set temp NO
		ife sprite[hitsprite].picnum APLAYER set temp NO
	}
	ife pdown YES ifpdistl 644 set temp NO
	// iffloordistl 4
	ife sprite[].zvel 0
	ife temp YES
	{
		sub z 18432
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 tempd hitwall hitsprite hitx hity hitz CLIPMASK1
		
		set starty sprite[].x
		sub starty hitx
		mul starty starty
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add starty y2
		sqrt starty starty
		ifg starty startx // new scan goes farther
		{
			sub starty startx
			ifg starty 256 // enough space to stand on
			{
				cactor WESBOT
				ai AIWESBOTJUMP
				ifg gametype 0 move WESJUMPCTF geth getv
			}
		}
	}
	else ifg startx 384 ifl startx 1280
	ife gametype 0
	ifn player[].cursectnum sprite[].sectnum ifn player[].cursectnum -1
	ifpdistg 1560 operate

ends

defstate wesbotfindplayercrumb

	ife monstatus 70 break // ordered to wait for player
	ife seeplayer YES { set shotpitch 0 set countvarc 0 }
	else 
	{ 
		add shotpitch 1 add countvarc 1 
		ifg ptargeted 0 { add shotpitch 1 add countvarc 2 }
	}
	
	ifg shotpitch 120
	ife team 1
	{
		set savedvalue -1
		set xydist2 0
		headspritestat spriteid 999
		ife spriteid -1
		{
			setsprite THISACTOR player[].posx player[].posy player[].posz
			cactor WESBOT
			ai AIWESBOTRUN
			set shotpitch 0
		}
		else
		{
			whilevarn spriteid -1
			{
				canseespr spriteid player[].i temp
				
				ifg countvarc 180
				{
					ife temp YES set savedvalue spriteid
				}
				else
				{
					ife bottarget -1
					ife temp NO set savedvalue spriteid
				}
				
				
				nextspritestat spriteid spriteid
				ifn savedvalue -1 set spriteid -1
			}
			ifn savedvalue -1
			{
				setsprite THISACTOR sprite[savedvalue].x sprite[savedvalue].y sprite[savedvalue].z
				cactor WESBOT
				ai AIWESBOTRUN
				set shotpitch 0
			}
		}
	}

	ifg crumbwait 0 { sub crumbwait 1 break }


	ife seeplayer YES ifawayfromwall set xydist2 4096
	else set xydist2 2048

	set B -1 // reset crumb and search from scratch
	// linked list of sprites already in order from most recent to oldest
	headspritestat spriteid 999
	whilevarn spriteid -1
	{
		ldist xydist THISACTOR spriteid
		ifg xydist 384 ifl xydist xydist2
		ifge actorvar[spriteid].mtype lastcrumb
		{
			canseespr THISACTOR spriteid temp
			ife temp YES
			{ set B spriteid getav[B].mtype lastcrumb }
		}
		
		nextspritestat spriteid spriteid
		ifn B -1 set spriteid -1
	}

	ife B -1 // longer distance check
	{
		mul xydist2 2
		headspritestat spriteid 999
		whilevarn spriteid -1
		{
			ldist xydist THISACTOR spriteid
			ifg xydist 384 ifl xydist xydist2 
			ifge actorvar[spriteid].mtype lastcrumb
			{
				canseespr THISACTOR spriteid temp
				ife temp YES
				{ set B spriteid getav[B].mtype lastcrumb }
			}
			nextspritestat spriteid spriteid
			ifn B -1 set spriteid -1
		}
	}
	
	ife B -1
	ife seeplayer YES
	getp[].i B

	ifn B -1 // face crumb
	{
		geta[B].x x
		geta[B].y y
		sub x sprite[].x
		sub y sprite[].y
		getangle angvel x y
		set droptile B
		ifai AIWESBOTWANDER { cactor WESBOT ai AIWESBOTRUN }
		
		ifn mtype 0 // bumping into something
		{

			set temp mtype
			add temp 16384
			ife temp player[].i break
				
			set crumbwait 20
			geta[].z z
			geta[B].z temp
			sub z temp
			ifg z 10240 // jump trigger
			{
				ifinwater nullop else
				iffloordistl 4
				{
					cactor WESBOT
					ai AIWESBOTJUMP
					ifg gametype 0 move WESJUMPCTF geth getv
				}
			}
			
		}
	}
	else 
	{
		set droptile -1
		ifg shotpitch 90
		{
			ifai AIWESBOTWANDER nullop else state wesbotstartwander
		}
		
	}
	
ends

defstate wesbotswimstate

	set FEMFALLDMG 0
	set mysector sprite[].sectnum
	updatesectorz sprite[].x sprite[].y sprite[].z mysector
	ifn sprite[].sectnum mysector ifn mysector -1 { changespritesect THISACTOR mysector }
	
	set B YES
	ife pdown NO ife player[].cursectnum sprite[].sectnum ifpdistl 2048
	set B NO
	ife B YES
	{
		getincangle temp lastangvel angvel
		ifl temp 0 mul temp -1
		shiftr temp 4
		add SPRITELOTAG temp
		sub SPRITELOTAG 8
		
		ifg SPRITELOTAG 160 set SPRITELOTAG 160
		ifl SPRITELOTAG 80 set SPRITELOTAG 80
		
		cos xvel angvel
		sin tempb angvel

		div xvel SPRITELOTAG
		div tempb SPRITELOTAG
		
		ifn droptile -1 // most recent crumb followed
			geta[droptile].z zdist
		else
		getp[].posz zdist
		add zdist 8192
		sub zdist sprite[].z
		
		ifl zdist -2560 set zdist -2560
		ifg zdist 2560 set zdist 2560
		ifceilingdistl 12 ifl zdist 0 set zdist 0
		iffloordistl 12 ifg zdist 0 set zdist 0
		
		set dodgetime 0
		ifpdistg 512
		movesprite THISACTOR xvel tempb zdist CLIPMASK0 mtype
		set lastangvel angvel
		
		ife pdown YES 
		ifge sprite[].extra 30
		ife team 1
		{
			ldist temp THISACTOR player[].i
			ifl temp 768
			ifpdistl 1560
			state wesbotreviveplayer
		}
	}
	ifrnd 12 spawn WATERBUBBLE
	
	getplayer[].cursectnum mysector
		ifn mysector -1
		gets[mysector].lotag tempc
		else set tempc 0
	set tempd 0 // to be used as flag during loop below
	ifn tempc 2 // player not in water!
	{
		geta[].sectnum mysector
		ifn mysector -1
		{
			headspritesect spriteid mysector
			whilevarn spriteid -1
			{
				ife sprite[spriteid].picnum SECTOREFFECTOR
				ife sprite[spriteid].lotag 7
				{
					geta[spriteid].statnum tempb
					geta[spriteid].hitag B // B is shared by the SE in the sector it goes to
					headspritestat temp 9
					whilevarn temp -1
					{
						ife sprite[temp].lotag 7
						ifn spriteid temp
						ife sprite[temp].hitag B // the matching water SE
						ife sprite[temp].sectnum player[].cursectnum
						{
							geta[spriteid].x x
							sub x sprite[temp].x
							geta[spriteid].y y
							sub y sprite[temp].y
							changespritesect THISACTOR sprite[temp].sectnum
							geta[].x x2
							sub x2 x
							seta[].x x2
							geta[].y y2
							sub y2 y
							seta[].y y2
							seta[].z player[].posz
							set tempd YES
						}
						nextspritestat temp temp
						ife tempd YES set temp -1
					}
				}
				nextspritesect spriteid spriteid
				ife tempd YES set spriteid -1
			}
		}
	}

ends

defstate wesbotfollowplayer

	set B NO
	
	ife pdown YES
	ifpdistl 1560
	ife team 1
	{
		ifge sprite[].extra 30
		{
			
			ldist temp THISACTOR player[].i
			ifl temp 768
			{	
				ifonwater set B YES
				ifinwater set B YES
				ifai AIWESBOTSTAND set B YES
				ifai AIWESBOTCROUCH set B YES
				ifai AIWESBOTRUN set B YES
				ifn dodgetime 0 set B NO
				ife seeplayer NO
				{
					ifonwater nullop else set B NO
					ifinwater nullop else set B NO
				}
			}
		}
		else
		ifg player[].firstaid_amount 0
			state healmybot
		else
		{
			ifl player[].fta 90 quote 1159 
		}
	}
	
	ife B YES
		state wesbotreviveplayer
		
	// hack to make her leave standing position to kick an enemy if she is reloading
	ifgapzl 48 nullop else
	ifpdistl FOLLOWRANGE // MAXRANGE
	ifn bottarget -1
	ifn thirdbaseval 4
	ife ikicked 0
	ife FEMKILLCOUNT 0
	ifn monstatus 70 // don't do this if ordered to wait for player
	iffloordistl 4
	ifg sprite[].extra SHELLYBIGHURT
	// don't run into bosses!
	ifl actorvar[bottarget].inithp 1000
	ifn sprite[bottarget].picnum GREENSLIME
	ifn sprite[bottarget].picnum ROTATEGUN
	ifn sprite[bottarget].picnum DOLLBOMB
	ifl targetdist 3072
	{
		state movewesbot
		cactor WESBOT
		ifai AIWESBOTRUN nullop else ai AIWESBOTRUN
		set initflags bottarget
		break
	}
	

	findplayer xydist
	ife monstatus 70 set xydist 64 // waiting for player, not following
	else ife team 1 set monstatus 30 // failsafe
	else set monstatus 1
	
	ife player[].cursectnum -1 set xydist2 768
	else
	{
		ifg PSHRINKING 0 { set xydist 64 set xydist2 2048 } else
		ife pdown YES set xydist2 768 else
		ife bottarget -1 ifg ptargeted 0 set xydist2 768
		else
		ifn bottarget -1 ifg targetdist 4096 ifl targetdist 10240 set xydist2 FOLLOWRANGE
		else
		ife sprite[].sectnum player[].cursectnum set xydist2 FOLLOWRANGE else
		ifg sector[player[].cursectnum].lotag 2 set xydist2 768 else
		ifg sector[player[].cursectnum].hitag 0 set xydist2 768 else
		ife seeplayer YES 
		{
			ifp pfacing set xydist2 1280
			else
			set xydist2 3072
		}
		else set xydist2 1560
	}
	
	ifg xydist xydist2
	{
		ifn FEMKILLCOUNT 0 ifai AIWESBOTCROUCH ifg countvar 10 set countvar 9
		
		ifg countvar 10
		{
			
			state movewesbot
			ifaction AWESBOTSTANDGUN action AWESBOTRUN
			
			ifai AIWESBOTSTAND ai AIWESBOTRUN
			
			ifai AIWESBOTRUN
			ifn mtype 0 ife gametype 0
			{
				set temp player[].player_par
				mod temp 15
				ife temp 0
				{
					cactor WESBOT
					ai AIWESBOTJUMP
					move WESJUMPCTF geth
				}
			}
			else
			ifai AIWESBOTCROUCH 
			ai AIWESBOTCRAWL
			
			ifai AIWESBOTCRAWL
			{
				ifgapzl 48 nullop else
				{
					cactor WESBOT
					ai AIWESBOTRUN
				}
			}
		}
		add countvar 1
	}
	else
	{
		ifp pfacing
		ifpdistl 1280
		ifg forwinput 2
		ife pdown NO
		{
			resetcount
			set angvel player[].ang
			state movewesbot
			
			ifaction AWESBOTFALL break
			ifaction AWESBOTGLIDE break
			
			ifai AIWESBOTSTAND ai AIWESBOTRUN
			ifai AIWESBOTCROUCH ai AIWESBOTCRAWL
			
			ifai AIWESBOTCRAWL
			ife FEMKILLCOUNT 0
			{
				ifgapzl 48 nullop else
				{
					cactor WESBOT
					ai AIWESBOTRUN
				}
			}
			break
		}
		set countvar 0
		
		ifaction AWESBOTFALL break
		ifaction AWESBOTGLIDE break
		
		ifai AIWESBOTRUN 
		{
			ifonwater
			{
				cactor WESBOT
				ai AIWESBOTSTAND
			}
			else
			ifrnd 128
			{
				cactor WESBOT
				ai AIWESBOTSTAND
			}	
			else
			{
				cactor WESBOTCROUCH
				ai AIWESBOTCROUCH
			}
			set SPRITELOTAG 128 
		}
		ifai AIWESBOTCRAWL ai AIWESBOTCROUCH
	}
ends

defstate wesbotstartdodge

	state incoming_eval
	
	ife actorvar[myspawner].monstatus 1
	{
		ifl x xydist sub angvel 256 else
		add angvel 256
	}
		
	set dodgetime 10
	set crumbwait 10
	
	ife actorvar[myspawner].monstatus 1 ifrnd 128 sound WESOLEMF
	
	set myspawner -1
	cactor WESBOT
	ai AIWESBOTRUN
	
		
ends

defstate wesbotswordcode

	set myspawner -1
	ife bottarget -1 set dodgetime 3
	else dist xydist THISACTOR bottarget
	ifaction AWESBOTSWORDSTAND 
	{
		ifactioncount 1 
		{
			ifn bottarget -1
			ifl xydist 1560
			{
				action AWESBOTSWORD1 
				sound SWISH
				sound SWISH
				sound SWISH
			}
			else ifrnd 128 ai AIWESBOTSTAND else
			{
				cactor WESBOTCROUCH
				ai AIWESBOTCROUCH
			}
		}
	} 
	else
	{
		ifn bottarget -1
		{
			geta[bottarget].x x2
			geta[bottarget].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2

			// ifaction AWESBOTSWORD3 nullop else
			ifg xydist 512
			{
				cos xvel angvel
				sin tempb angvel
				shiftr xvel 7
				shiftr tempb 7
				movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
			}
		}
		ifaction AWESBOTSWORD1
		ifactioncount 2 
		{
			ifg sprite[].htextra 0
			ife sprite[].htowner bottarget
				seta[].htextra -1
			ifactioncount 4 { action AWESBOTSWORD2 break }
			else
				state shootsaberstate
		}
		ifaction AWESBOTSWORD2 
		{ 
			ifactioncount 2 
			{
				action AWESBOTSWORD3 
				sound SWISH
				sound SWISH
				sound SWISH
				break
			}
		}
		ifaction AWESBOTSWORD3
		{
			ifactioncount 1
			{
				ifg sprite[].htextra 0
				ife sprite[].htowner bottarget
					seta[].htextra -1
					
				state shootsaberstate
			}
			ife bottarget -1 move BOTNOMOVE geth
			ifactioncount 3
			{
				ifn bottarget -1
				{
					ifl xydist 1792
					{
						ifrnd 80
						{
							set botclip 1
							ai AIWESBOTSTAND
							action AWESSHOTTY1
							set float 20
							set thirdbaseval 2
							move BOTNOMOVE geth
						}
						// else
						// {
							// ai AIWESBOTKNEE
							// setav[bottarget].stun 15
							// seta[bottarget].htpicnum KNEE
							// seta[bottarget].htowner THISACTOR
							// seta[bottarget].htang sprite[].ang
							// seta[bottarget].htextra 50
							// sound KICKIMPACT
						// }
						else
						ifrnd 160
						ifl xydist 1304
							action AWESBOTSWORDSTAND
						else
							ai AIWESBOTKICK
					}
					else ai AIWESBOTRUN
				}
				else
				action AWESBOTSWORDSTAND
			}
		}
	} 

ends

defstate wesbotkickcode

	set myspawner -1
	ife bottarget -1 set dodgetime 3
	else
	{
		geta[].z z
		geta[bottarget].z z2
		ifg z z2
		{
			sub z2 z
			mul z2 320
			ldist temp THISACTOR bottarget
			div z2 temp
			movesprite THISACTOR 0 0 z2 CLIPMASK0 RETURN
		}
	
	}
	ifaction AWESBOTSWORDSTAND { ifactioncount 1 action AWESBOTKICKING } 
	else
	ifaction AWESBOTKICKING 
	{
		ifn bottarget -1
		ifactioncount 1
		{
			ifactorsound THISACTOR KICKOUT nullop else sound KICKOUT
			geta[bottarget].x x2
			geta[bottarget].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2

			dist xydist THISACTOR bottarget
			
			ifg xydist 512
			{
				cos xvel angvel
				sin tempb angvel
				shiftr xvel 6
				shiftr tempb 6
				movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
			}
		}
		ifactioncount 4 
		{
			action AWESBOTKICKOUT 
			ifn bottarget -1
			{
				ifl xydist 1024
				{
					sound KICKIMPACT
					seta[bottarget].htowner THISACTOR
					seta[bottarget].htang sprite[].ang
					seta[bottarget].htextra 55
					setav[bottarget].stun 12
					seta[bottarget].htpicnum KICKIMPACT
				}
			}
		}
	} 
	else
	ifaction AWESBOTKICKOUT 
	{ 
		ife bottarget -1 move BOTNOMOVE geth
		ifactioncount 1 action AWESBOTKICKEND 
	} 
	else
	ifaction AWESBOTKICKEND 
	{ 
		ifactioncount 2 
		{
			ifn bottarget -1 ife actorvar[bottarget].monstatus 1 ifl xydist 1560
			ifrnd 128
			{
				ai AIWESBOTSWORD
				break
			}
			action AWESBOTKICKPOSE 
			move BOTNOMOVE geth
			set ikicked 150
			
		}
	} 
	else
	ifaction AWESBOTKICKPOSE 
	{ 
		ifn bottarget -1 ife actorvar[bottarget].monstatus 1 ifl xydist 1560 
		{
			ifrnd 160
			{
				ai AIWESBOTSWORD
				break
			}
			else
			{
				set botclip 1
				ai AIWESBOTSTAND
				action AWESSHOTTY1
				set thirdbaseval 2
				set float 20
				move BOTNOMOVE geth
				break
			}
		}
		ifactioncount 1
			ai AIWESBOTSTAND

	}
	
ends

defstate wesbotfallhandling

	ifai AIWESBOTSWIM break

	ifaction AWESBOTGLIDE
	{
		geta[].zvel zdist
		ifg zdist 768 set zdist 768
		seta[].zvel zdist
		set FEMFALLDMG 0
		ifactorsound THISACTOR GLIDELOOP nullop else 
			sound GLIDELOOP 
		cos xvel angvel
		sin tempb angvel
		
		div xvel SPRITELOTAG
		div tempb SPRITELOTAG
		shiftl xvel 1 shiftl tempb 1
		movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
	}
	else stopactorsound THISACTOR GLIDELOOP
	ifg sprite[].zvel 0
		add FEMFALLDMG 2
		
	ifg FEMFALLDMG 6
	{
		ifactor WESBOT
		{
		ifaction AWESBOTSTAND action AWESBOTFALL else
		ifaction AWESBOTRUN action AWESBOTFALL
		}
		ifactor WESBOTCROUCH { cactor WESBOT ai AIWESBOTSTAND action AWESBOTFALL }
		// ifaction AWESBOTCROUCH { cactor WESBOT ai AIWESBOTSTAND action AWESBOTFALL }
		
		ifaction AWESBOTFALL ifg FEMFALLDMG 20
			action AWESBOTGLIDE
	}
	
	ife globalnofall YES ifg FEMFALLDMG 8 set FEMFALLDMG 8
		
	iffloordistl 4
	{
		ifonwater set FEMFALLDMG 0
		ifinwater set FEMFALLDMG 0
		ifg FEMFALLDMG 0
		{
			sub FEMFALLDMG 40
			ifg FEMFALLDMG 0
			{
				state wesbotpainsounds
				// seta[].htextra FEMFALLDMG
				// seta[].htpicnum SHOTSPARK1
				// seta[].htowner THISACTOR
				// ifg FEMFALLDMG 15 { sound SQUISHED guts JIBS6 4 }
				ifonwater
				{
					cactor WESBOT
					ai AIWESBOTSTAND
				}
				cactor WESBOTCROUCH	
				ai AIWESBOTCROUCH
			}
			set FEMFALLDMG 0
		}
		ifaction AWESBOTFALL
		{
			ifonwater
			{
				cactor WESBOT
				ai AIWESBOTSTAND
				break
			}
			ifai AIWESBOTSTAND { cactor WESBOTCROUCH ai AIWESBOTCROUCH }
			ifai AIWESBOTRUN action AWESBOTRUN
		}
		ifaction AWESBOTGLIDE
		{
			ifonwater
			{
				cactor WESBOT
				ai AIWESBOTSTAND
				break
			}
			ifai AIWESBOTSTAND { cactor WESBOTCROUCH ai AIWESBOTCROUCH }
			ifai AIWESBOTRUN action AWESBOTRUN
		}
	}


ends

defstate wesbotmultiplayer

	ifai AIWESBOTSTAND
	ife gametype SURVIVAL ifl player[].player_par 150 break
	
	// hack to make her leave standing position to kick an enemy if she is reloading
	ifgapzl 48 nullop else
	ifn bottarget -1
	ifn redcarrier THISACTOR
	ifn bluecarrier THISACTOR
	ifn thirdbaseval 4
	ife ikicked 0
	ife FEMKILLCOUNT 0
	ifn monstatus 70 // don't do this if ordered to wait for player
	iffloordistl 4
	// don't run into bosses!
	ifl actorvar[bottarget].inithp 1000
	ifn sprite[bottarget].picnum GREENSLIME
	// ifn sprite[bottarget].picnum BOSS1
	// ifn sprite[bottarget].picnum BOSS2
	// ifn sprite[bottarget].picnum BOSS3
	// ifn sprite[bottarget].picnum BOSS4
	// ifn sprite[bottarget].picnum PSPIDER
	// ifn sprite[bottarget].picnum DEVOURER
	// ifn sprite[bottarget].picnum LIZBOSS
	// ifn sprite[bottarget].picnum PIGBOSS
	// ifn sprite[bottarget].picnum CANHEAD
	// ifn sprite[bottarget].picnum CANHEADNOARMS
	{
		dist xydist THISACTOR bottarget
		ifl xydist 3072
		{
			set angvel initsprite
			set navpoint -1
			state movewesbot
			cactor WESBOT
			ifai AIWESBOTRUN nullop else ai AIWESBOTRUN
			break
		}
	}
	
	
	ifai AIWESBOTJUMP nullop else
	state movewesbot
	
	ifai AIWESBOTSTAND ai AIWESBOTRUN
	
	ifai AIWESBOTCROUCH ai AIWESBOTCRAWL
	
	ifai AIWESBOTCRAWL
	ife FEMKILLCOUNT 0
	{
		ifgapzl 48 nullop else
		{
			cactor WESBOT
			ai AIWESBOTRUN
		}
	}
	
	ifn mtype 0 { set navpoint -1 ifn gametype -1 state wesbotstartwander }
	ifl navpoint 0 ife crumbwait 0 state wesbotstartwander
	
	ife gametype SURVIVAL
	iffloordistl 8 ifonwater
	ifl navpoint 0
		setsprite THISACTOR loadx[LEVEL] loady[LEVEL] loadz[LEVEL]

ends

defstate wesbotcode

	
	ifai 0
	{
		cactor WESBOT
		set SPRITELOTAG 128
		set botclip 3
		set thirdbaseval 2
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		cstator 257
		sizeat 22 20
		ifvarand monstflags 131072 
		{ 
			seta[].extra bluebaseval
			clipdist 32 spritepal 17 
		} 
		else
		{
			clipdist 32
			seta[].pal wespal
		}
		geta[].ang lastang
		
		orvar monstflags YES
		ifvarand monstflags 131072 
			ai AIWESBOTSTAND
		else
		{
			ifl shellyhp 10 set shellyhp 10
			ife gametype SURVIVAL { strength 200 set shellyhp 200 }
			else ife attmode YES { set shellyhp pdamage seta[].extra pdamage }
			ifspawnedby APLAYER { seta[].extra shellyhp orvar startmode 4 }
			ifcansee ifpdistl 4096
			{
				set monstatus 30
				set team 1
				ai AIWESBOTSTAND
				ifg player[].player_par 60
				ifl initflags 32766
					sound WESGOTYOURBACK
				
			}
			else { seta[].extra shellyhp seta[].htextra -1 break }
		}
		ifge initflags 32766 ai AIWESBOTSTAND else
		set initflags -1
	}
	
	seta[].mdflags 16
	ifge initflags 32766
	{
		state charidlecode
		break
	}
	
	
	ifg stun 0 sub stun 1
	
	ife pchar 2 ife myshelly THISACTOR
	ifn monstatus 60
	{
		set monstatus 60
		ifstrength 10 strength 10
	}
	

	ife monstatus 60
	{
		set monstatus 30
		ifvarand startmode 1
		{
			cactor DUKEBOT
			ai AIDUKESTAND
			sizeat 42 36
			cstat 257
			set navpoint -1
			set float -20
			set botclip 3
			set thirdbaseval 2
			seta[].pal dukepal
			break
		}
		else
		ifvarand startmode 2
		{
			cactor SHELLY
			ai AISHELLYSTAND
			sizeat 23 21
			cstat 257
			set navpoint -1
			set botclip 30
			set thirdbaseval 3
			set float -30
			seta[].pal shellypal
			break
		}
		else
		ifvarand startmode 8
		{
			cactor MANDOFETT
			ai AIMANDOWAIT
			sizeat 26 24
			clipdist 40
			cstator 257
			set float 0
			set initflags -1
			set navpoint -1
			set starty 0
			set botclip 30
			set mtype 0 // functions as his kickback_pic !
			set thirdbaseval 3
			ife mandopal 21 seta[].pal 10 else
			seta[].pal mandopal
			break
		}
		else
		ifvarand startmode 16
		{
			cactor DEABOT
			ai AIDEABOTSTAND
			sizeat 23 21
			cstat 257
			set navpoint -1
			set botclip 30
			set thirdbaseval 3
			set float -30
			seta[].pal novapal
			break
		}
	}

	ifai AIWESBOTSWIM seta[].zvel 0 else
	fall

	ife spawnprotect 0
	ife monstatus 30 ifspritepal 33 seta[].pal wespal
	
	ife team 1
	{
		ifcansee set seeplayer YES else set seeplayer NO
		ifn monstatus 2 ifge sprite[].extra 30 set shellyinmap 10
		ife startmode -1 // leftover from previous level
		{
			set team -1
			set myshelly -1
			killit
			break
		}
		set myshelly THISACTOR
		orvar charsel 4
		orvar startmode 4
		ife monstatus 1 set monstatus 30
		set mlevel plevel
		set inithp player[].max_player_health
		state checksectvisited
		state wesbotcheckorders
		ifn myshelly -1 ife sprite[].htextra -1 ifg sprite[].extra 0 geta[].extra shellyhp
	}
	else state monsterai
	
	ifge botleach 50
	{
		sub botleach 50
		ifl sprite[].extra inithp addstrength 1
	}
	
	// ifn monstatus 2 ife mysignpost -1 state spawnmysignpost
	
	ifai AIWESBOTSHRUNK { state wesbotshrunkstate break }
	ifai AIWESBOTFROZEN { state wesbotfrozenstate break }
	ifai AIWESBOTDIE
	{
		strength 0
		ifaction AWESBOTDIE
		{
			ifinwater fall // falls too slow under water otherwise
			ifactioncount 6
			{
				action AWESBOTDEAD
				ifhitweapon { }
			}
		}
		ifaction AWESBOTDEAD
		{
			// iffloordistl 4
			ife team 0 
			{
				ifwasweapon RADIUSEXPLOSION seta[].htpicnum RPG
				ifhitweapon
				ifwasweapon RPG
				{
				  sound SQUISHED
				  state standard_jibs
				  killit
				}
				break
			}
			ifactioncount 4
			{
				set B NO
				ifl gametype 1 set B YES
				ife gametype SURVIVAL set B YES
				ife B YES
				{
					cactor WESBOTCROUCH
					ifrnd 128 sound WESDIEHERE else sound WESIMMAD
					ai AIWESBOTDOWN
					quote 1193
					set countvarc 0
					strength 5
				}
				else ifcount 150
				{
					state resetwesbotmultiplayer
					break
				}
			}

		}
		break
	}
	ifai AIWESBOTDOWN
	{
		set monstatus 2
		set shellysquish NO
		ifg gametype 0
		ifn gametype SURVIVAL
		{
			ifcount 120
			state resetwesbotmultiplayer
			break
		}
		set burning 0
		set wescharge 0
		ifhitweapon 
		{
			ifstrength 5 strength 5
		}
		ife ikicked -2 // healed by medic
		{
			addstrength 4 // 5
			ifspritepal 33 getlastpal else spritepal 33
		}
		else
		{
			set TMP_A player[].player_par
			sub TMP_A ikicked
			
			set temp NO
			
			ife pdown NO
			ifpdistl 1280
			ifp pfacing
			ifp palive
			ife seeplayer YES
			ifhitspace
			set temp YES
			else
			ifge TMP_A 300 state checkautorevive
			
			ife temp YES
			{
				ifvarand perks 1024
				ifge TMP_A 300 nullop else
				quote 136
				
				set temp NO
				ifhitspace set temp YES
				
				ifvarand perks 1024 ifge TMP_A 300 set temp YES
				
				ife temp YES
				{
					resetcount
					ifphealthl 5
					{
						quote 130
						ifspritepal 33 getlastpal
						
						cactor WESBOTCROUCH
						ai AIWESBOTCROUCH
						
						cstat 257
						ife player[].ftq 130 setp[].fta 0
						ife player[].ftq 136 setp[].fta 0
						
						// ifrnd 84 sound WESBOT_REVIVE1 else
						// ifrnd 128 sound WESBOT_REVIVE2 else
						// sound WESBOT_REVIVE3
						sound WESNICE
						ifstrength 10 strength 10
						set monstatus 30
						set ikicked 0
						seta[].pal wespal
						set countvarc 0
						set shotpitch 0
						set mtype 0
						set bottarget -1
						seta[].htextra -1 set burning 0 set bleeding 0
						seta[].htowner THISACTOR
						break
						
					}
					else
					{
						ifstrength 10
						ife pchar 1
						{
							rand temp 2
							ife temp 0 globalsound B_TRYNOTODIE
							ife temp 1 globalsound B_HANGINTHERE
							ife temp 2 globalsound B_TRUSTME
						}
						else ife pchar 0 globalsound DUKE_HEAL2
						addstrength 5
						set countvarb 0
						ifspritepal 33 getlastpal else spritepal 33
						ifg player[].firstaid_amount 0
						{
							getp[].firstaid_amount temp
							sub temp 1
							setp[].firstaid_amount temp
						}
						else addphealth -1
					}
				}
				else ifspritepal 33 getlastpal
			}
			else 
			{
				ifcount 360
				ifp palive
				{
					ife player[].fta 0 quote 1194
					ifhitspace add countvarc 1
					else set countvarc 0
					ifg countvarc 25
					{
						spawn TRANSPORTERSTAR
						globalsound TELEPORTER
						setsprite THISACTOR player[].posx player[].posy player[].posz
						ifn player[].cursectnum -1 changespritesect THISACTOR player[].cursectnum
						spawn TRANSPORTERSTAR
						resetcount
						set countvarc 0
						ife player[].ftq 1194 setp[].fta 0
					}
				}
				
				ifspritepal 33 getlastpal
				ife player[].ftq 130 setp[].fta 0
				ife player[].ftq 136 setp[].fta 0
			}
		}
		
		set temp NO
		ifge sprite[].extra player[].max_player_health set temp YES
		ife pdown YES ifge sprite[].extra 100 set temp YES
		ife temp YES
		{
			set shellyhp sprite[].extra
			
			ife gametype SURVIVAL { strength 200 set shellyhp 200 }

			cactor WESBOTCROUCH
			ai AIWESBOTCROUCH
			// ifrnd 84 sound WESBOT_REVIVE1 else
			// ifrnd 128 sound WESBOT_REVIVE2 else
			// sound WESBOT_REVIVE3
			sound WESNICE
			set ikicked 0
			set shotpitch 0
			set mtype 0
			set bottarget -1
			seta[].htextra -1 set burning 0 set bleeding 0
			seta[].htowner THISACTOR
			set monstatus 30
			cstat 257
			ife player[].ftq 130 setp[].fta 0
			ife player[].ftq 136 setp[].fta 0
			ifspritepal 33 getlastpal
			set spawnprotect 150
		}
		break
	}
	
	ife player[].crack_time 300
	ife seeplayer YES
	ife team 1
	{
		ifrnd 128
		sound WESDINNER
		else sound WESEVERYBODY
	}
	
	findnearspritez LASERLINE 1024 10240 spriteid
	ifn spriteid -1 
	{
		geta[spriteid].x x2
		geta[spriteid].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvar x2 y2
		getincangle temp angvar sprite[].ang
		abs temp
		ifl temp 512
		set FEMKILLCOUNT 8
	}
	
	ifg FEMKILLCOUNT 0 sub FEMKILLCOUNT 1
	
	ifg dodgetime 0 sub dodgetime 1
	
	ife team 1
	{
		ife gametype -1 set wescharge 0 else
		add wescharge 1
		ife gametype 0
		ifge wescharge FULLBOTCHARGE
		ifl damagecount BOOSTMIN sub wescharge 1
	}
	
	ife myspawner -1
	ife dodgetime 0
	{
		ifp pfacing ife seeplayer YES 
		{
			ifpdistl 1408 ifg player[].curr_weapon KNEE_WEAPON ifvarand bits 4
			set myspawner player[].i
			
			ifn team actorvar[player[].i].team
			ifp pfacing
			ifpdistl 8192
			ife bottarget player[].i
			ifn player[].kickback_pic 0
			ifl player[].curr_weapon RPG_WEAPON
				set myspawner player[].i
		}
	}
	ifai AIWESBOTKNEE set myspawner -1
	ifai AIWESBOTKICK set myspawner -1
	ifai AIWESBOTSWORD set myspawner -1
	ifai AIWESBOTFLINTCH set myspawner -1
	ifaction AWESBOTFALL set myspawner -1
	ifaction AWESBOTGLIDE set myspawner -1
	ifmove BOTNOMOVE set myspawner -1
	ifn myspawner -1 state wesbotstartdodge
	
	state wesbothitscan
	seta[].ang lastang
	ifl gametype 1
	{
		state wesbotfindplayercrumb
		getp[].cursectnum mysector
		ifn mysector -1
		ifn mysector sprite[].sectnum
		ifn monstatus 70
		ife switchmode 0
		{
			ife seeplayer NO
			ifg sector[mysector].lotag 2
			ifpdistg 6144
			ife bottarget -1
				setsprite THISACTOR player[].posx player[].posy sprite[player[].i].z
		}
	}
	else
	{
		set navmode 2
		ife dodgetime 0
		state navigationsmart
	}
	state spawnprotectcode
	ifg padmove 0 state jumppadmove
	add float 1
	ifg float 100 set float 0 // { set float 0 set thirdbaseval 1 set botclip 16 }
	
	ife team 1
	ife pdown YES ifpdistl 1024 set bottarget -1 else
	state targetsearch
	
	ifn bottarget -1 
	{
		ife team actorvar[bottarget].team set bottarget -1 else
		dist targetdist THISACTOR bottarget
	}
	ifg bot_taunt 0 sub bot_taunt 1
	ifg ikicked 0 { sub ikicked 1 ifn bottarget -1 ifn actorvar[bottarget].shrunken 0 ifg ikicked 10 set ikicked 10 }
	ifai AIWESBOTFLINTCH set bottarget -1
	
	ifn bottarget -1
	{
		dist xydist THISACTOR bottarget
		
		ife bot_taunt 0 
		{ 
			
			set bot_taunt 2000 
		}
		geta[bottarget].x x2
		geta[bottarget].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle initsprite x2 y2
		
		ifl gametype 1
		ifpdistl FOLLOWRANGE // MAXRANGE
			ifn thirdbaseval 4
				ife initflags bottarget
			{
				set angvel initsprite
				set navpoint -1
				state avoidboss
			}
				
		
		
		ifaction AWESBLOCK nullop else
		ifaction AWESSHOTTY1 nullop else
		ifaction AWESSHOTTY2 nullop else
		ifai AIWESBOTKICK nullop else
		ifai AIWESBOTSWIM nullop else
		ifai AIWESBOTSWORD nullop else
		ifai AIWESBOTKNEE nullop else
		ifgapzl 48 nullop else
		iffloordistl 4
		ifl xydist 1560
		ife ikicked 0
		// ife actorvar[bottarget].stun 0
		{
			cactor WESBOT
			ai AIWESBOTKICK
		}
		
		ifai AIWESBOTKICK nullop else
		ifai AIWESBOTSWORD nullop else
		ifai AIWESBOTBLOCK nullop else
		ifai AIWESBOTKNEE nullop else
		{
			ifle botclip 0 ifge float 0 state wesbotweap
			
			ife pdown YES
			ife dodgetime 0
			ifpdistl 4096
			ife seeplayer YES
			{
				getp[].posx x2
				getp[].posy y2
				sub x2 sprite[].x
				sub y2 sprite[].y
				getangle angvel x2 y2
			}
		}
		
		
		// thirdbaseval which weapon selected
		// botclip how many left in clip
		// float time between shots
		getincangle temp lastang initsprite
		ifl temp 0 mul temp -1
		ifai AIWESBOTKICK nullop else
		ifai AIWESBOTSWORD nullop else
		ifai AIWESBOTBLOCK nullop else
		ifai AIWESBOTKNEE nullop else
		ifl temp 64
		{
			switch thirdbaseval
			case 1 // pistols
				ifai AIWESBOTSTAND
					action AWESBOTSTANDGUN

				ifge plevel 5 set TMP_A 5
				else set TMP_A 7
				
				ifge float TMP_A
				{
					set float 0
					sub botclip 1

					spawn SHELL

					sound REVOLVER_FIRE
					
					state hitscan_targetprep
					state randzshoot
					zshoot tempb DEAGLEBULLET
					
					ifle botclip 0 set float -60
				}
			break
			
			case 2 // shotgun
				ifn monstatus 2
				{
					ifactor WESBOTCROUCH cactor WESBOT
					
					ifaction AWESSHOTTY2
					{
						ifg float 4 action AWESSHOTTY1
					}
					else
					ifinwater nullop else
					ifaction AWESSHOTTY1 nullop else
					action AWESSHOTTY1
					
					ifmove BOTNOMOVE nullop else move BOTNOMOVE geth
				}
				else set float 0
						
				ife float 10
				{
					sound NEWSHOTCOCK
					espawn SHOTGUNSHELL
				}
				ife float 25
				{
					action AWESSHOTTY2
					
					ifl xydist 8192
					ifn bottarget -1
					ife actorvar[bottarget].stun  0
					ifrnd 192
					{
						sound SHOOTSTAKE sound SHOOTSTAKE
						set xvel 768
						state rpg_targetprep
						zshoot zdist STAKEPROJ
					}
					else
					{
						sound NEWSHOTFIRE
						state hitscan_targetprep
						state randzshoot
						zshoot tempb SHOTGUN
						state randzshoot
						zshoot tempb SHOTGUN
						state randzshoot
						zshoot tempb SHOTGUN
						state randzshoot
						zshoot tempb SHOTGUN
						state hitscan_targetprep
						state randzshoot
						zshoot tempb SHOTGUN
						state randzshoot
						zshoot tempb SHOTGUN
						state randzshoot
						zshoot tempb SHOTGUN
						ifg plevel 3
						{
							state randzshoot
							zshoot tempb SHOTGUN
							state randzshoot
							zshoot tempb SHOTGUN
						}
					}
				}
				ifge float 29 { set float 0 sub botclip 1 }
			break
			case 4 // swords
				
			break
			case 5 // icebombs or rocket
				ifge float 15
				{
					sub botclip 1
					set float -30
					
					set temp NO
					ifge plevel 6
					ifg xydist 4096
					ifpdistg 4096
					{ 
						ifrnd 64 set temp YES
						else ifge xydist 10240 set temp YES
					}
					ife temp YES
					{
						set xvel 644
						state rpg_targetprep
						ezshoot zdist RPG
						seta[RETURN].xrepeat 48
						seta[RETURN].yrepeat 48
						seta[RETURN].pal 11
						globalsound NUKEFIRE
						seta[RETURN].extra 500
						seta[RETURN].owner player[].i
						setav[RETURN].mtype TARGETLOCK
						setav[RETURN].bottarget bottarget
					}
					else
					{
						geta[].z savz
						sub savz 6144
						seta[].z savz
						
						espawn WESBOMB
						setav[RETURN].team 1
						rand B 512
						sub B 256
						add B -2048
						setav[RETURN].mtype B
						seta[RETURN].zvel B
						setav[RETURN].dodgetime 40
						seta[RETURN].owner player[].i
					  
						espawn WESBOMB
						setav[RETURN].team 1
						set angle2 sprite.ang
						sub angle2 36
						seta[RETURN].ang angle2
						setav[RETURN].team 1
						rand B 512
						sub B 256
						add B -2048
						setav[RETURN].mtype B
						seta[RETURN].zvel B
						setav[RETURN].dodgetime 40
						seta[RETURN].owner player[].i
						
						espawn WESBOMB
						setav[RETURN].team 1
						set angle2 sprite.ang
						add angle2 36
						seta[RETURN].ang angle2
						setav[RETURN].team 1
						rand B 512
						sub B 256
						add B -2048
						setav[RETURN].mtype B
						seta[RETURN].zvel B
						setav[RETURN].dodgetime 40
						seta[RETURN].owner player[].i
						
						add savz 6144
						seta[].z savz
					}
				}
			break
			case 6 // blood gun
				ifge float 6
				{
					set x2 sprite[].x
					add x2 512
					rotatepoint sprite[].x sprite[].y x2 sprite[].y sprite[].ang savx savy
							
					sound RPG_SPLIT
					sub botclip 1
					set float 0
					ifle botclip 0 set float -60
					set xvel 844
					state rpg_targetprep
					ezshoot zdist BLOODBULLET
					seta[RETURN].x savx, seta[RETURN].y savy
					set xvel 844
					state rpg_targetprep
					ezshoot zdist BLOODBULLET
					seta[RETURN].x savx, seta[RETURN].y savy
					geta[RETURN].ang angvar
					sub angvar 32
					seta[RETURN].ang angvar
					set xvel 844
					state rpg_targetprep
					ezshoot zdist BLOODBULLET
					seta[RETURN].x savx, seta[RETURN].y savy
					geta[RETURN].ang angvar
					add angvar 32
					seta[RETURN].ang angvar
				}	
			break
			endswitch
		}
	
	}
	else
	{
		set initsprite angvel
		ifai AIWESBOTSTAND ifaction AWESBOTSTANDGUN action AWESBOTSTAND
		ifaction AWESSHOTTY1 ai AIWESBOTSTAND
		ifaction AWESSHOTTY2 ai AIWESBOTSTAND
		ifai AIWESBOTFLINTCH nullop else
		ifai AIWESBOTKICK nullop else
		ifai AIWESBOTKNEE nullop else
		ifmove BOTNOMOVE move STOPPED geth
	}
	
	ife dodgetime 0
	ifcount 2
	ifn sprite[].ang initsprite
	{
		geta[].ang angvar
		getincangle temp angvar initsprite
		set B MAXSHELLYTURN
		mul B -1
		ifg temp B ifl temp MAXSHELLYTURN
			seta[].ang initsprite
		else
		{
			ifl temp 0 sub angvar MAXSHELLYTURN
			else add angvar MAXSHELLYTURN
			seta[].ang angvar
		}
		set lastang sprite[].ang
	}
	
	ifaction AWESBOTJUMP1
	{
		set countvar 0
		ifactioncount 2
		{
			action AWESBOTJUMP2
			move WESJUMPVELS geth
		}
	}
	ifaction AWESBOTJUMP2
	{
			
		iffloordistl 4
		{
			cactor WESBOTCROUCH
			ai AIWESBOTCROUCH
			set countvar 11
			ifonwater 
			{
				cactor WESBOT
				ai AIWESBOTSTAND
				sound LANDWATER
			}
			else
			ifg FEMFALLDMG 40
			sound LANDNORMAL
		}
	}
	
	ifgapzl 48 
	{
		// ifgapzl 24 nullop else
		ifaction AWESBOTRUN 
		{
			cactor WESBOTCROUCH 
			ai AIWESBOTCRAWL
		}
		else
		ifai AIWESBOTSTAND
		{
			cactor WESBOTCROUCH
			ai AIWESBOTCROUCH
		}
	}
	else
	ifaction AWESBOTCRAWL
	ife FEMKILLCOUNT 0
	{
		cactor WESBOT
		ai AIWESBOTRUN
	}
	
	state botblocking
	state wesbotfallhandling
	
	ifg dodgetime 0 state movewesbot else
	ifai AIWESBOTWANDER state wesbotwander
	else ifai AIWESBOTKICK state wesbotkickcode
	else ifai AIWESBOTSWORD state wesbotswordcode
	else ifai AIWESBOTBLOCK state wesblockstate
	else ifai AIWESBOTKNEE state wesbotkneecode
	else ifai AIWESBOTFLINTCH state wesbotflintch
	else ifai AIWESBOTSWIM state wesbotswimstate
	else 
	{
		ife pdown YES state wesbotfollowplayer else
		ifg gametype 0 state wesbotmultiplayer
		else state wesbotfollowplayer
	}
	
	ifg FEMKILLCOUNT 0 ifai AIWESBOTRUN
	{
		cactor WESBOTCROUCH 
		ife seeplayer YES ifpdistl 8192 ai AIWESBOTCROUCH else
		ai AIWESBOTCRAWL
	}
	
	getplayer[].cursectnum mysector
		ifn mysector -1
		gets[mysector].lotag tempc
		else set tempc 0
			
		
	geta[].htflags temp orvar temp 134234112 seta[].htflags temp
	
	ifinwater { ifai AIWESBOTSWIM nullop else { cactor WESBOT ai AIWESBOTSWIM } }
	else ifai AIWESBOTSWIM
	{
		ifgapzl 48 { cactor WESBOTCROUCH ai AIWESBOTCROUCH }
		else
		ai AIWESBOTJUMP
	}
	// ifsquished
	// {
		// seta[].htextra 200
		// seta[].htpicnum RPG
		// seta[].htowner THISACTOR
		// sound SQUISHED
		// set shellysquish YES
		// quote 164
	// }
	// else 
	
	ife team 1
	{
		ifn monstatus 2
		ifgapzl 24 set shellysquish YES
		else set shellysquish NO
		ifg burning 0 state imonfire
		else ifl burning 0 add burning 1
		state predamage
	}
	
	ifg sprite[].htextra 0
	{
		set blueiteration 90
		ife sprite[].htextra 1
		ife sprite[].htpicnum SHOTSPARK1
		ife sprite[].extra 0 // hardcoded falling damage
		{
			seta[].htextra -1
			seta[].extra shellyhp
		}
		
		
		geta[].htowner spriteid
		geta[].htextra temp
		ife gametype DM
		{
			ife team actorvar[spriteid].team set temp -1
		}
		else
		ifn sprite[].htpicnum BURNING
		shiftr temp 1
		ife team 1 ife spriteid player[].i shiftr temp 1
		ifl temp 1 set temp 1
		seta[].htextra temp
	}
	else ifg blueiteration 0 sub blueiteration 1
	
	ifai AIWESBOTBLOCK nullop else
	ifai AIWESBOTFLINTCH nullop else
	ifai AIWESBOTKICK nullop else
	ifai AIWESBOTKNEE nullop else
	ifg sprite[].htextra 0
	ifl targetdist 1560 ifrnd 96
	ifn bottarget -1
	ifn sprite[].htpicnum RPG
	{
		set spriteid bottarget
		state facesprite
		cactor WESBOT
		ai AIWESBOTBLOCK
		sound BLADEHITMET
		seta[].htextra -1
	}
	
	ifhitweapon
	{
		ife team 1 ifn myshelly -1 geta[].extra shellyhp
		
		ife team 1
		ifwasweapon SHRINKSPARK
		{	
		  cactor WESBOT
		  sound ACTOR_SHRINKING
		  ai AIWESBOTSHRUNK
		  // set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		ifwasweapon FREEZEBLAST nullop else state random_wall_jibs
		ifdead
		{
			cactor WESBOT
			getp[].player_par ikicked
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor WESBOT
			  ai AIWESBOTFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ife team 1 set monstatus 2
			else { set droptile 0 state enemy_death }
			ifg gametype 0 
			ife team 1
			{ 
				ife gametype DM set tempb 1 else set tempb 5
				ife team 0 add bluescore tempb else ife team 1 add redscore tempb 
				state playerscorecheck
			}
			
			  ifrnd 84
                  sound WESDEATH1
			  else
				ifrnd 128
				  sound WESDEATH2
			  else
				  sound WESDEATH3
         
			guts JIBS6 4
			ife gametype 0 money 6
			cstat 0
			strength 0
			ai AIWESBOTDIE
		}
		else 
		{
			state wesbotpainsounds
			ifai AIWESBOTKICK set stun 0
			ifai AIWESBOTBLOCK set stun 0
			ifg stun 0
			{
				ifl targetdist 1560 ifrnd 128
				ifn sprite[].htpicnum RPG
				{
					cactor WESBOT
					ai AIWESBOTBLOCK
					break
				}
				set TMP_B plevel
				mul TMP_B 8
				rand TMP_A 100
				ifl TMP_A TMP_B
				set stun 0
				else
				{
					cactor WESBOT
					ai AIWESBOTFLINTCH
				}
			}
			
		}
	}
	
ends

appendevent	EVENT_KILLIT

switch sprite[].picnum
	case WESBOT
	case WESBOTCROUCH
	ife team 1 
	{
	hitradius 768 2 2 2 2
	cactor WESBOTCROUCH
    ai AIWESBOTDOWN
	set monstatus 2
	getp[].player_par ikicked
    quote 1193
    set countvarc 0
    strength 5
	set RETURN 1
	cstat 257
	sizeat 42 36
	}
	break
endswitch

endevent

useractor enemy WESBOT 150 state wesbotcode enda
useractor enemy WESBOTCROUCH 150 state wesbotcode enda
