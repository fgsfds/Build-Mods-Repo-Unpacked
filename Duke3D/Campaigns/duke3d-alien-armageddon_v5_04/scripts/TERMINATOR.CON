// DUKINATOR
// sprites by sebabdukeboss20, code by Dan Gaskill

// define TERMINATOR 6144
// define TERMINATORSTAYPUT 6145
// define TERMINATORDUCKING 6184
// define TERMINATORCRAWLING 6194
// define TERMINATORSTRENGTH 200
// define TERMINATORCRAWLSTRENGTH 100


gamevar termtime 0 0

move TERMLEGSTOP
state termlegcode

ifmove 0
{
	move TERMLEGSTOP
	ifactor TERMLEGPART1GROUND cstat 8
	ifrnd 128 cstator 4
	ifspritepal 57 sizeat 34 34 else
	sizeat 26 26
	ife gametype 0 insertspriteq
}
fall
ifg gametype 0
{
	ifcount 1800 killit else
	ifcount 90 ifl framerate 50 killit
}

ends

useractor notenemy TERMLEGPART1GROUND 0 state termlegcode enda
useractor notenemy TERMLEGPART2GROUND 0 state termlegcode enda
useractor notenemy TERMLEGPART3GROUND 0 state termlegcode enda

useractor notenemy TERMHEAD 0

ifmove 0
{
	move TERMLEGSTOP
	ifspritepal 57 sizeat 48 48 else
	sizeat 36 36
	ifrnd 128 cstator 4
	ife gametype 0 insertspriteq
}

ifg gametype 0
{
	ifcount 1800 killit else
	ifcount 90 ifl framerate 50 killit
}

ifcount 20 nullop else
ifcount 19 
ife myspawner player[].i
{
	ife termtime 0
	{
		ife pchar 1 globalsound B_TERMINATED
		else ife pchar 0
		{
			rand temp 3
			ife temp 0 globalsound DUKE_TERMIN1
			ife temp 1 globalsound DUKE_TERMIN2
			ife temp 2 globalsound DUKE_TERMIN3
			ife temp 3 globalsound DUKE_TERMIN4
		}
		set termtime player[].player_par
	}
	else
	{
		getp[].player_par temp
		
		sub temp termtime
		ifl temp 0 set temp 5000
		ifg temp 1800
		{
			ife pchar 1 globalsound B_TERMINATED
			else ife pchar 0
			{
				rand temp 3
				ife temp 0 globalsound DUKE_TERMIN1
				ife temp 1 globalsound DUKE_TERMIN2
				ife temp 2 globalsound DUKE_TERMIN3
				ife temp 3 globalsound DUKE_TERMIN4
			}
			set termtime player[].player_par
		}
	}
}
fall

enda

// action ATERMINATOROFF			0  1  5  1  10
action ATERMINATORON			5  1  5  1  10
action ATERMINATORWALK			10 4  5  1  20
action ATERMINATORRUN			10 4  5  1  12
action ATERMINATORSHOOT			30 2  5  1  6
action ATERMINATORDUCK1			0 1  5  1  10 // 40
action ATERMINATORDUCKSHOOT		0 2  5  1  6
action ATERMINATORCRAWL			0 4  5  1  20 // 50
action ATERMINATORCRAWLSHOOT	20 2  5  1  6
action ATERMINATORHIT			80 1  1  1  10
action ATERMINATORDIE			80 3  1  1  12
action ATERMINATORCRAWLDIE		37 2  1  1  12 // 87

action ATERMINATORGRAB1			14486  1  5  1  20
action ATERMINATORGRAB2			14491  1  5  1  20

move TERMINATORSTOP
move TERMINATORWALKVELS 71
move TERMINATORRUNVELS 112
move TERMINATORCRAWLVELS 38
move TERMCRAWLVELS 32

ai AITERMOFF ATERMINATOROFF TERMINATORSTOP geth
ai AITERMWAIT ATERMINATORON TERMINATORSTOP geth
ai AITERMSEEK ATERMINATORWALK TERMINATORWALKVELS seekplayer
ai AITERMSHOOT ATERMINATORSHOOT TERMINATORSTOP geth
ai AITERMDUCK ATERMINATORDUCK1 TERMINATORSTOP geth
ai AITERMCRAWL ATERMINATORCRAWL TERMINATORCRAWLVELS faceplayerslow
ai AITERMHIT ATERMINATORHIT TERMINATORSTOP faceplayer
ai AITERMFROZEN ATERMINATOROFF TERMINATORSTOP geth
ai AITERMSHRUNK ATERMINATORWALK TERMCRAWLVELS fleeenemy
ai AITERMDYING ATERMINATORDIE TERMINATORSTOP geth
ai AITERMCRAWLDYING ATERMINATORCRAWLDIE TERMINATORSTOP geth
ai AITERMGRAB ATERMINATORGRAB1 STOPPEDFORPLAYER geth
ai AITERMRUN ATERMINATORRUN TERMINATORRUNVELS geth

defstate spawnpartset

	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	seta[RETURN].pal sprite[].pal

ends

defstate termspawnlegparts

	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART1
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	
	ifrnd 128
	{
	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART1
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	}
	
	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART2
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	
	ifrnd 128
	{
	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART2
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	}
	
	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART3
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	
	ifrnd 128
	{
	rand z 3072
	mul z -1
	ezshoot z TERMLEGPART3
	randvar temp  2047
	setactor[RETURN].ang temp 
	randvar temp  128 add temp 128
	setactor[RETURN].xvel temp
	state spawnpartset
	}

ends

state termtaunt

ifn shrunken 0 break

ifsound TERMBALLS nullop else
ifsound TERMGUTS nullop else
ifsound	TERMTAUNT1 nullop else
ifsound	TERMTAUNT2 nullop else
ifsound	TERMTAUNT3 nullop else
{
	rand temp 4
	switch temp
	case 0 sound TERMBALLS break
	case 1 sound TERMGUTS break
	case 2 sound TERMTAUNT1 break
	case 3 sound TERMTAUNT2 break
	case 4 sound TERMTAUNT3 break
	endswitch
}

ends

state termfrozenstate

ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

 ifcount THAWTIME
    {
      ai AITERMWAIT
	  // sound TERMON
      getlastpal
	  strength 0
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  ife sprite[].htowner player[].i set myspawner player[].i
	  lotsofglass 30
      
      sound GLASS_BREAKING
		geta[].z z
		sub z 4096
		seta[].z z
		state spawnpartset
		add z 4096
		seta[].z z
		state termspawnlegparts
		
		state enemy_death
		ifrnd SPAWNAMMOODDS
		{
			ifrnd 84
			ife gametype 0
			spawn ATOMICHEALTH
			else
			ifrnd 128
			spawn PLASMASPRITE
		}
		
		espawn MINEEXP
		sound ROBOT_EXPLODE
		seta[RETURN].xrepeat 32
		seta[RETURN].yrepeat 32
		seta[RETURN].pal 1
		set countvarb 0
		whilevarn countvarb 4
		{
		   rand z 3072
		   add z 2048
		   mul z -1
		   ezshoot z CARXPLODE
		   randvar temp  2047
		   setactor[RETURN].ang temp 
		   seta[RETURN].pal sprite[].pal
		   randvar temp  192 add temp 128
		   setactor[RETURN].xvel temp 
		   add countvarb 1
		}
		debris SCRAP1 3
		debris SCRAP2 3
		debris SCRAP6 3
		espawn TERMHEAD
		setav[RETURN].myspawner myspawner
		seta[RETURN].pal sprite[].pal
		killit

    }
	else ifl sprite[].extra -10
	{
		seta[].htextra 10
		seta[].htpicnum RPG
		seta[].htowner player[].i
	}
	
	
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
ends

state termshrunkstate


  ifcount SHRUNKDONECOUNT
    ai AITERMSEEK
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 57 sizeto 36 32 else
      sizeto 28 24 // 42 36

	}
  else
    state genericshrunkcode

ends

state termdyingstate

	ife padang 1337 
	{ 
		ifrnd SPAWNAMMOODDS
		{
			ifrnd 84
			ife gametype 0
			spawn ATOMICHEALTH
			else
			ifrnd 128
			spawn PLASMASPRITE
		}
		state spawnoutline 
		killit 
		break 
	}

	seta[].shade -127
	seta[].htextra -1
	ifhitweapon { }
	ifaction ATERMINATORDIE
		ifactioncount 6
	{
		geta[].z z
		sub z 4096
		seta[].z z
		ife gametype 0
		{
			espawn TERMTORSO
			ifrnd 96
				setav[RETURN].countvarc YES
			state spawnpartset
			add z 4096
			seta[].z z
			state termspawnlegparts
			// getp[].max_actors_killed temp
			// sub temp 1
			// setp[].max_actors_killed temp
			killit
		}
		else
		{
			add z 4096
			seta[].z z
			state termspawnlegparts
			// state enemy_death
			ifrnd SPAWNAMMOODDS
			{
				ifrnd 84
				ife gametype 0
				spawn ATOMICHEALTH
				else
				ifrnd 128
				spawn PLASMASPRITE
			}
			espawn MINEEXP
			sound ROBOT_EXPLODE
			seta[RETURN].xrepeat 32
			seta[RETURN].yrepeat 32
			set countvarb 0
			whilevarn countvarb 4
			{
			   rand z 3072
			   add z 2048
			   mul z -1
			   ezshoot z CARXPLODE
			   randvar temp  2047
			   setactor[RETURN].ang temp 
			   randvar temp  192 add temp 128
			   setactor[RETURN].xvel temp 
			   add countvarb 1
			}
			debris SCRAP1 3
			debris SCRAP2 3
			debris SCRAP6 3
			espawn TERMHEAD
			seta[RETURN].pal sprite[].pal
			setav[RETURN].myspawner myspawner
			killit
		}
	}

ends

state squishterm

	setp[].sound_pitch SHRUNKPITCH
	spawn EXPLOSION2
	debris SCRAP1 3
	debris SCRAP2 3
	sound PIPEBOMB_EXPLODE
	addkills 1
	ifrnd SPAWNAMMOODDS
	{
		ifrnd 84
		ife gametype 0
		spawn ATOMICHEALTH
		else
		ifrnd 128
		spawn PLASMASPRITE
	}
	state enemy_death
	setp[].sound_pitch 0
	ife pchar 1 globalsound B_TERMINATED
	else ife pchar 0
	{
		rand temp 3
		ife temp 0 globalsound DUKE_TERMIN1
		ife temp 1 globalsound DUKE_TERMIN2
		ife temp 2 globalsound DUKE_TERMIN3
		ife temp 3 globalsound DUKE_TERMIN4
	}
	killit
ends

defstate termgrabcheck

	ifn zombieonplayer -1 break
	ifn sprite[].htextra -1 break
	ife bottarget player[].i
	{
		ifn zombieonplayer -1 break
		ifn zombiecount 0 break
		ifn slidekick 0 break
		ifp pfacing ifn sidekick 0 break
		ifn shrunken 0 break
	}
	
	ife team 1 ife bottarget player[].i { set bottarget -1 break }
	
	ife bottarget myshelly ifrnd 224 break

	ifn bottarget -1
	ifl tiledata[sprite[bottarget].picnum].ysize 180
	ife seemytarget YES
	ife shootmytarget YES
	ife targetalive YES
	ifl targetdist 1024
	{
		cactor TERMINATOR
		ai AITERMGRAB
		sound NEWDOOR5
		set myspawner bottarget
		set spriteid bottarget
		state facesprite
		ife bottarget player[].i
		{
			sound FORCECHOKE
			set zombieonplayer THISACTOR
			set zombietile TERMGRABTILE
			geta[].x x2
			geta[].y y2
			sub x2 player[].posx
			sub y2 player[].posy
			getangle angvar x2 y2
			setp[].ang angvar
			setp[].oang angvar
			ifn sector[].lotag 2 lockplayer 40
			setp[].weapon_pos -9
			cstat 33024
		}
		else
		ifl sprite[bottarget].extra inithp
		{
			setav[myspawner].stun 10
			seta[myspawner].htextra 3
			seta[myspawner].htowner THISACTOR
			seta[myspawner].htang sprite[].ang
			seta[myspawner].htpicnum TERMINATOR
			
			set x2 sprite[].x
			add x2 384
			rotatepoint sprite[].x sprite[].y x2 sprite[].y sprite[].ang startx starty
			seta[myspawner].x startx
			seta[myspawner].y starty
			set z sprite[myspawner].z
			sub z 2048
			seta[myspawner].z z
		}
		break
	}
ends

defstate termgrabstate

	set dodgetime 10
	ifaction ATERMINATORGRAB2
	{
		ifactioncount 4 ai AITERMSEEK
		break
	}
	ife myspawner -1
	{
		ai AITERMSEEK
		cstat 257
		break
	}
	ife sprite[myspawner].statnum 1024
	{
		set myspawner -1
		ai AITERMSEEK
		cstat 257
		break
	}
	ife actorvar[myspawner].monstatus 2
	{
		set myspawner -1
		ai AITERMSEEK
		cstat 257
		break
	}
	

	ife zombieonplayer THISACTOR
	{
		ife player[].over_shoulder_on NO cstat 33024 else cstat 257
		
		ifp palive nullop else ife zombieonplayer THISACTOR { set zombieonplayer -1 set zombiecount -30 }
		
		ifn zombieonplayer THISACTOR
		{
			ai AITERMSEEK
			cstat 257
			break
		}
		else
		{
			cstator 256
			set z sprite[].z
			sub z 10240
			setp[].posz z
			
			getp[].oang angvar
			
			set x2 player[].posx
			add x2 512
			rotatepoint player[].posx player[].posy x2 player[].posy angvar x y
			
			setsprite THISACTOR x y sprite[].z
			setp[].posxv 0
			setp[].posyv 0
			setp[].poszv -256
			setp[].jumping_counter 0
			set spriteid player[].i
			state facesprite
			setp[].ang player[].oang
			setp[].horiz 80
			ifcount 40
			{
				set myspawner -1
				set zombieonplayer -1
				stopactorsound THISACTOR FORCECHOKE
				screensound MELEE_IMPACT
				screensound SPIDERJUMP
				action ATERMINATORGRAB2
				cstat 257
				
				cos xvel sprite[].ang
				sin yvel sprite[].ang
				
				shiftvarl xvel 10
				shiftvarl yvel 10
				setplayer[THISACTOR].posxv xvel
				setplayer[THISACTOR].posyv yvel
				getp[].posz z
				sub z 2048
				setp[].posz z
				setp[].poszv -3072
				// push player backwards
				seta[player[].i].htextra 40
				seta[player[].i].htpicnum KNEE
				seta[player[].i].htang sprite[].ang
				seta[player[].i].htowner THISACTOR
			}
		}
	}
	else
	{
		ife myspawner player[].i
		{
			set zombieonplayer -1 set zombiecount -30 
			ai AITERMSEEK
			cstat 257
			break
		}
		set spriteid myspawner
		state facesprite
		setav[myspawner].stun 10
		seta[myspawner].xvel 0
		
		set x2 sprite[].x
		add x2 256
		rotatepoint sprite[].x sprite[].x x2 sprite[].y sprite[].ang x y
		set z sprite[].z
		sub z 2048
		
		set tempb sprite[myspawner].sectnum
		updatesectorz x y z tempb
		ifn tempb sprite[myspawner].sectnum
		{
			set x2 sprite[].x
			add x2 128
			rotatepoint sprite[].x sprite[].x x2 sprite[].y sprite[].ang x y
		}
		
		setsprite myspawner x y z
		seta[myspawner].zvel -256
		
		ifcount 40
		{
			sound MELEE_IMPACT
			sound SPIDERJUMP
			action ATERMINATORGRAB2
			cstat 257
				
			set spriteid myspawner
			state facesprite
			
			seta[myspawner].htextra 40
			seta[myspawner].htpicnum KNEE
			seta[myspawner].htowner THISACTOR
			ife myspawner myshelly
			{
				cos xvel sprite[].ang
				sin yvel sprite[].ang
				shiftr xvel 2 shiftr yvel 2
				movesprite bottarget xvel yvel 0 CLIPMASK0 RETURN
			}
			else
			{
				set temp 546000
				div temp tiledata[sprite[myspawner].picnum].xsize
				div temp sprite[].xrepeat
				ifg temp 4096 set temp 4096
				setav[myspawner].blueflagval temp
				setav[myspawner].lastangvel sprite[].ang
				seta[myspawner].zvel -1024
				set myspawner -1
			}
		}	
	}

ends

defstate breakgripcheck

	ife zombieonplayer THISACTOR 
	{
		ifn sector[].lotag 2 lockplayer 1
		stopactorsound THISACTOR FORCECHOKE
		set zombieonplayer -1
		set zombiecount -30
		cstat 257
	}
	else ifai AITERMGRAB
	{
		set myspawner -1
	}

ends

state termwaitstate

	state termgrabcheck
	ifcount 25
	{
		ai AITERMSEEK
		state termtaunt
	}
	
ends

state termhitstate
	ifg stun 0 nullop else
	ifcount 10 ai AITERMSEEK
	
ends

state termshootstate

	ifaction ATERMINATORSHOOT
	{
		ife mtype 0
		{
			ife initsprite 0
			ife player[].holoduke_on -1
			{
				ifn bottarget -1 set spriteid bottarget else
				getp[].i spriteid
				state facesprite
			}
			ifactioncount 1 
			{
				ife initsprite 5
				{
					set initsprite 0
					set mtype 0
					ai AITERMSEEK
					break
				}
				ife player[].holoduke_on -1
				{
					ifn bottarget -1 set spriteid bottarget else
					getp[].i spriteid
					state facesprite
				}
				
				geta[].z savz
				sub savz 2048
				seta[].z savz
				ifvarand initflags 32
				{
					sound LASERSHOOT
					set xvel 844
					state rpg_targetprep
					ezshoot zdist NEWPLASMA
				}
				else
				{
					sound TERMLASER
					eshoot PLASMAPROJ
				}
				seta[RETURN].ang sprite[].ang
				add savz 2048
				seta[].z savz
				set mtype 1
				add initsprite 1
			}
		}
		ife mtype 1
		{
			ifactioncount 2
			{
				set mtype 0
				resetactioncount
			}
		}
	}

	ifaction ATERMINATORDUCKSHOOT
	{
		ife mtype 0
		{
			// ife initsprite 0
			// {
				// ifn bottarget -1 set spriteid bottarget else
				// getp[].i spriteid
				// state facesprite
			// }
			ifactioncount 1 
			{
				ife initsprite 5
				{
					set initsprite 0
					set mtype 0
					cactor TERMINATOR
					ai AITERMSEEK
					break
				}
				ife player[].holoduke_on -1
				{
					ifn bottarget -1 set spriteid bottarget else
					getp[].i spriteid
					state facesprite
				}
				ifvarand initflags 32
				{
					sound LASERSHOOT
					set xvel 844
					state rpg_targetprep
					ezshoot zdist NEWPLASMA
				}
				else
				{
					sound TERMLASER
					eshoot PLASMAPROJ
				}
				seta[RETURN].ang sprite[].ang
				set mtype 1
				add initsprite 1
			}
		}
		ife mtype 1
		{
			ifactioncount 2
			{
				set mtype 0
				resetactioncount
			}
		}
	}

	

ends

state termseekstate

	state termgrabcheck
	ifai AITERMGRAB break

ifn myspawner -1
ife sprite[myspawner].statnum 4
{
	state incoming_eval
	ai AITERMRUN
	seta[].ang angvel
	set dodgetime 12
	count 10
	break
}

ifle SKILL 4
ifg player[].curr_weapon CHAINGUN_WEAPON
ifp pfacing
	ifbulletnear
		ifai AITERMSEEK
			ife team 0
{
	cactor TERMINATORDUCKING 
	ai AITERMDUCK
	break
}

ifai AITERMRUN ifnotmoving 
{
	ai AITERMSEEK
	set dodgetime 0
}

ifrnd 1 ifrnd 128 state termtaunt

set temp NO
ifge SKILL 4 { ifcount 40 set temp YES }
else ifcount 80 set temp YES

ife mtype 0
	ife temp YES
ife seemytarget YES
	ife shootmytarget YES
		ife targetalive YES
	{
		set mtype 0
		set initsprite 0
		state lowlineofsight
		ifrnd 128 ife B YES
		{ cactor TERMINATORDUCKING ai AITERMDUCK }
		else
		ai AITERMSHOOT
	}

ends

state termduckstate
	
	ifaction ATERMINATORDUCK1
	{
		// getp[].i spriteid
		// state facesprite
		ifcount 8
		{
			ife seemytarget YES ife shootmytarget YES
			ife targetalive YES
			{
				action ATERMINATORDUCKSHOOT
				set initsprite 0
				set mtype 0
			}
			else
			{
				cactor TERMINATOR
				ai AITERMSEEK
			}
		}
	}
	ifaction ATERMINATORDUCKSHOOT
		state termshootstate

ends

state terminatorcode

ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  { state squishterm break }
  
ifai 0
{
	ai AITERMWAIT
	ifspritepal 57 { sizeat 36 32 orvar initflags 32 clipdist 80 } else
	{
		sizeat 28 24
		clipdist 64
	}
	cstat 257
	
	sound TERMBEEP
}
fall
state monsterai
ife monstatus -1 break

ifai AITERMFROZEN { state termfrozenstate break }
ifai AITERMDYING { state termdyingstate break }

ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	setp[].sound_pitch SHRUNKPITCH

ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }
	
ifhitweapon
{
    ifwasweapon SHRINKSPARK
	{
		state breakgripcheck
		sound ACTOR_SHRINKING
		// ai AITERMSHRUNK
		set shrunken 1
		setp[].sound_pitch 0
		break
	}
	else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	
	ifdead
	{
		state breakgripcheck
		ifactor TERMINATORDUCKING cactor TERMINATOR
		ifwasweapon FREEZEBLAST
		{
		  sound NEWFREEZE
		  spritepal 1
		  ai AITERMFROZEN
		  strength 0
		  state freezeme
		  setp[].sound_pitch 0
		  break
		}
		ife sprite[].htowner player[].i set myspawner player[].i
		ai AITERMDYING
		geta[].htflags temp, orvar temp 4, seta[].htflags temp
		seta[].shade -100
		sound TERMELECT
		setp[].sound_pitch 0
		state enemy_death
		break
	}
	
	ifai AITERMHIT nullop else
	ifai AITERMGRAB nullop else
	ifg stun 0 { cactor TERMINATOR ai AITERMHIT }
	
	ifai AITERMHIT nullop else
	ifai AITERMSHRUNK nullop else
	ifai AITERMGRAB nullop else
	ifn sprite[].picnum TERMINATORDUCKING
	ifn sprite[].picnum TERMINATORCRAWLING
	{
		ifrnd 16 
		ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
		{ 
			// sound TERMPAIN 
			ai AITERMHIT 
			cactor TERMINATOR
			setp[].sound_pitch 0
			break 
		}
		ifrnd 64 
		ife targetalive YES
		{
			// sound TERMPAIN
			ifai AITERMSHOOT nullop else 
			{
				set mtype 0
				set initsprite 0
				state lowlineofsight
				ifrnd 128 ife B YES
				{ cactor TERMINATORDUCKING ai AITERMDUCK }
				else
				ai AITERMSHOOT
			}
		}
	}
}

ifai AITERMSHRUNK state termshrunkstate else
ifai AITERMWAIT state termwaitstate else
ifai AITERMHIT state termhitstate else
ifai AITERMSEEK state termseekstate else
ifai AITERMRUN state termseekstate else
ifai AITERMSHOOT state termshootstate else
ifai AITERMDUCK state termduckstate else
ifai AITERMGRAB state termgrabstate

set temp NO

ifaction ATERMINATORWALK set temp YES
ifaction ATERMINATORRUN set temp YES
ife temp YES
{
	ife mtype 0 // first step
	{
		ifactioncount 1
		{
			ifactorsound THISACTOR TERMSTEP1 nullop else
			sound TERMSTEP1
			set mtype 1
		}
	}
	else ife mtype 1
	{
		ifactioncount 2
		{
			ifactorsound THISACTOR SERVOSND nullop else
			sound SERVOSND
			set mtype 2
		}
	}
	else ife mtype 2
	{
		ifactioncount 3
		{
			ifactorsound THISACTOR TERMSTEP2 nullop else
			sound TERMSTEP2
			set mtype 3
		}
	}
	else ife mtype 3
	{
		ifactioncount 4
		{
			set mtype 0
			resetactioncount
			// ifactorsound THISACTOR SERVOSND nullop else
			// sound SERVOSND
		}
	}
}

ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	setp[].sound_pitch 0

ends

useractor enemy TERMINATOR TERMINATORSTRENGTH state terminatorcode enda

useractor enemystayput TERMINATORSTAYPUT TERMINATORSTRENGTH cactor TERMINATOR state terminatorcode enda

useractor enemy TERMINATORDUCKING TERMINATORSTRENGTH state terminatorcode enda

state termcrawlstate

ifaction ATERMINATORCRAWLSHOOT
{
	ife mtype 0
	{
		ife initsprite 0
		ife player[].holoduke_on -1
		{
			getp[].i spriteid
			state facesprite
		}
		ifactioncount 1 
		{
			ife initsprite 3
			{
				set initsprite 0
				set mtype 0
				action ATERMINATORCRAWL
				move TERMINATORCRAWLVELS seekplayer
				break
			}
			
			sound TERMLASER
			geta[].z savz
			add savz 2048
			seta[].z savz
			eshoot PLASMAPROJ
			// seta[RETURN].ang sprite[].ang
			sub savz 2048
			seta[].z savz
			set mtype 1
			add initsprite 1
			ife initsprite 2 ifrnd 128 set initsprite 3
		}
	}
	ife mtype 1
	{
		ifactioncount 2
		{
			set mtype 0
			resetactioncount
		}
	}
	break
}

ifaction ATERMINATORCRAWL
{
	ife mtype 0 // first step
	{
		ifactioncount 1
		{
			// sound TERMSTEP1
			set mtype 1
		}
	}
	else ife mtype 1
	{
		ifactioncount 2
		{
			sound TERMCRAWL
			set mtype 2
		}
	}
	else ife mtype 2
	{
		ifactioncount 3
		{
			// sound TERMSTEP2
			set mtype 3
		}
	}
	else ife mtype 3
	{
		ifactioncount 4
		{
			set mtype 0
			resetactioncount
			// sound SERVOSND
		}
	}
}

ife countvarc YES
{
	add countvar 1
	set temp countvar
	modvar temp 4
	ife temp 0 
	{
		sound LASERTRIP_ARMING
		ifspritepal 2 getlastpal else spritepal 2
	}
	ifge countvar 90
	{
		addkills 1
		ife sprite[].htowner player[].i set myspawner player[].i
		geta[].htflags temp, orvar temp 4, seta[].htflags temp
		seta[].shade -100
		action ATERMINATORCRAWLDIE
		move TERMINATORSTOP
		sound TERMELECT
	}
}
else
ife mtype 0
	ifcount 60	
ife seemytarget YES
	ife shootmytarget YES
		ife targetalive YES
	{
		set mtype 0
		set initsprite 0
		action ATERMINATORCRAWLSHOOT
		move TERMINATORSTOP geth
	}

ends

useractor enemy TERMINATORCRAWLING TERMINATORCRAWLSTRENGTH 

ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ifpdistl 1280 // SQUISHABLEDISTANCE
  ife player[].knee_incs 14
  { state squishterm break }
  
ifai 0
{
	ai AITERMCRAWL
	
	ifspritepal 57 { sizeat 36 32 orvar initflags 32 } else
	sizeat 28 24 // 42 36
	cstat 257
	geta[].htflags temp
	orvar temp 2097152
	seta[].htflags temp
}
fall

ife monstatus -1 break


ifaction ATERMINATORCRAWLDIE
{
	ife padang 1337 
	{
		state enemy_death
		ifrnd SPAWNAMMOODDS
		{
			ifrnd 84
			ife gametype 0
			spawn ATOMICHEALTH
			else
			ifrnd 128
			spawn PLASMASPRITE
		}
		state spawnoutline 
		killit 
		break 
	}
	
	ife countvarc YES { ifspritepal 2 getlastpal else getlastpal }
	seta[].shade -127
	seta[].htextra -1
	ifactioncount 6
	{
		state enemy_death
		ifrnd SPAWNAMMOODDS
		{
			ifrnd 84
			ife gametype 0
			spawn ATOMICHEALTH
			else
			ifrnd 128
			spawn PLASMASPRITE
		}
		sound ROBOT_EXPLODE
		ife countvarc YES
		{
			espawn EXPLOSION2
			setav[RETURN].mtype WESBOT
			setav[RETURN].myspawner THISACTOR
		}
		else
		{
			espawn MINEEXP
			seta[RETURN].xrepeat 32
			seta[RETURN].yrepeat 32
		}
		set countvarb 0
		whilevarn countvarb 4
		{
		   rand z 3072
		   add z 2048
		   mul z -1
		   ezshoot z CARXPLODE
		   randvar temp  2047
		   setactor[RETURN].ang temp 
		   randvar temp  192 add temp 128
		   setactor[RETURN].xvel temp 
		   add countvarb 1
		}
		debris SCRAP1 3
		debris SCRAP2 3
		debris SCRAP6 3
		espawn TERMHEAD
		setav[RETURN].myspawner myspawner
		killit
	}
	break
}

state monsterai
state termcrawlstate

ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

ifhitweapon
{
    ifwasweapon SHRINKSPARK
	{
		  sound ACTOR_SHRINKING
		  // ai AITERMSHRUNK
		  set shrunken 1
		  break
	}
	else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	
	ifdead
	{
		addkills 1
		ife sprite[].htowner player[].i set myspawner player[].i
		geta[].htflags temp, orvar temp 4, seta[].htflags temp
		seta[].shade -100
		action ATERMINATORCRAWLDIE
		move TERMINATORSTOP
		sound TERMELECT
	}	
}

enda


useractor enemy TERMTORSO TERMINATORCRAWLSTRENGTH

fall
iffloordistl 2
{
	// sound hit floor
	cactor TERMINATORCRAWLING
	strength TERMINATORCRAWLSTRENGTH
	cstat 257
}
enda
