// NOTENEMY.CON
// VARIOUS ACTORS THAT ARE NOT ENEMIES

spritenopal NUKEENDING
spritenoshade NUKEENDING
eventloadactor NUKEENDING
	cstat 32768
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
enda

	
useractor notenemy FAKETILE 100

	ifmove 0
	{
		move STOPPEDFORPLAYER geth
		seta[].mdflags 16
		set droptile initsprite
		ifn SPRITELOTAG 0 { set monstatus 200 ife countvar 0 set countvar 1 }
	}
	ife countvarb NO // not changed yet
	ifn SPRITELOTAG 0
	ife myspawner 0
	{
		findplayer xydist
		
		ifl xydist 1560
		ifp pfacing
		ifp palive
		ifcansee
		{
			ifl xydist 1408 ifl player[].fta 25 { quote 1580 setp[].fta 50 set handicon 3 }
			ifhitspace
			{
				set countvarb YES
				set droptile mtype
				set monstatus 0
				sound MENUPAGE sound MENUPAGE sound MENUPAGE
				add countvarc 1
				set spriteid 0
				whilevarn spriteid 16384
				{
					ife sprite[spriteid].picnum FAKETILE
					ifn sprite[spriteid].statnum 1024
					ife actorvar[spriteid].SPRITELOTAG SPRITELOTAG
						setav[spriteid].countvarc countvarc
					add spriteid 1
				}
				ife countvarc countvar
				{
					ifg countvar 1
					{
					set bigmsg 1496
					set bigmsgcount 90
					}
					state AActivation
					operateactivators SPRITELOTAG 0
					operatemasterswitches SPRITELOTAG
					operaterespawns SPRITELOTAG
				}
			}
		}
	}

enda

// 15580 is start of nuke explosion animation
// action ANEWNUKE1 15504 16 1 1 8
// action ANEWNUKE2 15520 52 1 1 12
// action ANEWNUKE3 15573 11 1 1 50

action ANEWNUKE1 15504 65 1 1 12
action ANEWNUKE2 15569 15 1 1 30

defstate spawnshockwave

	ife countvarb 0 // distance not determined yet
	{
		ldist countvarb THISACTOR player[].i
		sub countvarb 65536
		ifl countvarb 1 set countvarb 1
		set initsprite player[].posz
		screensound SHOCKWAVE
	}
	else
	{
		set countvarc 0
		set angvel sprite[].ang
		sub angvel 64
		whilevarn countvarc 128
		{
			espawn BIGSMOKE
			set x sprite[].x
			add x countvarb
			set angvar sprite[].ang
			set z initsprite
			rand z2 20480, sub z2 16384
			add z z2
			rotatepoint sprite[].x sprite[].y x sprite[].y angvel x2 y2
			setsprite RETURN x2 y2 z
			add angvel 2
			add countvarc 2
		}
		add countvarb 512
	}

ends

/*
eventloadactor SCANLIGHT geta[].extra initsprite enda

useractor notenemy SCANLIGHT

ife initsprite -1 break
ifmove 0
{
	move STOPPEDFORPLAYER
	cstator 2
	seta[].blend 1
	seta[].htflags 4
	gets[].floorz startx sub startx 512
	gets[].ceilingz starty add starty 512
	
	seta[].htflags 8192
}

ife mtype 0 // going up
{
	movesprite THISACTOR 0 0 -768 CLIPMASK0 RETURN
	ifle sprite[].z starty set mtype 1
} else
ife mtype 1 // going down
{
	movesprite THISACTOR 0 0 768 CLIPMASK0 RETURN
	ifge sprite[].z startx set mtype 0
}

enda
*/

useractor notenemy NUKEENDING
	ife mtype 0 break
	ife mtype 1
	{
		ifspritepal 2
		{
			add countvar 1
			
			ifle countvar 10
			palfrom 63 63 63 63
			
			ife countvar 15
			{
				spritepal 0
				move STOPPEDFORPLAYER faceplayer
				set mtype 1996
				// seta[].blend 1
				// cstat 2
				seta[].shade -127
				cstat 1024
				sizeat 255 255
				action ANEWNUKE1
				screensound NUKESOUND
				screensound NUKESOUND
				screensound NUKESOUND
				
				quake 600
				set rainpal SCRAPFALL
				set raining 6
				set rainradius 20480
				set raincstat 0
				
				setvar I 0
				whilevarvarn I NUMSECTORS
				{
					ifvarand sector[I].ceilingstat 1
					{
						sets[I].ceilingpal 2
						set temp sector[I].ceilingshade
						add temp 8
						sets[I].ceilingshade temp
					}
					addvar I 1
				}
				
				headspritestat spriteid 1
				whilevarn spriteid -1
				{
					ife actorvar[spriteid].monstatus 1
					ifn actorvar[spriteid].team 1
					{
						seta[spriteid].picnum TRACER
						seta[spriteid].cstat 32768
					}
					nextspritestat spriteid spriteid
				}
			}
		}
		
	}
	ife mtype 1996
	{
		setp[].posxv 0
		setp[].posyv 0
		setp[].jumping_counter 0
		ifaction ANEWNUKE1
		{
			ifactioncount 65
				action ANEWNUKE2
			else // spawn shockwave
			ifactioncount 40
				state spawnshockwave
				
			
			
		}
		// else ifaction ANEWNUKE2
		// {
			// ifactioncount 53 action ANEWNUKE3
		// }
		else ifaction ANEWNUKE2
		{
			state spawnshockwave
			geta[].alpha temp
			add temp 2
			seta[].alpha temp
			ifactioncount 15
			{
				setp[].timebeforeexit 30
				killit
			}
		}
	
		// seta[].sectnum player[].cursectnum
		// ifactioncount 80
			// cstat 32768
		// ifg countvar 15	 add countvar 1 else
		// ifactioncount 35 
		// {
			// set countvar 16
			// quake 240
		// }
		
	}
	
	
enda

action AEMPNEUTRAL -12
action AEMPTALK -11
action AEMPCONFUSED -6
action AEMPSURPRISED -5
action AEMPPLEASED -4
action AEMPBORED -3
action AEMPWRATH -1
action AEMPSCREEN 0

useractor notenemy EMPERORSCREEN 0

	ife emp_frame 10901 action AEMPNEUTRAL else
	ife emp_frame 10902 action AEMPTALK else
	ife emp_frame 10907 action AEMPCONFUSED else
	ife emp_frame 10908 action AEMPSURPRISED else
	ife emp_frame 10909 action AEMPPLEASED else
	ife emp_frame 10910 action AEMPBORED else
	ife emp_frame 10912 action AEMPWRATH else
	ife emp_frame 10913 action AEMPSCREEN 

enda

spritenoshade NUKECOIN
spriteshadow NUKECOIN
action COINFRAMES -1 1 1 1 1

move COINVELUP 480 -32
move COINVELDOWN 480 -8

useractor notenemy NUKECOIN 0

fall
set monstatus 100 strength 1
ifmove 0
{
	move STOPPED
	geta[].htflags temp, orvar temp 2097152, seta[].htflags temp
	ife attmode YES
	ifrnd 11 // 12
	ifg startguns 0
	{
		state startgun_replace
		killit
	}
	ifpdistg 32768 killit
	sizeat 28 28
	action COINFRAMES
	rand initsprite 64
}

geta[].ang angvar
sub angvar initsprite
seta[].ang angvar


ifvarand perks 32 // coin magnet
{
	ifpdistl 2048
	{
		ifmove STOPPED move COINVELUP faceplayer getv
	}
	else move STOPPED
}

ifcount 1500
{
   ife lastang 0 cstat 0 else
   ife lastang 1 cstat 2 else
   ife lastang 2 cstat 514 else
   ife lastang 3 cstat 32768 else
   ife lastang 4 cstat 514 else
   ife lastang 5 cstat 2
   
   add lastang 1
   ifg lastang 5 set lastang 0
   ifcount 1800 killit
}

ifp pshrunk nullop
else
  ifp palive
	ifcanseetarget
	ifpdistl 1024
	{
		
		globalsound COINSND
		palfrom 20 63 58 0
		
		set B sprite[player[].i].extra
		mul B 100
		div B player[].max_player_health
		
		
		ife es_type 0
		{
			getp[].shield_amount temp
			ifge temp 100 add temp 5 else add temp 10
			ifg temp 150 set temp 150
			setp[].shield_amount temp
			cactor ATOMICHEALTH
			ifge B 140 nullop else
			ifge B 100 addphealth 1 else
			ifge B 70 addphealth 2 else
			ifge B 50 addphealth 3 else
			addphealth 5
		}
		else
		{
			cactor ATOMICHEALTH
			ifge B 140 nullop else
			ifge B 100 addphealth 1 else
			ifge B 70 addphealth 2 else
			ifge B 50 addphealth 3 else
			addphealth 5
			ife gametype 0 ifl es_battery es_batterymax add es_battery 10
		}
		sub o2damage 5 ifl o2damage 0 set o2damage 0
		ifinwater
		ifl player[].airleft 390
		{
			getp[].airleft temp
			add temp 150, ifg temp 390 set temp 390
			setp[].airleft temp
		}
		ife gametype 0
		// ifge SKILL 5
		{
			set TMP_A 0
			set tempb 0
			ife player[].gotweapon PISTOL_WEAPON YES 
			{
				add TMP_A player[].ammo_amount PISTOL_WEAPON
				ifl TMP_A 48 orvar tempb 1
			}
			ife player[].gotweapon SHOTGUN_WEAPON YES
			{
				set TMP_B player[].ammo_amount SHOTGUN_WEAPON
				mul TMP_B 6
				ifl TMP_B 48 orvar tempb 2
				add TMP_A TMP_B
			}
			ife player[].gotweapon CHAINGUN_WEAPON YES 
			{
				set TMP_B player[].ammo_amount CHAINGUN_WEAPON
				ifl TMP_B 48 orvar tempb 4
				add TMP_A TMP_B
			}
			ife player[].gotweapon RPG_WEAPON YES
			{
				set TMP_B player[].ammo_amount RPG_WEAPON
				mul TMP_B 20
				ifl TMP_B 60 orvar tempb 8
				add TMP_A TMP_B
			}
			ifg player[].ammo_amount HANDBOMB_WEAPON 0
			{
				set TMP_B player[].ammo_amount HANDBOMB_WEAPON
				mul TMP_B 20
				ifl TMP_B 60 orvar tempb 16
				add TMP_A TMP_B
			}
			ife player[].gotweapon SHRINKER_WEAPON YES
			{
				set TMP_B player[].ammo_amount SHRINKER_WEAPON
				mul TMP_B 10
				ifl TMP_B 50 orvar tempb 32
				add TMP_A TMP_B
			}
			ife player[].gotweapon DEVISTATOR_WEAPON YES
			{
				set TMP_B player[].ammo_amount DEVISTATOR_WEAPON
				mul TMP_B 4
				ifl TMP_B 48 orvar tempb 64
				add TMP_A TMP_B
			}
			ifg player[].ammo_amount TRIPBOMB_WEAPON 0
			{
				set TMP_B player[].ammo_amount TRIPBOMB_WEAPON
				mul TMP_B 25
				ifl TMP_B 51 orvar tempb 128
				add TMP_A TMP_B
			}
			ife player[].gotweapon FREEZE_WEAPON YES
			{
				set TMP_B player[].ammo_amount FREEZE_WEAPON
				mul TMP_B 2
				ifl TMP_B 48 orvar tempb 256
				add TMP_A TMP_B
			}
			ife player[].gotweapon GROW_WEAPON YES
			{
				set TMP_B player[].ammo_amount GROW_WEAPON
				mul TMP_B 2
				ifl TMP_B 48 orvar tempb 512
				add TMP_A TMP_B
			}
			ifvarand gotplasma 1 ifg plasmammo 0
			{
				set TMP_B plasmammo
				mul TMP_B 3
				ifl TMP_B 48 orvar tempb 1024
				add TMP_A TMP_B
			}
			ifvarand gotbow 1 ifg arrows 0
			{
				set TMP_B arrows
				mul TMP_B 4
				ifl TMP_B 48 orvar tempb 2048
				add TMP_A TMP_B
			}
			
			ifn tempb 0
			ifl TMP_A 100
			{
				rand temp 6
				switch temp
				case 0 case 1
				ife player[].gotweapon PISTOL_WEAPON YES
				ifl player[].ammo_amount PISTOL_WEAPON 48 { userquote 65 addammo PISTOL_WEAPON 12 }
				break
				
				case 2 case 3
				ife player[].gotweapon SHOTGUN_WEAPON YES
				ifl player[].ammo_amount SHOTGUN_WEAPON 8 { userquote 69 addammo SHOTGUN_WEAPON 2 }
				break
				
				case 4 case 5
				ife player[].gotweapon CHAINGUN_WEAPON YES
				ifl player[].ammo_amount CHAINGUN_WEAPON 60 { userquote 63 addammo CHAINGUN_WEAPON 15 }
				break
				
				case 6
				ife player[].gotweapon FREEZE_WEAPON YES
				ifl player[].ammo_amount FREEZE_WEAPON 32 { userquote 347 addammo FREEZE_WEAPON 8 }
				break
				
				// case 7
				// ife player[].gotweapon SHRINKER_WEAPON YES
				// ifl player[].ammo_amount SHRINKER_WEAPON 8 { userquote 347 addammo SHRINKER_WEAPON 2 }
				// break
				
				// case 8
				// ife player[].gotweapon GROW_WEAPON YES
				// ifl player[].ammo_amount GROW_WEAPON 40 { userquote 347 addammo GROW_WEAPON 10 }
				// break
				
				endswitch
				
			}
		}
		killit
		
	}


enda

defstate givecoin
 
  ife gametype 0 ife vendorupgrade YES 
  {
	  ifspritepal 8 { userquote 786 set B 10 } else
	  ifspritepal 1 { userquote 787 set B 25 } else
	  ifspritepal 97 { userquote 788 set B 50 } else
	  ifspritepal 10 { userquote 789 set B 100 } else
	  ifspritepal 63 { userquote 790 set B 200 } else
	  { userquote 798 set B 75 }
  }
  else
  {
	  ifspritepal 8 { userquote 273 set B 1 } else
	  ifspritepal 1 { userquote 274 set B 5 } else
	  ifspritepal 97 { userquote 275 set B 10 } else
	  ifspritepal 10 { userquote 276 set B 20 } else
	  ifspritepal 63 { userquote 277 set B 50 } else
	  set B 15
  }
  add credits B
  add totalcredits B
  set lastcredits credits
  set gotcoins 90
  ifpdistl 1024
  palfrom 16 0 32
  sound COINPICK
  killit
ends

spritenoshade IMPCOIN
spriteshadow IMPCOIN
spritenopal IMPCOIN

useractor notenemy IMPCOIN 0

	// set monstatus 100 strength 1
	ifmove 0
	{
		seta[].shade -80
		move STOPPED
		ifspritepal 8 sizeat 10 10 else
		ifspritepal 1 sizeat 11 11 else
		ifspritepal 97 sizeat 12 12 else
		ifspritepal 10 sizeat 14 14 else
		ifspritepal 63 sizeat 15 15 else
		sizeat 13 13
		geta[].htflags temp, orvar temp 2097152, seta[].htflags temp
		ife gametype 0 ife vendorupgrade YES nullop else
		ifpdistg 20480 killit
		
		set z sprite[].z
		sub z player[].posz
		ifg z 81920 state givecoin
	}
	ife gametype 0 ife vendorupgrade YES resetcount


	ifvarand perks 32 // coin magnet
	{
		ifpdistl 6144
		{
			ifmove STOPPED move COINVELUP faceplayer getv
		}
		else move STOPPED
	}
	else
	{
		ifpdistl 2048
		{
			ifmove STOPPED move COINVELUP faceplayer getv
		}
		else move STOPPED
	}

	ifmove COINVELUP ifl sprite[].z player[].posz
	move COINVELDOWN faceplayer


	ifcount 300
	{
	   ife lastang 0 cstat 0 else
	   ife lastang 1 cstat 2 else
	   ife lastang 2 cstat 514 else
	   ife lastang 3 cstat 32768 else
	   ife lastang 4 cstat 514 else
	   ife lastang 5 cstat 2
	   
	   add lastang 1
	   ifg lastang 5 set lastang 0
	   ifcount 450 killit
	}

	ifpdistl 896
		// ifcanseetarget
		  state givecoin
	else 
	ifn player[].timebeforeexit 0 state givecoin
	else
	iffloordistl 4
	{
		switch sector[].floorpicnum
		case FLOORSLIME
		case FLOORPLASMA
		case PURPLELAVA
			state givecoin
		break
		endswitch
	}
	else
	ifg sprite[].zvel 3500 state givecoin

	ifg Numsprites 15000 state givecoin

	fall
enda

action BOUNTYFRAMES -1 
useractor notenemy BOUNTYICON 0 BOUNTYFRAMES

	ifmove 0
	{
		move STOPPED
		sizeat 16 16
		spritepal 21
		ife myspawner mybounty { espawn BOUNTYICON setav[RETURN].myspawner THISACTOR setav[RETURN].mtype 1 }
	}
	ife mybounty -1 killit

	ife mtype 1
	{
		ife sprite[myspawner].statnum 1024 killit
		ifn sprite[myspawner].picnum BOUNTYICON killit
		ife userdef[].overhead_on 2 
		{
			cstat 32
			seta[].mdflags 16
			seta[].xrepeat sprite[mybounty].xrepeat
			seta[].yrepeat sprite[mybounty].yrepeat
		}
		else
		{
			cstat 32768
			sizeat 2 2
		}
		setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
		break
	}
	ifn actorvar[myspawner].monstatus 1 killit
	ife sprite[myspawner].statnum 1024 killit

	geta[myspawner].picnum picnum
	set y tiledata[picnum].ysize
	mul y sprite[myspawner].yrepeat

	set x y
	div x 128
	ifg x 192 set x 192
	seta[].xrepeat x
	seta[].yrepeat x

	shiftl y 2
	add y 2048
	geta[myspawner].z z
	sub z y

	setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z
enda

useractor notenemy JUMPAD 0

	ifn SPRITELOTAG 0
		break

	ifcount 18
	ifpdistl 10240
	{
		resetcount
		set temp shotpitch
		mul temp 3
		div temp 2
		sub temp 1560
		ezshoot temp PADPROJ
		seta[RETURN].xvel startx
		setthisprojectile[RETURN].vel startx
		set temp starty
		add temp 13
		setthisprojectile[RETURN].range temp
	}

	
	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		set B 0
		ife actorvar[spriteid].padmove 0
		{
			ife actorvar[spriteid].monstatus 1 set B YES
			ife spriteid myshelly set B YES
			ife B YES
			{
				geta[].z z
				geta[spriteid].z z2
				sub z z2
				abs z
				ife sprite[spriteid].picnum EDFDRONE sub z 5120
				ife sprite[spriteid].picnum NEWSENTRY sub z 5120
				ifl z 6144 // 4096
				{
					ldist xydist THISACTOR spriteid
					ifl xydist 512
					ife sector[].lotag sector[sprite[spriteid].sectnum].lotag
					{
						geta[].ang padang
						set padmove starty
						set jumpadxvel startx
						setav[spriteid].padmove padmove
						setav[spriteid].jumpadxvel jumpadxvel
						setav[spriteid].padang padang
						setav[spriteid].navpoint -1
						setav[spriteid].savedvalue -1
						geta[spriteid].z z
						sub z 4096
						seta[spriteid].z z
						set z2 shotpitch
						mul z2 3
						seta[spriteid].zvel z2
						soundonce JUMP_PAD
					}
				}
			}
		}
		nextspritestat spriteid spriteid
	}
	
	cstat 32768
	getplayer[].i spriteid
	ldist xydist THISACTOR spriteid
	ifl xydist 512 // 640
	ifp palive
	ife sector[].lotag sector[player[].cursectnum].lotag
	ifle sprite[spriteid].z sprite[].z
	{
		getav[spriteid].padmove padmove
		getplayer[].falling_counter temp
		ifvarg temp 8
		{
			ifpdistg 1560 setvar temp 0
		}
		else
		{
			ifpdistl 1024 setvar temp 1
			else ifpdistl 1560 ifvarn padmove 0 setvar temp 1
			else setvar temp 0
		}
		ifn padmove 0 
		{
			getav[spriteid].padang padang
			ifn padang sprite[].ang 
				setvar temp 0 // don't reset a jump in progress
		}
				
		ifn temp 0
		{
			getp[].i spriteid
			geta[spriteid].z z
			sub z 4096
			seta[spriteid].z z
			getp[].posz z
			sub z 4096
			setp[].posz z
			setp[].jumping_counter 0
			setp[].jumping_toggle 0
			setp[].poszv shotpitch
			geta[].ang padang
			set padmove starty
			set jumpadxvel startx
			setav[spriteid].padmove padmove
			setav[spriteid].jumpadxvel jumpadxvel
			setav[spriteid].padang padang
			setp[].falling_counter 0
			set nofalldamage 1
			setp[].movement_lock 3
			soundonce JUMP_PAD
		}
	}
	
enda

move MOVECAMVEL 250

useractor notenemy MOVECAM 0
	ifn SPRITELOTAG 0
	{
		ifn sprite[].sectnum player[].cursectnum break
		set SPRITELOTAG 0
	}
	set movecam THISACTOR
	add countvar 1
	setp[].kickback_pic 0
	setp[].weapon_pos -9
	setp[].quick_kick 0
	ife player[].ftq 80 setp[].fta 0
	stopactorsound THISACTOR DUKE_GRUNT
	setp[].knuckle_incs 0
	lockplayer 10
	setp[].jumping_counter 0
	seta[player[].i].htextra -1
	setp[].jumping_counter 0
	ifmove 0
	{
		move MOVECAMVEL geth
	}
	ifmove MOVECAMVEL ifn angvel 0
		seta[].xvel angvel
	ifg countvar initflags
	{
		ifg initsprite 0
		{
			ifmove MOVECAMVEL
			{
				move STOPPED
				setp[].posx sprite[].x setp[].posy sprite[].y setp[].cursectnum sprite[].sectnum
			}
			sub initsprite 1
			set temp mtype
			sub temp 50
			mul temp 2
			add temp 100
			setp[].horiz temp
			ifg initsprite 0 break
			else { setp[].posx sprite[].x setp[].posy sprite[].y setp[].cursectnum sprite[].sectnum }
		}
		set movecam -1
		killit
	}
enda

useractor notenemy SECRETCOUNTSPRITE 0

findplayer xydist
	ifl xydist SPRITELOTAG
		ifp palive
			set secretsfound 10

enda

useractor notenemy EMPERORTALK
ife player[].cursectnum sprite[].sectnum
{
	set empactivation SPRITELOTAG
	set emptalk initflags
	set emp_frame 10902
	stopallmusic
	killit
}
enda

move THROWNMOVE 512
useractor notenemy THROWNOBJECT 10

	ifmove STOPPED move 0
	
	ifmove 0
	ifn stun 1
	{
		move THROWNMOVE geth
		set z player[].horiz
		sub z 100
		mul z -32
		sub z 512
		seta[].zvel z
		
	}
	ifnotmoving ifcount 3
	{
		set spriteid sprite[].htmovflag
		add spriteid 16384
		ifge spriteid 0 ifl spriteid 16384 // hit a sprite
		{
			seta[].htextra 10
			seta[].htowner spriteid
			seta[].htpicnum SHOTSPARK1
			
			seta[spriteid].htextra 10
			seta[spriteid].htowner player[].i
			seta[spriteid].htpicnum SHOTSPARK1
		}
	
		seta[].picnum mtype
		seta[].statnum savedvalue
		move 0
	}
	
	fall
	iffloordistl 2
	{
		ifcount 5
		{
			move 0
			seta[].picnum mtype
			// seta[].statnum savedvalue
			changespritestat THISACTOR savedvalue
		}
	}
	else resetcount

enda

spritenoshade TRACER
move TRACEMOVE
useractor notenemy TRACER 0

ifmove 0
{
	move TRACEMOVE
	cstator 130
	sizeat 28 28
	seta[].shade -127
	ifn sprite[].pal 1
	seta[].blend 255
}
sizeto 0 0
sizeto 0 0
sizeto 0 0
ifcount 8 killit

enda

spritenoshade TRACER2
useractor notenemy TRACER2 0

ifmove 0
{
	move TRACEMOVE
	cstat 130
	sizeat 24 20
	seta[].shade -127
	seta[].blend 255
}
sizeto 0 0
sizeto 0 0
sizeto 0 0
ifcount 8 killit

enda

spritenoshade GLOWLIGHT1
spritenopal GLOWLIGHT1
eventloadactor GLOWLIGHT1 seta[].blend 1 geta[].lotag initsprite seta[].lotag 0 seta[].alpha initsprite cstator 2 enda
useractor notenemy GLOWLIGHT1 0 seta[].blend 1 seta[].alpha initsprite enda

spritenoshade BEAMTRAIL
useractor notenemy BEAMTRAIL 0

	ifmove 0
	{
		seta[].shade -127
		move STOPPED
		cstat 0
		ife rendmode 0 { cstator 128 ifrnd 128 cstator 4 ifrnd 128 cstator 8 }
		ife mtype DISINBEAM 
		{
			sizeat 28 28
			geta[].z z add z 384 seta[].z z
			spritepal 15
		}
		else
		{
			geta[].z z add z 512 seta[].z z
			sizeat 24 24
		}
		seta[].pitch shotpitch		
	}

	sizeto 0 0
	ifcount 6 sizeto 0 0

	// ifcount 2 cstator 2
	// ifcount 4 cstator 512

enda

action REND0ACTION -1
spritenoshade ICEBEAMTRAIL
useractor notenemy ICEBEAMTRAIL 0

	ifmove 0
	{
		seta[].shade -127
		move STOPPED
		cstat 0
		ifspritepal 12 ifvarand THISACTOR 1 killit
		sizeat 48 48
		ife rendmode 0 { cstator 128 ifrnd 128 cstator 4 ifrnd 128 cstator 8 action REND0ACTION sizeat 16 16 }
		geta[].z z add z 512 seta[].z z
		// ifspawnedby BRACBEAM spritepal 12
		ifspritepal 3
		{
			sizeat 32 32
			espawn SWIRLTRAIL
			seta[RETURN].cstat 32768
		}
		
		seta[].pitch shotpitch
	}

	sizeto 0 0
	sizeto 0 0
	ifn sprite[].pal 3 ifcount 4 { sizeto 0 0 sizeto 0 0 }

	ifn sprite[].pal 3
	{
	ifcount 4 cstator 2
	ifcount 6 cstator 512
	}

enda


move STARFAST 480
move STARSLOW 320

move STARSLOWER 48
move STARSLOWEST 32

spritenopal STARFLY
spritenoshade STARFLY

useractor notenemy STARFLY 0

ifmove 0
{
	setactor[THISACTOR].shade -127
	sizeat 12 12
	spriteflags 16
	ife initsprite YES move STARSLOWER geth else
	move STARFAST geth
	getactor[THISACTOR].ang angvar
	randvar temp 64 // 48
	subvar temp 32 // 24
	addvarvar angvar temp
	setactor[THISACTOR].ang angvar
	randvar temp 3
	ifvare temp 1 spritepal 1
	ifvare temp 2 spritepal 2
	ifvare temp 3 spritepal 7
	cstator 2
	seta[].blend 1
	ifrnd 16
	{
		ife initsprite YES move STARSLOWEST geth else
		move STARSLOW geth
		espawn LIGHTHALO
		seta[RETURN].pal sprite[].pal
		setactorvar[RETURN].myspawner THISACTOR
		set myspawner RETURN
	}
	
	
	ifn portalcam -1
	{
		set spriteid portalcam
		geta[spriteid].x x2
		geta[spriteid].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvar x2 y2 
		getincangle tempc angvar sprite[].ang
		ifl tempc 0 ifg tempc -32
		{
			geta[].ang temp sub temp 32 seta[].ang temp
		}
		else
		ifg tempc 0 ifl tempc 32
		{
			geta[].ang temp add temp 32 seta[].ang temp
		}
	}
	ife initsprite YES // ifl player[].player_par 600
	{
		rand x2 32768
		set x sprite[].x, add x x2
		rotatepoint sprite[].x sprite[].y x sprite[].y angvar newx newy
		setsprite THISACTOR newx newy sprite[].z
	}
	
}

ifnotmoving 
{
	ifn myspawner -1
	setav[myspawner].monstatus 2
	killit
}

enda


spritenopal LIGHTHALO
spritenoshade LIGHTHALO
useractor notenemy LIGHTHALO 0

ifaction 0
{
	setactor[THISACTOR].shade -100
	spriteflags 16
	seta[].mdflags 16
	action ANULLACTION
	sizeat 16 16
	cstat 642
}
ife myspawner -1 killit
getactor[myspawner].z z
getactor[myspawner].zvel z2
ifvarl z2 0 
subvar z 256
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z

ifvare sprite[myspawner].statnum 1024 killit
ife monstatus 2 killit

enda


spritenoshade PLASMA_EXP
action PLASMA_EXPFRAME 0 6 1 1 8
action PLASMA_EXPFRAME2 123 6 1 1 8
useractor notenemy PLASMA_EXP 0
ifaction 0
{
	switch sprite[].htpicnum
	// case MANDOLASER 
	case PLAYERPLASMA 
		action PLASMA_EXPFRAME 
		spawn SPARKFALL
		spawn SPARKFALL
		ifrnd 64 sound LASERHIT1 else
		ifrnd 64 sound LASERHIT2
	break
	
	case FIRELASER2 case FIRELASER3
	case EDFLASER case SHOTSPARK1
		action PLASMA_EXPFRAME2
		ifspritepal 0 spritepal 2
	break
	
	default
		action PLASMA_EXPFRAME
	break
	
	endswitch
	
	sizeat 26 26
	cstat 130
	seta[].blend 1
	seta[].shade -127
}
ifactioncount 6 killit
enda

spritenoshade GREEN_EXP

useractor notenemy GREEN_EXP 0 PLASMA_EXPFRAME
ifmove 0
{
	move STOPPED
	ifspawnedby REPAIRPROJ
	{
		set initsprite 0
		whilevarn initsprite 4
		{
			espawn CAPTUREPLUS
			ifn RETURN -1
			{
				seta[RETURN].pal 11
				rand x2 1024
				sub x2 512
				add x2 sprite[].x
				seta[RETURN].x x2
				
				rand x2 1024
				sub x2 512
				add x2 sprite[].y
				seta[RETURN].y x2
				seta[RETURN].z sprite[].z
			}
			add initsprite 1
		}
		sizeat 16 16
	}
	ifspawnedby BIGBOIPROJ 
	{
		sizeat 96 96
		rand x 64 add x sprite[].x
		seta[].x x
		rand y 64 add y sprite[].y
		seta[].y y
		
		ifspritepal 122 hitradius 1536 3 4 6 8
	}
	else
	sizeat 26 26
	cstat 128
	seta[].shade -127
	cstator 2
	seta[].blend 1
}
ifactioncount 6 killit
enda


spritenoshade ELEC_EXP
action ELECFRAMES 0 6 1 1 8
move ELECMOV
useractor notenemy ELEC_EXP 0 ELECFRAMES

	ifmove 0
	{
		ifspawnedby SCUBAPROJ { spritepal 23 sizeat 32 32 }
		else
		ifspawnedby INFERNO { spritepal 83 sizeat 64 64 }
		else
		sizeat 48 48
		move ELECMOV
		ifrnd 84 { stopsound MEGAIMPACT1 sound MEGAIMPACT2 }
		ifspawnedby BIGSHOCKER sizeat 64 64
		cstat 128
		seta[].blend 1
		cstator 2
		ifrnd 128 cstator 4
		
		seta[].shade -127
	}
	ifactioncount 6 killit
enda

spritenoshade LASERHALO
useractor notenemy LASERHALO 0

ife initsprite 0 break

seta[].shade -127
// seta[].blend 255
cstat 642
sizeat 18 18
ifrnd 128 { cstator 4 geta[].xoffset temp mul temp -1 seta[].xoffset temp }
ifrnd 128 cstator 8
spritepal 2

ifcount 1 killit

enda

spritenoshade ROCKETHALO
useractor notenemy ROCKETHALO 0

seta[].blend 1
seta[].shade -127
ife initsprite 0 break

seta[].xrepeat initsprite
seta[].yrepeat initsprite

cstat 642
ifrnd 128 cstator 4
ifrnd 128 cstator 8
ife mtype 1 spritepal 1 else
spritepal 2

ifcount 1 killit

enda

spritenoshade SHRINKHALO
useractor notenemy SHRINKHALO 0

seta[].blend 1
seta[].shade -127
ife initsprite 0 break

cstat 642

sizeat 24 24
ifrnd 128 cstator 4
ifrnd 128 cstator 8
spritepal 8
geta[].x x sub x 10 seta[].x x
geta[].y y sub y 10 seta[].y y

ifcount 1 killit

enda

action FREEZEFRAME2 1 1 1
action FREEZEFRAME3 2 1 1
spritenoshade FREEZEHALO
useractor notenemy FREEZEHALO 0

ife initsprite 0 break

ifmove 0
{
	ifrnd 170
	{
		ifrnd 128 action FREEZEFRAME2 else
		action FREEZEFRAME3
	}
	move STOPPED
	sizeat 192 192
	cstator 642
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	spritepal 1
}

ifcount 1 killit

enda

spritenoshade ENERGYIMPACT
useractor notenemy ENERGYIMPACT 0 
ifaction 0
{
	action PLASMA_EXPFRAME
	sizeat 26 26
	cstat 130
	seta[].blend 1
	seta[].shade -127
	
	// status effects
	ife bottarget player[].i
	{
		getav[bottarget].burning temp
		ifspritepal 1 ifpdistl 1024 palfrom 40 0 0 63
		else
		ifspritepal 21 ifpdistl 1024 palfrom 40 48 16 16
		else
		ifspritepal 11 ifpdistl 1024 palfrom 40 0 63 0
	}
}
ifactioncount 6 killit
enda

spritenoshade SHOCKBALL_EXP
spritenopal SHOCKBALL_EXP
useractor notenemy SHOCKBALL_EXP 0

	ifmove 0
	{
		move STOPPED
		sizeat 2 3
		cstat 642
		seta[].blend 1
		flash
		ifspawnedby PLASDISKPROJ spritepal 2 else
		ifspawnedby SHOCKBALL2 spritepal 49 else
		ifspawnedby ARCBALLPROJ2 spritepal 23 else
		ifspawnedby FIRELASER2 spritepal 10
		
		ifn mtype 0 ifn mtype 3 { ifpdistl 4096 quake 20 } else
		ifpdistl 1280 ifn mtype 3 quake 20
	}
	ifcount 18 
	{ 
		sizeto 0 0 sizeto 0 0 
		ifg sprite[].xrepeat sprite[].yrepeat 
			seta[].yrepeat sprite[].xrepeat
	}
	else 
	{
		ife mtype 1 { sizeto 64 64 sizeto 64 64 sizeto 64 64 }
		else ife mtype 2 { sizeto 192 192 sizeto 192 192 sizeto 192 192 sizeto 192 192 sizeto 192 192 sizeto 192 192 }
		else ife mtype 3 sizeto 16 16
		else
		{
			sizeto 32 32 sizeto 32 32
		}
	}

	geta[].cstat B
	add countvar 1
	ifg countvar 2
	{
		set countvar 0
		ife B 642 set B 646 else
		ife B 646 set B 650 else
		ife B 650 set B 642
		seta[].cstat B
		
		ifspritepal 23
		ifl countvar 24
		ife myspawner player[].i
		{
			headspritestat myvictim 1
			whilevarn myvictim -1
			{
				ife actorvar[myvictim].monstatus 1
				{
					dist xydist THISACTOR myvictim
					ifl xydist 2560
					{
						espawn STICKYFIRE
						setav[RETURN].mtype sprite[myvictim].picnum
						setav[RETURN].myspawner myvictim
						seta[RETURN].pal 94
						setsprite RETURN sprite[myvictim].x sprite[myvictim].y sprite[myvictim].z
		
						geta[myvictim].htextra temp
						add temp 3
						seta[myvictim].htextra temp
						seta[myvictim].htpicnum RADWOUND
						seta[myvictim].htowner player[].i
					}
				}
				nextspritestat myvictim myvictim
			}
		}
	}

enda

action COMETEXPFRAMES  0  14  1  1  4
spritenoshade COMETEXP
spritenopal COMETEXP
useractor notenemy COMETEXP 0 COMETEXPFRAMES

	ifmove 0
	{
		move STOPPED
		ifspawnedby SLIMEPROJ { spritepal 8 set mtype 1 sizeat 36 32 } else
		ifspawnedby PLASGOOPROJ { set initsprite 1 sizeat 64 60 } else
		ifspawnedby KINETICPROJ 
		{ 
			set mtype 1 sizeat 32 28 
			sound SUBWOOF
			sound SUBWOOF
			sound SUBWOOF
		}
		else
		{
			flash
			sizeat 36 32
		}
		cstat 642
		seta[].blend 1
		seta[].shade -100
		
	}
	ifactioncount 14 
	{
		ifspritepal 8 { espawn BIGSMOKE seta[RETURN].pal 8 }
		killit
	}
	
	ifactioncount 9 ife mtype 0
	{
		iffloordistl 64 
		{
			espawn GROUNDSHOCK
			ife initsprite 1 setav[RETURN].mtype 1
			getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum temp temp z spriteid 192 CLIPMASK0
			seta[RETURN].z z
			seta[RETURN].htbposz z
			seta[RETURN].pal sprite[].pal
			
		}
		set mtype 1
	}
enda

action POMEXPFRAMES  0  5  1  1  6
spritenoshade POMEXP
spritenopal POMEXP
useractor notenemy POMEXP 0 POMEXPFRAMES

	ifmove 0
	{
		spritepal 12
		move STOPPED
		sizeat 40 38
		cstat 642
		seta[].blend 1
		seta[].shade -100
	}
	ifactioncount 5 killit
	
	sound SHOOTSPIKE
	set countvarb 0
	rand angvel 2047
	ife sprite[myspawner].pal 43
	{
		setprojectile[POMPROJ].range 15
		setprojectile[STICKYPROJ].xrepeat 12
		setprojectile[STICKYPROJ].yrepeat 12
	}
	
	whilevarn countvarb 14
	{
		rand zdist 10240
		sub zdist 6144
		ezshoot zdist POMPROJ
		seta[RETURN].ang angvel
		seta[RETURN].z sprite[].z
		seta[RETURN].pal 33
		seta[RETURN].owner myspawner
		
		ife sprite[myspawner].pal 43
		ifvarand countvarb 1
		{
			ezshoot zdist STICKYPROJ
			seta[RETURN].ang angvel
			seta[RETURN].z sprite[].z
			seta[RETURN].pal 33
			seta[RETURN].owner myspawner
			setav[RETURN].myspawner myspawner
		}
		
		add countvarb 1
		add angvel 146
	}
	
	ife sprite[myspawner].pal 43
	{
		setprojectile[POMPROJ].range 10
		setprojectile[STICKYPROJ].xrepeat 26
		setprojectile[STICKYPROJ].yrepeat 26
	}
		
enda

action PUFFRAMES  0  5  1  1  6
useractor notenemy LILPUFF 0 PUFFRAMES

ifmove 0
{
	move STOPPED
	spritepal 12
	lotsofglass 1
	getlastpal
	ifrnd 64 sound GLASSTINK
	sizeat 40 40
	cstator 128
}
ifactioncount 5 killit

enda

spritenoshade LASERHIT
action LASHITFRAMES  0  6  1  1  8
useractor notenemy LASERHIT 0 LASHITFRAMES

ifmove 0
{
	move STOPPED
	sizeat 32 32
	cstator 128
	ifrnd 128 cstator 4
	seta[].shade -100
	spawn SPARKFALL
	// ifrnd 32 sound LASERHIT1 else
	// ifrnd 32 sound LASERHIT2
}
ifactioncount 6 killit

enda

move WAKEMOVE 256 -4
useractor notenemy WAKEY 0
ifmove 0 { geta[].z z sub z 4096 seta[].z z sizeat 4 4 cstat 32768 move WAKEMOVE geth getv }
ifcount 10 killit
enda

action SPLASHFRAMES -1 5 1 1 20
useractor notenemy WATERSPLASH 0 SPLASHFRAMES
ifactioncount 5 killit
enda

action DRIPSPLASHFRAMES 0 9 1 1 3
useractor notenemy WATERDRIPSPLASH 0 DRIPSPLASHFRAMES
ifmove 0 { move STOPPED state checkfloordist geta[].z z add z temp seta[].z z }
ifactioncount 9 killit
enda

// action SPARKFRAME -1 1 1 1 1
action CONCRETEFRAMES -2 3 1 1 4

spritenoshade SHOCK_ACTOR
useractor notenemy SHOCK_ACTOR 0
ifmove 0
{
	ifn mtype 0 add mtype THISACTOR
	move STOPPED
	sound SPARKING3
	setactor[THISACTOR].shade -127
	setactor[THISACTOR].pitch 512
	ifspawnedby ARROWPROJ spritepal 117
	ifvarn myspawner -1
	{
		ifn mtype 0 add TOTALSHOCKS 1
		getactor[myspawner].picnum picnum
		set starty tiledata[picnum].ysize
		set z2 sprite[myspawner].yrepeat
		mul starty z2
		shiftr starty 7
		ife rendmode 0 shiftr starty 2
		ifg starty 255 set starty 255
		seta[].yrepeat starty
		
		set startx tiledata[picnum].xsize
		set x2 sprite[myspawner].xrepeat
		mul startx x2
		shiftr startx 5
		ife rendmode 0 shiftr startx 2
		ifg startx 255 set startx 255
		seta[].xrepeat startx
	}
	else 
	{
		sizeat 80 24
		ife rendmode 0 sizeat 20 12
	}
	cstat 0
	
	ife rendmode 0
	{
		cstator 128
		seta[].blend 1
	}
}
randvar angvar 2047
// failsafe cstat check due to myserious bug that can make them solid
geta[].cstat temp
ifvarand temp 256 xorvar temp 256
ifvarand temp 1 xorvar temp 1
seta[].cstat temp

ife rendmode 0
{
	geta[].cstat temp
	ifvarand temp 512 xorvar temp 512
	ifvarand temp 2 xorvar temp 2
	ife temp 128 set temp 132 else
	ife temp 132 set temp 136 else
	ife temp 136 set temp 140 else
	ife temp 140 set temp 128
	seta[].cstat temp
}
setactor[THISACTOR].ang angvar
ifvarn myspawner -1
{
	geta[myspawner].z z
	sub z 6144
	setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z
	geta[].x x add x 2 seta[].x x
	geta[].y y add y 2 seta[].y y
	ifl actorvar[myspawner].stun 6 
	ifvarand player[].player_par 1
	{
		getav[myspawner].stun stun
		add stun 2 ifg stun 6 set stun 6
		setav[myspawner].stun stun
	}
	ifl sprite[myspawner].htextra 1
	ifrnd 36
	{
		seta[myspawner].htextra 2
		seta[myspawner].htpicnum RPG
	}
	ifn mtype 0
	ifle TOTALSHOCKS 160
	{
		set team 1
		// shoot all other visible enemies within 6144 units with weak shockbeam
		set temp player[].player_par
		add temp 16384
		sub temp mtype
		modvar temp 13
		ife temp 0
		{
			set x startx
			div x 3
			seta[].xrepeat x
			set y starty
			div y 3
			seta[].yrepeat y
			set safecount 0 // max 3 targets
			
			headspritestat myvictim 1 // 990
			whilevarn myvictim -1
			{
				ife sprite[myvictim].picnum SHOCK_ACTOR
				ifn myvictim THISACTOR
				ife actorvar[myvictim].myspawner myspawner
					cstat 32768
					
				ife actorvar[myvictim].monstatus 1
				// ife actorvar[myvictim].team 0
				ifn actorvar[myvictim].team 1
				ifn actorvar[myvictim].team 3
				ifle safecount 2
				ife sprite[myvictim].htextra -1
				{
					set target myvictim // actorvar[myvictim].mysignpost
					state targetcheck
					ifn target -1
					ifn target myspawner
					// ife actorvar[target].monstatus 1
					{
						ifl xydist 10240
						{
							geta[myspawner].xrepeat init_xrepeat
							geta[myspawner].yrepeat init_yrepeat
							
							seta[myspawner].xrepeat 0
							seta[myspawner].yrepeat 0
							
							set spriteid target
							state facesprite
							set bottarget target
							state hitscan_targetprep
							setprojectile[SHOCKBEAM].trail -1
							setprojectile[SHOCKBEAM].spawns -1
							ife botclip YES 
							{
								ifrnd 128 setprojectile[SHOCKBEAM].extra 4 else
								setprojectile[SHOCKBEAM].extra 3
								geta[bottarget].x x2
								geta[bottarget].y y2
								sub x2 sprite[].x
								sub y2 sprite[].y
								getangle angvar x2 y2
									
								add angvar 1024
								cos xvel angvar
								sin yvel angvar
									  
								shiftvarr xvel 6
								shiftvarr yvel 6
								
								movesprite bottarget xvel yvel 0 CLIPMASK0 RETURN
								set RETURN -1
							}
							else
							setprojectile[SHOCKBEAM].extra 1
							zshoot zdist SHOCKBEAM
							setprojectile[SHOCKBEAM].extra 52
							setprojectile[SHOCKBEAM].trail LIGHTNING_TRAIL1
							setprojectile[SHOCKBEAM].spawns ELEC_EXP

							seta[myspawner].xrepeat init_xrepeat
							seta[myspawner].yrepeat init_yrepeat

							// spawn trail sprites
							ifl actorvar[bottarget].stun 6
							{
								set zdist sprite[bottarget].z
								sub zdist 6144 // this actor is 6144 off the ground
								sub zdist sprite[].z
								set z zdist
								
								ldist xydist3 bottarget THISACTOR
								ifvare xydist3 0 setvar xydist3 1 
								shiftl zdist 8
								divvarvar zdist xydist3

								set countvarb xydist3
								div countvarb 256
								ifg countvarb 32 set countvarb 32
								ifl countvarb 1 set countvarb 1
								
								shiftvarr z 4
								getangle shotpitch xydist3 z
								ifvarg shotpitch 1023 subvar shotpitch 2048

								geta[].x x
								geta[].y y
								geta[].z z sub z 2048
								
								set countvar 0
								mul zdist 3 div zdist 2
								whilevarvarn countvar countvarb
								{
									setvarvar x2 x
									addvar x2 384 // 256
									rotatepoint x y x2 y sprite[].ang x y
									set temp sprite[].sectnum
									updatesectorz x y z temp
									ifn temp -1 		
									ifle Numsprites 15000
									{
										espawn LIGHTNING_TRAIL1
										ifn RETURN -1
										{
											setsprite RETURN x y z
											seta[RETURN].pitch shotpitch
											setav[RETURN].shotpitch shotpitch
											setav[RETURN].mtype 1
											seta[RETURN].ang sprite[].ang
										}
									}
									addvarvar z zdist
									
									addvar countvar 1
								}
							}
							set bottarget -1
							add safecount 1
							
							ifrnd 16
							ife player[].curr_weapon 3
							ifg player[].ammo_amount 3 0
							{
								getp[].ammo_amount 3 temp
								sub temp 1
								setp[].ammo_amount 3 temp
							}
						}
					}
				}
				nextspritestat myvictim myvictim
			}
			seta[].xrepeat startx
			seta[].yrepeat starty
		}
		
	}
}
ifcount 14 cstator 2
ifcount 22 { ife myspawner player[].i killit cstator 512 }
ifcount 30 
{
	stopactorsound THISACTOR SPARKING3
	killit
}

enda


state lightningtrailcode

ifle framerate 20 ifpdistg 3072 killit
	
ifmove 0
{
	
	ife rendmode 0 { cactor 13052 break }
	move STOPPED
	cstat 0
	sizeat 10 10
	ife pchar 4 ife player[].curr_weapon SHRINKER_WEAPON ifspawnedby APLAYER spritepal 125
	// sizeat 18 18
	addvar LGtrail_pic 1
	ifrnd 128 addvar LGtrail_pic 1
	ifvarg LGtrail_pic 25655 setvar LGtrail_pic 25650
	setactor[].picnum LGtrail_pic
	getactor[].owner spriteid	
	
	ifvare mtype NO
	{
		getactorvar[spriteid].shotpitch shotpitch
		setactor[].pitch shotpitch
	}
	setactor[].shade -127
	
	// ifvare rendmode 0 { cstator 128 sizeat 8 8 }
	
}
ifvare mtype YES 
	setactor[].pitch shotpitch
	
ifcount 8 killit
ends

useractor notenemy 13052 0

	ifmove 0
	{
		move STOPPED
		sizeat 16 16
		cstat 642
		seta[].blend 1
	}
	sizeto 0 0

enda

spritenopal LIGHTNING_TRAIL1
spritenopal LIGHTNING_TRAIL2
spritenopal LIGHTNING_TRAIL3
spritenopal LIGHTNING_TRAIL4
spritenopal LIGHTNING_TRAIL5
spritenopal LIGHTNING_TRAIL6
spritenoshade LIGHTNING_TRAIL1
useractor notenemy LIGHTNING_TRAIL1 0 state lightningtrailcode enda
spritenoshade LIGHTNING_TRAIL2
useractor notenemy LIGHTNING_TRAIL2 0 state lightningtrailcode enda
spritenoshade LIGHTNING_TRAIL3
useractor notenemy LIGHTNING_TRAIL3 0 state lightningtrailcode enda
spritenoshade LIGHTNING_TRAIL4
useractor notenemy LIGHTNING_TRAIL4 0 state lightningtrailcode enda
spritenoshade LIGHTNING_TRAIL5
useractor notenemy LIGHTNING_TRAIL5 0 state lightningtrailcode enda
spritenoshade LIGHTNING_TRAIL6
useractor notenemy LIGHTNING_TRAIL6 0 state lightningtrailcode enda

spritenoshade SPARKFALL
move DRIFT 64
move SLOWSPARK 96

useractor notenemy SPARKFALL 0 // SPARKFRAME

ifmove 0
{
	ifspawnedby GRENADEPROJ ifpdistl 2048 killit
	geta[].ang angvar
	add angvar 768
	rand temp 512
	add angvar temp
	seta[].ang angvar
	move DRIFT geth
	
	sizeat 3 3
	seta[].shade -127
	seta[].blend 1 // 255
	cstator 2
	randvar mtype 512
	sub mtype 1024
	seta[].htflags 2048
	ifrnd 128 cstator 8
	ifrnd 128 cstator 4
	ifspawnedby GREENEXP2 { spritepal 125 set initsprite -2 }
	ifspawnedby PLASROCKET spritepal 124 else
	ifspawnedby FIRELASER spritepal 2 else
	ifspawnedby ARROWPROJ spritepal 117 else
	ifspawnedby POMPROJ spritepal 1 else
		spritepal 12
	ife initsprite 2 { geta[].z z add z 2048 seta[].z z add mtype 768 }
	ife initsprite -2 { geta[].z z sub z 1024 seta[].z z sub mtype 512 }
	
}
ifceilingdistl 16 fall

movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
ife initsprite -2 { ifg mtype 1560 killit }
else
iffloordistl 4
{
	ife monstatus 2 killit
	ifrnd 128 killit else
	{
		set monstatus 2
		geta[].z z
		sub z 1024
		seta[].z z
		seta[].zvel -1024
		set mtype 0
	}
}
add mtype 160
ifg mtype 3072 ifrnd 16 killit
ifcount 4 { resetcount sizeto 1 1 }
ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }

enda

useractor notenemy WOODFALL 0

ifn initsprite 0
{
	ifmove 0
	{
		ifrnd 32
		{
			rand zdist 844
			sub zdist 2560
			ezshoot zdist WOODDEBRIS
			rand zdist 2048
			seta[RETURN].ang zdist
			rand zdist 3
			ife zdist 0 seta[RETURN].cstat 128 else
			ife zdist 1 seta[RETURN].cstat 132 else
			ife zdist 2 seta[RETURN].cstat 136 else
			ife zdist 3 seta[RETURN].cstat 140
			killit
		}
		geta[].ang angvar
		add angvar 768
		rand temp 512
		add angvar temp
		seta[].ang angvar
		move DRIFT geth
		sizeat 8 8
		randvar mtype 512
		sub mtype 1024
		ife initsprite 2 { geta[].z z add z 2048 seta[].z z add mtype 768 }
		ife initsprite -2 { geta[].z z sub z 1024 seta[].z z sub mtype 512 }
		seta[].htflags 2048
		ifrnd 128 cstator 8
		ifrnd 128 cstator 4
		spritepal 15
	}
	ifceilingdistl 16 fall
	
	ifrnd 32
	{
		ifrnd 64 cstat 0 else
		ifrnd 128
		{
			ifrnd 128 cstat 12
			else cstat 4
		}
		else cstat 8
	}

	movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
	ife initsprite -2 { ifg mtype 2048 killit }
	else
	iffloordistl 8
	{
		ife botclip 1 killit
		ifrnd 128 killit else
		{
			set botclip 1
			geta[].z z
			sub z 1024
			seta[].z z
			seta[].zvel -1024
			set mtype 0
		}
	}
	add mtype 160
	ifg mtype 3072 ifrnd 16 killit
	ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }
}

enda

useractor notenemy CONCRETEFALL 0 CONCRETEFRAMES

ifn initsprite 0
{
	ifmove 0
	{
		action CONCRETEFRAMES
		geta[].ang angvar
		add angvar 768
		rand temp 512
		add angvar temp
		seta[].ang angvar
		move DRIFT geth
		// sizeat 16 16
		sizeat 8 8
		set init_xrepeat 8
		set init_yrepeat 8
		randvar mtype 512
		sub mtype 1024
		ife initsprite 2 { geta[].z z add z 2048 seta[].z z add mtype 768 }
		ife initsprite -2 { geta[].z z sub z 1024 seta[].z z sub mtype 512 }
		seta[].htflags 2048
		ifrnd 128 cstator 8
		ifrnd 128 cstator 4
	}
	ifceilingdistl 16 fall
	
	ifrnd 32
	{
		ifrnd 64 cstat 0 else
		ifrnd 128
		{
			ifrnd 128 cstat 12
			else cstat 4
		}
		else cstat 8
	}

	movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
	ife initsprite -2 { ifg mtype 2048 killit }
	else
	iffloordistl 8
	{
		ife botclip 1 killit
		ifrnd 128 killit else
		{
			set botclip 1
			geta[].z z
			sub z 1024
			seta[].z z
			seta[].zvel -1024
			set mtype 0
		}
	}
	add mtype 160
	ifg mtype 3072 ifrnd 16 killit
	ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }
}

enda

action SCRAPFRAMES 1  4  1  1  5
useractor notenemy LILSCRAP 0


	ifmove 0
	{
		action SCRAPFRAMES
		geta[].ang angvar
		add angvar 768
		rand temp 512
		add angvar temp
		seta[].ang angvar
		move DRIFT geth
		// sizeat 12 12
		sizeat 6 6
		randvar mtype 512
		sub mtype 1024
		ife initsprite 2 { geta[].z z add z 2048 seta[].z z add mtype 768 }
		ife initsprite -2 { geta[].z z sub z 1024 seta[].z z sub mtype 512 }
		seta[].htflags 2048
		ifrnd 128 cstator 8
		ifrnd 128 cstator 4
	}
	ifceilingdistl 16 fall
	
	ifrnd 32
	{
		ifrnd 64 cstat 0 else
		ifrnd 128
		{
			ifrnd 128 cstat 12
			else cstat 4
		}
		else cstat 8
	}

	movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
	ife initsprite -2 { ifg mtype 2048 killit }
	else
	iffloordistl 8
	{
		ife botclip 1 killit
		ifrnd 128 killit else
		{
			set botclip 1
			geta[].z z
			sub z 1024
			seta[].z z
			seta[].zvel -1024
			set mtype 0
		}
	}
	add mtype 160
	ifg mtype 3072 ifrnd 16 killit
	ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }


enda

action ABLUESPARKLE 0  2  1  1  3
useractor notenemy BLUESPARK 0

	ifmove 0
	{
		action ABLUESPARKLE
		geta[].ang angvar
		add angvar 768
		rand temp 512
		add angvar temp
		seta[].ang angvar
		move DRIFT geth
		
		sizeat 24 20
		cstator 2
		seta[].shade -127
		seta[].blend 1
		randvar mtype 1024
		sub mtype 1560
		seta[].htflags 10240
	}
	
	ifceilingdistl 16 fall

	movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
	
	iffloordistl 2
	{
		ifge monstatus 4 ifrnd 128 killit
		
		add monstatus 2
		geta[].z z
		sub z 512
		seta[].z z
		seta[].zvel -1280
		set mtype 0
		
	}
	add mtype 160
	ifg mtype 3072 ifrnd 32 killit
	ifcount 4 { resetcount sizeto 4 3 }
	ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }

enda

useractor notenemy BLUESPARKER 0

	flash
	spawn BLUESPARK
	spawn BLUESPARK
	spawn BLUESPARK
	cactor BLUESPARK
	ifspawnedby PLASPELPROJ
		spawn PLASPELIMPACT
	killit

enda

defstate clipfallcode
	ifmove 0
	{
		move DRIFT geth
		ifactor CLIPFALL sizeat 24 24 else
		sizeat 16 16
		ifrnd 128 cstator 4
		randvar mtype 512
		sub mtype 1024
		set countvar 0
	}
	ifceilingdistl 16 fall
	
	add countvar 1

	iffloordistl 8 fall
	else
	{
		geta[].z savz
		movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
		ife sprite[].z savz fall
	}

	iffloordistl 2
	{
		move 0
		ifactor CLIPFALL3 cactor CLIPGROUND3 else
		ifactor CLIPFALL2 cactor CLIPGROUND2 else
		cactor CLIPGROUND
		getflorzofslope sprite[].sectnum sprite[].x sprite[].y z // z is the return var
		setsprite THISACTOR sprite[].x sprite[].y z
		insertspriteq
		sound MAGFALL
		break
	}
	add mtype 160

	ifnotmoving ifcount 3 ifl countvar 60 { resetcount move SLOWSPARK randomangle }
	
	ifinwater nullop else
	ifg countvar 150
	{
		move 0
		ifactor CLIPFALL3 cactor CLIPGROUND3 else
		ifactor CLIPFALL2 cactor CLIPGROUND2 else
		cactor CLIPGROUND
		getflorzofslope sprite[].sectnum sprite[].x sprite[].y z // z is the return var
		setsprite THISACTOR sprite[].x sprite[].y z
		insertspriteq
		sound MAGFALL
	}

ends



useractor notenemy CLIPFALL 0 state clipfallcode enda
useractor notenemy CLIPFALL2 0 state clipfallcode enda
useractor notenemy CLIPFALL3 0 state clipfallcode enda

action STICKYBURN -30 14 1 1 2
action STICKYBURN2 -70 14 1 1 2

spritenoshade STICKYFIRE
useractor notenemy STICKYFIRE 0

ifcount 1 nullop
  else 
    ifcount 0
	{
		seta[].shade -127
		seta[].blend 1
		cstator 514
		ifrnd 128 action STICKYBURN else action STICKYBURN2
    	ifrnd 128 soundonce FIRE_CRACKLE
    	cstat 130
    	ifrnd 128 cstator 4
		randvar temp 10
		switch temp
		case 0 sizeat 2 2 break
    	case 1 sizeat 2 3 break
		case 2 sizeat 3 3 break
		case 3 sizeat 3 4 break
		case 4 sizeat 4 5 break
		case 5 sizeat 4 6 break
		case 6 sizeat 5 6 break
		case 7 sizeat 5 7 break
		case 8 sizeat 6 7 break
		case 9 sizeat 6 8 break
		case 10 sizeat 7 8 break
		default break
    	endswitch
    	
    	
    	ifrnd 96
    	{
	    	geta[].xrepeat x
	    	mul x 2
	    	seta[].xrepeat x
	    	geta[].yrepeat y
	    	mul y 2
	    	seta[].yrepeat y
    	}

		ifvarn myspawner -1
		{
			geta[myspawner].z lastang
			geta[myspawner].yrepeat y
			
			set y2 tiledata[mtype].ysize
			mul y y2
			mul y 3

			randvarvar tempb y
			// add tempb mtype
		  	geta[myspawner].z z
		  	ife sprite[myspawner].picnum COMMANDER subvar z 3072
			ife sprite[myspawner].picnum NEWCOMM subvar z 3072
		   	sub z tempb
		  	seta[].z z
	  	}
	}

ifvarn myspawner -1 
{
	geta[myspawner].z z
	sub z lastang
	geta[].z temp
	add temp z
	seta[].z temp
	ifg countvar 8
	{
		set countvar 0
		geta[myspawner].statnum temp
		ife temp 1024 set myspawner -1 else
		{
			geta[myspawner].xrepeat tempb
			set x2 tiledata[mtype].xsize
			mul tempb x2
			div tempb 12
			
			geta[myspawner].x x
			geta[myspawner].y y
			randvarvar temp tempb
			ifrnd 128 add x temp else sub x temp
			randvarvar temp tempb
			ifrnd 128 add y temp else sub y temp
			seta[].x x
			seta[].y y
		}
	}
	ifn myspawner -1
	geta[myspawner].z lastang
}
add countvar 1

ifcount 30 sizeto 0 0

seta[].htbposx sprite[].x
seta[].htbposy sprite[].y
seta[].htbposz sprite[].z

enda

action ASHELLROLL  0  2  1  1  8
move SHELLROLL1 48
move SHELLBOUNCE 48 -20
move SHELLBOUNCE2 48
move SHELLROLL2	32
move SHELLROLL3 16
move SHELLSTOP

state shellcode
fall

ifinwater
{
	ifmove SHELLSTOP nullop else
	{
		ifmove SHELLROLL3 nullop else
		move SHELLROLL3 geth
	}
}

ifmove 0
{
  clipdist 8
  geta[].ang angvar
  randvar temp 256
  ifrnd 128 mulvar temp -1
  add angvar temp
  seta[].ang angvar
  move SHELLBOUNCE geth getv
  cstat 32
  ife sprite[].blend 1 cstator 2
  action ASHELLROLL
}

ifmove SHELLBOUNCE ifcount 6
{
	move SHELLBOUNCE2 geth
	resetcount
}

ifmove SHELLBOUNCE2
{
	geta[].zvel temp
	ifg temp 96 seta[].zvel 96
	set tempb 0
	iffloordistl 1 set tempb 1
	ifcount 8 set tempb 1
	ifvare tempb 1
	{
		ifrnd 128 move SHELLROLL3 geth else
	  	move SHELLROLL2 geth
	    resetcount
    }
}

ifmove SHELLROLL1 ifcount 22 { move SHELLROLL2 geth resetcount }
ifmove SHELLROLL2 ifcount 16 { move SHELLROLL3 geth resetcount }
ifmove SHELLROLL3 
  ifcount 12
    ifrnd 32 
{
	 action ANULLACTION
	 ifactor NEWSHELL cactor NEWSHELL2
	 seta[].mdflags 2
     move SHELLSTOP
	 seta[].mdflags 0
     geta[].htflags temp
	 orvar temp 128
	 seta[].htflags temp
     geta[].x x
     geta[].y y
     geta[].sectnum mysector
	 ifn mysector -1
	 {
     getflorzofslope mysector x y z // z is the return var
     seta[].z z
	 }
}

ifnotmoving
{
	geta[].ang angvar
	add angvar 1024
	randvar temp 256
	ifrnd 128 mulvar temp -1
	add angvar temp
	seta[].ang angvar
}

ifcount 780 killit

ends

useractor notenemy NEWSHOTGUNSHELL 0 state shellcode enda
useractor notenemy NEWSHELL 0 state shellcode enda
useractor notenemy NEWSHELL2 0 fall ifcount 780 killit enda

move CANROLL
move SNACKVELS 64


action CANCRUSHED 2 1 1
action ACANROLL1 2 6 1 1 5
action ACANROLL2 2 6 1 1 7
action ACANROLL3 2 6 1 1 9
action ACANROLL4 2 6 1 1 12

// define COLACANSTAND 6545
// define COLACANSIDE 6546

// stand and crushed are relative to COLACANSTAND, others relative to COLACANSIDE

action ABRAWNDOSTAND 9347 1 1
action ABRAWNDOCRUSHED 11251 1 1
action ABRAWNDOSIDE 11251 1 1
action ABRAWNDOROLL1 11251 6 1 1 5
action ABRAWNDOROLL2 11251 6 1 1 7
action ABRAWNDOROLL3 11251 6 1 1 9
action ABRAWNDOROLL4 11251 6 1 1 12

action AREACTORSTAND 11258 1 1
action AREACTORCRUSHED 11259 1 1
action AREACTORSIDE 11259 1 1
action AREACTORROLL1 11259 6 1 1 5
action AREACTORROLL2 11259 6 1 1 7
action AREACTORROLL3 11259 6 1 1 9
action AREACTORROLL4 11259 6 1 1 12

action ABLUEBALLSTAND 10022 1 1
action ABLUEBALLCRUSHED 10023 1 1
action ABLUEBALLSIDE 10015 1 1
action ABLUEBALLROLL1 10015 6 1 1 5
action ABLUEBALLROLL2 10015 6 1 1 7
action ABLUEBALLROLL3 10015 6 1 1 9
action ABLUEBALLROLL4 10015 6 1 1 12

action AENERGYSTAND 10030 1 1
action AENERGYCRUSHED 10031 1 1
action AENERGYSIDE 10023 1 1
action AENERGYROLL1 10023 6 1 1 5
action AENERGYROLL2 10023 6 1 1 7
action AENERGYROLL3 10023 6 1 1 9
action AENERGYROLL4 10023 6 1 1 12

action APEASTAND 10038 1 1
action APEACRUSHED 10039 1 1
action APEASIDE 10031 1 1
action APEAROLL1 10031 6 1 1 5
action APEAROLL2 10031 6 1 1 7
action APEAROLL3 10031 6 1 1 9
action APEAROLL4 10031 6 1 1 12

state canrollingcode

ifmove CANROLL
{
	getactor[THISACTOR].ang angvar
	cos xvel angvar
	sin yvel angvar
	mulvarvar xvel mtype
	mulvarvar yvel mtype
	divvarvar xvel startx
	divvarvar yvel starty
	setvar temp 0
	ifinwater 
	{
		shiftvarr xvel 2
		shiftvarr yvel 2
	}
	addvar startx 2
	addvar starty 2
	movesprite THISACTOR xvel yvel 0 CLIPMASK0 RETURN
	ifvare RETURN 0
	{
		ifvarg startx 1024 
		{ 
			move STOPPED
			ife droptile BRAWNDOCAN action ABRAWNDOSIDE else
			ife droptile REACTORCAN action AREACTORSIDE else
			ife droptile ENERGYCAN action AENERGYSIDE else
			ife droptile BLUEBALLCAN action ABLUEBALLSIDE else
			ife droptile PEACAN action APEASIDE else
			action 0 
		}
		ifvarl xvel 0 mulvar xvel -1
		ifvarl yvel 0 mulvar yvel -1
		addvarvar xvel yvel
		
		ifaction ABRAWNDOROLL1 ifl xvel 200 action ABRAWNDOROLL2
		ifaction ABRAWNDOROLL2 ifl xvel 150 action ABRAWNDOROLL3
		ifaction ABRAWNDOROLL3 ifl xvel 100 action ABRAWNDOROLL4
		
		ifaction AREACTORROLL1 ifl xvel 200 action AREACTORROLL2
		ifaction AREACTORROLL2 ifl xvel 150 action AREACTORROLL3
		ifaction AREACTORROLL3 ifl xvel 100 action AREACTORROLL4
		
		ifaction ACANROLL1 ifl xvel 200 action ACANROLL2
		ifaction ACANROLL2 ifl xvel 150 action ACANROLL3
		ifaction ACANROLL3 ifl xvel 100 action ACANROLL4
		
		ifaction ABLUEBALLROLL1 ifl xvel 200 action ABLUEBALLROLL2
		ifaction ABLUEBALLROLL2 ifl xvel 150 action ABLUEBALLROLL3
		ifaction ABLUEBALLROLL3 ifl xvel 100 action ABLUEBALLROLL4
		
		ifaction AENERGYROLL1 ifl xvel 200 action AENERGYROLL2
		ifaction AENERGYROLL2 ifl xvel 150 action AENERGYROLL3
		ifaction AENERGYROLL3 ifl xvel 100 action AENERGYROLL4
		
		ifaction APEAROLL1 ifl xvel 200 action APEAROLL2
		ifaction APEAROLL2 ifl xvel 150 action APEAROLL3
		ifaction APEAROLL3 ifl xvel 100 action APEAROLL4
		
		shiftvarl xvel 2
		getactor[THISACTOR].pitch temp
		mulvarvar xvel mtype
		addvarvar temp xvel
		setactor[THISACTOR].pitch temp
	}
	else 
	{
		mulvar mtype -1
		shiftvarl startx 2
		shiftvarl starty 2
	}

}

ends

useractor notenemy BLUEBALLCAN

ifmove 0
{
	ifspawnedby COLAMACHINE
	move SNACKVELS randomangle else
	move STOPPED
	sizeat 12 10
	cstat 256
	clipdist 8
}
ifmove SNACKVELS iffloordistl 2
{
	move STOPPED
}

set tempb healthbuff
add tempb 20

fall
set monstatus 100 strength 1 strength 1
ife drinkcola 0
ife eatsnack 0
 // ifphealthl 120
 ifl sprite[player[].i].extra tempb
  iffloordistl 2
	ifp palive
	 ifcansee
	   ifpdistl 844
	     ife player[].kickback_pic 0
		   ife player[].weapon_pos 0
	   {
		 set drinkcola 1
		 set snackpic BLUEBALLCAN
		 set snackpal sprite[].pal
		 setp[].weapon_pos -1
		 killit
       }
	   

ifhitweapon
{
	ifaction CANCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	{
		set monstatus 2
		cactor COLACANSTAND
		sizeat 20 20
		action CANCRUSHED
		espawn BLOODPOOL
		ifspritepal 0
		setactor[RETURN].pal 1
		else setactor[RETURN].pal 2
		seta[RETURN].cstat 34
		ifspritepal 0
		spritepal 46
	}
}
enda

useractor notenemy COLACANSTAND 5
ifmove 0
{
	cstator 257
	sizeat 10 8
	clipdist 8
	move STOPPED
	ife droptile REACTORCAN action AREACTORSTAND
	ife droptile BRAWNDOCAN action ABRAWNDOSTAND
	ife droptile BLUEBALLCAN action ABLUEBALLSTAND
	ife droptile ENERGYCAN action AENERGYSTAND
	ife droptile PEACAN action APEASTAND
}

fall

ifhitweapon
{
	ifaction CANCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	ifaction ABLUEBALLCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	ifaction AENERGYCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	ifaction APEACRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	ifaction ABRAWNDOCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	ifaction AREACTORCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	{
		ife droptile REACTORCAN action AREACTORCRUSHED
		else
		ife droptile BRAWNDOCAN action ABRAWNDOCRUSHED
		else
		ife droptile BLUEBALLCAN action ABLUEBALLCRUSHED
		else
		ife droptile ENERGYCAN action AENERGYCRUSHED
		else
		ife droptile PEACAN action APEACRUSHED
		else
		action CANCRUSHED
		sizeat 24 20
		ifrnd 128 cstat 0 else cstat 4
		ife myspawner player[].i 
			screensound CRUSHCAN 
		else
		{
			espawn BLOODPOOL
			ifaction ABLUEBALLCRUSHED seta[RETURN].pal 1
			else
			ifaction APEACRUSHED seta[RETURN].pal 113
			else
			ifaction ABRAWNDOCRUSHED seta[RETURN].pal 113
			else
			setactor[RETURN].pal 2
			seta[RETURN].cstat 34
		}
		set monstatus 2
	}
	break
}

set tempb healthbuff
add tempb 20

ifaction CANCRUSHED nullop else
ifaction ABLUEBALLCRUSHED nullop else
ifaction AENERGYCRUSHED nullop else
ifaction APEACRUSHED nullop else
ifaction ABRAWNDOCRUSHED nullop else
ifaction AREACTORCRUSHED nullop else
{
	set monstatus 100 strength 1
	ife drinkcola 0
	ife eatsnack 0
	 // ifphealthl 120
	 ifl sprite[player[].i].extra tempb
	  iffloordistl 2
		ifp palive
		 ifcansee
		   ifpdistl 844
			 ife player[].kickback_pic 0
		   {
			 set drinkcola 1
			 set snackpal sprite[].pal
			 ife droptile 0 set snackpic 6607
			 // ifn droptile 0 
			 set snackpic droptile
			 setp[].weapon_pos -1
			 killit
		   }
}   
enda

useractor notenemy COLACANSIDE 5
ifmove 0
{
	cstator 257
	sizeat 24 20
	clipdist 8
	// setactor[THISACTOR].roll 512
	move STOPPED
	ifspawnedby COLACANSIDE nullop else 
	{
		move CANROLL geth
		action ACANROLL1
		ife droptile BRAWNDOCAN action ABRAWNDOROLL1
		ife droptile REACTORCAN action AREACTORROLL1
		ife droptile BLUEBALLCAN action ABLUEBALLROLL1
		ife droptile ENERGYCAN action AENERGYROLL1
		ife droptile PEACAN action APEAROLL1
		ifrnd 128 { cstator 4 seta[].mdflags 16 }
		
	}
	setvar startx 64
	setvar starty 64
	
	ifrnd 128 setvar mtype 1 else
	{
		getactor[THISACTOR].ang angvar
		addvar angvar 1024
		setactor[THISACTOR].ang angvar
		setvar mtype -1
	}
	
}

fall
set monstatus 100 strength 1


getactor[THISACTOR].xvel xvel

ifhitweapon
{
	ifaction ABRAWNDOCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else ifaction AREACTORCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else ifaction CANCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else ifaction ABLUEBALLCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else ifaction AENERGYCRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else ifaction APEACRUSHED
	{
		debris SCRAP1 1
		killit
	}
	else
	{
		cactor COLACANSTAND
		move STOPPED
		setactor[THISACTOR].roll 0
		setactor[THISACTOR].pitch 0
		ife droptile BRAWNDOCAN action ABRAWNDOCRUSHED else
		ife droptile REACTORCAN action AREACTORCRUSHED else
		ife droptile BLUEBALLCAN action ABLUEBALLCRUSHED else
		ife droptile ENERGYCAN action AENERGYCRUSHED else
		ife droptile PEACAN action APEACRUSHED else
		action CANCRUSHED
		set monstatus 2
		espawn BLOODPOOL
		ife droptile BLUEBALLCAN seta[RETURN].pal 1 else
		ife droptile PEACAN seta[RETURN].pal 113 else
		ife droptile BRAWNDOCAN seta[RETURN].pal 113 else
		setactor[RETURN].pal 2
		seta[RETURN].cstat 34
	} 
}

state canrollingcode

ifand bits 4 break
ifand extbits 64 break

set tempb healthbuff
add tempb 20

ife drinkcola 0
ife eatsnack 0
// ifphealthl 120
 ifl sprite[player[].i].extra tempb
  iffloordistl 2
	ifp palive
	 ifcansee
	   ifpdistl 844
	   ife player[].kickback_pic 0
	{
	   set temp NO
	   ifspawnedby COLAMACHINE set temp YES
	   
	   ifcount 15
	   ifhitspace set temp YES
	   
	   ife temp YES
	   {
		 set drinkcola 1
		 set snackpal sprite[].pal
		 ifn droptile 0 set snackpic droptile
		 setp[].weapon_pos -1
		 killit
       }
	   else
	   ifcount 15
	   ifl player[].fta 30 { quote 1180 setp[].fta 40 }
	}

enda


action ACOOKIESNACK 23
action APORKSNACK 24

useractor notenemy SNACKSPRITE
	fall
	set monstatus 100 strength 1
	ifmove 0
	{
		ifspawnedby COLAMACHINE
		move SNACKVELS geth else
		move STOPPED
		sizeat 12 10
		ife droptile 6577 { action ACOOKIESNACK sizeat 14 12 } else
		ife droptile 6578 { action APORKSNACK sizeat 14 12 } else
		ife droptile 1 action ANULLACTION else
		ifrnd 128
		{
			sizeat 14 12
			ifrnd 128 action ACOOKIESNACK else
			action APORKSNACK
		}
	}
	ifmove SNACKVELS iffloordistl 2
	ifcount 16
	{
		sound SNACKDROP
		move STOPPED
	}

	ifand bits 4 break
	ifand extbits 64 break

	set tempb healthbuff
	add tempb 20

	ife eatsnack 0
	ife drinkcola 0
	 // ifphealthl 120
	 ifl sprite[player[].i].extra tempb
	  iffloordistl 2
		ifp palive
		 ifcansee
		   ifpdistl 844
			 ife player[].kickback_pic 0
	{
	   set temp NO
	   ifspawnedby COLAMACHINE set temp YES
	   
	   ifcount 15
	   ifhitspace set temp YES
	   
	   ife temp YES
	   {
		 set eatsnack 1
		 set snackpal sprite[].pal
		 ifaction 0 set snackpic SNACKSPRITE else
		 ifaction ACOOKIESNACK set snackpic 6577 else
		 ifaction APORKSNACK set snackpic 6578
		 setp[].weapon_pos -1
		 killit
       }
	   else
	   ifcount 15
	   ifl player[].fta 30 { quote 1180 setp[].fta 40 }
	}
enda

defstate trashcode

ifaction 0
{
	action ANULLACTION
	cstator 256
	ifl sprite[].xrepeat 4 sizeat 24 24
	set monstatus 109 // trash monstatus!!!
}
fall
ife sprite[].cstat 32768 killit
ifhitweapon 
{ 
	spawn SMALLSMOKE 
	debris SCRAP3 1
	spritepal 2 debris SCRAP3 1
	spritepal 1 debris SCRAP3 1
	spritepal 7 debris SCRAP3 1
	killit
}
ends

useractor notenemy TRASH1 10 state trashcode enda
useractor notenemy TRASH2 10 state trashcode enda
useractor notenemy TRASH3 10 state trashcode enda
useractor notenemy TRASH4 10 state trashcode enda
useractor notenemy TRASH5 10 state trashcode enda
useractor notenemy TRASH6 10 state trashcode enda
useractor notenemy TRASH7 10 state trashcode enda
useractor notenemy TRASH8 10 state trashcode enda
useractor notenemy TRASH9 10 state trashcode enda
useractor notenemy TRASH10 10 state trashcode enda
useractor notenemy TRASH11 10 state trashcode enda
useractor notenemy TRASH12 10 state trashcode enda

useractor notenemy SMOKER 0

ifn player[].timebeforeexit 0 break

add countvar 1
ifge countvar SPRITELOTAG
{
	randvarvar temp SPRITELOTAG
	mul temp 2
	ifge temp SPRITELOTAG
	{
		espawn BIGSMOKE
		rand angvar 2047
		
		set x sprite[].x
		randvarvar x2 initsprite
		add x x2
		rotatepoint sprite[].x sprite[].y x sprite[].y angvar y y2
		geta[].z z, sub z 4096
		setsprite RETURN y y2 z
		seta[RETURN].pal sprite[].pal
		seta[RETURN].shade botclip
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].htbposx y
		seta[RETURN].htbposy y2
		seta[RETURN].htbposz z
		seta[RETURN].htflags 4
		ifn mtype 0 seta[RETURN].alpha mtype
		set countvar 0
	}
}

enda

action FAKEDOORANIM	620  5  1  1  12
action FAKEDOOROPEN 624
action FAKEDOORCLOSED 618

// SEE AAPRELOAD.CON
// eventloadactor FAKEDOOR

// ifspritepal 4 cstat 32768
// ifspritepal 33 cstat 32768
// getactor[THISACTOR].hitag initsprite // must match hitag on another FAKEDOOR
// setactor[THISACTOR].hitag 0
// getactor[THISACTOR].lotag SPRITELOTAG // sound played (will play default sound if not set)
// setactor[THISACTOR].lotag 0
// ifn sprite[].extra -1 geta[].extra initflags
// enda

useractor notenemy FAKEDOOR 0 FAKEDOORCLOSED

ife transition 0
{
	action FAKEDOORCLOSED
}
else
{
	
	ifaction FAKEDOORANIM
	ifactioncount 5 action FAKEDOOROPEN
	break
}
	
ife monstatus 2 killit

ifangdiffl 768 nullop else
ifspritepal 4 nullop else
ifp palive
ifcansee ifpdistl 1408
{
	ifhitspace
	{
		stopsound B_GRUNT stopsound B_GRUNT2 stopsound B_SEARCH
		stopsound DUKE_SEARCH stopsound DUKE_SEARCH2
		ifvarn SPRITELOTAG 0 soundvar SPRITELOTAG else globalsound DOOR_OPERATE2
		setvar transition 60
		action FAKEDOORANIM
		getplayer[THISACTOR].i spriteid
		setactorvar[spriteid].myspawner THISACTOR
	}
	else setvar handicon 3
}

enda

action EXPLOSION3FRAMES  0  8  1  1  10
useractor notenemy EXPLOSION3 0 EXPLOSION3FRAMES

ifactioncount 8 killit
ifl sprite[].xrepeat 8 sizeat 64 60
ifmove 0
{
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	seta[].blend 1
	cstator 2
	seta[].shade -100
	move STOPPED
	ifspawnedby CBPROJ // fired by LILY
	{
		cstator 128
		sizeat 32 28
	}
	else
	iffloordistl 4 spawn EXPLOSION2BOT
}
state spawnsmokeonscrap

enda

useractor notenemy FIREBALL3D 0

ifmove 0
{
	move STOPPEDFORPLAYER geth
	cstat 130
	seta[].blend 1
	seta[].shade -100
	rand angvar 2047
	seta[].ang angvar
	sizeat 8 8
	ifrnd 128 set countvar -1
}
ifcount 42 killit
ifcount 11 nullop else ifcount 10
{
	espawn FIRERING
	setav[RETURN].mtype 2
}
geta[].xrepeat x
add x 3 seta[].xrepeat x
geta[].yrepeat y
add y 2 seta[].yrepeat y

geta[].ang angvar
ife countvar -1 sub angvar 17 else
add angvar 17
seta[].ang angvar

enda

spritenoshade FIRECIRCLE3D
// on this model, yrepeat does nothing
useractor notenemy FIRECIRCLE3D 0

ifmove 0
{	
	seta[].shade -100
	geta[].z z, sub z 1024 seta[].z z
	move STOPPED spin
}
sizeto 128 1
sizeto 128 1

ifcount 30 killit

enda

action BLOODFRAMES 0 4 1 1 15
actor BLOOD 0 BLOODFRAMES
  sizeto 72 72 sizeto 72 72 sizeto 72 72
  ifpdistg 3144 killit
  ifactioncount 4 killit
enda

spritenopal BLOODEXP
spritenoshade BLOODEXP
action ABLOODEXPFRAMES 0  4  1  1  14
useractor notenemy BLOODEXP 0 ABLOODEXPFRAMES

	ifmove 0
	{
		move STOPPED
		cstat 128
		ifrnd 128 cstator 4
		ifspawnedby GOOBULLET 
		{
			spritepal 126 seta[].shade -126 
			seta[].blend 1 cstator 2 
			sizeat 6 6
			ifvarand novaupgrades[FREEZE_WEAPON] 1
			{
				espawn BLOODPOOL2
				seta[RETURN].pal 126
				setav[RETURN].mtype 1
			}
		}
		else
		{
			spritepal 10 seta[].shade -5
		
			ifspawnedby BLOODBOMB
			{
				sizeat 8 8
				guts JIBS6 8
				guts JIBS3 4
				spawn BLOODPOOL2
				
				set countvar 0
				set angvel 0
				set startx sprite[].x
				
				whilevarn countvar 48
				{
					set startx sprite[].x
					add startx 512
					rand x 3072
					add startx x
					ifrnd 84 espawn BLOODSPLASH1 else ifrnd 128 espawn BLOODSPLASH2 else espawn BLOODSPLASH3
					rotatepoint sprite[].x sprite[].y startx sprite[].y angvel newx newy
					seta[RETURN].x newx
					seta[RETURN].htbposx newx
					seta[RETURN].y newy
					seta[RETURN].htbposy newy
					
					set savz sprite[].z
					sub savz 1024
					rand z 4096
					sub savz z
					seta[RETURN].z savz
					seta[RETURN].htbposz savz
					
					add countvar 1
					add angvel 43
				}
				headspritestat myspawner 1
				whilevarn myspawner -1
				{
					ife actorvar[myspawner].monstatus 1
					// ife actorvar[myspawner].team 0
					ifn actorvar[myspawner].team 1
					ifn actorvar[myspawner].team 3
					{
						dist temp THISACTOR myspawner
						ifl temp 3584
						{
							set TMP_A wweaplevel[SHRINKER_WEAPON]
							add TMP_A 1
							mul TMP_A 12
							add TMP_A 40 // damage
							
							set tempd TMP_A

							shiftl tempd 4
							add leach tempd
							geta[myspawner].htextra tempc
							add tempc TMP_A
							seta[myspawner].htextra tempc
							seta[myspawner].htowner player[].i
							seta[myspawner].htpicnum BLOODBULLET
		
							getav[myspawner].bleeding tempc
							ifl tempc 0 mul tempc -1
							add tempc TMP_A
							setav[myspawner].bleeding tempc
							
							espawn BIGSMOKE2
							seta[RETURN].pal 10
							setsprite RETURN sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
						}
					}
					nextspritestat myspawner myspawner
				}
				
			}
			else
			{
				sizeat 4 4
				guts JIBS6 2
				iffloordistl 4 ifrnd 32 spawn BLOODPOOL2
				sound SQUISHED6
			}
		}
	}
	ifspawnedby BLOODBOMB
	{
		sizeto 64 64
		sizeto 64 64
		sizeto 64 64
		sizeto 64 64
	}
	else ifspawnedby GOOBULLET
	{
		sizeto 64 64
		sizeto 64 64
		sizeto 64 64
		sizeto 64 64
	}
	else
	{
		sizeto 24 24
		sizeto 24 24
		sizeto 24 24
	}
	ifactioncount 4 killit
enda


useractor notenemy EXPLOSION4 0 EXPLOSION3FRAMES

ifactioncount 8 killit
ifl sprite[].xrepeat 8 sizeat 42 40
ifmove 0
{
	seta[].blend 1
	cstator 642
	seta[].shade -100
	move STOPPED
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	ifspawnedby BIGBOIPROJ 
	{
		spritepal 21
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 1
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1280
				{
					getav[spriteid].burning burning
					add burning 60
					setav[spriteid].burning burning
					seta[spriteid].htowner player[].i
					soundonce RADFRY
				}
			}
			nextspritestat spriteid spriteid
		}
	}
}

enda

action GROUNDSHOCKFRAMES  0  5  1  1  8
spritenoshade GROUNDSHOCK
spritenopal GROUNDSHOCK
useractor notenemy GROUNDSHOCK 0 GROUNDSHOCKFRAMES

	ifmove 0
	{
		move STOPPED
		ife mtype 1 
		{
			sizeat 64 60 
			sound SMALLEXP1
		}
		else
		sizeat 40 32
		cstat 514
		seta[].blend 1
		seta[].shade -100
		flash
	}
	ife mtype 1 
	{ 
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 1
			ife actorvar[spriteid].team 0
			{
				dist xydist THISACTOR spriteid
				ifl xydist 2048
				{
					set myvictim spriteid
					espawn STICKYFIRE
					setav[RETURN].mtype sprite[myvictim].picnum
					setav[RETURN].myspawner myvictim
					seta[RETURN].pal 46
					setsprite RETURN sprite[myvictim].x sprite[myvictim].y sprite[myvictim].z
					geta[myvictim].htextra temp
					add temp 7
					seta[myvictim].htextra temp
					seta[myvictim].htpicnum FIRELASER
					seta[myvictim].htowner player[].i
				}
			}
			nextspritestat spriteid spriteid
		}
		
		
	}
	ifactioncount 5 killit
enda

action AIRSHOCKFRAMES  0  5  1  1  8
spritenoshade AIRSHOCK
spritenopal AIRSHOCK
useractor notenemy AIRSHOCK 0 AIRSHOCKFRAMES

	ifmove 0
	{
		move STOPPED
		sizeat 40 32
		cstat 514
		seta[].blend 1
		seta[].shade -100
		switch sprite[].htpicnum
		case BLASTBALL
			cstator 128
			sizeat 24 24
		break
		case BIGPLASMA 
		case SWIRLPROJ
		case SHOCKPROJ
		case TIDALPROJ
			cstator 128
		break
		case PURPPROJ
			cstator 128
			sizeat 30 24
		break
		case LOVEBOMB
			cstator 128
			sizeat 72 72
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				ife actorvar[spriteid].monstatus 1
				ife actorvar[spriteid].team 0
				ifl sprite[spriteid].htextra 100
				ifn sprite[spriteid].htpicnum GROWSPARK
				ifn sprite[spriteid].htpicnum LOVEBOMB
				{
					dist temp THISACTOR spriteid
					ifl temp 5120
					{
						seta[spriteid].htextra 100
						seta[spriteid].htowner player[].i
						seta[spriteid].htpicnum GROWSPARK
					}
				}
				nextspritestat spriteid spriteid
			}
		break
		case RADPROJ
			spritepal 76
			espawn BIGSMOKE
			seta[RETURN].pal 76
			ife team 1 seta[RETURN].pal 90
		break
		
		default
			ife mtype 1
			spawn BIGSMOKE
		break
		
		endswitch
		flash
		
		ife mtype 2
		{
			ifpdistl 1024 palfrom 24 24
			cstator 128
		}
	}
	ifactioncount 5 
	{
		ifspritepal 6 
		{
			spawn POISONGAS
			sound SIZZLE
		}
		killit
	}
enda

action SPLASH2FRAMES -2  5  1  1  10

useractor notenemy NEWSPLASH2 0 SPLASH2FRAMES

	
	ifl sprite[].xrepeat 4 sizeat 8 8
	ifactioncount 5 
	{
		spritepal 1
		spawn BLOODPOOL
		killit
	}

enda


spritenoshade BIGSPLASH
spritenopal BIGSPLASH

action ABIGSPLASHFRAMES 0  6  1  1  12

useractor notenemy BIGSPLASH 0

	ifaction 0
	{
		// sound SPLOOSH
		action ABIGSPLASHFRAMES
		sizeat 48 48
		ifrnd 128 cstator 4
	}
	
	ifactioncount 6 
	{
		// spawn WATERSPLASH2 all around
		set countvar 0
		whilevarn countvar 16
		{
			espawn NEWSPLASH2
			rand x2 2048
			sub x2 1024
			add x2 sprite[].x
			seta[RETURN].x x2
			rand x2 2048
			sub x2 1024
			add x2 sprite[].y
			seta[RETURN].y x2
			add countvar 1
			seta[RETURN].xrepeat 32
			seta[RETURN].yrepeat 32
		}
		killit
	}

enda

spritenoshade BUBBEXP
spritenopal BUBBEXP

action BUBBEXPFRAMES 0  6  1  1  8

useractor notenemy BUBBEXP 0

	ifaction 0
	{
		sound SPLOOSH
		action BUBBEXPFRAMES
		sizeat 15 15
		cstat 130
		ifrnd 128 cstator 4
		seta[].shade -126
		
		ifpdistl 1280
		ifn team 1
		{
			set spriteid player[].i
			ifspritepal 11
			{
				set temp plevel
				mul temp 8
				add temp 20
				getav[spriteid].bleeding bleeding
				add bleeding temp
				clamp bleeding -240 240
				setav[spriteid].bleeding bleeding
				set water_screen -30
				set waterpal 8
			}
			else
			ifspritepal 2
			{
				set temp plevel
				mul temp 8
				add temp 30
				getav[spriteid].burning burning
				add burning temp
				clamp burning 1 300
				setav[spriteid].burning burning
				set water_screen -30
				set waterpal 2
			}
			else
			{
				set mtype WATERSPLASH2
				set water_screen -30
				set waterpal 0
			}
		}
	}
	
	ife mtype WATERSPLASH2
	{
		palfrom 30 0 16 48
		setp[].posxv 0
		setp[].posyv 0
		setp[].poszv 0
	}
	
	sizeto 32 32
	
	ifactioncount 6 
	{
		ifspritepal 0 spritepal 1 else
		ifspritepal 11 spritepal 6 else
		ifspritepal 24 spritepal 2
		espawn BLOODPOOL
		setav[RETURN].mtype sprite[].pal
		ife mtype WATERSPLASH2 cstat 32768 else killit
	}
	
	ifactioncount 12 	
		killit
	
	

enda


spritenoshade ICEBEAMIMPACT
spritenopal ICEBEAMIMPACT
useractor notenemy ICEBEAMIMPACT 0

	ifmove 0
	{
		move STOPPED
		sizeat 10 8
		cstat 130
		ifspawnedby BRACBEAM seta[].blend 1
		findnearactorz BURNING 844 6144 target
		ifn target -1 setav[target].monstatus 2
		findnearactorz BURNING2 844 6144 target
		ifn target -1 setav[target].monstatus 2
		findnearactorz FIRE 844 6144 target
		ifn target -1 setav[target].monstatus 2
		findnearactorz FIRE2 844 6144 target
		ifn target -1 setav[target].monstatus 2
		findnearspritez TOILETWATER 844 6144 target
		ifn target -1 
		ifg sprite[target].xrepeat 5
		{
			setav[target].monstatus 2
			geta[target].xrepeat x add x 2 seta[target].xrepeat x
			geta[target].yrepeat y add y 2 seta[target].yrepeat y
			sound NEWFREEZE
		}
	}
	ifcount 10 
	{ 
		sizeto 0 0 sizeto 0 0 
	}

	geta[].cstat B
	add countvar 1
	ifg countvar 2
	{
		set countvar 0
		ife B 130 set B 134 else
		ife B 134 set B 138 else
		ife B 138 set B 130
		seta[].cstat B
	}

enda

useractor notenemy SHOCKBALL 0 // actor version

	sizeat 24 24
	cstat 642
	ifcount 2 killit

enda

useractor notenemy SHOCKBALL2 0 // actor version

	sizeat 24 24
	cstat 642
	ifcount 2 killit

enda

useractor notenemy PADPROJ 0 // actor version

	sizeto 0 0
	ifcount 5 killit
	ifmove 0
	{
		move STOPPED
		sizeat 15 15
		seta[].shade -127
	}
	ife sprite[].cstat 642 cstat 646 else
	ife sprite[].cstat 646 cstat 650 else
	ife sprite[].cstat 650 cstat 654 else
	cstat 642

enda

action BMFGANIM1 0 17 1 1 4
action BMFGANIM2 10 1  1 1 4
action BMFGANIM3 9 8  1 1 4
action BMFGANIM4 16 1 1 1 4

spritenoshade BMFGIMPACT
spritenopal BMFGIMPACT
move BMFGACTIVE

defstate bmfgdamagestate
		
	ifn actorvar[myvictim].monstatus 1 { set myvictim -1 break }
	ife sprite[myvictim].statnum 1024 { set myvictim -1 break }
	geta[myvictim].z z
	sub z 8192
	setsprite THISACTOR sprite[myvictim].x sprite[myvictim].y z
	
	espawn STICKYFIRE
	setav[RETURN].mtype sprite[myvictim].picnum
	setav[RETURN].myspawner myvictim
	seta[RETURN].pal 46
	setsprite RETURN sprite[myvictim].x sprite[myvictim].y sprite[myvictim].z
	
	ifl sprite[myvictim].extra 1500 
	setav[myvictim].stun 3
	else
	ifrnd 12 setav[myvictim].stun 16
	
	geta[myvictim].htextra temp
	add temp 8
	seta[myvictim].htextra temp
	seta[myvictim].htpicnum RPG
	seta[myvictim].htowner player[].i
			
ends

defstate bmfgpullstate

	headspritestat spriteid 1 // 990
	whilevarn spriteid -1
	{
		ife actorvar[spriteid].monstatus 1
		ifn actorvar[spriteid].team team
		ifn actorvar[spriteid].team 3
		{
			set target spriteid // actorvar[spriteid].mysignpost
			ifn target -1
			{
				dist xydist THISACTOR target
				ifl xydist 8192
				{
					canseespr THISACTOR target temp
					ife temp YES
					{
						geta[target].x x2
						geta[target].y y2
						sub x2 sprite[].x
						sub y2 sprite[].y
						getangle angvar x2 y2
							
						add angvar 1024
						cos xvel angvar
						sin yvel angvar
						
						// ife target player[].i
						// {
							// shiftvarl xvel 5
							// shiftvarl yvel 5
							
							// add xvel player[].posxv
							// add yvel player[].posyv
							// setplayer[].posxv xvel
							// setplayer[].posyv yvel
						// }
							  
						shiftvarr xvel 7
						shiftvarr yvel 7
						
						geta[].z z
						sub z sprite[target].z
						shiftr z 1
							
						ifg xydist 1024
						movesprite target xvel yvel z CLIPMASK0 RETURN
						
						setav[target].bottarget -1
						rand angvar 2047
						seta[target].ang angvar
						seta[target].xvel 0
						
						ifl xydist 3072
						ife actorvar[target].shrunken 0
						{
							ifspritepal 10 nullop else
							{
								espawn SHRINKEREXPLOSION
								setav[RETURN].myvictim target
								setsprite RETURN sprite[target].x sprite[target].y sprite[target].z
							}
							ifspritepal 10 seta[target].htpicnum RPG else
							seta[target].htpicnum SHRINKSPARK
							ife team 1 seta[target].htowner player[].i
							else seta[target].htowner THISACTOR
							ifl sprite[target].htextra 1 
							{
								seta[target].htextra 1
							}
						}
						else ifspritepal 22
						ifl xydist 1280
						ifvarand player[].player_par 1
						{
							seta[target].htpicnum RPG
							ife team 1 seta[target].htowner player[].i
							else seta[target].htowner THISACTOR
							ifl sprite[target].htextra 3
							seta[target].htextra 3
						}
						
					}
				}
			}
		}
		nextspritestat spriteid spriteid
	}

ends

defstate bmfgspawn

	headspritestat myvictim 1
	whilevarn myvictim -1
	{
		ife actorvar[myvictim].monstatus 1
		ifn actorvar[myvictim].team team
		{
			dist xydist THISACTOR myvictim
			ifl xydist 6144
			{
				ifspritepal 22 espawn SHRINKEREXPLOSION else
				ifspritepal 10 { espawn SHRINKEREXPLOSION seta[RETURN].pal 10 } else
				espawn BMFGIMPACT
				setav[RETURN].mtype 1
				setav[RETURN].myvictim myvictim
				geta[myvictim].z z 
				ifn sprite[].pal 22 sub z 8192
				setsprite RETURN sprite[myvictim].x sprite[myvictim].y z
			}
		}
		nextspritestat myvictim myvictim
	}

ends

useractor notenemy BMFGIMPACT 0 BMFGANIM1

ifmove 0
{
	flash
	ifspawnedby ARCBALLPROJ 
	{
		set initsprite ARCBALLPROJ
		spritepal 23
		geta[].z z
		sub z 8192
		seta[].z z
		ifcansee ifpdistl 6632
		{
			set spriteid player[].i
			geta[spriteid].x x2
			geta[spriteid].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvar x2 y2
			cos xvel angvar
			sin yvel angvar
		
			shiftvarl xvel 11
			shiftvarl yvel 11
			add xvel player[].posxv
			add yvel player[].posyv
			setplayer[].posxv xvel
			setplayer[].posyv yvel
			ifp phigher nullop else
			{
			getp[].posz z
			sub z 1024
			setp[].posz z
			setp[].poszv -4096
			}
			// push player backwards
		}
		
		set countvar 16
		set angvar sprite[].ang
		whilevarn countvar 0
		{
			shoot ARCBALLPROJ2
			add angvar 128
			seta[].ang angvar
			sub countvar 1
		}
	
	}
	seta[].shade -100
	seta[].blend 1
	cstator 2
	move STOPPED
	cstator 128
	ife mtype 1 sizeat 8 8 else
	{
		sizeat 20 20
		ifpdistl 8192 quake 15
	}
	
}

ifaction BMFGANIM1
{
	ifactioncount 4 
	ifmove STOPPED
	{
		ife mtype 0 ifn initsprite ARCBALLPROJ state bmfgspawn
		move BMFGACTIVE
		ifcansee 
		{
			findplayer xydist
			setvar xydist2 16384
			subvarvar xydist2 xydist
			ifvarg xydist2 0
			{
				shiftvarr xydist2 8
				addvar xydist2 8
				ifvarg xydist2 63 setvar xydist2 63
				ifvarvarl player[THISACTOR].pals_time xydist2
				setplayer[THISACTOR].pals_time xydist2
				
				ifspritepal 22
				{
					setplayer[THISACTOR].pals 0 8
					setplayer[THISACTOR].pals 1 63
					setplayer[THISACTOR].pals 2 12
				}
				else
				ifspritepal 10
				{
					setplayer[THISACTOR].pals 0 16
				}
				else
				{
					setplayer[THISACTOR].pals 0 57
					setplayer[THISACTOR].pals 1 57
					setplayer[THISACTOR].pals 2 63
				}
			}
		}
	}
	ifactioncount 9 
		action BMFGANIM2
}

ife mtype 1 ifn myvictim -1 state bmfgdamagestate
else ifspritepal 22 state bmfgpullstate
else ifspritepal 10 { state bmfgpullstate ifactorsound THISACTOR WARPLOOP nullop else sound WARPLOOP }

ifaction BMFGANIM2
{
	
	ifactioncount 1
	{
		ife mtype 0
		ifn sprite[].pal 22
		ifn sprite[].pal 10
		{
			flash
			hitradius 3072 6 6 6 6
		}
		resetactioncount
		ife sprite[].cstat 130 cstat 134 else
		ife sprite[].cstat 134 cstat 138 else
		ife sprite[].cstat 138 cstat 142 else
		ife sprite[].cstat 142 cstat 130
		
		ife mtype 1 ifcount 16 action BMFGANIM3
		ifcount 24 action BMFGANIM3 
		
		ife initsprite ARCBALLPROJ action BMFGANIM3
	}
}

ifaction BMFGANIM3
	ifactioncount 8 { action BMFGANIM4 cstat 642 }

ifaction BMFGANIM4
{
	ifspritepal 10 ifactorsound THISACTOR WARPLOOP stopactorsound THISACTOR WARPLOOP
	ifactioncount 1
	{
		resetactioncount
		ife sprite[].cstat 642 cstat 646 else
		ife sprite[].cstat 646 cstat 650 else
		ife sprite[].cstat 650 cstat 654 else
		ife sprite[].cstat 654 cstat 642
	}
	ifcount 1 { sizeto 0 0 resetcount }
}

enda

move DRIFTTOPLAYER 32
action CARBONFRAMES -1 17 1 1 4

spritenoshade CARBONIMPACT
spritenopal CARBONIMPACT
useractor notenemy CARBONIMPACT 0 CARBONFRAMES
	
	ifmove 0
	{
		globalsound CARBONHIT
		globalsound CARBONHIT
		move DRIFTTOPLAYER faceplayer
		spritepal 27
		sizeat 12 12
		// seta[].blend 1
		cstat 130
		seta[].shade -64
		
		set B 0
		set x sprite[].x
		add x 768
		set z2 sprite[].z
		sub z2 1024
		rand angvar 2047
		whilevarn B 8
		{
			espawn BIGSMOKE
			rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
			setsprite RETURN x2 y2 z2
			add angvar 256
			add B 1
		}
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 1
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1400
				{
					set temp sprite[spriteid].htextra
					add temp 35
					seta[spriteid].htextra temp
					seta[spriteid].htowner player[].i
					seta[spriteid].htpicnum FREEZEBLAST
					ife actorvar[spriteid].padmove 0
						setav[spriteid].padang 131313
				}
			}
			nextspritestat spriteid spriteid
		}
	}
	ifactioncount 17 killit

enda

action PLASMEXPFRAMES 0 10 1 1 2
action PLASMSLOWFRAMES 0 10 1 1 4

spritenoshade PLASMEXPB
spritenoshade PLASMEXP
useractor notenemy PLASMEXP 0 PLASMEXPFRAMES
ifmove 0
{
	cstator 130
	seta[].blend 1
	sizeat 16 16
	move STOPPED
	ifrnd 128 cstator 4
}
ifactioncount 10 killit
enda

useractor notenemy PLASMEXPB 0
ifmove 0
{
	ifn myspawner -1
	ife pchar 2
	ifn saberpos 0
	{
		geta[myspawner].htg_t 8 hitsprite
		ifn hitsprite -1
		ifg actorvar[hitsprite].monstatus 0
		{
			geta[hitsprite].picnum picnum 
			state tiletype
			ifn TILETYPE 2
			{
				spawn AABLOOD
				killit
			}
		}
	}
	cstator 130
	seta[].shade -127
	seta[].blend 1
	ifspawnedby NEWPLASMA { sizeat 32 32 action PLASMSLOWFRAMES } else
	{ sizeat 10 10 action PLASMEXPFRAMES }
	move STOPPED
	ifrnd 128 cstator 4
	cactor PLASMEXP
	
}
enda

spritenoshade RAILIMPACT
action RAILIMPACTFRAMES 0 4 1 1 10

useractor notenemy RAILIMPACT 0 RAILIMPACTFRAMES
ifmove 0
{
	seta[].shade -127
	seta[].blend 1
	move STOPPED geth
	cstat 130
	sizeat 24 24
	spritepal 21
	spawn SPARKFALL
	spawn SPARKFALL
	spawn SPARKFALL
	spawn SPARKFALL
}
ifactioncount 4 killit

enda

spritenoshade SWIRLTRAIL
spritenopal SWIRLTRAIL
useractor notenemy SWIRLTRAIL 0 RAILIMPACTFRAMES
ifmove 0
{
	seta[].shade -50
	move STOPPED geth
	cstat 130
	seta[].alpha 96
	cstator 16
	sizeat 3 3
	spritepal 27
	
}
ifactioncount 4 killit

enda

useractor notenemy PLASPELIMPACT 0 RAILIMPACTFRAMES
ifmove 0
{
	seta[].shade -127
	seta[].blend 1
	move STOPPED geth
	cstat 514
	sizeat 4 3
	ifrnd 128 cstator 4
}
ifactioncount 4 killit

enda

action PARTICLE_FRAMES 0 10 1 1 3
useractor notenemy PART_EXP 0 PARTICLE_FRAMES

ifmove 0
{
	ifrnd 128 cstator 4
	
	move STOPPED
	cstator 128
	// ifn sprite[].htpicnum WATERIMPACT
	// {
		sizeat 32 32
		geta[].z z
		sub z 2048
		seta[].z z
	// }
	ifspawnedby SHOTSPARK1 
	{
		spritepal 2
		spriteflags 68
		seta[].shade -127
		seta[].blend 255
	}
	ifspawnedby FLAKPROJ
	{
		sizeat 16 16
		spritepal 2
		spriteflags 68
		seta[].shade -127
		seta[].blend 255
	}
}
ifactioncount 10 killit

enda

useractor notenemy WATERIMPACT 0

ifmove 0
{	
	move STOPPED
	cstat 32768
	sizeat 32 32
	rand countvar 8
}
add countvar 1

ifg countvar 9 
{
	set countvar 0
	espawn BIGSMOKE2
	getactor[THISACTOR].ang angvar
	ifvare countvarb 0 
	{ addvar angvar 512 setvar countvarb 1 } 
	else
	{ subvar angvar 512 setvar countvarb 0 }
	getactor[THISACTOR].x x
	getactor[THISACTOR].y y
	setvarvar x2 x
	randvarvar temp 1024
	addvarvar x2 temp
	rotatepoint x y x2 y angvar x2 y2
	setactor[RETURN].x x2
	setactor[RETURN].y y2
	setactor[RETURN].htbposx x2
	setactor[RETURN].htbposy y2
	setactor[RETURN].pal 1
	setactor[RETURN].xrepeat 16
	setactor[RETURN].yrepeat 10
	
	// espawn PART_EXP
	// setactor[RETURN].x x2
	// setactor[RETURN].y y2
	// setactor[RETURN].htbposx x2
	// setactor[RETURN].htbposy y2
	// setactor[RETURN].pal 1
	// setactor[RETURN].xrepeat 24
	// setactor[RETURN].yrepeat 12
	// setactor[RETURN].cstat 2
	
}

enda

action ABLOCKSIT  0  1  5  1  1
useractor notenemy COVERBLOCK 10000 ABLOCKSIT
ifmove 0
{
	move STOPPED geth
	set startx 1
	ifspawnedby APLAYER 
	{
		ifgapzl 96
		sizeat 64 48 
		else
		sizeat 96 72
		cstat 257 
		set mtype 1
		set countvar 600
		seta[].alpha 220
		set savz sprite[].z
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum COVERBLOCK
			ifn spriteid THISACTOR
			{
				ifle sprite[spriteid].z savz
				{
					dist xydist THISACTOR spriteid
					ifl xydist 1560
					{
						set savz sprite[spriteid].z
						sub savz 24576
					}
					
				}
			}
			
			ifvarand inven_upgrades 32
			{
				set z sprite[].z
				sub z 16384
				ifge sprite[spriteid].z z
				ifn spriteid THISACTOR
				ife actorvar[spriteid].monstatus 1
				{
					set z sprite[].z
					add z 8192
					ifle sprite[spriteid].z z
					{
						ldist xydist THISACTOR spriteid
						ifle xydist 1152
						{	
							set temp plevel
							shiftl temp 5
							add temp 30
							seta[spriteid].htextra temp
							seta[spriteid].htpicnum RPG 
							seta[spriteid].htowner player[].i
							seta[spriteid].htang sprite[].ang
							
						}
					}
				}
			}

			nextspritestat spriteid spriteid
		}
		
		seta[].z savz
	}
	findnearspritez GREENSLIME 1152 10240 spriteid
	ifn spriteid -1 
	{ seta[spriteid].htextra 100 seta[spriteid].htpicnum RPG seta[spriteid].htowner player[].i }
	spriteflags 1048577
}

fall
iffloordistl 2 ifonwater state floatcode
/*
	set zdist 0
	getsector[].floorslope zdist
	getflorzofslope sprite[].sectnum sprite[].x sprite[].y z
	sub z 2048
	ifg sprite[].z z
	ifvarn zdist 0
	{
		getsector[].wallptr temp
		getwall[temp].point2 tempb
		getwall[temp].x x2
		getwall[temp].y y2
		getwall[tempb].x x
		getwall[tempb].y y
		subvarvar x2 x
		subvarvar y2 y
		getangle angvar x2 y2
	
		getincangle tempc angvar sprite[].ang
		set lastang sprite[].ang
		sub lastang 512
		getincangle tempd angvar lastang
		

		ifvarg tempc 512 { subvar tempc 1024 mulvar tempc -1 } else
		ifvarl tempc -512 { addvar tempc 1024 mulvar tempc -1 }
		
		ifvarg tempd 512 { subvar tempd 1024 mulvar tempd -1 } else
		ifvarl tempd -512 { addvar tempd 1024 mulvar tempd -1 }
		
		mulvarvar zdist tempc
		shiftvarr zdist 13
		mul zdist -1
		seta[].pitch zdist
		
		getsector[].floorslope zdist
		mulvarvar zdist tempd
		shiftvarr zdist 13
		mul zdist -1
		seta[].roll zdist

	}
	else { seta[].pitch 0 seta[].roll 0 }
*/

state playerpush

// ifn sprite[].htowner player[].i ifn sprite[].htpicnum FIRELASER2 seta[].htextra -1
state predamage
ifvarand inven_upgrades 32 state damagepushblock

ifhitweapon strength 1000

ldist xydist THISACTOR player[].i
ifp phigher
{
	ifl xydist 1024
	{
		set z sprite[].z
		sub z player[].posz
		ifg z 28672
			cstat 256
		else cstat 257
	}
	else cstat 257	
}
else cstat 257

ife mtype 1 
{
	sub countvar 1
	ifle countvar 8
	{
		ifspritepal 121 getlastpal else spritepal 121
		geta[].alpha temp
		add temp 30
		seta[].alpha temp
		ife countvar 5 sound TELEPORTER
		ifle countvar 0
			killit
		
	}
	else
	ifge countvar 592
	{
		ifspritepal 121 getlastpal else spritepal 121
		geta[].alpha temp
		sub temp 30
		ifl temp 0 set temp 0
		seta[].alpha temp
	}
}

enda

action EXPLOSION7FRAMES  0  14  1  1  6

useractor notenemy EXPLOSION7 0 EXPLOSION7FRAMES

ifmove 0
{
	move DRIFTTOPLAYER faceplayer
	seta[].blend 1
	cstator 130
	seta[].shade -127
	ifl sprite[].xrepeat 4
	sizeat 20 18
}
state spawnsmokeonscrap

ifactioncount 14 killit

enda



action BURNING_FLAME  0   12   1   1   2
move BURNING_VELS 

defstate burningstate

  ife initsprite 1
  {
	ifcount 108 nullop else count 108
  }
  ife monstatus 2
  {
	cactor BIGSMOKE
	action 0
	move 0
  }
  ifspawnedby FIREBEAM
  {
	fall
	ifinwater killit
	iffloordistl 64 nullop else killit
	ifrnd 128 cactor BURNING2
  }
  ifspawnedby PLASMA_EXP
  {
	fall
	ifinwater killit
	iffloordistl 64 nullop else killit
  }
  ifspawnedby BURNING
  {
    ifgapzl 16
      break
  }
  else
    ifspawnedby BURNING2
  {
    ifgapzl 16
      break
  }

  ifcount 128
  {
    ifspawnedby TIRE
    {
      ifactioncount 512
        killit
      ifrnd 8 
        sizeto 64 48
    }
    else
    {
      sizeto 8 8
      sizeto 8 8
      ifcount 192
        killit
    }
  }
  else
  {
    ifmove 0
	{
      move BURNING_VELS
	  cstator 2
	  seta[].blend 1
	  findnearactor ICEBLOCK 1024 spriteid
	  ifn spriteid -1 count 108
	}
	ife team 1 sizeto 32 32 else
    sizeto 52 52

    ifp palive
      ifpdistl 844
        // ifrnd 32
          ifcansee
		    ifn sprite[].pal 1
				ife team 0
					ifn pchar 3 // Mando is immune
    {
      ifg player[].boot_amount 0
	  {
		ifvarand player[].player_par 1
		{
		  getp[].boot_amount temp
		  sub temp 1
		  setp[].boot_amount temp
		  // setp[].inven_icon 7
		  ife player[].fta 0 quote 75
		}
	  }
	  else
	  {
		  getp[].i spriteid
		  getav[spriteid].burning temp
		  ifl temp 60 add temp 1
		  setav[spriteid].burning temp
		  palfrom 32 16
	  }
    }
	
	ifn sprite[].pal 1
	{
		headspritesect spriteid sprite[].sectnum
		whilevarn spriteid -1
		{
			ife sprite[spriteid].statnum 1
			ife actorvar[spriteid].monstatus 1
			{
				dist xydist THISACTOR spriteid
				ifl xydist 844
				ife team actorvar[spriteid].team
				{
					getav[spriteid].burning temp
					ifl temp 100
					{
					ifrnd 128 add temp 2
					}
					// add temp 2
					setav[spriteid].burning temp
				}
			}
			nextspritesect spriteid spriteid
		}
	}
	
  }

ends

actor BURNING WEAK BURNING_FLAME
  state burningstate
enda

actor BURNING2 WEAK BURNING_FLAME
  state burningstate
enda

// action QUICK_FLAME  -2   12   1   1   2

state quickflamestate

  sleeptime 300
  ifcount 50
  {
      sizeto 8 8
	  sizeto 8 8
      ifcount 90
	  {
	    spawn BIGSMOKE
        killit
	  }
  }
  else
  {
    ifmove 0
	{
      move BURNING_VELS
	  cstator 128
	  ifrnd 128 cstator 4
	  ifrnd 128 cstator 8
	  seta[].shade -127
	}

    sizeto 52 52
	

    ifp palive
      ifpdistl 844
        // ifrnd 32
          ifcansee
    {
	   ifg player[].boot_amount 0
	  {
		ifvarand player[].player_par 1
		{
		  getp[].boot_amount temp
		  sub temp 1
		  setp[].boot_amount temp
		  // setp[].inven_icon 7
		  ife player[].fta 0 quote 75
		}
	  }
	  else
	  {
		  getp[].i spriteid
		  getav[spriteid].burning temp
		  ifl temp 60 add temp 4
		  setav[spriteid].burning temp
		  palfrom 32 16
	  }
	  killit
    }
	
	headspritesect spriteid sprite[].sectnum
	whilevarn spriteid -1
	{
		ife sprite[spriteid].statnum 1
		ife actorvar[spriteid].monstatus 1
		{
			dist xydist THISACTOR spriteid
			ifl xydist 844
			{
				getav[spriteid].burning temp
				ifl temp 180
				{
					add temp 20
					rand tempb 20
					add temp tempb
				}
				
				setav[spriteid].burning temp
				count 51
			}
		}
		nextspritesect spriteid spriteid
	}
	
  }

ends

useractor notenemy QUICKFLAME 0 // QUICK_FLAME
  state quickflamestate
enda

action TOILETWATERFRAMES 0 4 1 1 1
actor TOILETWATER 0 TOILETWATERFRAMES
  ife monstatus 2
  {
	ifaction ANULLACTION nullop else { resetcount action ANULLACTION stopactorsound THISACTOR WATER_GURGLE }
	cstator 259
	// ifhitweapon { spritepal 1 sound GLASS_BREAKING lotsofglass 30 spawn BIGSMOKE killit }
	ifcount 600 { set monstatus 0 cstat 0 action TOILETWATERFRAMES }
	break
  }
  ifpdistl 8192
  {
    soundonce WATER_GURGLE
    ifspawnedby TOILET
      sizeto 34 34
    else
    {
      ifspawnedby WATERFOUNTAINBROKE
        sizeto 6 15
      else
        ifspawnedby TOILETWATER
          nullop
      else
        sizeto 24 32
    }

    ifp palive
      ifpdistl RETRIEVEDISTANCE
        ifp pfacing
          ifactioncount 32
            ifphealthl MAXPLAYERHEALTH
              ifhitspace
                ifcansee
    {
      addphealth 1
      globalsound DUKE_DRINKING
      resetactioncount
    }
  }
enda

state steamcode
  ifpdistl 6144
    soundonce STEAM_HISSING

  ifcount 20
    {
      resetcount
      ifp palive
        ifpdistl 1024
		ifn pchar 3 // Mando's armor makes him immune
      {
        addphealth -1
        palfrom 16 16
      }
    }
  else
  {
    ifspawnedby STEAM
      break
    else
      ifspawnedby CEILINGSTEAM
        break

    sizeto 24 24
  }
ends

action STEAMFRAMES    0     5     1     1     1
actor STEAM 0 STEAMFRAMES
  state steamcode
enda

actor CEILINGSTEAM 0 STEAMFRAMES
  state steamcode
enda

actor WATERBUBBLEMAKER 0 0 randomangle
  ifpdistl 3084
    ifrnd 24
      spawn WATERBUBBLE
enda

action BUBBLE
action CRACKEDBUBBLE  1
move BUBMOVE       -10 -36
move BUBMOVEFAST     -10 -52

actor WATERBUBBLE 0 BUBBLE BUBMOVE getv geth randomangle

  ifaction CRACKEDBUBBLE
  {
    ifinwater
      ifrnd 192
        killit
    ifactioncount 7
      killit
  }
  else
  {
    ifcount 4
    {
      ifrnd 192
        move BUBMOVE getv geth randomangle
      else
        move BUBMOVEFAST getv geth randomangle
      resetcount

      ifrnd 84
        sizeat 8 10
      else
        ifrnd 84
          sizeat 10 8
      else
        sizeat 9 9
    }

    ifonwater
    {
      iffloordistl 8
        action CRACKEDBUBBLE
    }
    else
      ifactioncount 40
        action CRACKEDBUBBLE
  }
enda

eventloadactor AIRBUBBLEMAKER
cstat 32768
geta[].lotag countvar
seta[].lotag 0
ife countvar 0 set countvar 240
enda

spritenoshade AIRBUBBLE

state killbubble
	set countvarb 0
	geta[].x x
	geta[].y y
	geta[].z z
	whilevarn countvarb 12
	{
		espawn WATERBUBBLE
		rand temp 1024
		sub temp 512
		add temp x
		seta[RETURN].x temp
		rand temp 1024
		sub temp 512
		add temp y
		seta[RETURN].y temp
		rand temp 8192
		sub temp 4096
		add temp z
		seta[RETURN].z temp
		add countvarb 1
	}
	killit
ends

useractor notenemy AIRBUBBLE 0 BUBBLE

	ifmove 0 
	{
		seta[].shade -50
		move BUBMOVEFAST getv geth randomangle
		sizeat 48 8
		cstat 898
		geta[].z z
		sub z 8192
		seta[].z z
		ifrnd 128 set mtype 1
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
	}
	
	// vertical movement insurance policy
	geta[].z z
	sub z 128
	seta[].z z
	
	getceilzofslope sprite[].sectnum sprite[].x sprite[].y z
	geta[].z zdist
	sub zdist z

	ifl zdist 6144
	{
		sizeto 48 18
		ifl sprite[].yrepeat 20
			state killbubble
	}
	else sizeto 48 42
	
	set temp player[].player_par
	shiftl temp 4
	sin tempb temp
	shiftr tempb 8
	ife mtype 1
	{
		geta[].y y
		sub y tempb
		seta[].y y
	}
	else
	{
		geta[].x x
		sub x tempb
		seta[].x x
	}
	
	ifinwater nullop else
		state killbubble
		
	ifhitweapon
		state killbubble
		
	ifpdistl 1024
	{
		ifinwater
		ifl player[].airleft 390
		ifp palive
		{
			globalsound AIRBURST
			sound DUKE_TAKEPILLS
			palfrom 48 0 0 60
			setp[].airleft 390
			set lastbubble 150
			ifg o2damage 0
			ifl sprite[player[].i].extra player[].max_player_health
			{
				geta[player[].i].extra temp
				add temp o2damage
				ifg temp player[].max_player_health set temp player[].max_player_health
				seta[player[].i].extra temp
			}
			set o2damage 0
		}
		state killbubble
	}
	

enda

useractor notenemy AIRBUBBLEMAKER 0

ifmove 0
{
	move STOPPED
	cstat 32768
	set countvarb 0
	ife countvar 0 set countvar 240
}
add countvarb 1
ifg countvarb countvar
{
	set countvarb 0
	spawn AIRBUBBLE
}

enda

eventloadactor MESSAGER
cstat 32768
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag initsprite
ife initsprite 0 set initsprite 1
seta[].hitag 0
geta[].extra mtype
ife mtype -1 set mtype 0
geta[].xvel initflags // optional sound, only plays once
seta[].xvel 0
geta[].yvel myvictim
seta[].yvel 0
geta[].pal droptile
geta[].shade botclip // each shade adds a second
// pal 0 == permanent unless has channel, then dies on lotag activation
// pal 1 == just plays for 7 seconds
// pal 2 == turns on upon activation, then is timed

enda

useractor notenemy MESSAGER

	ife droptile 2
	{
		checkactivatormotion mtype
		ife RETURN 1 { set droptile 1 set mtype 0 }
		break
	}
	
	ifp palive
	ifpdistl 1280
	ifl subtitle_time 30
	{
		set subtitle_time 6
		set subtitle_start SPRITELOTAG
		set subtitle_numlines initsprite
		ifn initflags 0 { globalsoundvar initflags set initflags 0 }
		ifg myvictim 0 { lockplayer myvictim set myvictim 0 }
		
		ife droptile 1 // time based
		{
			set TMP_A 210
			set TMP_B botclip
			mul TMP_B 30
			add TMP_A TMP_B
			set subtitle_time TMP_A
			ifl botclip 0 set subtitle_time 0
			killit
		}
	}
	
	ifn mtype 0
	ife droptile 0
	{
		checkactivatormotion mtype
		ife RETURN 1 killit
	}
	ife monstatus 2 killit

enda

eventloadactor UPDRAFT_SPAWNER
cstat 32768
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag initsprite
geta[].extra initflags
seta[].hitag 0
set botclip 512
ifg initsprite 0 { shiftl initsprite 3 add botclip initsprite }

enda

useractor notenemy UPDRAFT_SPAWNER 0

ifspritepal 2
{
	headspritesect spriteid sprite[].sectnum
	whilevarn spriteid -1
	{
		ife sprite[spriteid].picnum UPDRAFT_SPAWNER
		ifn spriteid THISACTOR
		ifg sprite[spriteid].z sprite[].z
		{
			geta[].z float
			setav[spriteid].float float
			seta[spriteid].pal 4
		}
		nextspritesect spriteid spriteid
	}
	killit
}

ifp ponground set mtype NO

ife SPRITELOTAG 0
ife sprite[].sectnum player[].cursectnum
ifp phigher
{
	set temp YES
	ifspritepal 4 ifl player[].posz float set temp NO
	ldist xydist THISACTOR player[].i
	ife temp YES
	ifl xydist 2816 // 2304
	{
		setp[].jumping_toggle 1
		
		ife pchar 2 
		{
			set glidetime GLIDEMAX
			set glideboost 0
		}
		ife mtype NO { set mtype YES setp[].poszv -5120 screensound GLIDEUP } else
		ifg player[].poszv -2048
		{
			getp[].poszv zdist
			sub zdist botclip // 512
			set tempb botclip, mul tempb -4
			ifl zdist tempb set zdist tempb
			setp[].poszv zdist
			setp[].falling_counter 0
			
			ife initflags YES set nofalldamage YES
		}
	}
}

ife SPRITELOTAG 0
add countvar 1
ifge countvar 60
{
	espawn UPDRAFT_SPRITE
	setav[RETURN].myspawner THISACTOR
	ifspritepal 4 setav[RETURN].float float
	set countvar 0
}
enda

spritenoshade UPDRAFT_SPRITE
spritenopal UPDRAFT_SPRITE
move UPDRAFTVEL 0 -128
useractor notenemy UPDRAFT_SPRITE 0

ifmove 0
{
	seta[].shade -100
	seta[].alpha 96
	seta[].blend 1
	sizeat 40 38
	move UPDRAFTVEL geth getv
	cstat 514
}

ifn float 0 ifl sprite[].z float killit
ifn actorvar[myspawner].SPRITELOTAG 0 killit
ifnotmoving killit
ifceilingdistl 8 killit

enda

action ABIGSMOKEFRAMES 0 15 1 1 6
action ABIGSMOKEFAST 0 15 1 1 3
action ABIGSMOKESLOW 0 15 1 1 10
move BIGSMOKEVEL
move PLAYERSMOKEVEL 0 -8
move SMOKERVEL 32 -32
move TIRESMOKEVEL 64 -10
move BURNSMOKEVEL 96 -64
move BURN2SMOKEVEL 48 -32
move COPTER_DUST 192

useractor notenemy BIGSMOKE 0

ifspawnedby TEAMSPAWNER
{
	move 0
	action 0
	resetcount
	resetactioncount
	sizeat 40 40
	seta[].htpicnum BIGSMOKE
}

ifmove 0
{
	ifg Numsprites 15616 killit
	move BIGSMOKEVEL
	ifspritepal 76 
	{
		geta[].z z, sub z 5120, seta[].z z
		move BURN2SMOKEVEL geth getv
		cstator 2
		rand angvar 64
		sub angvar 32
		add angvar sprite[].ang
		seta[].ang angvar
	}
	ifspritepal 90 
	{
		sound STEAMRELEASE
		geta[].z z, sub z 5120, seta[].z z
		move BURN2SMOKEVEL geth getv
		cstator 2
		sizeat 96 96
		rand angvar 64
		sub angvar 32
		add angvar sprite[].ang
		seta[].ang angvar
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 1
			ife actorvar[spriteid].team 0
			{
				dist xydist THISACTOR spriteid
				ifl xydist 2048
				{
					set B actorvar[spriteid].inithp
					set temp 1024
					mul temp plevel
					div temp B
					set TMP_A temp
					ifg temp 0
					{
						getav[spriteid].stun tempb
						add tempb temp
						setav[spriteid].stun tempb
						shiftl temp 1
						getav[spriteid].bleeding tempb
						ifg tempb 0 mul tempb -1
						sub tempb TMP_A
						setav[spriteid].bleeding tempb
						geta[spriteid].mdflags temp
						orvar temp 16
						seta[spriteid].mdflags temp
					}
				}
			}
			nextspritestat spriteid spriteid
		}
	}
	action ABIGSMOKEFRAMES
	
	ifl sprite[].xrepeat 8 sizeat 40 40
	else ifg sprite[].xrepeat 64 action ABIGSMOKESLOW
	cstat 128
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	seta[].blend 1
	geta[].htflags temp
	orvar temp 8192
	seta[].htflags temp
	switch sprite[].htpicnum
	case SHELLY 
	case DEABOT
		sizeat 24 24 break
	case CARBONIMPACT
		cstator 2
		seta[].blend 0
	break
	case APLAYER
		move PLAYERSMOKEVEL geth getv
		seta[].alpha 64
	break
	case RPG  
		seta[].blend 254
		cstator 2 sizeat 20 20 
		action ABIGSMOKEFAST 
		ifpdistl 1560 killit 
		seta[].alpha 116
	break
	case SMOKER
		spriteflags 4
		move SMOKERVEL randomangle getv
		action ABIGSMOKESLOW
	break
	case PIGSUV 
		ifspritepal 4
		{
			spritepal 0
			spriteflags 4
			seta[].shade 20
			move BURNSMOKEVEL randomangle getv 
		}
		else
		move TIRESMOKEVEL geth getv randomangle 
		cstator 2 
	break
	case RECONWRECK
	case TANKWRECK
		spriteflags 4
		seta[].shade 20
		move BURNSMOKEVEL randomangle getv 
	break
	
	case SERIOUSBOMB
		sizeat 64 64
		spriteflags 4
		seta[].shade 18
		ifrnd 96 debris SCRAP1 1
		ifrnd 96 debris SCRAP2 1
		ifrnd 96 debris SCRAP3 1
		ifrnd 96 debris SCRAP5 1
		ifrnd 96 debris SCRAP6 1
	break

	case BURNING
	
		spriteflags 4
		seta[].shade 20
		move BURN2SMOKEVEL randomangle getv 
	break
	case NUKEEXPLOSION
		sizeat 64 64
		spriteflags 4
		seta[].shade 18
		ifrnd 128 debris SCRAP2 1
		ifrnd 128 debris SCRAP3 1
		ifrnd 128 debris SCRAP4 1
		ifrnd 128 debris SCRAP5 1
		ifrnd 48 { ifrnd 128 spawn BURNING else spawn BURNING2 }
	break
	case NUKEENDING
		sizeat 250 250
		
		ifpdistl 10240
		{
		ifrnd 16 debris SCRAP2 1
		ifrnd 16 debris SCRAP3 1
		ifrnd 16 debris SCRAP4 1
		ifrnd 16 debris SCRAP5 1
		}
		else seta[].alpha 160
		
	break
	case SCUBATROOP
	
		seta[].blend 0
		sizeat 32 32
		cstator 514
		geta[].z z
		sub z 4096
		seta[].z z
	break
	case BLACKHAWK
		move COPTER_DUST geth
		sizeat 18 14
		seta[].blend 0
		cstator 514
	break
	case GUNSHIP
		move TIRESMOKEVEL geth
		seta[].blend 0
		cstator 2
		seta[].alpha 64
	break
	case ICEBLOCK
		move SMOKERVEL randomangle getv
		action ABIGSMOKESLOW
		cstator 2
		spritepal 1
		sizeat 64 64
	break
	endswitch
	ife mtype 1
	{
		cstat 130
		sizeat 128 128
		spritepal 89
		seta[].shade -64
		action ABIGSMOKESLOW
	}
}
// ifnotmoving killit
ifactioncount 15 killit

enda

action BIGSMOKE2FRAMES 0 20 1 1 6

useractor notenemy BIGSMOKE2 0 BIGSMOKE2FRAMES

ifmove 0
{	
	cstat 0
	ifspritepal 0 spritepal 12
	move STOPPED
	ifspawnedby WATERIMPACT cstat 514
	else ifspawnedby BLOODEXP sizeat 48 48
	else
	{
		sizeat 40 40
		cstat 2
	}
	ifrnd 128 cstator 4
}

ifactioncount 20 killit

enda

move SMOKEVEL 8 -16
move ENGINE_SMOKE 64 -64
move RAD_SMOKE 32 -36
move RAD_SMOKE2 38 -16
move SMOKESHOOTOUT -192
action SMOKEFRAMES 0 4 1 1 10
actor SMALLSMOKE 0 SMOKEFRAMES

  ifspawnedby SHOTSPARK1
  {
	  ifonwater
	  iffloordistl 4 killit
	  
	  geta[].owner spriteid
	  ifn spriteid -1
	  {
		ifn sprite[spriteid].htg_t 8 -1
		killit
	  }
  }
  
  ifmove 0
  {
    ifg Numsprites 15616 killit
	switch sprite[].htpicnum
	case CARXPLODE
	case TERMLEGPART1
	case TERMLEGPART2
	case TERMLEGPART3
		sizeat 42 42 spriteflags 4 seta[].shade 20 cstator 2
		move SMOKEVEL geth getv faceplayer
		seta[].alpha 128
	break
	case MECHDEBRIS1
	case MECHDEBRIS2
		sizeat 42 42 spriteflags 4 seta[].shade 20 cstator 2
		seta[].alpha 172
		move SMOKEVEL geth getv faceplayer
	break
	case RECON
		move SMOKESHOOTOUT geth
	break
	case SECTOREFFECTOR
		 move ENGINE_SMOKE geth getv
	break
	case RADWOUND
		ifrnd 128
	    move RAD_SMOKE randomangle getv
		else
		move RAD_SMOKE2 randomangle getv
		cstator 2
	break
	case CARBONBEAM
		move STOPPED
		cstator 2
	break
	
	default
		 move SMOKEVEL geth getv faceplayer
	break
	endswitch
	
	ifspritepal 4 { spritepal 0 spriteflags 4 seta[].shade 20 }
    ifrnd 128 cstator 4
	ifrnd 128 cstator 8
    
    ifspawnedby RPG
	{
	  espawn BIGSMOKE
	  seta[RETURN].htpicnum RPG
	  killit
	}
	  
  }
  
  ifpdistl 1596
    ifspawnedby RPG
      killit
  ifactioncount 4
    killit
enda

// action SHRINKERFRAMES 0 4 1 1 10
action SHRINKERFRAMES 15164 8 1 1 8
actor SHRINKEREXPLOSION 0 SHRINKERFRAMES

  ifmove 0
  {
      move STOPPED
	  ifspritepal 0 spritepal 11
	  seta[].blend 1
	  cstator 2
	  geta[].yrepeat y, div y 2, seta[].yrepeat y
	  geta[].xrepeat x, div x 2, seta[].xrepeat x
  }
  ifn myvictim -1
  {
	set x tiledata[sprite[myvictim].picnum].ysize
	mul x sprite[myvictim].yrepeat
	shiftr x 6 // 5
	clamp x 8 255
	seta[].yrepeat x
	seta[].xrepeat x
	
	ife actorvar[myvictim].shrunken 0
	{
		ifspritepal 10 seta[myvictim].htpicnum SHOTSPARK1 else
		seta[myvictim].htpicnum SHRINKSPARK
		seta[myvictim].htowner player[].i
		ife sprite[myvictim].htextra -1 
		{
			ifspritepal 10
			seta[myvictim].htextra 1
			else
			seta[myvictim].htextra 3
		}
	}
  }
  ifactioncount 8
    killit
enda

action EXPLOSION5FRAMES  0  36  1  1  4
action EXP5FASTFRAMES  0  36  1  1  2

useractor notenemy EXPLOSION5 0 EXPLOSION5FRAMES


ifaction 0 action EXPLOSION5FRAMES
ifmove 0
{
	move STOPPED
	seta[].blend 1
	cstator 2
	seta[].shade -127
	ifl sprite[].xrepeat 4
	sizeat 18 16
	ifspawnedby ARMLIZTROOP
	{
		cstator 128
		sizeat 6 5
		action EXP5FASTFRAMES
	}
	else
	ifge sprite[].htpicnum FLAMEPROJ2 ifle sprite[].htpicnum FLAMEPROJ
	// ifspawnedby FLAMEPROJ 
	{
		cstator 128
		sizeat 10 8
		stopsound PISTOL_BODYHIT
		ife projectile[FLAMEPROJ2].pal 27 spritepal 27
		ifrnd 36
		{
			ifspritepal 27 spawn BIGSMOKE else
			ifspritepal 1 spawn BIGSMOKE
			else
			{
				ifrnd 128 espawn BURNING else
				espawn BURNING2
				ifspawnedby FLAMEPROJ2 setav[RETURN].team 1
			}
		}
		findnearactor ICEBLOCK 1024 spriteid
		ifn spriteid -1 sizeat 9 8
	}
}
ifn sprite[].pal 1 ifn sprite[].pal 27
state spawnsmokeonscrap

ifactioncount 35 killit

enda

action EXPLOSION6FRAMES  0  30  1  1  4

useractor notenemy EXPLOSION6 0 EXPLOSION6FRAMES

ifmove 0
{
	move STOPPED
	seta[].blend 1
	cstator 2
	seta[].shade -127
	ifl sprite[].xrepeat 4
	sizeat 18 16
}
state spawnsmokeonscrap

ifactioncount 30 killit

enda


action NUKEFRAMES2  8440  21  1  1  16

// action NUKEFRAMES3  13694 64  1  1  4
// action NUKEFRAMES4  13694 64  1  1  4

action NUKEFRAMES3  13690 65  1  1  3
action NUKEFRAMES4  13690 65  1  1  3
action NUKEFRAMES3B  13755 15  1  1  16
action NUKEFRAMES4B  13755 15  1  1  16


action EXPLOSION_FRAMES 0 20 1 1  4
action EXPLOSIONB_FRAMES 6558 17 1 1 6
actor EXPLOSION2 1 EXPLOSION_FRAMES
ifmove 0
{
	seta[].blend 1
	// cstator 2
	// seta[].alpha 32
	ifspawnedby FIREEXT
	{
		seta[].blend 0
		seta[].alpha 80
		spritepal 26
		cstator 514
		espawn PART_EXP
		seta[RETURN].pal 12
		espawn PART_EXP
		seta[RETURN].pal 12
		spritepal 33
		ifl Numsprites 15616
		debris SCRAP3 8
		getlastpal
	}
	else
	ifspawnedby NUKEEXPLOSION
	{
		ifawayfromwall move STOPPED else
		move DRIFTTOPLAYER faceplayer
		cstator 1024
		ife rendmode 0
		{
		action NUKEFRAMES2
		sizeat 255 224
		spritepal 76
		}
		else
		{
		action NUKEFRAMES3
		sizeat 72 64
		// getflorzofslope sprite[].sectnum sprite[].x sprite[].y z
		// getceilzofslope sprite[].sectnum sprite[].x sprite[].y z2
		// sub z z2
		// abs z
		// shiftr z 11
		// ifg z 255 set z 255
		// ifl z 64 set z 64
		// seta[].yrepeat z
		// mul z 4 div z 3
		// ifg z 255 set z 255
		// seta[].xrepeat z
		}
		ifrnd 128 cstat 0 else cstat 4
		// cstator 2
		// move STOPPED
		break
	}
	else
	ifspawnedby BOMBSECTOR
	{
		action NUKEFRAMES4
		sizeat 40 32
		move STOPPED
		cstat 0
		break
	}
	else
	ife mtype BOMBSECTOR
	{
		action NUKEFRAMES4
		sizeat 24 20
		move STOPPED
		cstat 0
		break
	}
	else
	ifspawnedby ARROWPROJ
	{
		action EXPLOSIONB_FRAMES
		geta[].xrepeat x
		add x 12
		geta[].yrepeat y
		add y 12
		seta[].xrepeat x
		seta[].yrepeat y
		move STOPPED
		break
	}
	else
	ifspawnedby STICKYBOMB
	{
		action EXPLOSIONB_FRAMES
		sizeat 32 32
		ifrnd 128 cstator 4
		move STOPPED
		break
	}
	else
	ifspawnedby CASTERPROJ
	{
		action EXPLOSIONB_FRAMES
		ifrnd 128 cstator 4
		move STOPPED
		cstator 130
		set angvel sprite[].ang
		set countvar 0
		setprojectile[BLASTBALL].extra 16
		setprojectile[BLASTBALL].hitradius 0
		setprojectile[BLASTBALL].isound MEGAIMPACT1
		setprojectile[BLASTBALL].velmult 1
		sizeat 0 0 // to make shots come out low
		whilevarn countvar 16
		{
			switch countvar
			case 1 case 2 case 3 case 4 case 5 case 6
			case 10 case 11 case 12 case 13 case 14 case 15
				eshoot BLASTBALL
				setav[RETURN].team 1
				seta[RETURN].ang angvel
				seta[RETURN].zvel 0
				seta[RETURN].htowner player[].i
				seta[RETURN].htpicnum APLAYER
				setav[RETURN].team 1
				ifvarand novaupgrades[SHOTGUN_WEAPON] 8
				setav[RETURN].mtype CASTERPROJ
			break
			endswitch
			add angvel 128
			add countvar 1
		}
		setprojectile[BLASTBALL].extra 32
		setprojectile[BLASTBALL].hitradius 1280
		setprojectile[BLASTBALL].isound SHOCKBALLIMPACT
		setprojectile[BLASTBALL].velmult 2
		sizeat 64 64
		break
	}
	else
	ifspawnedby SHOTSPARK1
	ife pchar 2
	ife player[].curr_weapon DEVISTATOR_WEAPON
	{
		// hitradius 1280 1 4 8 12
		headspritestat spriteid 5
		whilevarn spriteid -1
		{
			ifl sprite[spriteid].htextra 1
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1280
				{
					seta[spriteid].htextra 4
					seta[spriteid].htpicnum RADIUSEXPLOSION
					seta[spriteid].htowner player[].i
				}
			}
			nextspritestat spriteid spriteid
		}
		headspritestat spriteid 6
		whilevarn spriteid -1
		{
			ifl sprite[spriteid].htextra 1
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1280
				{
					seta[spriteid].htextra 4
					seta[spriteid].htpicnum RADIUSEXPLOSION
					seta[spriteid].htowner player[].i
				}
			}
			nextspritestat spriteid spriteid
		}
	}
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		geta[].xrepeat x div x 2 seta[].xrepeat x
		geta[].yrepeat y div y 2 seta[].yrepeat y
	}
	ife mtype 2 killit
	ifspawnedby BOMBLET sizeat 20 20
	ifinwater ifspritepal 0
	ifn mtype WESBOT
	{
		ifl sprite[].xrepeat 10 set mtype 1
		geta[].xrepeat temp
		mul temp 3 div temp 2
		seta[].xrepeat temp
		geta[].yrepeat temp
		geta[].z z
		mul temp 160
		add z temp
		seta[].z z
		espawn EXPLOSION2
		ifn RETURN -1
		{
			seta[RETURN].pal 1
			seta[RETURN].xrepeat sprite[].xrepeat
			seta[RETURN].yrepeat sprite[].yrepeat
			seta[RETURN].cstat 2
			geta[RETURN].yrepeat temp
			geta[RETURN].z z
			mul temp 160
			add z temp
			seta[RETURN].z z
			setav[RETURN].mtype mtype
		}
		spritepal 1
		cstator 138
		iffloordistl 8 killit
	
	}
	// else iffloordistl 32 nullop else
	// ifrnd 128 
	// {
		// action EXPLOSIONB_FRAMES
		// geta[].xrepeat x
		// add x 6
		// geta[].yrepeat y
		// add y 6
		// seta[].xrepeat x
		// seta[].yrepeat y
	// }
	ifspawnedby SHOTSPARK1 
	{
		action EXPLOSIONB_FRAMES
		geta[].x x
		geta[].y y
		ifg player[].posx sprite[].x add x 256
		else sub x 256
		ifg player[].posy sprite[].y add y 256
		else sub y 256
		seta[].x x
		seta[].y y
	}
	move STOPPED
	ifg sprite[].xrepeat 24
	{
		ifpdistl 4096 quake 20
	}
	ifspawnedby LILY
	{
		sizeat 18 18
		cstator 2
	}
	ifaction EXPLOSION_FRAMES
	ifspritepal 0
	{
		ifspawnedby WESBOMB
		{
			spritepal 1
			cstat 0
		}
		iffloordistl 4
		{
			cactor EXPLOSION6
			action EXPLOSION6FRAMES
			cstat 2
			ifrnd 128 cstator 4
		}
		else
		{
			cactor EXPLOSION5
			action EXPLOSION5FRAMES
		}
		ifspawnedby RAGNAROCKET
		{
			set mtype WESBOT
			seta[].htpicnum TERMINATORCRAWLING
			spawn FIREBALL3D
		}
		cstator 2
		seta[].shade -127
		geta[].xrepeat x, mul x 2, div x 3
		
		geta[].yrepeat y, mul y 2, ifg y 20 sub y 2, div y 3
		ife mtype WESBOT { mul x 3 mul y 3 }
		seta[].xrepeat x
		seta[].yrepeat y
		
		ife initsprite THERMAL
		{
			ife pchar 0 sizeat 144 128
			sizeat 128 96
			set mtype WESBOT
			set team 1
			stopsound THERMALWARN
		}
		
		ife mtype WESBOT 
		{
			flash
			ifn initsprite THERMAL
			{
				espawn FIRERING
				setav[RETURN].mtype 2
			}
			stopsound ROCKETLOOP
			ifpdistl 10240 quake 50
			
			
			sound NUKESOUND
			setactorsoundpitch THISACTOR NUKESOUND 768
			
			set x2 5120
			ife initsprite THERMAL ifvarand dukeupgrades[HANDBOMB_WEAPON] 4 add x2 1024 else
			ifspawnedby TERMINATORCRAWLING sub x2 512
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				geta[spriteid].z z
				sub z sprite[].z
				abs z
				ifl z 131072 // 65536
				{
					ldist xydist THISACTOR spriteid
					ifl xydist x2
					{
						ife initsprite THERMAL 
						{
							set xydist2 200000 
							ife pchar 0 ifvarand dukeupgrades[HANDBOMB_WEAPON] 4 add xydist2 50000
						}
						else
						set xydist2 400000
						ife xydist 0 set xydist 1
						div xydist2 xydist
						ifg xydist2 644 set xydist2 644
						
						geta[spriteid].htextra B
						add B xydist2
						seta[spriteid].htextra B
						seta[spriteid].htpicnum RPG
						// setav[spriteid].padang 1337
						ifspawnedby TERMINATORCRAWLING seta[spriteid].htowner sprite[].owner else
						seta[spriteid].htowner player[].i
						seta[spriteid].htang sprite[].ang
						
						getav[spriteid].stun tempb
						add tempb 55
						setav[spriteid].stun tempb
						
						ife initsprite THERMAL
						ife actorvar[spriteid].team 0
						ife pchar 3
						{
							getav[spriteid].burning tempb
							set tempc mweaplevel[5]
							mul tempc 10
							add tempc 40
							add tempb tempc
							setav[spriteid].burning tempb
							ifrnd 128 espawn BURNING else espawn BURNING2
							setsprite RETURN sprite[spriteid].x sprite[spriteid].y sprite[spriteid].z
							setav[RETURN].team 1
						}
						
					}
					
				}
				ife spriteid player[].i set spriteid -1 else
				{
					nextspritestat spriteid spriteid
					ife spriteid -1 ifcansee { ifspawnedby TERMINATORCRAWLING set spriteid player[].i }
				}
			}
			set countvar 0
			whilevarn countvar 20
			{
				add countvar 1
				espawn BIGSMOKE
				rand x2 8192
				sub x2 4096
				add x2 sprite[].x
				seta[RETURN].x x2
				rand x2 8192
				sub x2 4096
				add x2 sprite[].y
				seta[RETURN].y x2
			}
			
		}
		ife stasis 0
		changespritestat THISACTOR 1
		break
	}
	
}
  ifaction NUKEFRAMES4
  {
    ifawayfromwall move STOPPED
	ifactioncount 65 action NUKEFRAMES4B
	break
  }
  ifaction NUKEFRAMES4B
  {
	ifactioncount 15 killit
	geta[].alpha temp add temp 3 seta[].alpha temp
	break
  }
  ifaction NUKEFRAMES3
  {
    ifawayfromwall move STOPPED
	ifactioncount 65 action NUKEFRAMES3B
	break
  }
  ifaction NUKEFRAMES3B
  {
	ifactioncount 15 killit
	geta[].alpha temp add temp 3 seta[].alpha temp
	break
  }
  ifaction NUKEFRAMES2
  {
    ifactioncount 21 killit
    break
  }
  ifspritepal 1 ifactioncount 9 // under water
  ife mtype 0
  {
	sizeto 255 255
	geta[].xrepeat temp
	mul temp 32 div temp 33
	seta[].xrepeat temp
	geta[].z z
	geta[].yrepeat temp
	mul temp 14
	add z temp
	seta[].z z
	cstator 512
	ifactioncount 19 killit
  }
  
  state spawnsmokeonscrap

  ifaction EXPLOSIONB_FRAMES
	ifactioncount 17
		killit
  ifactioncount 20
    killit
enda

// NUKE EXPLOSION

spritenoshade NUKEEXPLOSION
spritenopal NUKEEXPLOSION
spritenoshade FIRERING
spritenopal FIRERING

useractor notenemy FIRERING 0

ifmove 0
{
	move STOPPED
	cstat 34
	geta[].z z
	ife mtype 2 sub z 8192 else
	sub z 14336
	seta[].z z
	seta[].shade -127
}
geta[].xrepeat x add x 4 seta[].xrepeat x
geta[].yrepeat y add y 4 seta[].yrepeat y
ifn mtype 0 ifcount 32 killit
ifcount 64 killit
geta[].ang angvar
add angvar 33
seta[].ang angvar


enda

move NUKEVELS 16
action NUKEFRAMES  0  21  1  1  16
useractor notenemy NUKEEXPLOSION

ifmove 0
{
	move NUKEVELS faceplayer
	action ANULLACTION
	sizeat 32 32
	ifspawnedby VERTROCKET { flash set mtype 1 }
	else palfrom 63 63 63 63
	cstat 32768
	ifrnd 128 cstator 4
	
	seta[].shade -127
	seta[].blend 1
	ifspritepal 0 spritepal 76
	
	ife mtype 1 
	{
		findnearactor BOSS3 5632 spriteid
		ifn spriteid -1
		geta[spriteid].htextra savz
		hitradius 5632 50 100 150 200
		ifn spriteid -1 seta[spriteid].htextra savz
	}
	else
	{
		// ifn mtype 2
		hitradius 6144 500 1000 1500 2000
		
		set x2 8192

		set spriteid 0
		whilevarn spriteid 16384
		{
			ifl sprite[spriteid].statnum 3
			ifg sprite[spriteid].statnum 0
			{
				geta[spriteid].z z
				sub z sprite[].z
				abs z
				ifl z 131072 // 65536
				{
					ldist xydist THISACTOR spriteid
					ifl xydist x2
					{
						set xydist2 2097152
						ife xydist 0 set xydist 1
						div xydist2 xydist
						ifl xydist2 256 set xydist2 256
						ifg xydist2 3000 set xydist2 3000
						add xydist2 256
						
						seta[spriteid].htextra xydist2
						seta[spriteid].htpicnum RPG
						ifge xydist2 sprite[spriteid].extra
							setav[spriteid].padang 1337
						seta[spriteid].htowner player[].i
						seta[spriteid].htang sprite[].ang
						getav[spriteid].burning tempb
						add tempb 180
						setav[spriteid].burning tempb
						getav[spriteid].stun tempb
						add tempb 90
						setav[spriteid].stun tempb
					}
					
				}
			}
			add spriteid 1
		}
	}
	set startx 2048
	ife rendmode 4
	{
		espawn SMALLSMOKE
		seta[RETURN].lotag 49
		seta[RETURN].hitag 16383
		seta[RETURN].xrepeat 2
		seta[RETURN].yrepeat 2
		seta[RETURN].xvel 255 // R value
		seta[RETURN].yvel 208 // G value
		seta[RETURN].zvel 208 // B value
		setav[RETURN].myspawner THISACTOR
		geta[].picnum picnum
		setav[RETURN].mtype picnum
		seta[RETURN].picnum SECTOREFFECTOR
		seta[RETURN].cstat 0
		changespritestat RETURN 3
	}
	seta[].mdflags 16
	iffloordistl 8 { geta[].z z sub z 3072 seta[].z z }
	globalsound EXPLODE4
}

ifaction ANULLACTION 
{
	ifactioncount 3 
	{ 
		// stopallsounds 
		globalsound NUKESOUND 
		action NUKEFRAMES 
		spawn EXPLOSION2
		//gets[].ceilingbunch mysector
		// nextsectorneighborz sprite[].sectnum sprite[].z -1 -1
		// ifn RETURN -1
		// {
			// set savedvalue RETURN
			// espawn EXPLOSION2
			// seta[RETURN].sectnum savedvalue
			// setav[RETURN].savedvalue savedvalue
			// al savedvalue
			// geta[].sectnum mysector al mysector
		// }
		quake 90
		espawn FIRERING
		ife mtype 1 setav[RETURN].mtype 1
		// ife mtype 2 setav[RETURN].mtype 1
	}
	break
}

// changespritesect THISACTOR player[].cursectnum

ifaction NUKEFRAMES
{
	ifactioncount 21 killit else
	ifactioncount 15 nullop else
	ifn mtype 1
	// ifn mtype 2
	{
		flash
		geta[].z savz
		sub savz 4096
		seta[].z savz
		set countvar 0
		add startx 256
		geta[].x savx
		add savx startx
		rand angvar 2047
		ifg framerate 30
		{
			whilevarn countvar 8
			{	
				espawn BIGSMOKE
				add angvar 256
				rotatepoint sprite[].x sprite[].y savx sprite[].y angvar x2 y2
				setsprite RETURN x2 y2 savz
				add countvar 1
			}
		}
		set x2 startx
		add x2 512
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			geta[spriteid].z z
			sub z savz
			abs z
			ifl z 65536
			{
				ldist xydist THISACTOR spriteid
				ifg xydist startx ifl xydist x2
				{
					geta[spriteid].htextra tempb
					add tempb 100
					seta[spriteid].htextra tempb
					seta[spriteid].htpicnum RPG
					ifge tempb sprite[spriteid].extra 
						setav[spriteid].padang 1337
					seta[spriteid].htowner player[].i
					seta[spriteid].htang sprite[].ang
					getav[spriteid].burning tempb
					add tempb 90
					setav[spriteid].burning tempb
					getav[spriteid].stun tempb
					add tempb 30
					setav[spriteid].stun tempb
				}
				
			}
			nextspritestat spriteid spriteid
		}
		
		
		add savz 4096
		seta[].z savz
	}
}

enda


actor EXPLOSION2BOT 1 EXPLOSION_FRAMES
  
  seta[].blend 1
  cstator 2
  ifinwater spritepal 1
  else ifspawnedby EXPLOSION2 killit
  else ifspawnedby RPG killit
  ifactioncount 20
    killit
enda

action FFLAME_FR 0 16 1 1 1
action FFLAME 0 1 1 1 1

actor FLOORFLAME 0 FFLAME_FR
  ifaction FFLAME_FR
  {
    ifpdistl 1024
      hitradius 1024 WEAKEST WEAKEST WEAKEST WEAKEST
    ifactioncount 16
      action FFLAME
  }
  ifaction FFLAME
    ifrnd 4
  {
    action FFLAME_FR
    sound CAT_FIRE
    resetactioncount
  }
enda

useractor notenemy HEALTHBOX 100
ife myspawner -1 break
ifg sprite[myspawner].statnum 2 killit
ife actorvar[myspawner].monstatus 2 killit
ifg sprite[].htextra 0
{
	changespritestat myspawner 1
	geta[].htextra temp
	geta[myspawner].htextra tempb
	ife tempb -1 set tempb 0
	add tempb temp
	seta[myspawner].htextra tempb
	seta[myspawner].htowner sprite[].htowner
	seta[myspawner].htang sprite[].htang
	seta[myspawner].htpicnum sprite[].htpicnum
	ifhitweapon strength 100
}
enda

action RIP_F 0 8 1 1 1
actor FORCERIPPLE 0 RIP_F
  ifactioncount 8
    killit
enda

useractor notenemy PHONEDROP 0

ifmove 0
{
	cstat 32
	sizeat 32 32
}

fall

enda

spritenoshade TRANSPORTERSTAR

action TRANSFOWARD 0  6  1  1  2
action TRANSBACK   5  6  1 -1  2
actor TRANSPORTERSTAR 0 TRANSFOWARD
seta[].shade -127
seta[].alpha 128
seta[].blend 1

ife initsprite FAKEDUKE killit
ifspawnedby RESPAWN ifge initsprite LIZTROOP 
ifn initsprite GREENSLIME ifn initsprite EGG
{
	espawn NEWTRANS 
	set y tiledata[initsprite].ysize
	setav[RETURN].initsprite y
	killit 
}

ifvare mtype 3 killit

ifvare mtype 1
{
	sizeat 3 3
	cstat 2
	
}
else ifvare mtype 0
{
	ifspawnedby MANDOLASER { set mtype 2 spritepal 23 cstator 2 break }
	ifspawnedby PLAYERPLASMA { set mtype 2 spritepal 77 cstator 2 break }
	ifspawnedby PLASMAPROJ { set mtype 2 spritepal 77 cstator 2 break }
	ifspawnedby FREEZEBLAST { set mtype 2 break }
	ifspawnedby FIRELASER { set mtype 2 spritepal 2 break }
	ifspawnedby FIRELASER2 { set mtype 2 spritepal 2 break }
	ifspawnedby FIRELASER3 { set mtype 2 spritepal 2 break }
	ifspawnedby EDFLASER { set mtype 2 spritepal 2 break }
	ifspawnedby DRWILY { move STOPPED faceplayer cstat 530 set mtype 2 spritepal 1 sizeat 16 32 break }
	// cstat 32768
	// addvar initsprite 1
	// set lastang 0
	// whilevarn lastang 8
	// {
		// espawn TRANSPORTERSTAR
		// setav[RETURN].initsprite initsprite
		// randvar x 192
		// randvar y 256
		// randvar z 6144
		// ifrnd 128 mulvar z -1
		// ifrnd 128 mulvar x -1
		// ifrnd 128 mulvar y -1
		// getactor[].x temp
		// add temp x
		// seta[RETURN].x temp
		// getactor[].y temp
		// add temp y
		// seta[RETURN].y temp
		// getactor[].z temp
		// add temp z
		// seta[RETURN].z temp
		// setav[RETURN].mtype 1
		// seta[RETURN].cstat 32768
		// ifspritepal 91 seta[RETURN].pal 91
		// else
		// {
			// ifrnd 128 seta[RETURN].pal 2 else seta[RETURN].pal 7
		// }
		// addvar lastang 1
		
	// }
	// ifvare initsprite 30 killit
	ife mtype 0 { spawn GLARESTAR killit }
}

  ifaction TRANSFOWARD
  {
    ifactioncount 6
      action TRANSBACK
  }
  else
    ifactioncount 6
      killit
enda

action BEAMFOWARD  0  4  1  1  9
actor TRANSPORTERBEAM 0 BEAMFOWARD
  sizeto 32 64
  sizeto 32 64
  sizeto 32 64
  ifactioncount 4
    killit
enda

move TARGETSPIN
defstate targetcode
	
	ifmove 0
	{
		ifspawnedby APLAYER
		{
			// state spawnmysignpost
			set monstatus 1
			sizeat 30 30
			cstator 256
			strength 15000
		}
		geta[].pal countvar
		move STOPPED
		spriteflags 1056769
		geta[].ang angvel
		ifn starty 0
		{
			set SPRITELOTAG startx
			set mtype 9
			set botclip 25
		}
	}
	state monsterai
	
	ifn starty 0
	{
		set monstatus 2
		set monxp 0
		ifmove STOPPED
		{
			set target reconpoints[SPRITELOTAG]
			geta[target].x x2
			geta[target].y y2
			geta[].x x
			geta[].y y
			sub x2 x
			sub y2 y
			getangle angvar x2 y2
			
			ldist xydist THISACTOR target
		
			ifvarl xydist 512
			{
				add SPRITELOTAG 1
				ifg SPRITELOTAG highrecon set SPRITELOTAG 0
			}
			else
			{
				cos xvel angvar
				sin yvel angvar
					  
				shiftr xvel mtype
				shiftr yvel mtype
				
				geta[].z z
				sub z sprite[target].z
				shiftr z 1
						 
				movesprite THISACTOR xvel yvel z CLIPMASK0 RETURN
			}
		}
	}
	
	ife attmode YES ife VOLUME 5 ife LEVEL 11
	ife starty 0
	{
		ife bigmsg 1137 ifg bigmsgcount 0 
		ifn SPRITELOTAG 0
		{
			strength 0
			seta[].htextra 10
			seta[].htpicnum STUCKARROW
		}
	}

	ifhitweapon 
	{
		ifn sprite[].htpicnum STUCKARROW 
		{
			sound CONCRETEHIT1
			move TARGETSPIN spin
			cstat 16
		}
		
		ifstrength 10 
		ife starty 0
		{
			ifn SPRITELOTAG 0 
			state activatechannel
			set SPRITELOTAG 0
		}
		
		ifn starty 0 
		{
			ifg mtype 4 sub mtype 1	
			set monxp botclip
			set monstatus 1
			state enemy_death
			set monstatus 1
			// add botclip 50
			mul botclip 2
			ifg botclip 300 set botclip 300
			ifl mtype 5
			{
				debris SCRAP3 3
				spritepal 2
				debris SCRAP3 3
				spritepal 8
				debris SCRAP3 3
				spritepal 4
				debris SCRAP3 4
				killit
			}
		}
	}
	
	ifmove TARGETSPIN 
	{
		ifangdiffl 512 spritepal 4 else seta[].pal countvar
		geta[].ang angvar
		add angvar 128
		seta[].ang angvar
		ifcount 15 
		{
			seta[].pal countvar
			move STOPPED geth
			seta[].ang angvel
			cstat 272
		}
	}

ends

eventloadactor NEWTROOPTARGET 
geta[].lotag SPRITELOTAG seta[].lotag 0 
geta[].extra startx // initial pigsuvpoint lotag target
ife startx -1 set startx 0
geta[].hitag starty // moving type
seta[].hitag 0
enda

eventloadactor NEWPIGTARGET 
geta[].lotag SPRITELOTAG seta[].lotag 0 
geta[].extra startx // initial pigsuvpoint lotag target
ife startx -1 set startx 0
geta[].hitag starty // moving type
seta[].hitag 0
enda

useractor notenemy NEWTROOPTARGET 900 0 state targetcode enda
useractor notenemy NEWPIGTARGET 900 0 state targetcode enda

spritenoshade RADWOUND
useractor notenemy RADWOUND 0

	ifmove 0
	{
		move STOPPED
		sizeat 8 8
		ifspawnedby BIGBOIPROJ sizeat 24 24
		seta[].shade -100
		rand countvar 6
		add countvar 4
	}
	cstat 128
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	// ifspritepal 0 ifrnd 64 spritepal 6 else spritepal 0

	ifn myvictim -1
	{

		ifg sprite[myvictim].statnum 2 { set myvictim -1 break }
		ife actorvar[myvictim].monstatus 2 { set myvictim -1 break }
		getactor[myvictim].x x
		getactor[myvictim].y y
		getactor[myvictim].ang angvar
		subvarvar angvar angvel
		// sub angvar 512
		setvarvar x2 x
		addvarvar x2 mtype
		rotatepoint x y x2 y angvar x y

		setactor[].x x
		setactor[].y y
		
		getactor[myvictim].z z
		
		subvarvar z shotpitch
		setactor[].z z
		
		ifrnd 64
		{
			espawn SMALLSMOKE seta[RETURN].pal 8
			ifrnd 84
			{
				geta[myvictim].htextra temp
				ifl temp 1 set temp 1 else
				add temp 1
				ife sprite[].xrepeat 24 { add temp 2 ife temp 3 seta[myvictim].htpicnum RADWOUND }
				seta[myvictim].htextra temp
				seta[myvictim].htowner player[].i
				ife temp 1
				seta[myvictim].htpicnum RADWOUND
				ife sprite[myvictim].htpicnum RADWOUND seta[myvictim].htowner player[].i
				sub countvar 1
			}
		}
		
		
		ifl countvar 1 set myvictim -1
	}
	else ifcount 20 sizeto 0 0


enda

spritenoshade WOUNDSPRITE
useractor notenemy WOUNDSPRITE 0

	ifmove 0
	{
		move STOPPED
		sizeat 16 14
		cstator 128
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
		spawn AABLOOD
	}
	ifcount 4 cstator 2
	ifcount 6 cstator 512
	ifcount 8 killit

enda

action ABLOODSPLASH  0  4  1  1  20

defstate bloodsplashcode

cstat 128

ifspawnedby SHOTSPARK1
{	
	sizeto 20 24 sizeto 20 24
	ifactioncount 3 cstator 2
	ifactioncount 4 killit
	break
}
sizeto 72 72 sizeto 72 72
ifactioncount 4 
{
	state random_wall_jibs
	killit
}
ends

useractor notenemy BLOODSPLASH1 0 ABLOODSPLASH state bloodsplashcode enda
useractor notenemy BLOODSPLASH2 0 ABLOODSPLASH state bloodsplashcode enda
useractor notenemy BLOODSPLASH3 0 ABLOODSPLASH state bloodsplashcode enda

action AABLOODFRAMES  1  4  1  1  15
useractor notenemy AABLOOD 0 AABLOODFRAMES

cstat 128
sizeto 72 72 sizeto 72 72
ifactioncount 4 killit

enda

defstate parkmetercode

	ifaction 0
	{
		action ANULLACTION
		cstator 257
	}
	
	ifhitweapon
	{
		ifdead
		{
			set value 8
			state spawnimpcoins
			debris SCRAP1 3
			debris SCRAP6 2
			sound VENT_BUST
			cactor PARKMETERBROKE
		}
	}

ends

useractor notenemy PARKMETER 40 state parkmetercode enda
useractor notenemy PARKMETER2 40 state parkmetercode enda

action FIRE_FRAMES   -1 14 1 1 1
move FIREVELS

state firestate

  ife monstatus 2
  {
	cactor BIGSMOKE
	action 0
	move 0
  }
  ifaction 0
    ifrnd 16
    {
      action FIRE_FRAMES
      cstator 128
    }

  sleeptime 300         // Never let it fall to sleep

  ifspawnedby FIRE
  {
    ifgapzl 16
      break
  }
  else
    ifspawnedby FIRE2
  {
    ifgapzl 16
      break
  }

  ifinwater
    killit

  ifp palive
    ifpdistl 844
      ifrnd 32
        ifcansee
  {
    // soundonce DUKE_LONGTERM_PAIN
    // addphealth -1
	getp[].i spriteid
	  getav[spriteid].burning temp
	  add temp 1
	  setav[spriteid].burning temp
    palfrom 32 32
  }

  ifactor FIRE
  {
    ifspawnedby FIRE
      break
  }
  else
    ifactor FIRE2
      ifspawnedby FIRE2
        break

  iffloordistl 128
  {
    ifrnd 128
    {
      ifcount 84
        killit
                // This line(and the else below)
                // is not necessary, since actors
                // with zero x-size are automatically
                // deleted anyway!
      else
        ifcount 42
          sizeto 0 0
      else
        sizeto 32 32
    }
  }
  else
    killit

ends

useractor notenemy FIRE WEAK 0 FIREVELS state firestate enda
useractor notenemy FIRE2 WEAK 0 FIREVELS state firestate enda

eventloadactor FECES sizeat 12 12 enda
actor FECES
  ifcount 24
  {
    ifpdistl RETRIEVEDISTANCE
    {
      ifrnd SWEARFREQUENCY
        soundonce DUKE_STEPONFECES

      sound STEPNIT

      spawn BLOODPOOL   // Will be brown
      killit
    }
  }
  else
    sizeto 12 12
enda


state toughgalspeech
  ifrnd 64
  {
    ifnosounds
      soundonce TOUGHGALSND1
  }
  else
    ifrnd 64
  {
    ifnosounds
      soundonce TOUGHGALSND2
  }
  else
    ifrnd 64
  {
    ifnosounds
      soundonce TOUGHGALSND3
  }
  else
    ifnosounds
      soundonce TOUGHGALSND4
ends

action FEMSHRUNK
action FEMFROZEN1 1
action FEMGROW
action FEMFROZEN2
action FEMDANCE1 19 1 1 1 16
action FEMDANCE3 19 1 1 1 26
action FEMDANCE2 20 2 1 1 10
action FEMDANCE4 5  1 1 1 30
action FEMDANCE5 6  1 1 1 10
action FEMDANCE6 7  1 1 1 20
action FEMDANCE7 11 1 1 1 20
action FEMDANCE8 5  1 1 1 30
action FEMDANCE9 1  1 1 1 30
action FEMDANCE10  2 1 1 1 30
action FEMDANCE11A 2 1 1 1 30
action FEMDANCE11B 3 1 1 1 60
action FEMDANCE11C 2 1 1 1 30
action FEMDANCE12 223 1 1 1 30
action NURSEHEAL1 1 1 1 1 30
action NURSEHEAL2 2 1 1 1 60

action FEMANIMATESLOW 0 2 1 1 100
action TOUGHGALANIM   0 5 1 1 25
action FEMANIMATE 0 1 1 1 1
action FEMNEW1 4262 3 1 1 100
action FEMNEW2 4257 0 1 1 1
action FEMNEW3 4985 3 1 1 100

defstate femcode
  set team 3 // noncombatant
  ife monstatus 0 set monstatus 1
  set spawnprotect 0
  state monsterai
  ife gametype -1 seta[].htextra -1
  
  ifactor NAKED1 nullop
  else
    ifactor FEM6 nullop
  else
    ifactor NEWHANGBABE nullop else
	ifactor NEWSLIMEBABE nullop else
	ifactor NEWSLIMEBABE2 nullop else
	ifactor BLUEHANGBABE nullop else
	ifactor SCUBAGIRL nullop else
    {
      fall
	  switch sprite[].picnum
	  case BLOODYPOLE
	  case BLOODYPOLE2
	  case BLOODYPOLE3
	  case BLOODYPOLE4
        ifhitweapon
	  {
          ifdead
		  {
		    sound SQUISHED
			set monstatus 2
			spritepal 6
			ifg Numsprites 15616 debris SCRAP3 4 else
			debris SCRAP3 12
			state lite_jibs
			killit
		  }
		  else sound PISTOL_BODYHIT
	  }
	  break
	  endswitch
    }

  ifaction FEMSHRUNK
  {
    ifcount SHRUNKDONECOUNT
    {
      action FEMANIMATE
      cstat 257
    }
    else
      ifcount SHRUNKCOUNT
	  {
	    switch sprite[].picnum
		case POLESTRIPPER case BLUEHANGBABE case NEWSLIMEBABE case NEWSLIMEBABE2 case SCUBAGIRL
		case LARRY case GARFIELD case ORCHID case CHUNLI case FEM12 case FEM13 case FEM14
		sizeto 30 28 break
		case FEM11 sizeto 28 28 break
		default sizeto 28 26 break
		endswitch
	  }
    else
      state genericshrunkcode
  }
  else ifaction FEMGROW
  {
    ifcount 32
    {
      respawnhitag
      guts JIBS4 20
      guts JIBS6 20
      spritepal 6
	
	  ifactor GARFIELD nullop else
	  ifactor LARRY soundonce 569 else
	  ifactor TUBEGUY soundonce 569 else
	  ifactor ROSHI soundonce 569 else
	  ifactor TEXTINGNPC1 soundonce 569 else
	  ifactor TEXTINGNPC3 soundonce 569 else
	  ifactor FLASHER4 soundonce 569 else
	  ifactor FLASHER3 soundonce PIG_DYING else
	  ifactor FROGMASCOT 
	  {
		soundonce 569 
		spritepal 32
		debris SCRAP3 18
	  }
	  else
	  ifactor CHUNLI soundonce CHUNLIDIES
	  else
      soundonce LADY_SCREAM
      ifactor NAKED1
        debris SCRAP3 4
      else
        ifactor PODFEM1
          debris SCRAP3 4
      sound SQUISHED
	  set monstatus 2
	  state spawnbabe
      killit
    }
    else
      sizeto MAXXSTRETCH MAXYSTRETCH
  }

  else
    ifaction FEMDANCE1
  {
    ifactioncount 2
      action FEMDANCE2
  }
  else
    ifaction FEMDANCE2
  {
    ifactioncount 8
      action FEMDANCE3
  }
  else
    ifaction FEMDANCE3
  {
    ifactioncount 2
    action FEMANIMATE
  }
  else
    ifaction FEMDANCE4
  {
    ifactioncount 1
	action FEMDANCE5
  }
  else
    ifaction FEMDANCE5
  {
    ifactioncount 8
	action FEMANIMATE
  }
  else
    ifaction FEMDANCE6
  {
    ifactioncount 16
	action FEMANIMATE
  }
  else
    ifaction FEMDANCE7
  {
	ifactioncount 12
	action FEMANIMATE
  }
  else
    ifaction FEMDANCE9
  {
    ifactioncount 8
	action FEMANIMATE
  }
  else
    ifaction FEMDANCE10
  {
    ifactioncount 4
	action FEMANIMATE
  }
  else
    ifaction FEMDANCE11A
  {
    ifactioncount 1 action FEMDANCE11B
  }
  else
  ifaction FEMDANCE11B
  {
    ifactioncount 1 action FEMDANCE11C
  }
  else
  ifaction FEMDANCE11C
  {
    ifactioncount 1 action FEMANIMATE
  }
  else
  ifaction FEMDANCE12
  {
    ifactioncount 6 action FEMANIMATE
  }
  else
  ifaction NURSEHEAL1
  {
	ifactioncount 1
	{
		ifpdistl 1280
		{
			sound AIRBURST
			action NURSEHEAL2
		}
		else action FEMANIMATE
	}
  }
  else
  ifaction NURSEHEAL2
  {
    ifl sprite[player[].i].extra player[].max_player_health { cactor ATOMICHEALTH add mtype 1 addphealth 1 cactor HELLONURSE }
	ifactioncount 1 action FEMANIMATE
  }
  else
    ifaction FEMFROZEN1
  {
    ifcount THAWTIME
    {
      action FEMANIMATE
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
	ifvarand sprite[].cstat 512 seta[].htextra -1
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  lotsofglass 10
      state standard_jibs
      sound GLASS_BREAKING
      respawnhitag
      ifrnd 84
        spawn BLOODPOOL
      set monstatus 2
      killit
    }
    else
      ifp pfacing
        ifpdistl FROZENQUICKKICKDIST
          state pkick_check
    break
  }
  else ifaction FEMFROZEN2
  {
    ifcount THAWTIME
    {
      ifactor TOUGHGAL
        action TOUGHGALANIM
      else
        ifactor FEM10
          action FEMANIMATESLOW
	  else
		ifactor SISTERS
		  action ANULLACTION
      else
        action FEMANIMATE

      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
	ifvarand sprite[].cstat 512 seta[].htextra -1
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
          break
      }

      state standard_jibs
	  lotsofglass 10
      sound GLASS_BREAKING
      ifrnd 84
        spawn BLOODPOOL
      respawnhitag
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2

      killit
    }
    else
      ifp pfacing
        ifpdistl FROZENQUICKKICKDIST
          state pkick_check
    break
  }

  ifactor FEM13
  {
	ifaction FEMDANCE8
	{
		ifcount 150 
		{
			action FEMANIMATE
			resetcount
		}
	}
	else
	ifaction FEMANIMATE
	{
		ifcount 150 
		{
			action FEMDANCE8
			resetcount
		}
	}
  }
  ifvarand sprite[].cstat 512 seta[].htextra -1
  ifhitweapon
  {
    ifdead
    {
      ifwasweapon GROWSPARK
      {
        cstat 0
        move 0
        sound ACTOR_GROWING
        action FEMGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifaction FEMSHRUNK
          break

        ifactor NAKED1
          action FEMFROZEN2
        else
          ifactor FEM5
            action FEMFROZEN2
        else
          ifactor FEM6
            action FEMFROZEN2
        else
          ifactor FEM8
            action FEMFROZEN2
        else
          ifactor FEM9
            action FEMFROZEN2
        else
          ifactor FEM10
            action FEMFROZEN2
        else
          ifactor TOUGHGAL
            action FEMFROZEN2
        else
          ifactor PODFEM1
            action FEMFROZEN2
	    else
		  ifactor SISTERS
		    action FEMFROZEN2
		else
		  ifactor BLUEHANGBABE
		    action FEMFROZEN2
        else
		  ifactor FEM15
		    action FEMFROZEN2
		else
		  ifactor ELFGIRL
			action FEMFROZEN2
		else
		  ifactor BOOTHBABE
			action FEMFROZEN2
		else
          action FEMFROZEN1

        move 0
        spritepal 1
        strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
      set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2

      respawnhitag
      state standard_jibs
      state random_wall_jibs
      spawn BLOODPOOL

      ifactor FEM1
        money 5
      else
        ifactor FEM2
      {
        money 7
		action 0
        cactor BARBROKE
        cstat 0
      }
      else
        ifactor FEM3
          money 4
      else
        ifactor FEM7
          money 8

      ifactor FEM5
      {
        strength TOUGH
		action FEMANIMATE
        cactor BLOODYPOLE
      }
      else ifactor FEM6
      {
        cstat 0
        cactor FEM6PAD
		action FEMANIMATE
      }
      else
        ifactor FEM8
      {
        strength TOUGH
        cactor BLOODYPOLE
      }
	  else
	    ifactor NEWSLIMEBABE2
	  {
	    strength TOUGH
		cactor BLOODYPOLE
		soundonce LADY_SCREAM
	  }
	  else
	   ifactor FEM12
	  {
	    strength TOUGH
		cactor BLOODYPOLE
		soundonce LADY_SCREAM
	  }
      else
      {
        spritepal 6
		ifactor GARFIELD nullop else
		ifactor LARRY soundonce 569 else
		ifactor TUBEGUY soundonce 569 else
		ifactor ROSHI soundonce 569 else
		ifactor TEXTINGNPC1 soundonce 569 else
		ifactor TEXTINGNPC3 soundonce 569 else
		ifactor FLASHER4 soundonce 569 else
		ifactor FLASHER3 soundonce PIG_DYING else
		ifactor CHUNLI soundonce CHUNLIDIES else
        soundonce LADY_SCREAM
		
        ifactor NAKED1
          debris SCRAP3 18
        else
          ifactor PODFEM1
            debris SCRAP3 18
		else
		  ifactor FEM11
		    debris SCRAP3 18
	    else
		  ifactor NEWSLIMEBABE
		    debris SCRAP3 18
		else
		  ifactor NEWSLIMEBABE2
		    debris SCRAP3 18
	    else
		  ifactor FEM12
		    debris SCRAP3 18
		else
		  ifactor BLUEHANGBABE
		    debris SCRAP3 10
		else
		  ifactor FROGMASCOT
		  {
		    soundonce 569
			spritepal 32
			debris SCRAP3 18
		  }
        killit
      }
    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		set shrunken 1
        // move 0
        // action FEMSHRUNK
        // cstat 0
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
      ifactor FEM8
        break

      ifactor TOUGHGAL
          state toughgalspeech
      else sound SQUISHED

      guts JIBS6 1
    }
  }
ends

state killme
  ifinwater nullop
  else
    ifp pfacing
      ifpdistl 1280
        ifhitspace
          soundonce KILLME
ends

defstate tipme
  ifspritepal 1 break
  ifaction FEMDANCE11B break
  ifp pfacing
    ifpdistl 1280
      ifhitspace
  {
    ifactor HELLONURSE
	{
		ifaction FEMANIMATE
		ifl mtype 100
			action NURSEHEAL1
	}
	else
	ife shrunken 0
	ifn sprite[].pal 1
	{
		tip
		ife jibsound_delay 0
		{
			ifrnd 128
			  soundonce DUKE_TIP1
			else
			  soundonce DUKE_TIP2
			set jibsound_delay -50
		}
		state giftcheck
		
	} 
    ifactor FEM1
      action FEMDANCE1
	else
	ifactor ORCHID
	  action FEMDANCE4
	else
	ifactor FEM13
	  action FEMDANCE7
	else
	ifactor FEM14
	  action FEMDANCE12
	ifactor CHUNLI
	{
	  action FEMDANCE6
	  soundonce CHUNLIHAPPY
	}
	else
	ifactor FEM15
	{
	  ifaction FEMANIMATE
	  {
		add mtype 1
		ifg mtype 3 set mtype 0
		  action FEMDANCE9
		ife mtype 0 soundonce BATHCOMM1
		ife mtype 1 soundonce BATHCOMM2
		ife mtype 2 soundonce BATHCOMM3
		ife mtype 3 soundonce BATHCOMM4
	  }
	}
	else
	ifactor FEM17
	  action FEMDANCE10
	ifactor BETTY
	  action FEMDANCE10
	else
	ifactor FEM18
	  action FEMDANCE11A
	
  }
ends

defstate simpletip

	tip state giftcheck
	ife jibsound_delay 0
	{
		ifrnd 128
		  soundonce DUKE_TIP1
		else
		  soundonce DUKE_TIP2
		set jibsound_delay -50
	}

ends

state toughgaltalk
  ifp pfacing
    ifpdistl 1280
      ifhitspace
        state toughgalspeech
ends

actor FEM1 TOUGH FEMANIMATE
  state tipme
  state femcode
enda
actor FEM2 TOUGH FEMANIMATE
  state tipme
  state femcode
enda
actor FEM3 TOUGH FEMANIMATE
  state tipme
  state femcode
enda
actor FEM4 TOUGH FEMANIMATE
  state femcode
enda
// actor FEM5 TOUGH FEMNEW1
  // state killme
  // state femcode
// enda

eventloadactor FEM16 cstator 256 enda
useractor notenemy FEM16 TOUGH FEMANIMATE
	state femcode
	ifaction FEMANIMATE
	ifp pfacing
	ifhitspace
	ifp palive
	ifpdistl 1408
	ifcansee
	{
		addphealth 20
		action ANEXTTILE
		screensound DUKE_DRINKING
		palfrom 31 0 32
		ife player[].steroids_amount 0
			setp[].steroids_amount 50
	}
enda

eventloadactor FEM14 cstator 256 enda
useractor notenemy FEM14 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

eventloadactor FEM15 cstator 256 rand mtype 3 enda
useractor notenemy FEM15 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

eventloadactor FEM17 cstator 256 enda
useractor notenemy FEM17 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

eventloadactor FEM18 cstator 256 enda
useractor notenemy FEM18 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

eventloadactor FEM19 cstator 256 enda
useractor notenemy FEM19 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor FEM20 cstator 256 enda
useractor notenemy FEM20 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor FEM21 cstator 256 enda
useractor notenemy FEM21 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor SLAVE1 cstator 256 enda
useractor notenemy SLAVE1 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor SLAVE2 cstator 256 enda
useractor notenemy SLAVE2 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor SLAVE3 cstator 256 enda
useractor notenemy SLAVE3 TOUGH FEMANIMATE
  state femcode
enda

eventloadactor TUBEGUY cstator 256 enda
useractor notenemy TUBEGUY TOUGH FEMANIMATE
  state femcode
enda

eventloadactor FROGMASCOT cstator 256 enda
useractor notenemy FROGMASCOT TOUGH FEMANIMATE
  state femcode
enda


// actor FEM6 TOUGH FEMNEW2
  // state killme
  // state femcode
// enda

// action ANEWFEMANG5  0  2  5  1  50
// action ANEWFEMANG8  0  2  8  1  50
// action ANEWANG5FRO  0  1  8  1  10
// action ANEWANG8FRO  0  1  8  1  10

defstate newfemcode
  ifmove 0
  {
	  move STOPPED geth
	  set team 3 // noncombatant
	  set monstatus 1
	  set spawnprotect 0
  }
  state monsterai
  ife gametype -1 seta[].htextra -1
  
  ifaction FEMSHRUNK
  {
    ifcount SHRUNKDONECOUNT
    {
      action FEMANIMATE
      cstat 257
    }
    else
      ifcount SHRUNKCOUNT
	    sizeto 40 40 
    else
      state genericshrunkcode
  }
  else ifaction FEMGROW
  {
    ifcount 32
    {
      ifn SPRITELOTAG 0 seta[].hitag SPRITELOTAG
      respawnhitag
	  seta[].hitag 0
      guts JIBS4 20
      guts JIBS6 20
      spritepal 6
	  
      soundonce LADY_SCREAM
      sound SQUISHED
	  set monstatus 2
      killit
    }
    else
      sizeto MAXXSTRETCH MAXYSTRETCH
  }
  else
    ifspritepal 1
  {
    ifcount THAWTIME
    {
	  ifactor NEWNAKED1
	  action ANEWFEMANG5
	  else
	  ifactor NEWNAKED2
	  action ANEWFEMANG5
	  else
	  ifactor NEWPODFEM1
	  action ANEWFEMANG5
	  else
	  ifactor NEWFEM9
	  action ANEWFEMANG5
	  else
      action ANEWFEMANG8
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  lotsofglass 10
      state standard_jibs
      sound GLASS_BREAKING
      ifn SPRITELOTAG 0 seta[].hitag SPRITELOTAG
      respawnhitag
	  seta[].hitag 0
      ifrnd 84
        spawn BLOODPOOL
      set monstatus 2
      killit
    }
    
    break
  }
  ifhitweapon
  {
    ifdead
    {
	  ife babesaved THISACTOR set babesaved -1
      ifwasweapon GROWSPARK
      {
        cstat 0
        sound ACTOR_GROWING
        action FEMGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifaction FEMSHRUNK
          break

		ifactor NEWNAKED1
		action ANEWANG5FRO
	    else
		ifactor NEWPODFEM1
		action ANEWANG5FRO
		else
		ifactor NEWFEM9
		action ANEWANG5FRO
		else
		ifactor NEWNAKED2
		action ANEWANG5FRO
		else
        action ANEWANG8FRO

        spritepal 1
        strength 0
		resetcount

        sound NEWFREEZE
		state freezeme
        break
      }
      set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  ifn SPRITELOTAG 0 seta[].hitag SPRITELOTAG
      respawnhitag
	  seta[].hitag 0
      state standard_jibs
      state random_wall_jibs
	  ifactor NEWPODFEM1
	  {
		spritepal 6
		debris SCRAP3 12
		killit
	  }
	  ifactor NEWFEM9
	  {
		cactor BLOODYPOLE3
		strength TOUGH
	  }
	  ifactor SNIPERCAUGHT
	  {
	    cactor BLOODYPOLE3
		strength TOUGH
		espawn EDFSNIPER
		seta[RETURN].pal sprite[].pal
		seta[RETURN].htextra 30
		seta[RETURN].htpicnum RPG
		seta[RETURN].htang sprite[].htang
	  }
	  ifactor SNIPERHANG
	  {
		espawn 18245
		// cactor NEWCEILSLIME
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		geta[].cstat temp
		ifvarand temp 1 xorvar temp 1
		ifvarand temp 256 xorvar temp 256
		seta[RETURN].cstat temp
		espawn EDFSNIPER
		seta[RETURN].pal sprite[].pal
		seta[RETURN].htextra 30
		seta[RETURN].htpicnum RPG
		seta[RETURN].htang sprite[].htang
		killit
	  }
	  ifactor NEWPODFEM2
	  {
		spritepal 6
		debris SCRAP3 12
		killit
	  }
	  ifactor NEWFEM5
	  {
		cactor BLOODYPOLE
		strength TOUGH
	  }
	  ifactor NEWFEM8
	  {
		cactor BLOODYPOLE2
		strength TOUGH
	  }
	  ifactor NEWFEM11
	  {
		cactor BLOODYPOLE4
		strength TOUGH
	  }
      ifactor NEWFEM6
	  {
		cactor FEM6PAD
        cstat 0
	  }
	  ifactor NEWFEM10
	  {
	    cactor FEM6PAD
		cstat 0
	  }
	  ifactor NEWFEM7
	  {
	    cactor FEM6PAD
        cstat 0
	  }
	  ifactor NEWNAKED1 
	  {
	    espawn 18245
		// cactor NEWCEILSLIME
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		geta[].cstat temp
		ifvarand temp 1 xorvar temp 1
		ifvarand temp 256 xorvar temp 256
		seta[RETURN].cstat temp
		killit
	  }
	  ifactor NEWNAKED2
	  {
		espawn 18245
		// cactor NEWCEILSLIME2
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		geta[].cstat temp
		ifvarand temp 1 xorvar temp 1
		ifvarand temp 256 xorvar temp 256
		seta[RETURN].cstat temp
		killit
	  }
	  action FEMANIMATE
    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		set shrunken 1
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
      

      sound SQUISHED

      guts JIBS6 1
    }
  }
ends

defstate saveme

  ifg countvar 0 // rescue process has started
  {
	seta[].htextra -1
	add countvar 1
	ifle countvar 60
	{
		set temp player[].player_par
		modvar temp 6
		ife temp 0
		{
			geta[].z savz, sub savz 8192 seta[].z savz
			espawn BIGSMOKE seta[RETURN].pal 6 
			rand x 768 sub x 384 add x sprite[RETURN].x seta[RETURN].x x
			rand y 768 sub y 384 add y sprite[RETURN].y seta[RETURN].y y
			add savz 8192 seta[].z savz
		}
		// ifspritepal 6 getlastpal else spritepal 6
		geta[].alpha temp
		add temp 4
		seta[].alpha temp
	}
	else 
	{
		ifspritepal 6 getlastpal
		ife countvar 61 
		{
			ifactor NEWFEM5 
			{ 
				espawn BLOODLESSPOLE
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWFEM5 
			} 
			else
			ifactor NEWFEM8
			{ 
				espawn BLOODLESSPOLE2
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDFEM8
			}
			else
			ifactor NEWFEM11
			{ 
				espawn BLOODLESSPOLE4
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDFEM11
			}
			else
			ifactor NEWPODFEM2 
			{ 
				espawn EMPTYPOD2
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDPOD2 
			} 
			else
			ifactor NEWFEM6
			{
				espawn NEWFEM6PAD
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWFEM6 
			}
			else
			ifactor NEWFEM10
			{
				espawn NEWFEM6PAD
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDFEM10
			}
			else
			ifactor NEWFEM7
			{
				espawn NEWFEM6PAD
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWFEM7 
			}
			else
			ifactor NEWNAKED1 
			{
				espawn 18245 // NEWCEILSLIME
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWNAKED1 
			}
			else
			ifactor NEWNAKED2
			{
				espawn 18245 // NEWCEILSLIME2
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWNAKED2 
			}
			else
			ifactor NEWPODFEM1
			{
				espawn EMPTYPOD
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDNEWFEM5
			}
			else
			ifactor SNIPERCAUGHT
			{
				espawn BLOODLESSPOLE3
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn EDFSNIPER
				seta[RETURN].pal sprite[].pal
				killit
			}
			else
			ifactor SNIPERHANG
		  {
			espawn 18245 // NEWCEILSLIME
			ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
			seta[RETURN].xrepeat sprite[].xrepeat
			seta[RETURN].yrepeat sprite[].yrepeat
			espawn EDFSNIPER
			seta[RETURN].pal sprite[].pal
			seta[RETURN].htextra 30
			seta[RETURN].htpicnum RPG
			seta[RETURN].htang sprite[].htang
			killit
		  }
			else
			ifactor NEWFEM9
			{
				espawn BLOODLESSPOLE3
				ifvarand sprite[].cstat 8 seta[RETURN].cstat 8
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				espawn SAVEDFEM9
			}
			else
			espawn SAVEDNEWFEM5
			
			seta[RETURN].pal sprite[].pal
			seta[RETURN].xrepeat sprite[].xrepeat
			seta[RETURN].yrepeat sprite[].yrepeat
			setav[RETURN].mtype 1
			killit
		}
	}
	break
  }
  ifg sprite[].htextra 0
  ifn sprite[].htowner -1
  {
	ifvarand perks 8
	ife actorvar[sprite[].htowner].team 1
		seta[].htextra -1
  }
  
  ife babesaved -1
  ifpdistl 2048 // 1280
  // ifp pfacing
  ifrnd 1
  {
	ifrnd 128
	soundonce KILLME
	else
	ifrnd 128
	soundonce GROAN1
	else
	soundonce GROAN2
  }
  
  // ife babejuice 0
  // {
	// ife babesaved THISACTOR set babesaved -1
	  // ifinwater nullop
	  // else
		// ifp pfacing
		  // ifpdistl 1280
		  // {
			// ifhitspace
			  // soundonce KILLME
			  
			// ifl player[].fta 60
			// {
				// setp[].fta 120 setp[].ftq 1115
			// }
		  // }
		// break
  // }
   
   
  ife monstatus 2 { ife babesaved THISACTOR set babesaved -1 break }
  
  ifp pfacing
  ifp palive
  ifpdistl 2560
  // ifcansee
  {
    ifn babesaved -1 ifn babesaved THISACTOR
	{
		dist xydist player[].i babesaved
		dist xydist2 player[].i THISACTOR
		ifl xydist2 xydist set babesaved THISACTOR
	}
				
	ife juice_pos 0
	ife player[].kickback_pic 0
	ifcansee
	{
		ife babesaved -1 set babesaved THISACTOR
		ifhitspace
		{
			set juice_pos 1
			ife player[].weapon_pos 0 setp[].weapon_pos -1
		}
		else ifl player[].fta 60 
		{ 
			setp[].fta 120 setp[].ftq 1114 
			ife babesaved -1 set babesaved THISACTOR
		}
	}
	ifge juice_pos 20 ife babesaved THISACTOR
	{
		set juice_pos 50
		ifg babejuice 0 sub babejuice 1
		set countvar 1 // starts rescue process
		set babesaved -1
		geta[].htflags temp, orvar temp 4, seta[].htflags temp
		seta[].shade -100
		sound RIDSLIME sound RIDSLIME
		ifn SPRITELOTAG 0 seta[].hitag SPRITELOTAG
		respawnhitag
		seta[].hitag 0
	}
  }
  else ife babesaved THISACTOR set babesaved -1
ends

eventloadactor SNIPERCAUGHT sizeat 28 26 cstator 256 
geta[].lotag SPRITELOTAG seta[].lotag 0 enda

eventloadactor SNIPERHANG sizeat 28 26 cstator 256 
geta[].lotag SPRITELOTAG seta[].lotag 0 enda

useractor notenemy SNIPERCAUGHT SNIPERSTRENGTH ANULLACTION
   state saveme
   state newfemcode
enda

useractor notenemy SNIPERHANG SNIPERSTRENGTH ANULLACTION
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM5 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM8 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM11 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWPODFEM2 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM6 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM7 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM10 ROBOTMOUSESTRENGTH ANEWFEMANG8
   state saveme
   state newfemcode
enda

useractor notenemy NEWPODFEM1 ROBOTMOUSESTRENGTH ANEWFEMANG5
   state saveme
   state newfemcode
enda

useractor notenemy NEWFEM9 ROBOTMOUSESTRENGTH ANEWFEMANG5
   state saveme
   state newfemcode
enda

useractor notenemy NEWNAKED1 ROBOTMOUSESTRENGTH ANEWFEMANG5
   state saveme
   state newfemcode
enda

useractor notenemy NEWNAKED2 ROBOTMOUSESTRENGTH ANEWFEMANG5
   state saveme
   state newfemcode
enda

defstate bloodlesspolecode

  fall
  cstator 257
      
  ifhitweapon
     ifdead
  {
    sound SQUISHED
	spritepal 6
	debris SCRAP3 12
	state lite_jibs
	killit
  }

ends

useractor notenemy BLOODLESSPOLE TOUGH ANULLACTION state bloodlesspolecode enda

useractor notenemy BLOODLESSPOLE2 TOUGH ANULLACTION state bloodlesspolecode enda

useractor notenemy BLOODLESSPOLE3 TOUGH ANULLACTION state bloodlesspolecode enda

useractor notenemy BLOODLESSPOLE4 TOUGH ANULLACTION state bloodlesspolecode enda

useractor notenemy NEWFEM6PAD TOUGH ANULLACTION state bloodlesspolecode enda

useractor notenemy EMPTYPOD TOUGH ANULLACTION  state bloodlesspolecode enda


move NUDGEVEL 64

defstate savedbabecode
	set team 3
	fall
	ife mtype 1
	{
		add countvar 1
		ife countvar 1 move NUDGEVEL faceplayer
		ife countvar 6 move STOPPED faceplayer
		ife countvar 60 
		{
			sound MEGAPORT
			spawn TRANSPORTERSTAR
		}
		ifg countvar 60
		{
			geta[].alpha temp
			add temp 16
			seta[].alpha temp
			ifge temp 240 
			{
				ife pchar 0
				ifvarand perks 128
				{
					addphealth 20
					set value 10
					state spawnimpcoins
				}
				ife PROGRESSION YES
				ifg babejuice 0
				{
				set value 20
				state spawnimpcoins
				}
				killit
			}
		}
	}

ends
			
useractor notenemy SAVEDNEWFEM5 TOUGH state savedbabecode enda
useractor notenemy SAVEDPOD2 TOUGH state savedbabecode enda
useractor notenemy SAVEDNEWFEM6 TOUGH state savedbabecode enda
useractor notenemy SAVEDNEWFEM7 TOUGH state savedbabecode enda
useractor notenemy SAVEDFEM8 TOUGH state savedbabecode enda
useractor notenemy SAVEDFEM9 TOUGH state savedbabecode enda
useractor notenemy SAVEDFEM10 TOUGH state savedbabecode enda
useractor notenemy SAVEDFEM11 TOUGH state savedbabecode enda
useractor notenemy SAVEDNEWNAKED1 TOUGH state savedbabecode enda
useractor notenemy SAVEDNEWNAKED2 TOUGH state savedbabecode enda

useractor notenemy 18245 TOUGH

	ifn sprite[].sectnum -1
	{
		ifvarand sprite[].cstat 8 
		getflorzofslope sprite[].sectnum sprite[].x sprite[].y temp
		else
		{
			getceilzofslope sprite[].sectnum sprite[].x sprite[].y temp
			set tempb sprite[].yrepeat, shiftl tempb 8
			add temp tempb
		}
		setsprite THISACTOR sprite[].x sprite[].y temp
	}

enda

actor FEM7 TOUGH FEMANIMATE
  state tipme
  state femcode
enda
actor FEM8 TOUGH FEMANIMATE
  state femcode
enda
actor FEM9 TOUGH FEMANIMATE
  state femcode
enda
actor FEM10 TOUGH FEMANIMATESLOW
  state tipme
  state femcode
enda
useractor notenemy FEM11 TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy HELLONURSE MANWOMANSTRENGTH FEMANIMATE
  state tipme
  state femcode
enda

useractor notenemy BETTY MANWOMANSTRENGTH FEMANIMATE
  state tipme
  state femcode
enda

useractor notenemy NEWHANGBABE TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy POLESTRIPPER TOUGH FEMANIMATE
  state tipme
  state femcode
enda

useractor notenemy NEWSLIMEBABE TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy NEWSLIMEBABE2 TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy FEM12 TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy FEM13 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

useractor notenemy ORCHID TOUGH FEMANIMATE
  state tipme
  state femcode
enda

useractor notenemy CHUNLI MANWOMANSTRENGTH FEMANIMATE
  state tipme
  state femcode
enda


useractor notenemy BLUEHANGBABE TOUGH FEMANIMATE
  state killme
  state femcode
enda

useractor notenemy SCUBAGIRL TOUGH FEMANIMATE
  state femcode
enda

action LARADEAD1 2
action LARADEAD2 1
action LARAFROZEN

state laracode
  state killme
  // sizeat 32 32
  cstator 257
  ife monstatus 0 set monstatus 1
  set team 3
  state monsterai
  
      fall
      ifaction ANULLACTION nullop else
	  ifaction LARAFROZEN nullop else
        ifhitweapon
          ifdead
      {
		set monstatus 2
		spritepal 6
        state standard_jibs
        killit
      }
    

  ifaction FEMSHRUNK
  {
    ifcount SHRUNKDONECOUNT
    {
      action ANULLACTION
      cstat 257
    }
    else
      ifcount SHRUNKCOUNT
        sizeto 32 32
    else
      state genericshrunkcode
  }
  else ifaction FEMGROW
  {
    ifcount 32
    {
      respawnhitag
      guts JIBS4 20
      guts JIBS6 20
      spritepal 6
	  ifactor CHUNLI soundonce CHUNLIDIES
	  else
	  ifactor TUBEGUY soundonce 569 else
	  ifactor FROGMASCOT 
	  {
		soundonce 569 
		spritepal 32
		debris SCRAP3 18
	  }
	  else
	  ifactor LARRY soundonce 569 else
      soundonce LADY_SCREAM
      debris SCRAP3 4
      sound SQUISHED
	  set monstatus 2
      killit
    }
    else
      sizeto MAXXSTRETCH MAXYSTRETCH
  }

  
  ifaction LARAFROZEN
  {
    ifcount THAWTIME
    {
      action ANULLACTION
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  lotsofglass 10
      state standard_jibs
      sound GLASS_BREAKING
      respawnhitag
      ifrnd 84
        spawn BLOODPOOL
      set monstatus 2
      killit
    }
    else
      ifp pfacing
        ifpdistl FROZENQUICKKICKDIST
          state pkick_check
    break
  }

  ifhitweapon
  {
    ifdead
    {
	  set monstatus 2
      ifwasweapon GROWSPARK
      {
        cstat 0
        move 0
        sound ACTOR_GROWING
        action FEMGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      { 
        ifaction FEMSHRUNK
          break

        
        action LARAFROZEN
        move 0
        spritepal 1
        strength 0

        sound NEWFREEZE
		state freezeme
        break
      }

      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2

      respawnhitag
      state standard_jibs
      state random_wall_jibs
      spawn BLOODPOOL
	  ifactor LARATRAPPED action LARADEAD1 else
	  action LARADEAD2
	  strength 30
    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
        move 0
        action FEMSHRUNK
        cstat 0
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
      sound SQUISHED
      guts JIBS6 1
    }
  }
ends

useractor notenemy LARATRAPPED TOUGH ANULLACTION state laracode enda
useractor notenemy LARATRAPPED2 TOUGH ANULLACTION state laracode enda

action AWALLFEMDEAD 3

useractor notenemy WALLFEM1 TOUGH ANULLACTION

ifaction ANULLACTION
{
	cstator 256
	ifhitweapon
	{
		guts JIBS6 1
		sound SQUISHED
		ifdead
		{
			state lite_jibs
			action AWALLFEMDEAD
		}
	}
}

enda

useractor notenemy WALLFEM2 TOUGH ANULLACTION

ifaction ANULLACTION
{
	cstator 256
	ifhitweapon
	{
		guts JIBS6 1
		sound SQUISHED
		ifdead
		{
			state lite_jibs
			action AWALLFEMDEAD
		}
	}
}

enda

useractor notenemy HANGBABEDEAD TOUGH

	cstator 256
	ifhitweapon
	{
		spritepal 6 guts JIBS6 1 getlastpal
		sound SQUISHED
		ifdead
		{
			state lite_jibs
			killit
		}
	}

enda

useractor notenemy FEM6DEAD TOUGH

cstator 256
ifhitweapon
{
	spritepal 6
	guts JIBS6 1
	sound SQUISHED
    ifdead
	{
		state lite_jibs
		cactor FEM6PAD
	}
	getlastpal
}
enda   

useractor notenemy FEM5DEAD TOUGH

ifhitweapon
{
	spritepal 6
	guts JIBS6 1
	sound SQUISHED
    ifdead
	{
		state lite_jibs
		cactor BLOODYPOLE
	}
	getlastpal
}
enda

useractor notenemy PODFEMDEAD TOUGH

ifhitweapon
{
	spritepal 6
	guts JIBS6 1
	sound SQUISHED
    ifdead
	{
		state lite_jibs
		killit
	}
	getlastpal
}
enda 

actor TOUGHGAL MANWOMANSTRENGTH TOUGHGALANIM
  state toughgaltalk
  state femcode
enda

// actor NAKED1 TOUGH FEMNEW3
  // state killme
  // state femcode
// enda
// actor PODFEM1 TOUGH FEMANIMATE
  // state killme
  // state femcode
// enda
actor BLOODYPOLE TOUGH
  state femcode
enda
actor BLOODYPOLE2 TOUGH
  state femcode
enda
actor BLOODYPOLE3 TOUGH
  state femcode
enda
actor BLOODYPOLE4 TOUGH
  state femcode
enda
actor STATUEFLASH
  fall
  ifcount 32
    cactor STATUE
enda
actor STATUE
  fall
  ifp pfacing
    ifpdistl 1280
      ifhitspace
  {
    cactor STATUEFLASH
    move 0
  }
enda

actor MIKE
  ifp pfacing
    ifpdistl 1280
      ifhitspace
        mikesnd
enda

move EXPLODING
spriteshadow SERIOUSBOMB
useractor notenemy SERIOUSBOMB 1

fall
ifmove 0
{
	move STOPPED
	cstator 256
}
ifhitweapon
{
	ifmove STOPPED
		move EXPLODING
}

ifmove EXPLODING
{	
	ifspritepal 6 getlastpal else spritepal 6
	ifcount 10
	{
		spawn EXPLOSION2
		ifrnd 128 sound PIPEBOMB_EXPLODE
		else
		sound ROBOT_EXPLODE
		debris SCRAP1 3
		debris SCRAP2 3
		getlastpal
		debris SCRAP1 3
		debris SCRAP2 3
		hitradius 2560 20 35 50 60
		killit
	}
}
enda

action ATIFASHAKE  2  3  1  1  10

spriteshadow TIFA
useractor notenemy TIFA 60 ANULLACTION

fall
ifmove 0
{
	move STOPPED
	cstator 256
}

ifaction ANULLACTION
ifhitspace
ifp pfacing
ifpdistl 1560
ifcansee
{
	action ATIFASHAKE
}

ifaction ATIFASHAKE
	ifactioncount 6
		action ANULLACTION
		
ifhitweapon
{
	spawn BLOOD
	ifdead
	{
		sound LADY_SCREAM
		state standard_jibs
		killit
	}
}

enda


// used when actor vaporized
spritenoshade OUTLINE
useractor notenemy OUTLINE 0

	seta[].shade -100
	ifn sprite[].pal 4
	spritepal 33
	cstator 2
	add countvar 5
	seta[].alpha countvar
	ifg countvar 250 killit
	
	ifn droptile 0 ifg countvar 100
	{
		ifrnd 128
		globalsoundvar droptile
		set droptile 0
	}
enda

move BLURVELS 64

spritenopal SPEEDBLUR
action ABLURFRAME 0 1 5 1 1
useractor notenemy SPEEDBLUR 0 ABLURFRAME

	ifmove 0 ifn sprite[].xvel 0
	{
		set startx sprite[].xvel
		set starty sprite[].zvel
		move BLURVELS geth getv
	}
	ifcount 5 killit
	geta[].alpha temp
	add temp 32
	ifg temp 255 set temp 255
	seta[].alpha temp
	
	ifmove BLURVELS
	{
		seta[].xvel startx
		seta[].zvel starty
	}

enda


spritenopal EBARRIER
spritenoshade EBARRIER
spritenopal BARRICADE
spritenoshade BARRICADE

defstate breakbarrier

	ifn myspawner -1 { getav[myspawner].monstflags temp ifvarand temp 8 xorvar temp 8 setav[myspawner].monstflags temp }
	ife mtype APLAYER ifhitspace { killit break }
	spritepal 1
	lotsofglass 30
	ifsound SHIELDBREAK nullop else screensound SHIELDBREAK
	sound GLASS_BREAKING
	killit
	
ends

useractor notenemy EBARRIER 100

ife myspawner -1 state breakbarrier

ifmove 0
{
	move STOPPED
	cstat 338
	sizeat 12 12
	seta[].shade -50
	getav[myspawner].inithp inithp
	add inithp 200
	ifg inithp 16384 set inithp 16384
	ifspritepal 21 { mul inithp 2 div inithp 3 }
	setactor[THISACTOR].extra inithp
	set monstatus 1
	getav[myspawner].team team
	set monxp 50
	ife sprite[myspawner].picnum ROLLYTURRET ife team 1 set mtype APLAYER
}

ife sprite[myspawner].picnum MONSHADOW cstat 32768 else
ifge sprite[myspawner].alpha 160 cstat 32768 else
ifge sprite[myspawner].cstat 32768 cstat 32768 else
ife sprite[myspawner].picnum NURGLE ifn actorvar[myspawner].countvar 0 cstat 32768 else
cstat 338

getav[myspawner].team team

geta[myspawner].picnum picnum
set x tiledata[picnum].xsize
mul x sprite[myspawner].xrepeat
div x tiledata[sprite[].picnum].xsize
add x 4
seta[].xrepeat x
set y tiledata[picnum].ysize
mul y sprite[myspawner].yrepeat
div y tiledata[sprite[].picnum].ysize
ife sprite[myspawner].picnum ROTATEGUN add y 4
add y 2
seta[].yrepeat y

geta[myspawner].alpha temp, add temp 16, ifg temp 255 set temp 255
seta[].alpha temp
seta[].ang sprite[myspawner].ang
ife actorvar[myspawner].monstatus 2 state breakbarrier
ife sprite[myspawner].statnum 1024 state breakbarrier
set x sprite[myspawner].x
set x2 tiledata[sprite[myspawner].picnum.ysize
ifl x2 30 set x2 30
mul x2 sprite[myspawner].yrepeat

div x2 8

// mul x2 16
add x x2


rotatepoint sprite[myspawner].x sprite[myspawner].y x sprite[myspawner].y sprite[myspawner].ang x2 y2

set mysector sprite[myspawner].sectnum
updatesectorz x2 y2 sprite[myspawner].z mysector
ifn mysector sprite[myspawner].sectnum
{
	set x sprite[myspawner].x
	// add x 384
	set x2 sprite[myspawner].yrepeat
	mul x2 8
	add x x2
	rotatepoint sprite[myspawner].x sprite[myspawner].y x sprite[myspawner].y sprite[myspawner].ang x2 y2
}

geta[myspawner].z z
getav[myspawner].bottarget bottarget
ifn bottarget -1
{
	set z2 sprite[bottarget].z
	sub z2 z
	shiftr z2 2
	clamp z2 -4096 4096
	add z z2
}


setsprite THISACTOR x2 y2 z

ifcansee
ifl sprite[].alpha 144
	state kickcheckcode	

state predamage

ifg sprite[].htextra 0
ife sprite[].htowner player[].i
ifvarand gotDB 2 ife pchar 3
ifvarand mandoupgrades[SHOTGUN_WEAPON] 32
ife player[].curr_weapon SHOTGUN_WEAPON
{
	geta[].htextra temp
	shiftl temp 2, ifg temp 32000 set temp 32000
	seta[].htextra temp
}


ifhitweapon
{
	ifrnd 128
	soundonce HITSHIELD else
	soundonce HITSHIELD2
	ifdead state breakbarrier
	else ifn sprite[].htowner -1
	ife actorvar[myspawner].bottarget -1
	ifn team actorvar[sprite[].htowner].team
		setav[myspawner].bottarget sprite[].htowner
	
}

enda

defstate breakbarrier2

	set monstatus 2
	spritepal 1
	lotsofglass 30
	ifsound SHIELDBREAK nullop else screensound SHIELDBREAK
	sound GLASS_BREAKING
	killit
	
ends

useractor notenemy BARRICADE 500

ife myspawner -1 state breakbarrier2

ifmove 0
{
	move STOPPED
	cstat 339
	ifn initsprite 0 seta[].cstat initsprite
	sizeat 40 1
	seta[].shade -50
	getav[myspawner].inithp inithp
	mul inithp 2
	add inithp 1000
	ifg inithp 30000 set inithp 30000
	setactor[THISACTOR].extra inithp
	getav[myspawner].team team
	set monxp 200
	
	// 2nd barricade to make it blocking on other side
	ifn sprite[myspawner].picnum BARRICADE
	{
		espawn BARRICADE
		set angvar sprite[].ang
		add angvar 1024
		seta[RETURN].ang angvar
		setsprite RETURN sprite[].x sprite[].y sprite[].z
		setav[RETURN].initsprite 83 // 32849
		setav[RETURN].myspawner THISACTOR
	}
}

ifcount 450 state breakbarrier2 // delay before death to make ARMLIZTROOP wait before spawning another
else ifcount 330 sizeto 0 20
else sizeto 40 20

ifcansee
ifl sprite[].alpha 144
	state kickcheckcode	

state predamage

ifg sprite[].htextra 0
ife sprite[].htowner player[].i
ifvarand gotDB 2 ife pchar 3
ifvarand mandoupgrades[SHOTGUN_WEAPON] 32
ife player[].curr_weapon SHOTGUN_WEAPON
{
	geta[].htextra temp
	shiftl temp 2, ifg temp 32000 set temp 32000
	seta[].htextra temp
}

ife actorvar[myspawner].monstatus 2 state breakbarrier2
ife sprite[myspawner].statnum 1024 state breakbarrier2

ifhitweapon
{
	ifrnd 128
	soundonce HITSHIELD else
	soundonce HITSHIELD2
	ifdead state breakbarrier2
	else ifn sprite[].htowner -1
	ife actorvar[myspawner].bottarget -1
	ifn team actorvar[sprite[].htowner].team
		setav[myspawner].bottarget sprite[].htowner
	
}

enda

spritenoshade RADIMPACT
spritenopal RADIMPACT
action RADEXPFRAMES 0  4  1  1  18
useractor notenemy RADIMPACT 0

ifaction 0
{
	action RADEXPFRAMES
	seta[].shade -100
	cstat 130
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	ifn inithp 0
	{
		div inithp 12
		ifl inithp 18 set inithp 18
		ifg inithp 80 set inithp 80
		setprojectile[RADPROJ].extra inithp
	}
	set countvar 0
	geta[].ang angvel
	whilevarn countvar 16
	{
		rand zdist 2048
		sub zdist 2560
		ezshoot zdist RADPROJ
		ife myspawner player[].i seta[RETURN].owner player[].i
		add countvar 1
		add angvel 128
		seta[].ang angvel
	}
	setprojectile[RADPROJ].extra 20
}

ifactioncount 4 killit else
ifactioncount 3
{
	sizeto 0 0
	sizeto 0 0
	sizeto 0 0
}
else
ifactioncount 2
{
	sizeto 0 0
}
else
{
	sizeto 64 64
	sizeto 64 64
	sizeto 64 64
}

enda

spritenoshade RADAURA
spritenopal RADAURA

defstate radauradie
	ifn initsprite GREENSLIME
	ifn initsprite ARMYANTCRAWL
	{
		geta[].yrepeat y
		shiftl y 7
		geta[].z z
		sub z y
		seta[].z z
	}
	espawn RADIMPACT
	setav[RETURN].inithp inithp
	setav[RETURN].team 2
	sound RADEXPLOSION sound RADEXPLOSION
	geta[].xrepeat x, div x 4
	seta[RETURN].xrepeat x
	seta[RETURN].yrepeat x
	killit
ends

useractor notenemy RADAURA 0

ife myspawner -1 state radauradie
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z

getav[myspawner].team team
ifmove 0
{
	move STOPPED geth
	cstat 32
	cstator 2
	spritepal 6
	// seta[].blend 1
	seta[].shade -100
	set x tiledata[sprite[myspawner].picnum].xsize
	mul x sprite[myspawner].xrepeat
	getav[myspawner].inithp inithp
	ifl inithp 8 set inithp 8
	shiftr x 5
	ife sprite[myspawner].picnum GREENSLIME shiftr x 1
	ifg x 255 set x 255
	seta[].xrepeat x
	seta[].yrepeat x
	set initsprite sprite[myspawner].picnum
}
rand angvar 2047
seta[].ang angvar

seta[].alpha sprite[myspawner].alpha

ife actorvar[myspawner].monstatus 2 state radauradie
ife sprite[myspawner].statnum 1024 state radauradie
ife sprite[myspawner].picnum EGG ife actorvar[myspawner].team 3 killit

enda

defstate shockauradie

killit

ends

useractor notenemy SHOCKAURA 0

ife myspawner -1 state shockauradie
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z

getav[myspawner].team team
ifmove 0
{
	move STOPPED geth
	cstat 32
	cstator 2
	spritepal 16
	seta[].blend 1
	seta[].shade -100
	set x tiledata[sprite[myspawner].picnum].xsize
	mul x sprite[myspawner].xrepeat
	getav[myspawner].inithp inithp
	ifl inithp 8 set inithp 8
	// shiftr x 6
	div x 80
	ife sprite[myspawner].picnum GREENSLIME shiftr x 1
	ifg x 255 set x 255
	seta[].xrepeat x
	seta[].yrepeat x
	set initsprite sprite[myspawner].picnum
	geta[myspawner].htflags temp
	orvar temp 4
	seta[myspawner].htflags temp
	seta[myspawner].shade -60
}
rand angvar 2047
seta[].ang angvar

seta[].alpha sprite[myspawner].alpha
ife actorvar[myspawner].monstatus 2 state shockauradie
ife sprite[myspawner].statnum 1024 state shockauradie
ife sprite[myspawner].picnum EGG ife actorvar[myspawner].team 3 killit

enda

spritenoshade HEALINGAURA
spritenopal HEALINGAURA

defstate healauradie

	espawn GROUNDSHOCK
	seta[RETURN].pal 11
	sound SHIELDBREAK
	ifn myspawner -1
	{
		getav[myspawner].monstflags monstflags
		ifvarand monstflags 1024 xorvar monstflags 1024
		setav[myspawner].monstflags monstflags
	}
	killit

ends

useractor notenemy HEALINGAURA 0

ife myspawner -1 state healauradie
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z

getav[myspawner].team team
ifmove 0
{
	move STOPPED geth
	cstat 32
	cstator 514
	spritepal 11
	seta[].shade -100
	set x tiledata[sprite[myspawner].picnum].xsize
	mul x sprite[myspawner].xrepeat
	shiftr x 4
	ifg x 255 set x 255
	set startx x
	seta[].blend 1
	getav[myspawner].inithp inithp
	ifl inithp 500 mul inithp 2
}
rand angvar 2047
seta[].ang angvar

ife actorvar[myspawner].monstatus 2 state healauradie
ife sprite[myspawner].statnum 1024 state healauradie

set RETURN -1
set mtype NO
headspritestat spriteid 1
whilevarn spriteid -1
{
	ife actorvar[spriteid].team team
	ife actorvar[spriteid].monstatus 1
	ifl sprite[spriteid].extra actorvar[spriteid].inithp 
	{
		dist xydist THISACTOR spriteid
		set savx startx
		// shiftl savx 5
		mul savx 24
		
		ifl xydist savx
		{
			ifn spriteid THISACTOR 
			{
				seta[].xrepeat startx
				seta[].yrepeat startx
			}
			set temp player[].player_par
			modvar temp 2
			ife temp 0
			{
				geta[spriteid].extra tempb
				add tempb 1
				seta[spriteid].extra tempb
				sub inithp 1
			}
			ife mtype NO
			{
				set mtype YES
				espawn CAPTUREPLUS
				ifn RETURN -1
				{
					seta[RETURN].pal 11
					rand x2 savx
					set temp savx, div temp 2
					sub x2 temp
					add x2 sprite[].x
					seta[RETURN].x x2
					
					rand y savx
					sub y temp
					add y sprite[].y
					seta[RETURN].y y
					seta[RETURN].z sprite[].z
				}
			}
			ifactorsound THISACTOR CAPTURING nullop else sound CAPTURING
		}
	}
	nextspritestat spriteid spriteid
}
ife RETURN -1 
{
	ifactorsound THISACTOR CAPTURING stopactorsound THISACTOR CAPTURING
	set x startx, div x 3
	seta[].xrepeat x
	seta[].yrepeat x
}
ifl inithp 1 state healauradie

enda

move VERTROCKUP 0 -192
move VERTROCKDOWN 0  384

defstate select_vertlanding

	set botclip 0
	set countvar 0
	whilevarn botclip 1
	{
		rand x 20480
		add x 2560
		set x2 startx
		ifrnd 128 add x2 x else sub x2 x
		
		rand y 20480
		add y 2560
		set y2 starty
		ifrnd 128 add y2 y else sub y2 y
		
		ifn myspawner -1 set mysector sprite[myspawner].sectnum
		updatesector x2 y2 mysector
		ifn mysector -1
		ife sector[mysector].floorslope 0 
		{
			getceilzofslope mysector x2 y2 z
			getflorzofslope mysector x2 y2 z2
			set temp z2
			sub temp z
			ifge temp 65536
			{
				setsprite THISACTOR x2 y2 z
				findnearactor GROUNDTARGET 5120 spriteid
				ife spriteid -1
				{
					espawn GROUNDTARGET
					changespritestat RETURN 1
					seta[RETURN].cstat 32
					seta[RETURN].shade -100
					setav[RETURN].myspawner THISACTOR
					set myspawner RETURN
					sub z2 256
					setsprite RETURN x2 y2 z2
					set botclip 1
				}
			}
		}
		add countvar 1
		ifge countvar 644 set botclip 1
	}
	ife myspawner -1 killit else set countvar -150

ends



useractor notenemy VERTROCKET 0

ifmove 0
{
	sizeat 32 32
	move VERTROCKUP geth
	sound ROCKETFIRE2
	seta[].shade -80
	set mtype -48
	geta[].x startx
	geta[].y starty
}

movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
sub mtype 48

getceilzofslope sprite[].sectnum sprite[].x sprite[].y z
geta[].z zdist
sub zdist z
ifl zdist 2048
ifmove VERTROCKUP
{
	geta[].z temp
	sub temp 3072
	seta[].z temp
	
	ifcount 180
	{
		cstat 32768
		move STOPPED
		state select_vertlanding
	}
}

ifmove VERTROCKUP spawn SMALLSMOKE
ifmove VERTROCKDOWN spawn SMALLSMOKE

ifn myspawner -1
{
	add countvar 1
	ifge countvar 0
	{
		ife countvar 0
		{
			cstat 8
			set mtype 0
			move VERTROCKDOWN geth getv
			geta[].z countvarb
		}
		movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
		add mtype 48
		getflorzofslope sprite[].sectnum sprite[].x sprite[].y z
		sub z sprite[].z
		ifl z 4096
		{
			espawn NUKEEXPLOSION
			setav[RETURN].mtype 1
			setav[myspawner].monstatus 2
			killit
		}
		ifle sprite[].z countvarb { fall geta[].z z add z 1024 seta[].z z }
		geta[].z countvarb
	}
}

enda

eventloadactor ROCKETRAIN
cstat 32768
enda

useractor notenemy GROUNDTARGET
ife monstatus 2 killit
ifn myspawner -1
sizeat 128 128
state shadepulse
seta[].shade shade
enda

action SPIKEFRAME 6739
action THERMALFRAME 14456
spritenoshade LOBTRAIL
useractor notenemy LOBTRAIL 0 
	
	sizeat 4 4
	ife pchar 3 { action THERMALFRAME sizeat 8 7 } else
	ife pchar 0 ifvarand dukeupgrades[HANDBOMB_WEAPON] 1 action SPIKEFRAME
	ifcount 1 killit
	ife countvar 0 killit 
	cstat 0
	ife player[].over_shoulder_on YES { cstator 2176 sizeat 8 8 } else
	cstator 658
	ifvarand countvar 1 cstator 4
	ifg countvar 128 spritepal 6
	
	iffloordistl 8 spritepal 6 // giving wrong result
enda

move STAKEVELS 64

useractor notenemy STAKESTUCK 0

ife myspawner -1 
{
	// ifcount 450 killit
	ifhitspace ifp palive 
	ifpdistl 1024 ifp pfacing ifcansee
	cactor STAKESPRITE
	break
}
ifvarg sprite[myspawner].statnum 2 killit
ifvarn actorvar[myspawner].monstatus 1 
ifvarn actorvar[myspawner].monstatus 3
killit

ifmove 0
{
	cstat 0
	sizeat 10 10
	geta[].ang lastang
	geta[myspawner].ang ikicked
	move STAKEVELS geth
}

// mtype = distance
// angvel = angle offset for rotatepoint

// mtype = distance
// angvel = angle offset for rotatepoint

ifmove STAKEVELS
{
	ife sprite[myspawner].picnum NEWPIGTARGET killit
	ife sprite[myspawner].picnum NEWTROOPTARGET killit
	// move victim instead
	
	seta[].ang lastang
	
	ifn sprite[myspawner].picnum ROTATEGUN
	ifn sprite[myspawner].picnum LIZTURRET
	ifn sprite[myspawner].picnum EDFTURRET
	ifn sprite[myspawner].picnum BOSS2FLY
	{
		cos xvel lastang
		sin yvel lastang
		  
		shiftvarr xvel 4
		shiftvarr yvel 4
		seta[myspawner].clipdist 32
		movesprite myspawner xvel yvel 0 CLIPMASK0 RETURN
		ifn RETURN 0 
		{
			set initsprite 1 // STUCK TO SOMETHING!
			set startx sprite[myspawner].x
			set starty sprite[myspawner].y
			set float sprite[myspawner].z
		}
	}
	ifcount 3 move STOPPED geth
}

ife initsprite 1
{
	cos xvel lastang
	sin yvel lastang
	  
	shiftvarr xvel 4
	shiftvarr yvel 4
	
	movesprite myspawner xvel yvel 0 CLIPMASK0 RETURN
	ife RETURN 0 set initsprite 0
	
	seta[].ang lastang
	seta[myspawner].xvel 0
	seta[myspawner].zvel 0
	ifn mynaamah -1
	ife actorvar[mynaamah].myvictim THISACTOR 
	set float sprite[myspawner].z
	
	setsprite myspawner startx starty float
	set B NO
	// ifl actorvar[myspawner].inithp 600 set B YES else
	// {	
		
		set picnum sprite[myspawner].picnum
		set TMP_A tiledata[picnum].xsize
		mul TMP_A sprite[myspawner].xrepeat
		ifle TMP_A	3200 set B YES
		
	// }
	
	ife B YES
	{
		setav[myspawner].stun 10
		setav[myspawner].bottarget -1
		set angvar lastang, add angvar 1024
		seta[myspawner].ang ikicked // angvar
	}
}

getactor[myspawner].x x
getactor[myspawner].y y
getactor[myspawner].ang angvar
subvarvar angvar angvel
setvarvar x2 x
ife initsprite 1 sub x2 128
else
addvarvar x2 mtype
rotatepoint x y x2 y angvar x y
setactor[THISACTOR].x x
setactor[THISACTOR].y y

getactor[myspawner].ang angvar
subvarvar angvar botclip
setactor[THISACTOR].ang angvar
getactor[myspawner].z z
subvarvar z shotpitch
setactor[THISACTOR].z z


ifrnd 8
{
	geta[myspawner].picnum picnum
	set TILETYPE -1
	state tiletype
	ifge TILETYPE 2 ifle TILETYPE 4
	{
		cactor ARROWPROJ
		spawn SPARKFALL spawn SPARKFALL
		spawn SPARKFALL spawn SPARKFALL
		cactor STAKESTUCK
		ifn picnum ARMPIG sound SPARKING1
	}
	else
	{
		espawn JIBS6
		setactor[RETURN].zvel 0
		setactor[RETURN].xvel 0
		setactor[RETURN].xrepeat 32
		setactor[RETURN].yrepeat 32
		setactor[RETURN].pal sprite[myspawner].pal
	}
	getactor[myspawner].htextra temp
	ifvarl temp 1 setactor[myspawner].htpicnum STUCKARROW
	addvar temp 7
	setactor[myspawner].htextra temp
	setactor[myspawner].htowner player[THISACTOR].i
	// setactor[myspawner].htang sprite[THISACTOR].ang
}

ifge sprite[myspawner].picnum PODHEAD ifle sprite[myspawner].picnum PODTAIL ifcount 30 killit
ifcount 450 killit
ife sprite[myspawner].picnum DEANOVA ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SITH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SYTH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
enda

useractor notenemy BLADEPROJSTUCK 0

ife myspawner -1 
{
	break
}
ifvarg sprite[myspawner].statnum 2 killit
ifvarn actorvar[myspawner].monstatus 1 
ifvarn actorvar[myspawner].monstatus 3
killit

ifmove 0
{	
	cstat 0
	sizeat 10 10
	geta[].ang lastang
	move STOPPED geth
	// geta[].pitch B al B
	
	rand angvar 72
	sub angvar 36
	seta[].roll angvar
	
	getactor[myspawner].z z
	subvarvar z shotpitch
	setactor[THISACTOR].z z
	
	// espawn DRIPBLOOD
	// setav[RETURN].myspawner THISACTOR
}



// mtype = distance
// angvel = angle offset for rotatepoint

// mtype = distance
// angvel = angle offset for rotatepoint

getactor[myspawner].x x
getactor[myspawner].y y
getactor[myspawner].ang angvar
subvarvar angvar angvel
setvarvar x2 x
addvarvar x2 mtype
rotatepoint x y x2 y angvar x y
setactor[THISACTOR].x x
setactor[THISACTOR].y y

getactor[myspawner].ang angvar
subvarvar angvar botclip
setactor[THISACTOR].ang angvar
getactor[myspawner].z z
subvarvar z shotpitch
setactor[THISACTOR].z z



add countvar 1
ifge countvar 8
{
	set countvar 0
	geta[myspawner].picnum picnum
	set TILETYPE -1
	state tiletype
	ifge TILETYPE 2 ifle TILETYPE 4
	{
		cactor ARROWPROJ
		spawn SPARKFALL spawn SPARKFALL
		spawn SPARKFALL spawn SPARKFALL
		cactor BLADEPROJSTUCK
		ifn picnum ARMPIG soundonce SPARKING1
	}
	else
	{
		espawn JIBS6
		setactor[RETURN].zvel 0
		setactor[RETURN].xvel 0
		setactor[RETURN].xrepeat 32
		setactor[RETURN].yrepeat 32
		setactor[RETURN].pal sprite[myspawner].pal
	}
}
ifrnd 10
{
	getactor[myspawner].htextra temp
	ifvarl temp 1 setactor[myspawner].htpicnum STUCKARROW
	addvar temp 7
	setactor[myspawner].htextra temp
	setactor[myspawner].htowner player[THISACTOR].i

}


ifcount 300 killit
ife sprite[myspawner].picnum DEANOVA ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SITH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SYTH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
enda

spritenoshade GREENBULLET
spritenopal GREENBULLET

useractor notenemy GREENBULLET 0

	ife countvar 0
	{
		add countvarb 1
		ifg countvarb 600
		{
			rand countvar 4
			add countvar 1
		}
		
		ife pchar 4
		ife player[].curr_weapon PISTOL_WEAPON
		{
			ife burstfired GREENBULLET
			{
				ifg player[].kickback_pic 15
				{
					rand countvar 4
					add countvar 1
				}
			}
			else
			ife player[].reloading YES
			ife player[].kickback_pic 33
			{
				set initsprite YES
				geta[].xrepeat x, add x 6 seta[].xrepeat x
				geta[].yrepeat y, add y 4 seta[].yrepeat y
				cstator 2
				seta[].blend 1
			}
		}
		
		ifn myshelly -1
		ife actorvar[myshelly].thirdbaseval GREENBULLET
		ife actorvar[myshelly].float -33
		{
			rand countvar 4
			add countvar 1
		}
	}
	
	ifn myspawner -1
	{
		ifvarg sprite[myspawner].statnum 2 set countvar 1
		ifvarn actorvar[myspawner].monstatus 1 
		ifvarn actorvar[myspawner].monstatus 3
			set countvar 1
	}
	ifg countvar 0
	{
		sub countvar 1
		ife countvar 0
		{
			espawn GREENEXP
			setav[RETURN].initsprite initsprite
			ifsound SMALLEXP2 ifrnd 128 nullop else
			sound SMALLEXP2
			ifn myspawner -1
			{
				geta[myspawner].htextra temp
				add temp 10
				ife initsprite YES add temp 10
				seta[myspawner].htextra temp
				seta[myspawner].htpicnum RPG
				seta[myspawner].htowner player[].i
			}
			killit
		}
	}

	ife myspawner -1 
	{
		ifcount 300 sizeto 0 0
		ifcount 330 killit
	}
	else
	{
		ifcount 160 nullop else
		ifvarand novaupgrades[PISTOL_WEAPON] 1
		{
			set temp player[].player_par
			modvar temp 30
			ife temp 0
			{
				set temp 1
				add temp nweaplevel[PISTOL_WEAPON]
				geta[myspawner].htextra tempb
				ife tempb -1 set tempb 0
				add tempb temp
				seta[myspawner].htextra tempb
				espawn SMALLSMOKE
				seta[RETURN].cstat 2
				seta[RETURN].blend 1
				seta[RETURN].pal 8
			}
		}
		// mtype = distance
		// angvel = angle offset for rotatepoint

		// mtype = distance
		// angvel = angle offset for rotatepoint
		getactor[myspawner].x x
		getactor[myspawner].y y
		getactor[myspawner].ang angvar
		subvarvar angvar angvel
		setvarvar x2 x
		addvarvar x2 mtype
		rotatepoint x y x2 y angvar x y
		setactor[THISACTOR].x x
		setactor[THISACTOR].y y

		getactor[myspawner].ang angvar
		subvarvar angvar botclip
		setactor[THISACTOR].ang angvar
		getactor[myspawner].z z
		subvarvar z shotpitch
		setactor[THISACTOR].z z
	}

enda

spritenoshade GREENEXP
spritenopal GREENEXP
spritenoshade GREENEXP2
spritenopal GREENEXP2

useractor notenemy GREENEXP 0

	ifaction 0
	{
		action ANULLACTION
		ife initsprite YES sizeat 24 24 else
		sizeat 12 12
		cstat 130
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
		seta[].blend 1
		seta[].shade -126
	}
	sizeto 16 10
	sizeto 16 10
	ifcount 4 
	{ 
		cactor GREENEXP2 
		spawn SPARKFALL spawn SPARKFALL 
		ife initsprite YES { spawn SPARKFALL spawn SPARKFALL }
		resetcount 
	
	}

enda

useractor notenemy GREENEXP2 0

	ife initsprite YES { sizeto 48 24 sizeto 48 24 } else
	sizeto 24 12
	
	ifcount 3 killit

enda

action ADRIPB 12 1 1 1 1
action ADRIPC 13 1 1 1 1
action ADRIPD 14 1 1 1 1
useractor notenemy DRIPBLOOD 0

ifaction 0
{
	rand temp 3
	ife temp 0 action ANULLACTION else
	ife temp 1 action ADRIPB else
	ife temp 2 action ADRIPC else
	ife temp 3 action ADRIPD
	cstator 1024
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	sizeat 12 8
	
	ifspawnedby APLAYER seta[].z player[].posz
}


ifl sprite[].yrepeat 80
{
	sizeto 32 80
	geta[].z z
	add z 172 // 160
	seta[].z z
	
}

ifcount 90
{
	geta[].alpha temp
	add temp 1, ifge temp 254 killit
	seta[].alpha temp
}

	ifn myvictim -1
	{
		ife sprite[myvictim].statnum 1024 killit else
		ife actorvar[myvictim].monstatus 2 killit else
		{
			cstat 0
			set x startx
			sub x sprite[myvictim].x
			set x2 sprite[].x
			sub x2 x
			seta[].x x2
			geta[myvictim].x startx
			
			set y starty
			sub y sprite[myvictim].y
			set y2 sprite[].y
			sub y2 y
			seta[].y y2
			geta[myvictim].y starty
			
			set z float
			sub z sprite[myvictim].z
			set z2 sprite[].z
			sub z2 z
			seta[].z z2
			geta[myvictim].z float
		}
	}

enda

useractor notenemy STUCKARROW 0
ifvarg sprite[myspawner].statnum 2 killit
ifvarn actorvar[myspawner].monstatus 1 
ifvarn actorvar[myspawner].monstatus 3
killit

// mtype = distance
// angvel = angle offset for rotatepoint

getactor[myspawner].x x
getactor[myspawner].y y
getactor[myspawner].ang angvar
subvarvar angvar angvel
setvarvar x2 x
addvarvar x2 mtype
rotatepoint x y x2 y angvar x y
setactor[THISACTOR].x x
setactor[THISACTOR].y y

getactor[myspawner].ang angvar
subvarvar angvar botclip
setactor[THISACTOR].ang angvar
getactor[myspawner].z z
subvarvar z shotpitch
setactor[THISACTOR].z z

ifn sprite[myspawner].xvel 0
ifl sprite[myspawner].extra 1000
{
	geta[myspawner].xvel xvel
	mul xvel 4 div xvel 5
	seta[myspawner].xvel xvel
}
// 0 = regular pal 0; 1 = burning pal 2; 2 = explosive pal 7; 
// 3 = multi pal 12; 4 = freeze pal 1; 5 = spy pal 15; 
// 6 = electric pal 117
// 7 = blood pal 54; 8 = radioactive pal 6

ifspritepal 1
{
	// getactorvar[myspawner].burning burning
	// subvar burning 1
	// setactorvar[myspawner].burning burning
	ifrnd 24
	{
		seta[myspawner].htpicnum FREEZEBLAST
		geta[myspawner].htextra temp
		ife temp -1 set temp 3 else add temp 3
		seta[myspawner].htextra temp
		setactor[myspawner].htowner player[THISACTOR].i
		setactor[myspawner].htang sprite[THISACTOR].ang
	}
	ifcount 210 killit
}
else
ifspritepal 2
{
	getactorvar[myspawner].burning burning
	addvar burning 1
	setactorvar[myspawner].burning burning
	ifrnd 24
	{
		seta[myspawner].htpicnum FIREBOLT
		geta[myspawner].htextra temp
		ife temp -1 set temp 1 else add temp 1
		seta[myspawner].htextra temp
		setactor[myspawner].htowner player[THISACTOR].i
		setactor[myspawner].htang sprite[THISACTOR].ang
	}
	ifcount 210 killit
}
else
ifspritepal 6
{
	ifrnd 64
	{
		espawn BIGSMOKE seta[RETURN].pal 76
		soundonce RADFRY
		ifrnd 84
		{
			geta[myspawner].htextra temp
			add temp 4
			seta[myspawner].htextra temp
			
			ifle temp 5
			{
				seta[myspawner].htpicnum RADWOUND
				seta[myspawner].htowner player[].i
			}
			
			ifrnd 32
			{
				getav[myspawner].monstflags temp
				ifvarand temp 4096 nullop else
				{
					
					ife actorvar[hitsprite].monstatus 1
					ifn sprite[hitsprite].picnum APLAYER
					{
						orvar temp 4096
						setav[myspawner].monstflags temp
						espawn RADAURA
						setav[RETURN].myspawner hitsprite
						
					}
					set hitsprite myspawner
					state spawnradwound
				}
				
			}
		}
	}
	ifcount 300 killit
}
else
ifrnd 8
{
	geta[myspawner].picnum picnum
	set TILETYPE -1
	state tiletype
	ifge TILETYPE 2 ifle TILETYPE 4
	{
		cactor ARROWPROJ
		spawn SPARKFALL spawn SPARKFALL
		spawn SPARKFALL spawn SPARKFALL
		cactor STUCKARROW
		ifn picnum ARMPIG sound SPARKING1
	}
	else
	{
		espawn JIBS6
		setactor[RETURN].zvel 0
		setactor[RETURN].xvel 0
		setactor[RETURN].xrepeat 32
		setactor[RETURN].yrepeat 32
		setactor[RETURN].pal sprite[myspawner].pal
	}
	getactor[myspawner].htextra temp
	ifvarl temp 1 setactor[myspawner].htpicnum STUCKARROW
	addvar temp 4 ifg gametype 0 add temp 2
	setactor[myspawner].htextra temp
	ifspritepal 54 ifn sprite[myspawner].picnum EBARRIER
	ifn sprite[myspawner].picnum BARRICADE
	{
		geta[myspawner].picnum picnum
		state tiletype
		ifn TILETYPE 2
		add leach 50
	}
	ifspritepal 117
	{
		ifspritepal 117
		{
			espawn SHOCK_ACTOR
			setav[RETURN].myspawner myspawner
			setav[RETURN].mtype 1
		}
	}
	setactor[myspawner].htowner player[THISACTOR].i
	setactor[myspawner].htang sprite[THISACTOR].ang
}

ifspritepal 117 ifcount 210 killit
ifcount 450 killit
ife sprite[myspawner].picnum DEANOVA ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SITH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
ife sprite[myspawner].picnum SYTH ife actorvar[myspawner].mtype 1 ifrnd 16 killit
enda

action ASTICKYFRAME -1

defstate stickyblow

	
	ifg myvictim -1
	{
		hitradius 2176 20 27 33 40
		geta[myvictim].htextra temp
		ife myspawner player[].i add temp 60 else
		ife myvictim player[].i add temp 13 else
			add temp 40
		
		ifg mlevel 0
		{
			mul mlevel 5
			add temp mlevel
		}
		seta[myvictim].htextra temp
		seta[myvictim].htpicnum RPG
		ifn myspawner -1 seta[myvictim].htowner myspawner else
		seta[myvictim].htowner THISACTOR
		
		ifpdistl 2177 
		ifn myspawner -1
		ifn player[].i myvictim
			seta[player[].i].htowner myspawner
			
		ife myvictim myspawner seta[myvictim].htextra -1
		
		ifn myspawner -1
		{
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				ifn actorvar[spriteid].monstatus 0
				{
					dist temp THISACTOR spriteid
					ifle temp 2176 seta[spriteid].htowner myspawner
				}
				nextspritestat spriteid spriteid
			}
		}
			
	}
	else ife myspawner player[].i 
	{
		hitradius 2176 25 30 38 50 
		ifvarand perks 8 ifpdistl 2180
		{
			getp[].i spriteid
			ife sprite[spriteid].htpicnum RADIUSEXPLOSION
				seta[spriteid].htextra -1
		}
	}
	else
	{
		hitradius 2176 20 27 33 40
		ifn myspawner -1
		{
			dist xydist THISACTOR myspawner
			ifle xydist 2176 seta[myspawner].htextra -1
		}
	}
	espawn EXPLOSION3
	seta[RETURN].xrepeat 20 seta[RETURN].yrepeat 20
	sound SMALLEXP1
	spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL 
	killit

ends

useractor notenemy STICKYBOMB 0 ASTICKYFRAME

ifmove 0
{
	sound MAGLOCK
	move STOPPED
	// sound stickon
	cstator 128
	ifn myvictim -1
	{
		ife sprite[myspawner].picnum CHEERBOT ife actorvar[myspawner].team actorvar[myvictim].team
		set myvictim -1
	}
	ife myvictim -1 iffloordistl 4 cstat 0
	ifspritepal 33 sizeat 12 12 else
	sizeat 26 26
	
	getflorzofslope sprite[].sectnum sprite[].x sprite[].y z
	ifg sprite[].z z seta[].z z
	ifn myspawner -1
	ife actorvar[myspawner].team 0
		set mlevel actorvar[myspawner].mlevel
}

ifg myvictim -1
{
	// mtype = distance
	// angvel = angle offset for rotatepoint
	ife actorvar[myvictim].monstatus 2 { set myvictim -2 break }
	ifn myvictim player[].i ifg sprite[myvictim].statnum 2 { set myvictim -2 break }
	getactor[myvictim].x x
	getactor[myvictim].y y
	getactor[myvictim].ang angvar
	subvarvar angvar angvel
	setvarvar x2 x
	addvarvar x2 mtype
	rotatepoint x y x2 y angvar x y
	setactor[].x x
	setactor[].y y
	
	getactor[myvictim].z z
	subvarvar z shotpitch
	setactor[].z z
}
else ife myvictim -2 fall

ifvarand extbits 64 
ife myspawner player[].i
{
	ifvarand gametips 64 { xorvar gametips 64 savegamevar gametips }
	rand temp 5
	ife temp 0 count 75 else
	ife temp 1 count 76 else
	ife temp 2 count 77 else
	ife temp 3 count 78 else
	ife temp 4 count 79 else
	count 80
}

ifcount 40
{
	ifspritepal 121 spritepal 0 else spritepal 121
	ifcount 80 state stickyblow
}

enda


spritenoshade SHOTSPARK1
spriteflags SHOTSPARK1 33554432
action WEAP2FRAMES 0  4  1  1  6
actor SHOTSPARK1 PISTOL_WEAPON_STRENGTH WEAP2FRAMES
/*
  ife sprite[].blend 0
  {
	// geta[].htg_t 6 hitwall
	// ifn hitwall -1 al hitwall
	// geta[].htg_t 8 hitsprite
	
	ifn hitsprite -1 
	{ 
		
		// set x sprite[].x
		// al x
		// set y sprite[].y
		// al y
		// set x2 sprite[hitsprite].x
		// al x2
		// set y2 sprite[hitsprite].y
		// al y2
		switch sprite[hitsprite].picnum
		case 1181 case 1186
		case 21249 case 936 case 7065
			geta[hitsprite].cstat temp
			ifvarand temp 256 xorvar temp 256
			seta[hitsprite].cstat temp
			geta[hitsprite].htextra tempb al tempb
		break
		default
			al hitsprite
			geta[hitsprite].picnum picnum al picnum 
			geta[].htg_t 7 hitsector
			al hitsector
			geta[].sectnum mysector al mysector 
			geta[hitsprite].htextra tempb al tempb
		break
		endswitch
	}
	
  }
*/
  seta[].shade -127
  seta[].blend 1
  cstator 2
  ife sprite[].yvel SWITCHBULLET killit
 
  // ifdead
    // killit
  ifactioncount 4
    killit
  else
  {
    ifactioncount 3
    {
      ifinwater
      spawn WATERBUBBLE
    }
    else
      ifcount 2 nullop
      else ife initsprite 0
	  {
	    set temp NO
		
		ife sprite[].owner myshelly
		ife actorvar[myshelly].thirdbaseval GREENBULLET set temp YES
		else
		ife pchar 4 ife player[].curr_weapon PISTOL_WEAPON
		ifn player[].kickback_pic 0
		ifspawnedby APLAYER
		set temp YES
		
		ife temp YES
		{
			ifoutside ifceilingdistl 2 killit
			espawn GREENBULLET
			seta[RETURN].cstat 128
			seta[RETURN].xrepeat 14
			seta[RETURN].yrepeat 12
			seta[RETURN].shade -127
			geta[].htg_t 8 hitsprite
			setav[RETURN].myspawner hitsprite
			// spriteid is referred to as myspawner in the arrow/stake
			ifn hitsprite -1
			ife sprite[hitsprite].statnum 1
			{
				set spriteid hitsprite
				state facesprite
				getincangle temp angvar sprite[hitsprite].ang
				setav[RETURN].botclip temp
				ldist temp THISACTOR hitsprite
				add temp 48
				setav[RETURN].mtype temp
				geta[RETURN].x x2
				geta[RETURN].y y2
				subvarvar x2 sprite[hitsprite].x
				subvarvar y2 sprite[hitsprite].y
				getangle tempc x2 y2 // angle to make monster face arrow
				getincangle angvar tempc sprite[hitsprite].ang
				setav[RETURN].angvel angvar
				getactor[hitsprite].z z2
				subvarvar z2 sprite[].z
				setav[RETURN].shotpitch z2
			}
		}
		
		ife sprite[].yvel DEAGLEBULLET
		{
			cstat 32768
			espawn LASERHIT
			ife pchar 2 ife sprite[].owner player[].i
			ife player[].curr_weapon GROW_WEAPON
			{
				seta[RETURN].blend 1
				seta[RETURN].cstat 2
				seta[RETURN].pal 1
			}
		}
		else
	    ifvare sprite[THISACTOR].yvel FIREBEAM
		{
		  geta[].htg_t 8 hitsprite
		  ifn hitsprite -1
		  {
			getav[hitsprite].burning burning
			add burning 36
			setav[hitsprite].burning burning
		  }
		  else ifrnd 16 { ifrnd 128 spawn BURNING else spawn BURNING2 }
		  ifn shotpitch 0
		  {
			  headspritestat spriteid 1
			  whilevarn spriteid -1
			  {
				  ife sprite[spriteid].picnum BEAMTRAIL
				  {
					  ife actorvar[spriteid].shotpitch 0
					  {
					  setav[spriteid].shotpitch shotpitch
					  seta[spriteid].pitch shotpitch
					  }
				  }
				  nextspritestat spriteid spriteid
			  }
			  killit
		  }
		}
		else
		ifvare sprite[THISACTOR].yvel DISINBEAM
		{
		  geta[].htg_t 8 savedvalue
		  ifn savedvalue -1
		  {
			seta[savedvalue].htpicnum RPG
			ifn sprite[savedvalue].picnum ICESPRITE
			setav[savedvalue].padang 1337
			ifvarand mandoupgrades[DEVISTATOR_WEAPON] 4 // ground zero splash zone
			{
				// set bleeding to <0 for rad damage
				set xydist2 999999
				set myvictim -1
				headspritestat spriteid 1
				whilevarn spriteid -1
				{
					ife actorvar[spriteid].monstatus 1
					ifn spriteid savedvalue
					{
						dist xydist THISACTOR spriteid
						ifvarand mandoupgrades[DEVISTATOR_WEAPON] 8 // smart ricochet
						ife actorvar[spriteid].team 0
						ifspawnedby APLAYER
						{
							ifl xydist xydist2
							{
								set myvictim spriteid
								set xydist2 xydist
							}
						}
						
						ifvarand perks 8 ife actorvar[spriteid].team 1 nullop else
						ifle xydist 2048
						{
							set temp 60
							set tempb mweaplevel[7]
							mul tempb 10
							add temp tempb
							set tempc actorvar[spriteid].bleeding
							sub tempc temp
							setav[spriteid].bleeding tempc
						}
					}
					nextspritestat spriteid spriteid
				}
				
			}
			else
			ifvarand mandoupgrades[DEVISTATOR_WEAPON] 8 // smart ricochet
			ifspawnedby APLAYER
			{
				set xydist2 999999
				set myvictim -1
				headspritestat spriteid 1
				whilevarn spriteid -1
				{
					ife actorvar[spriteid].monstatus 1
					ifn spriteid savedvalue
					ife actorvar[spriteid].team 0
					{
						dist xydist THISACTOR spriteid
						ifl xydist xydist2
						{
							set myvictim spriteid
							set xydist2 xydist
						}
					}
					nextspritestat spriteid spriteid
				}
			}
		  }
		  spawn FIRECIRCLE3D
		  ifn shotpitch 0
		  {
			  headspritestat spriteid 1
			  whilevarn spriteid -1
			  {
				  ife sprite[spriteid].picnum BEAMTRAIL
				  ife actorvar[spriteid].mtype 0
				  {
					  setav[spriteid].mtype DISINBEAM
					  ife actorvar[spriteid].shotpitch 0
					  {
					  setav[spriteid].shotpitch shotpitch
					  seta[spriteid].pitch shotpitch
					  }
					  seta[spriteid].pal 15
				  }
				  nextspritestat spriteid spriteid
			  }
		  }
		  ifn myvictim -1
		  {
				geta[savedvalue].cstat initsprite
				seta[savedvalue].cstat 0
				set spriteid myvictim
				state facesprite
				set bottarget myvictim
				setactor[].picnum sprite[savedvalue].picnum
				state hitscan_targetprep
				
				// geta[bottarget].z zdist
				// sub zdist sprite[savedvalue].z 
				
				// shiftl zdist 8
				// ldist xydist savedvalue bottarget
				// ifvare xydist 0 setvar xydist 1 
				// divvarvar zdist xydist
				
				zshoot zdist DISINBEAM
				orvar initsprite 256 // failsafe
				seta[savedvalue].cstat initsprite
				cactor SHOTSPARK1
				ife sprite[myvictim].htpicnum SHOTSPARK1
					seta[myvictim].htowner player[].i
					
				
		  }
			ifn shotpitch 0 killit
		}
		else
		ifvare sprite[THISACTOR].yvel ICEBEAM
		{
		  geta[].htg_t 8 hitsprite
		  ifn hitsprite -1
			seta[hitsprite].htpicnum FREEZEBLAST
		  
		  spritepal 1
		  lotsofglass  2
		  ifn shotpitch 0
		  {
			  headspritestat spriteid 1
			  whilevarn spriteid -1
			  {
				  ife sprite[spriteid].picnum ICEBEAMTRAIL
				  ife actorvar[spriteid].shotpitch 0
				  {
				  setav[spriteid].shotpitch shotpitch
				  seta[spriteid].pitch shotpitch
				  }
				  nextspritestat spriteid spriteid
			  }
			  killit
		  }
		}
		else
		ifvare sprite[THISACTOR].yvel BRACBEAM
		{
		  geta[].htg_t 8 hitsprite
		  ifn hitsprite -1
			seta[hitsprite].htpicnum FREEZEBLAST
		  ifn shotpitch 0
		  {
			  headspritestat spriteid 1
			  whilevarn spriteid -1
			  {
				  ife sprite[spriteid].picnum ICEBEAMTRAIL
				  ife actorvar[spriteid].shotpitch 0
				  {
				  setav[spriteid].shotpitch shotpitch
				  seta[spriteid].pitch shotpitch
				  seta[spriteid].pal 12
				  }
				  nextspritestat spriteid spriteid
			  }
			  killit
		  }
		}
		else
		ifvare sprite[THISACTOR].yvel CARBONBEAM
		{
		  geta[].htg_t 8 hitsprite
		  ifn hitsprite -1
		  {
			seta[hitsprite].htpicnum FREEZEBLAST
			ife actorvar[hitsprite].padmove 0
				setav[hitsprite].padang 131313
		  }
		  spritepal 27
		  lotsofglass  8
		  ifn shotpitch 0
		  {
			  headspritestat spriteid 1
			  whilevarn spriteid -1
			  {
				  ife sprite[spriteid].picnum ICEBEAMTRAIL
				  ife actorvar[spriteid].shotpitch 0
				  {
					  setav[spriteid].shotpitch shotpitch
					  seta[spriteid].pitch shotpitch
					  seta[spriteid].pal 3
				  }
				  nextspritestat spriteid spriteid
			  }
			  killit
		  }
		}
		else
		ife sprite[].yvel SHOCKBEAM
		{
			geta[].htg_t 8 hitsprite
			ifn hitsprite -1 ife sprite[hitsprite].picnum SHOCK_ACTOR { killit break }
			espawn SHOCK_ACTOR
			
			ifn hitsprite -1
			{
				ifvarand actorvar[hitsprite].monstflags 1048576
				{
					seta[hitsprite].htextra -1
					geta[hitsprite].extra temp
					add temp 5, ifg temp 32767 set temp 32767
					seta[hitsprite].extra temp
				}
				else
				{
					setav[RETURN].myspawner hitsprite
					ifn sprite[].htowner -1
					ife actorvar[sprite[].htowner].team 1
					ifn startshocktime -1
					setav[RETURN].mtype player[].player_par
					ife pchar 0 ifvarand dukeupgrades[CHAINGUN_WEAPON] 16 setav[RETURN].botclip YES
					ife pchar 1 ifvarand shellyupgrades[CHAINGUN_WEAPON] 16 setav[RETURN].botclip YES
					ife pchar 2 ifvarand wesupgrades[CHAINGUN_WEAPON] 16 setav[RETURN].botclip YES
					ife pchar 3 ifvarand mandoupgrades[CHAINGUN_WEAPON] 16 setav[RETURN].botclip YES
					ife pchar 4 ifvarand novaupgrades[CHAINGUN_WEAPON] 16 setav[RETURN].botclip YES
				}
			}
			 
			ifn sprite[].htowner player[].i
			{
				headspritestat spriteid 1
			    whilevarn spriteid -1
			    {
				  ife sprite[spriteid].picnum LIGHTNING_TRAIL1
				  {
					  ife actorvar[spriteid].shotpitch 0
					  ife actorvar[spriteid].mtype NO
					  {
						setav[spriteid].shotpitch shotpitch
						seta[spriteid].pitch shotpitch
						setav[spriteid].mtype YES
					  }
				  }
				  
				  nextspritestat spriteid spriteid
			    }
			    killit
			}
		}
		else
		ife sprite[].yvel TIMEBEAM
		{
			set spriteid 0
			ifvarand novaupgrades[SHRINKER_WEAPON] 1 
			mul pipehold 4
			else set pipehold 0
							
			whilevarn spriteid 16384
			{
			  ifle sprite[spriteid].statnum 4
			  ifn sprite[spriteid].statnum 3
			  ifn sprite[spriteid].statnum 0
			  ife actorvar[spriteid].stasis 0
			  {
			    switch sprite[spriteid].picnum
			    case MOVECAM
				case BLUEFLAGATBASE
				case BLUEBASENOFLAG
				case BLUEFLAG
				case REDFLAGATBASE
				case REDBASENOFLAG
				case REDFLAG
				case TEAMSPAWNER
				case HIGHSOUND
				case EMPERORSCREEN
				case CONTROL_A case CONTROL_B case CONTROL_C
				case SPEEDBLUR
				case SECTORLIGHT
				case SECTORPANNING
				case WALLPANNING
				case NPCNAV
				case JUMPAD
				case ANIMOPEN1
				case ANIMOPEN2
				case ANIMOPEN3
				case UPDRAFT_SPAWNER
				case LIGHTNING2
				case BLACKHAWK
				case COFFIN
				case NAAMAH
				case LILY
				case BOSS1STATUE case BOSS2STATUE case BOSS3STATUE
				case BREADCRUMB
				case ACCESSCARD
				case MOVESHIPCAM
				case LITBIGSTATUE
			    case SPINCUBE
			    case LIGHTNING_TRAIL1
			    case MONSHADOW
				case PODHEAD
				case PODBODY
				case PODTAIL
			    break
			    default
				  ifn spriteid THISACTOR
				  ifn spriteid myshelly
				  {
					dist xydist THISACTOR spriteid
					ifle xydist 3072
					{
						ife sprite[spriteid].statnum 2 changespritestat spriteid 1
						set lastcrumb sprite[spriteid].statnum
						mul lastcrumb -1
						ifn sprite[spriteid].statnum 1 setav[spriteid].lastcrumb lastcrumb
						else setav[spriteid].lastcrumb 0
						ife actorvar[spriteid].monstatus 1
						ife actorvar[spriteid].team 0
							ife jibsound -1
							{
								ifrnd 128 set jibsound DEA_TIMEGUN1 else
								set jibsound DEA_TIMEGUN2
								set jibsound_delay 10
							}
						
						changespritestat spriteid 2 // STASISSTAT
						set temp STASISTIME
						ifvarand novaupgrades[SHRINKER_WEAPON] 1 add temp pipehold
						setav[spriteid].stasis temp
						setav[spriteid].stun temp
						setav[spriteid].damflash sprite[spriteid].cstat
						set tempb sprite[spriteid].cstat
						ifvarand tempb 1 xorvar tempb 1
						ifvarand tempb 256 xorvar tempb 256
						seta[spriteid].cstat tempb
						geta[spriteid].mdflags temp
						orvar temp 16
						seta[spriteid].mdflags temp
						ife sprite[spriteid].htpicnum RESPAWN seta[spriteid].htpicnum SHOTSPARK1
						set instasis YES
					}
				  }
			    break
			    endswitch
			  }
			  add spriteid 1
			}
			ifvarand novaupgrades[SHRINKER_WEAPON] 1 set pipehold 0
			
			// headspritestat spriteid 4
			// whilevarn spriteid -1
			// {
				// dist xydist THISACTOR spriteid
				// ifle xydist 3072
				// {
					// setav[spriteid].stasis STASISTIME
					// setav[spriteid].damflash sprite[spriteid].cstat
					// set tempb sprite[spriteid].cstat
					// ifvarand tempb 1 xorvar tempb 1
					// ifvarand tempb 256 xorvar tempb 256
					// seta[spriteid].cstat tempb
					// setav[spriteid].lastcrumb -4
					// geta[spriteid].mdflags temp
					// orvar temp 16
					// seta[spriteid].mdflags temp
				// }
			  
			  
			  // nextspritestat spriteid spriteid
			// }
			
			killit
		}
		else
		ife sprite[].yvel FIREBOLT
		{
			
			findnearactorz BURNING 1024 8192 target
			ife target -1 findnearactorz BURNING2 1024 8192 target
			ife target -1
			{
				ifrnd 128 espawn BURNING else espawn BURNING2	
				ifspawnedby APLAYER setav[RETURN].team 1 else
				ife sprite[].owner myshelly setav[RETURN].team 1 
			}
			
			
		}
        ifonwater
          spawn WATERSPLASH2
		else
		{
			set initsprite 1
			geta[].xrepeat x mul x 2 seta[].xrepeat x
			geta[].yrepeat y mul y 2 seta[].yrepeat y
			cstator 128
		}	
		
		// ifspawnedby APLAYER
		set temp NO
		ifspawnedby APLAYER set temp YES else
		ife sprite[].owner myshelly set temp YES
		
		// ife player[].curr_weapon SHOTGUN_WEAPON
		ife temp YES
		ifn sprite[].yvel SWITCHBULLET
		{
			set temp NO
			ife pchar 0 
			{
				ifvarand gotDB 2 nullop else 
				ifvarand dukeupgrades[SHOTGUN_WEAPON] 8
				ifvarand dukeupgrades[SHOTGUN_WEAPON] 4
				ife player[].curr_weapon SHOTGUN_WEAPON
				ife sprite[].owner player[].i
				set temp YES
			}
			else
			ife pchar 3
			{
				ifvarand gotDB 2
				ifvarand mandoupgrades[SHOTGUN_WEAPON] 8
				ife player[].curr_weapon SHOTGUN_WEAPON
				ife sprite[].owner player[].i
				set temp YES
			}
			
			ifg plevel 3
			ife sprite[].owner myshelly ifge sprite[myshelly].picnum DUKEBOT
			ifle sprite[myshelly].picnum DUKEBOTCROUCH set temp YES
			
			ife temp YES
			{
				geta[].htg_t 8 hitsprite
				state spawnradwound
			}
			
			ifspawnedby APLAYER
			{
				ife player[].curr_weapon SHOTGUN_WEAPON
				{
					set temp NO
					ife pchar 0 ife shotexpduke YES set temp YES
					ife pchar 1 ife shotexpshelly YES set temp YES
					ife temp YES
					{
						ifvarand gametips 1 { xorvar gametips 1 savegamevar gametips }
						// ifpdistg 1280
						// {
							// geta[].htg_t 8 hitsprite
							// ifn hitsprite -1 geta[hitsprite].htextra savz
							// findnearspritez EXPLOSION2 1024 8192 temp
							// ife temp -1
							// {
								// hitradius 1024 4 8 12 16
								// headspritestat spriteid 6
								// whilevarn spriteid -1
								// {
									// getactor[spriteid].picnum picnum
									// switch picnum
									// case CRACK1 case CRACK2 case CRACK3 case CRACK4
									// dist xydist THISACTOR spriteid
									// ifl xydist 1025
									// {
									// setactor[spriteid].htextra -1
									// setactor[spriteid].htpicnum picnum
									// }
									// break
									// default break
									// endswitch
									// nextspritestat spriteid spriteid
								// }
							// }
							// ifn hitsprite -1 seta[hitsprite].htextra savz
						// }
						espawn EXPLOSION2
						
						seta[].yvel RPG
						seta[RETURN].xrepeat 12
						seta[RETURN].yrepeat 12
						stopsound EXPLODE2
						soundonce EXPLODE2
						geta[].htg_t 8 hitsprite
						ifn hitsprite -1
						{
							seta[RETURN].xrepeat 20
							seta[RETURN].yrepeat 20
							seta[hitsprite].htpicnum RPG
							// geta[hitsprite].htextra temp
							// ifg temp 0
							// {
								// ife pchar 0 add temp 5 else
								// add temp 4
								// seta[hitsprite].htextra temp
							// }
						}

					}
				}
			}
		}
		
		ifn sprite[].owner -1
		ifvarand actorvar[sprite[].owner].monstflags 128 // multishot
		{
			espawn EXPLOSION2
			seta[RETURN].xrepeat 12
			seta[RETURN].yrepeat 12
			stopsound EXPLODE2
			soundonce EXPLODE2
		}
		
		
		
		set TILETYPE -1
		geta[].htg_t 8 hitsprite
		ifn hitsprite -1
		{
			ife sprite[hitsprite].picnum LASERWALL
			{
				spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL
			}
			ife sprite[hitsprite].picnum SHOCKBALL2
			{
				setav[hitsprite].monstatus 2
			}
			ife sprite[hitsprite].picnum SHOCKBALL
			{
				setav[hitsprite].monstatus 2
			}
			// ife sprite[hitsprite].picnum BEACHBALLPROJ
			// {
				// seta[hitsprite].htextra 5
				// seta[hitsprite].htpicnum SHOTSPARK1
				// seta[hitsprite].htang sprite[].ang
				// seta[hitsprite].ang sprite[].ang
				// seta[hitsprite].htowner THISACTOR
				// geta[hitsprite].xvel x
				// add x 256 mul x 2 ifg x 644 set x 644
				// seta[hitsprite].xvel x
				// geta[hitsprite].zvel z
				// sub z 1024
				// seta[hitsprite].zvel z
				// geta[hitsprite].yvel temp
				// ifl temp 3 add temp 1
				// seta[hitsprite].yvel temp
				// break
			// }
			ife sprite[].yvel FIREBOLT
			ifn sprite[hitsprite].htowner hitsprite
			{
				ife hitsprite player[].i
				ife sprite[].owner myshelly nullop else
				{
					getav[hitsprite].burning temp
					add temp 10
					setav[hitsprite].burning temp
					break
				}
			}
			ife sprite[].yvel SABERPROJ
			{
				ifspawnedby APLAYER ife pchar 4 soundoncevar clawhitsnd else
				ifspawnedby DEANOVA soundoncevar clawhitsnd else
				ifspawnedby APLAYER ife pchar 2 soundonce RENDFLESH else
				ifspawnedby WESBOT soundonce RENDFLESH
				else
				soundoncevar sabhitsnd 
				ife actorvar[hitsprite].monstatus 1
				ifrnd 128
				{
					getav[hitsprite].stun temp
					ifl temp 3
					add temp 1
					setav[hitsprite].stun temp
				}
			}
			ife sprite[].yvel BARRETPROJ 
			{
				seta[hitsprite].htpicnum RPG
				seta[].yvel RAILPROJ
				set chainsparksprite THISACTOR
			}
			ife sprite[].yvel EXPBULLET seta[hitsprite].htpicnum RPG
			
			geta[hitsprite].picnum picnum
			
			switch picnum
			case LIZTROOP case LIZMAN case LIZRANGER 
			case PIGCOP case NEWPIG case NEWPIGDIVE
			case NEWPIGPISS
			case NEWTROOP case NEWTROOPDUCKING
			case NEWTROOPONTOILET
			case PIGCOPDIVE case LIZRANGERDUCKING
			case LIZTROOPONTOILET case LIZTROOPJUSTSIT
			case NEWBEAST
			case CYBERBEAST case CRONEN
			case ZOMBIE case ARMYANT case ARMYANTCRAWL
			case SPACEBULL case CRAZYLADY
			case COMMANDER case NEWCOMM
			case NEWTSABERDEATH case LIZMANSABERDEATH
			case NEWPIGSABERDEATH case DEASABERDEATH
			case MANDOSABERDEATH case SYTHSABERDEATH case SITHSABERDEATH
			case ARMLIZTROOP case ARMLIZTROOPDUCK
				ifn sector[].lotag 2
				{
					ife sprite[].yvel SAWPROJ { soundonce SAWFLESH set sawfleshtime 3 }
					else
					ife sprite[].yvel KNIFEPROJ sound KNIFEIMPACT
					else
					ife sprite[].yvel SABERPROJ
					{
						ife pchar 2
						{
							add saberblood 300
							ifg saberblood 5400 set saberblood 5400
							ife picnum NEWBEAST set saberpal 11 else
							ife picnum CYBERBEAST set saberpal 11 else
							ife picnum CRONEN set saberpal 11 else
							ife picnum ZOMBIE set saberpal 11 else
							ife picnum ARMYANT set saberpal 11 else
							ife picnum ARMYANTCRAWL set saberpal 11 else
							set saberpal 10
						}
					}
					else
					ifn sprite[].yvel RPG
					{
						ifsound BODYHIT ifsound BODYHIT2 nullop else
						{
							ifsound BODYHIT sound BODYHIT2 else
							ifsound BODYHIT2 sound BODYHIT
						}
					}
				}
				ifspawnedby EDFSNIPER seta[].htpicnum APLAYER
				ifspawnedby EDFSNIPERCROUCH seta[].htpicnum APLAYER
				
				ife picnum ARMYANTCRAWL ife actorvar[hitsprite].initsprite YES // upside down ant
					seta[].htpicnum SHOTSPARK1 // prevents headshot
					
				
				// ife picnum NEWCOMM
				// ifg sprite[hitsprite].extra 200 nullop
				// else
				// ife picnum COMMANDER
				// ifg sprite[hitsprite].extra 200 nullop
				// else
				// ife picnum NEWBEAST
				// ifg sprite[hitsprite].extra 160 nullop
				// else
				// ife picnum CYBERBEAST
				// ifg sprite[hitsprite].extra 180 nullop
				// else
				// ife picnum ARMPIG
				// ifg sprite[hitsprite].extra 160 nullop
				// else
				// ife picnum SPACEBULL
				// ifg sprite[hitsprite].extra 160 nullop
				// else
				
				ifspawnedby APLAYER
				ifn sprite[].yvel SAWPROJ
				ifn sprite[].yvel RPG
				{
					set zdist tiledata[picnum].ysize
					ife actorvar[hitsprite].mtype 3 
					{
						ife picnum LIZTROOP 
						{
						set zdist 52
						// seta[].yvel RAILPROJ
						}
						
						ife picnum NEWTROOP
						{
						set zdist 70
						// seta[].yvel RAILPROJ
						}
					}
					mul zdist sprite[hitsprite].yrepeat
					ife picnum PIGCOPDIVE mul zdist 7 else
					ife picnum NEWPIGDIVE mul zdist 7 else
					mul zdist 6 // 5
					div zdist 2 // distance up to the head
					geta[hitsprite].z z
					sub z zdist
					ifle sprite[].z z
					{
						set temp sprite[hitsprite].extra
						div temp 2
						ifge sprite[hitsprite].htextra temp
						{
							set xydist 3072
							ife pchar 2 ife player[].curr_weapon GROW_WEAPON
							set xydist 5120
							ife player[].curr_weapon SHOTGUN_WEAPON
							ife zoomin YES set xydist 8192
							ife player[].curr_weapon CHAINGUN_WEAPON
							ife zoomin YES set xydist 8192
							findplayer xydist2
							
							geta[].z z2
							ife sprite[].yvel RAILPROJ 
							{
								seta[hitsprite].htpicnum HEADJIB1
								ife zoomin YES ife actorvar[hitsprite].droptile 0 setav[hitsprite].droptile NUKECOIN
								ifrnd 84
								ife sprite[hitsprite].htowner player[].i
								ife player[].curr_weapon DEVISTATOR_WEAPON
								{
									ife pchar 0 // DUKE
									{
										ifrnd 128 globalsound DUKE_SNIPE1 else
										globalsound DUKE_SNIPE2
									}
									else ife pchar 1 // BOMBSHELL
									{
										ifrnd 128 globalsound B_HEADSHOT else
										globalsound B_GUNCONTROL
									}
									else ife pchar 2 // WES
									{
										ifrnd 128 globalsound WESONESHOTKILL else
										globalsound WESMADEPOINT
									}
								}
							}
							else
							ifl xydist2 xydist
							{
								ifg sprite[hitsprite].htextra 60
								{
									seta[hitsprite].htpicnum HEADJIB1
									ife sprite[].yvel SABERPROJ setav[hitsprite].thirdbasetimer 999
								}
								else
								ifg sprite[hitsprite].htextra sprite[hitsprite].extra
								{
									ife sprite[].yvel SABERPROJ
									{
										ifn sprite[hitsprite].htpicnum SABERPROJ
										seta[hitsprite].htpicnum HEADJIB1
										setav[hitsprite].thirdbasetimer 999
									}
									else
									ifrnd 64
										seta[hitsprite].htpicnum HEADJIB1	
								}
								
							}
						}
						// geta[hitsprite].htextra temp
						// ifg temp 0
						// {
							// mul temp 3 div temp 2
							// seta[hitsprite].htextra temp
						// }
					}
					
					
				}
				state checkbleeding
			break
			case ARMPIG case BOSS1 case NEWBATLORD case QUEEN
			case BOSS2 case BOSS3 case BOSS4 case PIGBOSS case NURGLE
			case FEMCIV case FEMCIVCROUCH case FEMCIV2 case FEMCIV2CROUCH 
			case FEMCIV3 case FEMCIV3CROUCH case FEMCIV5 case FEMCIV5CROUCH
			case MALECIV1 case MALECIV1CROUCH case MALECIV2 case MALECIV2CROUCH 
			case MALECIV4 case MALECIV4CROUCH case MALECIV3 case MALECIV3CROUCH
			case MALECIV5 case MALECIV5CROUCH
			case MALECIV6 case MALECIV6CROUCH case MALECIV2SMOKING
			case GUARD case GUARDCROUCH case CRYSTAL case MAIDNPC
			case EDFSNIPER case EDFSNIPERCROUCH case EDFTROOP
			case NAKEDBABE case NAKEDBABECROUCH case PIGSTRIPPER
			case STRIPPER case STRIPPERCROUCH case KBABE case KBABECROUCH case CHEERLEADER
			case CHEERLEADERCROUCH case HOOKER case HOOKERCROUCH case POLEDANCER
			case CAVEGIRL case CAVEGIRLCROUCH case BIKINIBABE case BIKINIBABE2
			case FATGUY1 case FATGUY2 case ALIENCIVMALE case ALIENCIVFEM
			case SHOWERGIRL case STRIPPER2
			case SHARK case OCTABRAIN case MEGABRAIN
			case LIZDOG case ROBOSHARK case EGG
			case SHELLY case SHELLYCROUCH case WESBOT case WESBOTCROUCH
			case DUKEBOT case DUKEBOTCROUCH 
			case DEABOT case DEABOTCROUCH
			case APLAYER case LIZELITE case LIZELITECROUCH case CORRUPT
			case MANDOFETT case MANDOCROUCH
			case NAAMAH case NEWPIGDOWN case BATBOSS
			case PODHEAD case PODTAIL case EDFFALLDIE case OCTATROOP
			case FIGHTMALE case FIGHTMALECROUCH case FIGHTFEM case FIGHTFEMCROUCH
			case GRAVESNPC case GRAVESNOGUN
				ifn sprite[].yvel SHOTGUN
				ifn sprite[hitsprite].picnum ARMPIG
				ifn sector[].lotag 2
				{
					ife sprite[].yvel SABERPROJ
					{
						ife pchar 2
						{
							add saberblood 30
							ifg saberblood 5400 set saberblood 5400
							ife picnum QUEEN set saberpal 11 else
							ife picnum EGG set saberpal 11
							set saberpal 10
						}
					}
					else
					ifn sprite[].yvel RPG
					{
						ife sprite[].yvel SAWPROJ { soundonce SAWFLESH set sawfleshtime 3 }
						else
						ife sprite[].yvel KNIFEPROJ sound KNIFEIMPACT
						else
						{
							ifsound BODYHIT ifsound BODYHIT2 nullop else
							{
								ifsound BODYHIT sound BODYHIT2 else
								ifsound BODYHIT2 sound BODYHIT
							}
						}
					}
				}
				else 
				ifn sector[].lotag 2
				{
					ifpdistl 4096
					{
						ifsound BODYHIT ifsound BODYHIT2 nullop else
						{
							ifsound BODYHIT sound BODYHIT2 else
							ifsound BODYHIT2 sound BODYHIT
						}
					}
					else soundonce BODYHIT
				}
				state checkbleeding
			break
			case EBARRIER
			case BARRICADE
				spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL
				
				ifspawnedby APLAYER
				{
					
					ifn hitsprite -1
						ife sprite[hitsprite].htpicnum SHOTSPARK1
					{
						set temp NO
						ife player[].curr_weapon PISTOL_WEAPON
						{
							ife pchar 0 ifvarand dukeupgrades[PISTOL_WEAPON] 1 set temp YES
							ife pchar 1 ifvarand shellyupgrades[PISTOL_WEAPON] 1 set temp YES
							ife pchar 2 ifvarand wesupgrades[PISTOL_WEAPON] 1 set temp YES
						}
						ifvarand gotshock 2 nullop else
						ife player[].curr_weapon CHAINGUN_WEAPON
						{
							ife pchar 0 ifvarand dukeupgrades[CHAINGUN_WEAPON] 2 set temp YES
							ife pchar 1 ifvarand shellyupgrades[CHAINGUN_WEAPON] 2 set temp YES
							ife pchar 2 ifvarand wesupgrades[CHAINGUN_WEAPON] 2 set temp YES
							ife pchar 3 ifvarand mandoupgrades[CHAINGUN_WEAPON] 2 set temp YES
							ife pchar 4 ifvarand novaupgrades[CHAINGUN_WEAPON] 2 set temp YES
						}
						
							
						ife temp YES
						{
							geta[hitsprite].htextra temp
							mul temp 3 div temp 2, ifg temp 16000 set temp 16000
							seta[hitsprite].htextra temp
						}
					}
				}

			break
			case SITH
			case SYTH
			case DEANOVA
				ife actorvar[hitsprite].mtype 1 // blocking
				{
					spawn SPARKFALL spawn SPARKFALL
				}
				else
				{
					ifn sprite[].yvel SHOTGUN
					{
						ife sprite[].yvel SABERPROJ
						{
							ife pchar 2
							{
								add saberblood 300
								ifg saberblood 5400 set saberblood 5400
								set saberpal 10
							}
						}
						else
						ifn sprite[].yvel RPG
						ifn sector[].lotag 2
						{
							ife sprite[].yvel SAWPROJ { soundonce SAWFLESH set sawfleshtime 3 }
							else
							ife sprite[].yvel KNIFEPROJ sound KNIFEIMPACT
							else
							{
								ifsound BODYHIT ifsound BODYHIT2 nullop else
								{
									ifsound BODYHIT sound BODYHIT2 else
									ifsound BODYHIT2 sound BODYHIT
								}
							}
								
						}
					}
					else 
					ifn sector[].lotag 2
					{
						ifpdistl 4096
						{
							ifsound BODYHIT ifsound BODYHIT2 nullop else
							{
								ifsound BODYHIT sound BODYHIT2 else
								ifsound BODYHIT2 sound BODYHIT
							}
						}
						else soundonce BODYHIT
					}
					state checkbleeding
				}
			break
			
			default
				ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
			break
			endswitch
			
			state tiletype
			switch TILETYPE
			case 1

			espawn WOODFALL
			setav[RETURN].initsprite 1

			break
			case 2 case 3 case 10
			ifsound RENDFLESH { stopsound RENDFLESH sound BLADEHITMET }
			
			
			spawn SPARKFALL
			ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
			else
			ifn sprite[].yvel FIREBOLT
			ifn sprite[].yvel SABERPROJ
			state hitmetalsounds
			
			ife sprite[hitsprite].picnum NEWRECON
			ifn sprite[].yvel FIREBOLT
			ifn sprite[].yvel SABERPROJ
			{
				rand temp 4 add temp METALSPARK1 espawnvar temp
				ifspawnedby APLAYER setav[RETURN].initsprite player[].curr_weapon
			}
			
			// apply AP damage for upgraded pistols or chainguns
			
			ifspawnedby APLAYER
			{
				set temp NO
				ife player[].curr_weapon PISTOL_WEAPON
				{
					ife pchar 0 ifvarand dukeupgrades[PISTOL_WEAPON] 1 set temp YES
					ife pchar 1 ifvarand shellyupgrades[PISTOL_WEAPON] 1 set temp YES
					ife pchar 2 ifvarand wesupgrades[PISTOL_WEAPON] 1 set temp YES
				}
				ifvarand gotshock 2 nullop else
				ife player[].curr_weapon CHAINGUN_WEAPON
				{
					ife pchar 0 ifvarand dukeupgrades[CHAINGUN_WEAPON] 2 set temp YES
					ife pchar 1 ifvarand shellyupgrades[CHAINGUN_WEAPON] 2 set temp YES
					ife pchar 2 ifvarand wesupgrades[CHAINGUN_WEAPON] 2 set temp YES
					ife pchar 3 ifvarand mandoupgrades[CHAINGUN_WEAPON] 2 set temp YES
				}
				ife temp YES
				{
					ifn hitsprite -1
					ife sprite[hitsprite].htpicnum SHOTSPARK1
					{
						geta[hitsprite].htextra temp
						mul temp 3 div temp 2
						seta[hitsprite].htextra temp
						spawn LILSCRAP
						state breakmetalsounds
					}
				}
			}
			break
			case 5 case 7
			ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
			else
			ifn sprite[].yvel FIREBOLT
			state hitconcretesounds
			espawn CONCRETEFALL
			setav[RETURN].initsprite 1
			break
			endswitch
		}
		else
		{
			geta[].htg_t 6 hitwall
			ifvarn hitwall -1
			{
				getwall[hitwall].nextsector mysector
				
				ifn mysector -1 ifvarand sector[mysector].ceilingstat 1 
				{
					ife sector[mysector].floorz sector[mysector].ceilingz
					killit
				}
				
				set TILETYPE -1
				getwall[hitwall].cstat tempb

				ifvarand tempb 2 // ignore this is upper half of wall
				{
					getwall[hitwall].nextsector B
					ife B -1 getwall[hitwall].picnum picnum
					else
					{
						ifg sprite[].z sector[B].ceilingz
						{
							getwall[hitwall].nextwall B
							ife B -1 getwall[hitwall].picnum picnum
							else getwall[B].picnum picnum
						}
						else getwall[hitwall].picnum picnum
					}
				}
				else
				getwall[hitwall].picnum picnum
				state tiletype
				switch TILETYPE
				case 1
				ife sprite[].yvel SABERPROJ ifn saberpos 0 
				{
					ife pchar 4 state clawclashsound else
					ife pchar 2 soundonce BLADEHITMET else
					soundoncevar sabclashsnd
				}
				espawn WOODFALL
				setav[RETURN].initsprite 1
				ife sprite[].yvel FIREBOLT seta[RETURN].pal 2
				break
				case 2 case 3 case 10
				spawn SPARKFALL
				ifn sprite[].yvel FIREBOLT
				ifn sprite[].yvel SABERPROJ
				{
					rand temp 4 add temp METALSPARK1 espawnvar temp
					ifspawnedby APLAYER setav[RETURN].initsprite player[].curr_weapon
				}
				ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
				else
				ife sprite[].yvel SABERPROJ ifn saberpos 0 
				{
					ife pchar 4 state clawclashsound else
					ife pchar 2 soundonce BLADEHITMET else
					soundoncevar sabclashsnd
				}
				else
				ifn sprite[].yvel FIREBOLT
				state hitmetalsounds
				break
				case 5 case 7
				ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
				else
				ife sprite[].yvel SABERPROJ ifn saberpos 0 
				{
					ife pchar 4 state clawclashsound else
					ife pchar 2 soundonce BLADEHITMET else
					soundoncevar sabclashsnd
				}
				else
				ifn sprite[].yvel FIREBOLT
				state hitconcretesounds
				espawn CONCRETEFALL
				ife sprite[].yvel FIREBOLT seta[RETURN].pal 2
				setav[RETURN].initsprite 1
				
				break
				default
				ife sprite[].yvel SABERPROJ ifn saberpos 0 
				{
					ife pchar 4 state clawclashsound else
					ife pchar 2 soundonce BLADEHITMET else
					soundoncevar sabclashsnd
				}
				break
				endswitch
			}
			else
			{
				geta[].htg_t 7 hitsector

				ifn hitsector -1
				{
					set TILETYPE -1
					
					getflorzofslope hitsector sprite[].x sprite[].y temp // temp is the return var
					sub temp sprite[].z
					ifl temp 3072
					{
						gets[hitsector].floorpicnum picnum
						set float -2 // floor
					}
					else
					{
						gets[hitsector].ceilingpicnum picnum
						set float 2 // ceiling
					}
					state tiletype
					switch TILETYPE
					case 1
						espawn WOODFALL
						setav[RETURN].initsprite float
						ife sprite[].yvel FIREBOLT seta[RETURN].pal 2
						ife sprite[].yvel SABERPROJ ifn saberpos 0 
						{
							ife pchar 4 state clawclashsound else
							ife pchar 2 soundonce BLADEHITMET else
							soundoncevar sabclashsnd
						}
					break
					case 2 case 3 case 10
						ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
						else
						ife sprite[].yvel SABERPROJ ifn saberpos 0 
						{
							ife pchar 4 state clawclashsound else
							ife pchar 2 soundonce BLADEHITMET else
							soundoncevar sabclashsnd
						}
						else
						ifn sprite[].yvel FIREBOLT
						state hitmetalsounds
						espawn SPARKFALL
						setav[RETURN].initsprite float
						ifn sprite[].yvel FIREBOLT
						ifn sprite[].yvel SABERPROJ
						{
							rand temp 4 add temp METALSPARK1 espawnvar temp
							ifspawnedby APLAYER setav[RETURN].initsprite player[].curr_weapon
						}
					break
					case 4
						ifrnd 128 sound BULLETDIRT1 else
						sound BULLETDIRT2
						// dirt debris
					break
					case 5 case 7
						ife sprite[].yvel SAWPROJ { soundonce SCRAPE set sawfleshtime 3 }
						else
						ife sprite[].yvel SABERPROJ ifn saberpos 0 
						{
							ife pchar 4 state clawclashsound else
							ife pchar 2 soundonce BLADEHITMET else
							soundoncevar sabclashsnd
						}
						else
						ifn sprite[].yvel FIREBOLT
						state hitconcretesounds
						espawn CONCRETEFALL
						ife sprite[].yvel FIREBOLT seta[RETURN].pal 2
						setav[RETURN].initsprite float
					break
					default
					ife sprite[].yvel SABERPROJ ifn saberpos 0 
					{
						ife pchar 4 state clawclashsound else
						ife pchar 2 soundonce BLADEHITMET else
						soundoncevar sabclashsnd
					}
					break
					endswitch
				}
			}
		}
			

		
	  }
  }
enda


eventloadactor LIGHTNING2
cstat 32768
enda

defstate start_strike

	set countvar -6
	cstat 2
	ifrnd 128 cstator 4
	rand temp 3
	ife temp 0
	{
		sound LIGHTN_1 sound LIGHTN_1 sound LIGHTN_1 sound LIGHTN_1 sound LIGHTN_1
	}
	else
	ife temp 1
	{
		sound LIGHTN_2 sound LIGHTN_2 sound LIGHTN_2 sound LIGHTN_2 sound LIGHTN_2
	}
	else
	ife temp 2
	{
		sound LIGHTN_3 sound LIGHTN_3 sound LIGHTN_3 sound LIGHTN_3 sound LIGHTN_3
	}
	else
	ife temp 3
	{
		sound LIGHTN_4 sound LIGHTN_4 sound LIGHTN_4 sound LIGHTN_4 sound LIGHTN_4
	}
	set x startx
	rand x2 1024
	sub x2 512
	add x x2
	seta[].x x
	seta[].htbposx x
	
	set y starty
	rand y2 1024
	sub y2 512
	add y y2
	seta[].y y
	seta[].htbposy y

ends

useractor notenemy LIGHTNING2

ifmove 0
{
	move STOPPED
	geta[].x startx
	geta[].y starty
	seta[].blend 1
	seta[].shade -127
	rand countvar 360
	add countvar 30
	seta[].alpha 128
	ifl sprite[].yrepeat 32 sizeat 72 255
	ife countvarb YES state start_strike
}
ifspritepal 6 spritepal 8

ifg countvar 0 sub countvar 1
else
ifl countvar 0
{
	ifl countvar -4 flash
	add countvar 1
	
	set x startx
	rand x2 512
	sub x2 256
	add x x2
	seta[].x x
	seta[].htbposx x
	
	set y starty
	rand y2 512
	sub y2 256
	add y y2
	seta[].y y
	seta[].htbposy y

	spawn SMALLSMOKE
	
	ifpdistl 1024
	ifp palive
	{
		set spriteid player[].i
		rand temp 3
		add temp 1
		add temp sprite[spriteid].htextra
		seta[spriteid].htextra temp
		seta[spriteid].htowner THISACTOR
		seta[spriteid].htpicnum FIRELASER
		state playerpainsounds
	}
	
	ife countvar 0 
	{
		cstat 32768
		seta[].x startx
		seta[].y starty
		rand countvar 360
		add countvar 180
		sound THUNDER sound THUNDER sound THUNDER sound THUNDER sound THUNDER
		
		ife countvarb YES killit
	}
}

ifrnd 4
ife countvar 0
{
	state start_strike
}

enda


useractor notenemy ARCBALLTRAIL 0 ARCBTRAILFRAMES

ifmove 0
{
	move STOPPED
	sizeat 32 32
	cstat 642
	seta[].alpha 128
	spritepal 23
}
sizeto 0 0

enda

useractor notenemy TIMEWARP 0

	ifaction 0
	{
		action ANULLACTION
		ifspawnedby SHOTSPARK1 spritepal 125
		seta[].blend 1
		cstat 130
		cstator 16
		ife mtype 0
		{
			ifrnd 128 set angvel 48 else set angvel -48
			espawn TIMEWARP
			seta[RETURN].pal 125
			setav[RETURN].mtype 1
			geta[RETURN].ang angvar
			add angvar 512
			seta[RETURN].ang angvar
			setav[RETURN].angvel angvel
		}
	}
	geta[].ang angvar
	add angvar angvel
	seta[].ang angvar
	
	ifcount 80 killit
	else
	ifcount 40
	{
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
	}
	else
	{
		sizeto 80 80
		sizeto 80 80
		sizeto 80 80
		sizeto 80 80
	}

enda

useractor notenemy SPINCUBE

	ifaction 0
	{
		action ANULLACTION
		seta[].blend 1
		cstat 130
		seta[].shade -126
		spritepal 125
		ife mtype 0
		{
			ifrnd 128 set angvel 48 else set angvel -48
			sizeat 32 32
			seta[].pitch 256
			espawn SPINCUBE
			setav[RETURN].mtype 1
			setav[RETURN].angvel angvel
		}
	}
	geta[].ang angvar
	add angvar angvel
	seta[].ang angvar
	
	ifcount 60 killit
	else
	ifcount 30
	{
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		ife mtype 0
		{
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		sizeto 0 0
		}
	}
	else
	{
		ife mtype 0
		{
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		sizeto 255 255
		}
		else
		{
		sizeto 128 128
		sizeto 128 128
		sizeto 128 128
		sizeto 128 128
		sizeto 128 128
		}
	}
	

enda

defstate chairbrokecode
	fall
	ifhitweapon ifdead
	{
		sound VENT_BUST
		ifactor CHAIRBROKE6
		{
		debris SCRAP3 4
		debris SCRAP4 4
		debris SCRAP5 4
		}
		else
		{
		debris SCRAP1 4
		debris SCRAP2 4
		debris SCRAP3 4
		debris SCRAP4 4
		}
		spawn BIGSMOKE
		killit
	}
ends

useractor notenemy CHAIRBROKE1 60 state chairbrokecode enda
useractor notenemy CHAIRBROKE4 60 state chairbrokecode enda
useractor notenemy CHAIRBROKE5 60 state chairbrokecode enda
useractor notenemy CHAIRBROKE6 60 state chairbrokecode enda
useractor notenemy GAMERCHAIRBROKE 60 state chairbrokecode enda

move CHAIRROLLVEL

defstate chaircode

fall
ifmove 0
{
	move STOPPED geth
	sizeat 30 28
	cstator 257
	ifactor SHOPPINGCART clipdist 64 else
	clipdist 40
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
}

ife heldobject THISACTOR break

state predamage
state damagepush
state playerpush

ifmove STOPPED
ifg dodgetime 0
{
	ifn sprite[].picnum CHAIR6 move CHAIRROLLVEL geth
	geta[].ang angvel
	set countvar 90
}

ifmove CHAIRROLLVEL
{
	cos xvel angvel
	sin yvel angvel
	setvarvar temp countvar
	
	mulvarvar xvel temp
	mulvarvar yvel temp
	shiftvarr xvel 13
	shiftvarr yvel 13
	sub countvar 1, ifl countvar 0 set countvar 0
	movesprite THISACTOR xvel yvel 0 CLIPMASK0 RETURN 
	
	ifn RETURN 0 
	{	
		set countvar 0
		move STOPPED geth
	}
	ife countvar 0
	{
		set countvar 0
		move STOPPED geth
	}
}
else ifpdistl 768
ife dodgetime 0
ifn sprite[].picnum CHAIR6
ifcansee
{
	move CHAIRROLLVEL geth
	seta[].ang angvel
	set countvar 60
}
	
ifhitweapon
{
	ifdead
	{
		spawn BIGSMOKE
		ifwasweapon RPG nullop else
		ifactor CHAIR1_AA
		{
			debris SCRAP1 5
			cactor CHAIRBROKE1
			move 0
			action 0
			strength 60
			break
		}
		ifactor CHAIR4
		{
			debris SCRAP1 5
			cactor CHAIRBROKE4
			move 0
			action 0
			strength 60
			break
		}
		ifactor CHAIR5
		{
			debris SCRAP1 5
			cactor CHAIRBROKE5
			move 0
			action 0
			strength 60
			break
		}
		ifactor CHAIR6
		{
			debris SCRAP4 5
			cactor CHAIRBROKE6
			move 0
			action 0
			strength 60
			break
		}
		ifactor GAMERCHAIR
		{
			debris SCRAP1 5
			cactor GAMERCHAIRBROKE
			move 0
			action 0
			strength 60
			break
		}
		ifspritepal 0 spritepal 16
		sound VENT_BUST
		debris SCRAP1 5
		debris SCRAP2 5
		debris SCRAP3 5
		debris SCRAP4 5
		
		killit
	}
}

ends

action ABIGDUKE 2906
useractor notenemy FAKEDUKE 1000

	ife mtype 0 break
	ifmove 0
	{	
		move STOPPEDFORPLAYER
		
		spriteflags 1
		action ABIGDUKE
		sizeat 15 14
		
		ifspawnedby RESPAWN seta[].alpha 255
	}
	ifaction ABIGDUKE ifcount 120
	{
		sizeat 24 22
		action ATERMINATOROFF
	}
	fall
	ifg sprite[].alpha 0
	{
		ifspritepal 33 getlastpal else spritepal 33
		geta[].alpha countvar
		
		ifl countvar 64
		{
			espawn LIGHTSTRAND
			set z sprite[].z
			sub z 656360
			seta[RETURN].z z
			setav[RETURN].myspawner THISACTOR
			setav[RETURN].initsprite 98304
			seta[RETURN].pal 11 // 23
			rand x 16 sub x 8 add x sprite[].x seta[RETURN].x x
			rand x 16 sub x 8 add x sprite[].y seta[RETURN].y x
		}
		
		sub countvar 4, ifle countvar 0 
		{
			set countvar 0
			ifspritepal 33 getlastpal
			globalsound DUKE_REVIVE2
		}
		seta[].alpha countvar
	}
	ife thiscam -1 
	{
		cstat 32768
		ife wave 50 
		{
			set wave 51
			screensound BIGCHEER
		}
	}

enda

useractor notenemy ALIENVINES 60

ifaction 0
{
	action ANULLACTION
	cstator 257
}
ifhitweapon
{
	spritepal 6
	guts JIBS6 1
	ifdead
	{
		sound SQUISHED
		cactor ALIENVINESBROKE
		debris SCRAP3 12
		state lite_jibs
		geta[].cstat temp
		xorvar temp 257
		seta[].cstat temp
	}
	else sound PISTOL_BODYHIT
	getlastpal
}

enda

// ARCBTRAILFRAMES is just -1

useractor notenemy FANACTIVATE 0 ANULLACTION

	ife mtype 1
	{
		ifaction ANULLACTION action ARCBTRAILFRAMES
		ifactorsound THISACTOR FANSOUND nullop else sound FANSOUND
	}
	else
	{
		ifaction ARCBTRAILFRAMES action ANULLACTION
		ifactorsound THISACTOR FANSOUND stopactorsound THISACTOR FANSOUND
	}

enda

useractor notenemy ICESPRITE 0

ifvarn myspawner -1
{
	ifge padang 131313 ifle padang 131314 ife sprite[myspawner].statnum 1 spritepal 1
	ifvarn sprite[myspawner].pal 1
	ifvarn sprite[myspawner].pal 27 spritepal 1
	ifvarvarn sprite[myspawner].picnum mtype spritepal 1
	ifn padang 131313 ifn padang 131314 ife actorvar[myspawner].monstatus 2 spritepal 1
	ife sprite[myspawner].statnum 1024 spritepal 1
	ifspritepal 1
	{
		ifge padang 131313 ifle padang 131314 
		{
			spritepal 27
			seta[myspawner].cstat 257
		}
		sound GLASS_BREAKING
		lotsofglass 10
		ifn padang 131313 ifn padang 131314 seta[].alpha 64
		state pottery_debris
		state pottery_debris
		state pottery_debris
		killit
	}
	ife initsprite 0
	{
		seta[].mdflags 16
		cstat 514
		ifrnd 128 cstator 4
		ifge padang 131313 ifle padang 131314 
		{
			spritepal 27
			geta[myspawner].cstat initflags
			seta[myspawner].cstat 0
		}
		ifvarand sprite[myspawner].cstat 128 cstator 128
		set initsprite 1
		
		set y tiledata[mtype].yoffset
		seta[].yoffset y
		
		set x tiledata[mtype].ysize
		mul x sprite[myspawner].yrepeat
		div x 96
		ifge padang 131313 ifle padang 131314 { mul x 3 div x 4 }
		seta[].yrepeat x
		
		set x tiledata[mtype].xsize
		mul x sprite[myspawner].xrepeat
		div x 96
		add x 4
		ifge padang 131313 ifle padang 131314 shiftr x 1
		seta[].xrepeat x
	}
	
	ifge padang 131313 ifle padang 131314
	{
		geta[].z z
		fall
		ifl z sprite[].z
		{
			sub z sprite[].z
			geta[myspawner].z z2
			abs z
			add z2 z
			seta[myspawner].z z2
		}
		cstator 256
		geta[].htextra temp
		ifg temp 0
		{
			geta[myspawner].htextra tempb
			add tempb temp
			seta[myspawner].htextra tempb
			seta[myspawner].htowner sprite[].htowner
			seta[myspawner].htang sprite[].htang
			seta[myspawner].htpicnum sprite[].htpicnum
		}
		state damagepush
		state playerpush
		ifhitweapon strength 0
		setsprite myspawner sprite[].x sprite[].y sprite[].z
		// seta[myspawner].x sprite[].x
		// seta[myspawner].y sprite[].y
	}
	else setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
}
enda

useractor notenemy SHOOT_THIS 20

// ife mysignpost -1 state spawnmysignpost
ifmove 0
{
	move STOPPED
	cstator 256
	set monstatus 1
}
ifhitweapon
{
	ifdead
	{
	set monstatus 2
	shoot SHRUNKBULLET
	killit
	}
}

enda

defstate bullet_spark
ifmove 0
{
	cstat 1154
	sizeat 1 1
	ifrnd 128 cstator 4 ifrnd 128 cstator 8
	seta[].blend 255
	move STOPPED
}
ifmove STOPPED
{
	seta[].shade -16
	geta[].xrepeat temp add temp 2 
	ife initsprite SHOTGUN_WEAPON ifg temp 4 set temp 4
	seta[].xrepeat temp
	seta[].yrepeat temp
	
	ifcount 4 killit
}
ends

useractor notenemy METALSPARK1 state bullet_spark enda
useractor notenemy METALSPARK2 state bullet_spark enda
useractor notenemy METALSPARK3 state bullet_spark enda
useractor notenemy METALSPARK4 state bullet_spark enda
useractor notenemy METALSPARK5 state bullet_spark enda


action BOLTACTION 0 2 1 1 10

spritenoshade BOLT_TRAIL
useractor notenemy BOLT_TRAIL 0 BOLTACTION

ifmove 0
{
	sizeat 8 8
	seta[].shade -100
	ifpdistl 644 killit
	cstat 130
	seta[].blend 1
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
}
ifactioncount 1 cstator 514
ifactioncount 2 killit

enda


useractor notenemy METALHOLE 0
ifmove 0
{
	sizeat 2 2
	cstator 514 // 2
	move STOPPED
	insertspriteq
	eqspawn METALHOLEINNER
	{
		getactor[THISACTOR].cstat temp
		subvar temp 2
		setactor[RETURN].cstat temp
		setactor[RETURN].xrepeat 2
		setactor[RETURN].yrepeat 2
		getactor[THISACTOR].ang angvar
		setactor[RETURN].ang angvar
	}
}
enda

useractor notenemy ORGANICHOLE 0
ifmove 0
{
	sizeat 4 4
	move STOPPED
	insertspriteq
}
enda

useractor notenemy WOODHOLE 0
ifmove 0
{
	iffloordistl 2 sizeat 6 3 else
	sizeat 4 4
	cstator 2
	move STOPPED
	insertspriteq
	eqspawn WOODHOLEINNER
	
	getactor[THISACTOR].cstat temp
	subvar temp 2
	setactor[RETURN].cstat temp
	setactor[RETURN].xrepeat 4
	setactor[RETURN].yrepeat 4
	getactor[THISACTOR].ang angvar
	setactor[RETURN].ang angvar
	
}
enda

useractor notenemy CONCRETEHOLE 0
ifmove 0
{
	sizeat 2 2
	cstator 2
	move STOPPED
	insertspriteq
}
enda

useractor notenemy ELECTRONICHOLE 0
ifmove 0
{
	sizeat 2 2
	cstator 2
	move STOPPED
	insertspriteq
}
enda


move DUKENOTMOVING
state handle_dead_dukes
  fall
  ifmove 0   // 1st time initializiation...
  {
    ifrnd 128
      cstat 4
    else
      cstat 0
    move DUKENOTMOVING
  }

  ifsquished
  {
    sound SQUISHED
    state random_ooz
    killit
  }
  else
    ifcount 1024
      ifpdistg 4096
        killit
  else
  {
    strength 0
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      state standard_jibs
      killit
    }
  }
ends

action PLYINGFRAMES 0 1 0 1 1
actor DUKELYINGDEAD 0 PLYINGFRAMES
  state handle_dead_dukes
enda


spritenoshade LASERDOT
spritenopal LASERDOT
useractor notenemy LASERDOT 0

	// ifinwater nullop else
	// ifonwater nullop else
	// {
		// geta[].sectnum mysector
		// set tempc NO
		// headspritesect spriteid mysector
		// whilevarn spriteid -1
		// {
			// ife sprite[spriteid].picnum SECTOREFFECTOR
			// ife sprite[spriteid].lotag 7 set tempc YES
			// nextspritesect spriteid spriteid
		// }
		// ife tempc YES killit
	// }
	seta[].shade -127
	seta[].blend 255
	sizeat 32 32
	cstator 128
	// ifcount 6 killit
	
	ifcount 2 killit
	// set xydist scandist
	// shiftr xydist 8 // 9
	// add xydist 8 // 18
	// ifg xydist 255 set xydist 255
	// seta[].xrepeat xydist seta[].yrepeat xydist
	// ifcount 1 
	// { 
		// ife dotsprite THISACTOR set dotsprite -1 
		// killit 
	// }

enda


spritenoshade AIRFLASH1
spritenoshade AIRFLASH2
useractor notenemy AIRFLASH1 0

ifmove 0
{
	move STOPPED
	seta[].shade -127
	ife mtype 1 sizeat 20 20 else
	ife mtype 2 sizeat 24 24 else
	sizeat 16 16
	cstat 146
	espawn AIRFLASH2
	setav[RETURN].mtype mtype
	geta[].ang angvar
	sub angvar 512
	seta[].ang angvar
}
sizeto 0 0
sizeto 0 0

enda

useractor notenemy AIRFLASH2

ifmove 0
{
	move STOPPED
	ife mtype 1 sizeat 20 20 else
	ife mtype 2 sizeat 24 24 else
	sizeat 16 16
	cstat 38
	seta[].shade -127
}
sizeto 0 0
sizeto 0 0
enda

spritenoshade FAKEBRACCUS
action AHOLOBRACCUS  0  2  5  1  90

useractor notenemy FAKEBRACCUS 0 AHOLOBRACCUS

	set team 7
	set monstatus 7
	sizeat 24 20
	ifrnd 96 seta[].blend 0 else
	seta[].blend 1
	seta[].shade 0
	cstat 2
	setsprite THISACTOR sprite[player[].holoduke_on].x sprite[player[].holoduke_on].y sprite[player[].holoduke_on].z
	set temp NO
	ife player[].holoduke_on -1 set temp YES
	ifn pchar 4 set temp YES
	ife temp YES
	{
		stopsound HOLOBRAC1
		stopsound HOLOBRAC2
		killit
	}
	set D NO
	set temp player[].player_par
	modvar temp 15
	ifvarand inven_upgrades 4
	ife temp 0
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].team 0
			ife actorvar[spriteid].gooify 0
			ife actorvar[spriteid].monstatus 1
			ifg actorvar[spriteid].monxp 0
			ife D NO
			{
				
				set TMP_A plevel
				shiftl TMP_A 7
				add TMP_A 150
				sub TMP_A actorvar[spriteid].inithp
				ifg TMP_A 0
				{
					set D YES
					ifl TMP_A 60 set TMP_A 60
					mul TMP_A -1
					setav[spriteid].gooify TMP_A
					setav[spriteid].team 1 sub maxkills 1
					setav[spriteid].bottarget -1
					setav[spriteid].bluetimer 0
				}
			}
			nextspritestat spriteid spriteid
		}
	}
	

enda


action ABAGGERSTAND -1  0  5  1  10
action ABAGGERCROUCH 85  0  5  1  10
useractor notenemy HOLOBAGGER 0 ABAGGERSTAND

ife mtype -1 killit
else
ifn mtype 0
{
	cstat 2
	ife player[].holoduke_on -1 killit
	ifcount 4
	{
		ifaction ABAGGERSTAND action ABAGGERCROUCH else
		action ABAGGERSTAND
		resetcount
	}
	setsprite THISACTOR sprite[mtype].x sprite[mtype].y sprite[mtype].z
}

enda


action ALAVABUBBLE
action ALAVABUBBLEANIM 0 5 1 1 16

useractor notenemy LAVABUBBLE
  ifaction 0
  {
    cstat 32768
    action ALAVABUBBLE
  }
  else
    ifaction ALAVABUBBLE
  {
    ifcount 72
      ifrnd 2
    {
      cstat 0
      action ALAVABUBBLEANIM
    }
  }
  else
  {
    ifactioncount 5
    {
      cstat 32768
      action ALAVABUBBLE
    }
  }
enda


action JIBSTILL3 -2 1 1 1 1
action JIBSTILL2 0 1 1 1 1
action JIBSTILL -1 1 1 1 1

defstate jibcode

ife initsprite 1
{
	ifspritepal 1 
	{
		ifrnd 128
		{
			ifsound ICE_BOUNCE { ifrnd 32 sound ICE_BOUNCE } else
			soundonce ICE_BOUNCE
		}
		else
		{
			ifsound GLASSTINK { ifrnd 32 sound GLASSTINK } else
			soundonce GLASSTINK
		}
		
	}
	else
	ifspritepal 27
	{
		ifrnd 128
		{
			ifsound ICE_BOUNCE { ifrnd 32 sound ICE_BOUNCE } else
			soundonce ICE_BOUNCE
		}
		else
		{
			ifsound GLASSTINK { ifrnd 32 sound GLASSTINK } else
			soundonce GLASSTINK
		}
	}
	else
	{
		ifrnd 64
		{
			ifrnd 96 soundonce STEPNIT else
			soundonce JIBLAND
		}
	}
	seta[].pitch 0
	seta[].roll 0
	clipdist 4
	// insertspriteq
	ifactor 1770 action JIBSTILL3 else
	ifactor 1774 { set yoffset -256 action JIBSTILL3 } else
	ifactor 1778 action JIBSTILL3 else
	ifactor 2203 action JIBSTILL3 else
	ifactor 2207 action JIBSTILL3 else
	ifactor 2211 action JIBSTILL3 else
	action JIBSTILL2
	cstat 256
	seta[].mdflags 16
	ifactor 1770 { set yoffset 256 ifn sprite[].pal 1 spawn BLOODPOOL2 } else
	ifactor 2203 { set yoffset 256 ifn sprite[].pal 1 spawn BLOODPOOL2 } else
	ifactor 2254 { set yoffset -384 } else
	ife yoffset 0
	set yoffset 512
	ifrnd 128 cstator 4
	strength 0
	set monstatus 109
	ifg THISACTOR 14000 killit
}

	ifl framerate 15 killit
	add initsprite 1
	ifge initsprite jibs_duration
	{
		geta[].alpha temp
		add temp 1
		seta[].alpha temp
	}
	ifg sprite[].alpha 250 killit

fall

ifhitweapon
{
	guts JIBS6 2 
	killit 
}

ends

useractor notenemy 2203 ifvarn initsprite 0 state jibcode enda // LIZMANHEAD
useractor notenemy 2207 ifvarn initsprite 0 state jibcode enda // LIZMANARM
useractor notenemy 2211 ifvarn initsprite 0 state jibcode enda // LIZMANLEG


useractor notenemy 2249 ifvarn initsprite 0 state jibcode enda // JIBS1 = spine
useractor notenemy 2254 ifvarn initsprite 0 state jibcode enda // JIBS2 = eyeball
useractor notenemy 2259 ifvarn initsprite 0 state jibcode enda // JIBS3 = heart
useractor notenemy 2264 ifvarn initsprite 0 state jibcode enda // JIBS4 = guts
useractor notenemy 2269 ifvarn initsprite 0 state jibcode enda // JIBS5 = ribcage

useractor notenemy 1770 ifvarn initsprite 0 state jibcode enda

useractor notenemy 1774 ifvarn initsprite 0 state jibcode enda // TROOPARM
useractor notenemy 1778 ifvarn initsprite 0 state jibcode enda // TROOPLEG

state jibgroundcode

ifmove 0
{
	move STOPPED
	ifl sprite[].xrepeat 8
	{
		sizeat 48 48
		set monstatus 109
		switch sprite[].picnum
		
		case PIGARMGROUND 
		case NEWCOMMHEADGROUND
			sizeat 32 32 
		break
			
		case RANGERARMGROUND
		case BEASTARMGROUND
		case BEASTLEGGROUND 
			sizeat 40 40 
		break
		
		case RANGERLEGGROUND 
			sizeat 42 38 
		break
		
		case ARMPIGARMGROUND
			sizeat 22 22
		break
		
		case ARMLIZARMGROUND
		case ARMLIZCHESTGROUND
		case ARMLIZLEGGROUND
			sizeat 24 24
		break
		
		case ARMPIGFOOTGROUND
		case ARMPIGLEGGROUND
			sizeat 40 36 
		break
		
		case ZOMBIEHEADGROUND 
		case CRAZYHEADGROUND
			sizeat 30 28
		break
		
		case ANTHEADGROUND 
			sizeat 36 36
		break
		
		case BULLHEADGROUND 
		case BULLLEGGROUND 
		case BULLARMGROUND
		case MANDOARMGROUND
		case MANDOLEGGROUND
		case MANDOCHESTGROUND
		case MANDOHEADGROUND
		
			 sizeat 26 26 
		break
		
		case PIGLEGGROUND 
			sizeat 28 28
		break
		
		default	sizeat 48 48 
		break
		
		endswitch
	
	}
	ifrnd 128 cstat 0 else cstat 4
	ifrnd 64
	{
		ifrnd 96 soundonce STEPNIT else
		soundonce JIBLAND
	}
	cstator 256
	strength 0
	clipdist 4
	ifn sprite[].pal 1
	{
	ifactor MANDOCHESTGROUND spawn BLOODPOOL2 else
	ifactor ARMLIZCHESTGROUND spawn BLOODPOOL2 else
	ifactor BULLHEADGROUND spawn BLOODPOOL2 else
	ifactor BBHEADJIBGROUND { spritepal 6 spawn BLOODPOOL getlastpal } else
	ifactor ANTHEADGROUND { spritepal 6 spawn BLOODPOOL getlastpal } else
	ifactor BEASTHEADGROUND { spritepal 6 spawn BLOODPOOL getlastpal } else
	ifactor BEASTLEGGROUND { spritepal 6 spawn BLOODPOOL getlastpal } else
	ifactor BEASTARMGROUND { spritepal 6 spawn BLOODPOOL getlastpal } else
	ifactor BBARMJIBGROUND nullop else
	ifactor ZOMBIEHEADGROUND nullop else
	ifrnd 128 spawn BLOODPOOL2
	}
	ifg THISACTOR 14000 killit
	
}

fall

	ifl framerate 15 killit
	add initsprite 1
	ifge initsprite jibs_duration
	{
		geta[].alpha temp
		add temp 1
		seta[].alpha temp
	}
	ifg sprite[].alpha 250 killit

ifhitweapon
{
	ifactor ZOMBIEHEADGROUND spritepal 8
	guts JIBS6 2 
	killit 
}

ends


action AHEARTSTOP 1  1  1  1  10
useractor notenemy JIBS7 0 // beating heart

ifmove 0
{
	sizeat 11 11
	clipdist 4
	move STOPPEDFORPLAYER geth
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	cstator 256
	spawn BLOODPOOL2
}
fall

	ifl framerate 15 killit
	add initsprite 1
	ifge initsprite jibs_duration
	{
		geta[].alpha temp
		add temp 1
		seta[].alpha temp
	}
	ife initsprite 600 action AHEARTSTOP
	ifg sprite[].alpha 250 killit

ifhitweapon
{
	guts JIBS6 2 
	killit 
}

enda

defstate shoothead

	set picnum -1
	switch sprite[].picnum
	case PIGHEADGROUND set picnum PIGHEADPROJ break
	case TROOPHEADGROUND set picnum TROOPHEADPROJ break
	case NEWTROOPHEADGROUND set picnum NEWTROOPHEADPROJ break
	case LIZHEADGROUND set picnum LIZHEADPROJ break
	case COMMHEADGROUND set picnum COMMHEADPROJ break
	case NEWCOMMHEADGROUND set picnum NEWCOMMHEADPROJ break
	case CRAZYHEADGROUND set picnum CRAZYHEADPROJ break
	case ZOMBIEHEADGROUND set picnum ZOMBIEHEADPROJ break
	case LIZRANGERHEAD set picnum RANGERHEADPROJ break
	case BEASTHEADGROUND set picnum BEASTHEADPROJ break
	case ANTHEADGROUND set picnum ANTHEADPROJ break
	case BULLHEADGROUND set picnum BULLHEADPROJ break
	case MANDOHEADGROUND set picnum MANDOHEADPROJ break
	endswitch
	
	ifn picnum -1 
	{
		ifpdistl 4096 screensound KICKIMPACT2
		rand zdist 1024
		sub zdist 3072
		ezshootvar zdist picnum
		setav[RETURN].countvar countvar
		seta[RETURN].ang sprite[].ang
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		add countvarc 1
		setav[RETURN].countvarc countvarc
		killit
	}
ends


defstate headgroundcode

ifmove 0
{
	move STOPPED
	ifl sprite[].xrepeat 8
	{
		sizeat 48 48
		set monstatus 109
		switch sprite[].picnum
		
		
		case NEWCOMMHEADGROUND
			sizeat 32 32 
		break

		case ZOMBIEHEADGROUND 
		case CRAZYHEADGROUND
			sizeat 30 28
		break
		
		case ANTHEADGROUND 
			sizeat 36 36
		break
		
		case BULLHEADGROUND 
		case MANDOHEADGROUND
		
			 sizeat 26 26 
		break
		
		case PIGLEGGROUND 
			sizeat 28 28
		break
		
		default	sizeat 48 48 
		break
		
		endswitch
	
	}
	ifrnd 128 cstat 0 else cstat 4
	ifrnd 64
	{
		ifrnd 96 soundonce STEPNIT else
		soundonce JIBLAND
	}
	cstator 256
	strength 100
	clipdist 4
	ifn sprite[].pal 1
	{
		ifactor BBHEADJIBGROUND { ifrnd 64 { spritepal 6 spawn BLOODPOOL getlastpal } } else
		ifactor ANTHEADGROUND { ifrnd 64 { spritepal 6 spawn BLOODPOOL getlastpal } } else
		ifactor BEASTHEADGROUND { ifrnd 64 { spritepal 6 spawn BLOODPOOL getlastpal } } else
		ifactor ZOMBIEHEADGROUND nullop else
		ifrnd 64 spawn BLOODPOOL2
	}
}

fall

ifp pfacing
ifp ponground
ifp palive
ifpdistl 1024
ifcansee
ifn disable_kickheads YES
{
	seta[].ang player[].ang
	state shoothead
}

ifg countvarc 1
{
	strength 1
	seta[].htextra 10
}
ifhitweapon
{
	ifdead
	{
		ifrnd 128 guts JIBS2 1
		ifactor ZOMBIEHEADGROUND spritepal 8
		guts JIBS6 2 
		spawn BLOODEXP
		killit 
	}
	else
	{
		seta[].ang sprite[].htang
		state shoothead
	}
}

ends

state canarmcode

ifmove 0
{
	move STOPPED
	insertspriteq
	ifl sprite[].xrepeat 4
		sizeat 64 64
	
	ifrnd 128 cstat 0 else cstat 4
}
fall

ends

useractor notenemy MECHESTGROUND state canarmcode enda

useractor notenemy NEWMECHARMGROUND state canarmcode enda

useractor notenemy REDARMGROUND 0 state canarmcode enda

useractor notenemy BLUEARMGROUND 0 state canarmcode enda

useractor notenemy PIGHEADGROUND 0 state headgroundcode enda

useractor notenemy TROOPHEADGROUND 0 state headgroundcode enda

useractor notenemy NEWTROOPHEADGROUND 0 state headgroundcode enda

useractor notenemy LIZHEADGROUND 0 state headgroundcode enda

useractor notenemy COMMHEADGROUND 0 state headgroundcode enda

useractor notenemy NEWCOMMHEADGROUND 0 state headgroundcode enda

useractor notenemy ZOMBIEHEADGROUND 0 state headgroundcode enda

useractor notenemy CRAZYHEADGROUND 0 state headgroundcode enda

useractor notenemy LIZRANGERHEAD 0 state headgroundcode enda

useractor notenemy COMMARMGROUND 0 state jibgroundcode enda

useractor notenemy PIGLEGGROUND 0 state jibgroundcode enda

useractor notenemy PIGARMGROUND 0 state jibgroundcode enda

useractor notenemy RANGERLEGGROUND 0 state jibgroundcode enda

useractor notenemy RANGERARMGROUND 0 state jibgroundcode enda

useractor notenemy BEASTHEADGROUND 0 state headgroundcode enda

useractor notenemy ANTHEADGROUND 0 state headgroundcode enda

useractor notenemy BEASTARMGROUND 0 state jibgroundcode enda

useractor notenemy BEASTLEGGROUND 0 state jibgroundcode enda

useractor notenemy ARMPIGARMGROUND 0 state jibgroundcode enda

useractor notenemy ARMPIGFOOTGROUND 0 state jibgroundcode enda

useractor notenemy ARMPIGLEGGROUND 0 state jibgroundcode enda

useractor notenemy BULLHEADGROUND 0 state headgroundcode enda

useractor notenemy BULLLEGGROUND 0 state headgroundcode enda

useractor notenemy BULLARMGROUND 0 state jibgroundcode enda

useractor notenemy ARMLIZCHESTGROUND 0 state jibgroundcode enda

useractor notenemy ARMLIZARMGROUND 0 state jibgroundcode enda

useractor notenemy ARMLIZLEGGROUND 0 state jibgroundcode enda

useractor notenemy BBHEADJIBGROUND 0 state headgroundcode enda

useractor notenemy BBARMJIBGROUND 0 state jibgroundcode enda

useractor notenemy MANDOCHESTGROUND 0 state jibgroundcode enda

useractor notenemy MANDOLEGGROUND 0 state jibgroundcode enda

useractor notenemy MANDOHEADGROUND 0 state headgroundcode enda

useractor notenemy MANDOARMGROUND 0 state jibgroundcode enda

useractor notenemy EGGJIBGROUND 0 

	ifmove 0
	{
		move STOPPED
		sizeat 40 40
		ifrnd 128 cstat 0 else cstat 4
		cstator 256
		strength 0
		ifrnd 32 sound PL_SQUISH2
		// espawn BLOODPOOL
		// seta[RETURN].pal 6
		
	}

	fall
	
	ifcount 30
	{
		geta[].alpha temp
		add temp 1
		seta[].alpha temp
		ifge temp 254 killit
	}

	ifhitweapon
	{
		spritepal 6
		guts JIBS6 2 
		killit 
	}

enda

move JIBDROP 132
useractor enemy COMMJIB 100

ifmove 0
	move JIBDROP geth

ifmove JIBDROP iffloordistl 2
{
	move STOPPED
	cstator 256
	soundonce JIBLAND
	spawn BLOODPOOL2
	spriteflags 4194304
	// getp[].max_actors_killed temp
	// sub temp 1
	// setp[].max_actors_killed temp
	geta[].yrepeat y
	sub y 4
	seta[].yrepeat y
}

fall

ifhitweapon
{
	ifdead
	{
		sound SQUISHED
		state lite_jibs
		killit
	}
}

enda

defstate EJECTCOLA
	espawn COLACANSIDE
	ife mtype BLUEBALLVEND setav[RETURN].droptile BLUEBALLCAN
	else ife mtype BRAWNDOVEND setav[RETURN].droptile BRAWNDOCAN
	else ife mtype REACTORVEND setav[RETURN].droptile REACTORCAN
	else
	{
		rand temp 4
		ife temp 1 setav[RETURN].droptile BLUEBALLCAN else ife temp 2
		setav[RETURN].droptile ENERGYCAN else ife temp 3
		setav[RETURN].droptile PEACAN
	}

	getactor[RETURN].z z
	subvarvar z initsprite
	setactor[RETURN].z z
	setvar countvar 0
	getactor[THISACTOR].ang angvar
	randvar temp 48
	ifrnd 128 mulvar temp -1
	addvarvar angvar temp
	setactor[RETURN].ang angvar
	set pal 0
	ifrnd 192 
	{ 
		rand temp 6 
		ife temp 0 set pal 10 else
		ife temp 1 set pal 14 else
		ife temp 2 set pal 16 else
		ife temp 3 set pal 20 else
		ife temp 4 set pal 21 else
		ife temp 5 set pal 22 else
		set pal 23
	}
	setactor[RETURN].pal pal
	addvar countvarb 1 // how many cans have spawned
	// ifvarg countvarb 12 killit
ends

state EJECTSNACK

	espawn SNACKSPRITE
	getactor[RETURN].z z
	subvarvar z initsprite
	setactor[RETURN].z z
	setvar countvar 0
	getactor[THISACTOR].ang angvar
	randvar temp 256
	sub temp 128
	addvarvar angvar temp
	setactor[RETURN].ang angvar
	rand pal 9
	add pal 10
	ife pal 19 set pal 23
	ife pal 18 set pal 22
	ife pal 17 set pal 21
	seta[RETURN].pal pal
	
	addvar countvarb 1 // how many snacks have spawned
	// ifvarg countvarb 12 killit

ends

state broken_vender
	ifcount 12
	{
		ifg botclip 0
		{
			set value 12
			state spawnimpcoins
			sub botclip 1
		}
		
		addvar countvarb 1
		ifle countvarb 8
		{
			ife mtype VENDMACHINE state EJECTSNACK else
			state EJECTCOLA 
		}
		resetcount
		// ifrnd 8 killit
		
		ifvarg countvarb 8
		ifl botclip 1 killit
	}
	ifrnd 16 stopsound DISPENSE_CAN
	soundonce DISPENSE_CAN
ends

useractor notenemy COLAMACHINE 0

ifmove 0
{
	cstator 16
	move STOPPED
	ifvarg initsprite 0 divvar initsprite  4 else setvar initsprite 4096
	// failsafe deletion check
	cactor SMALLSMOKE 
	findnearspritez COLAMACHINE 384 4096 spriteid
	ifn spriteid -1 killit // TOO CLOSE TOGETHER
	cactor COLAMACHINE
	ife mtype SNACKVEND set mtype VENDMACHINE
	
	set botclip 2
}

ifpdistl 1280
  ifp pfacing
      ifcount 12
        ifvarl countvar 2
			ifl countvarb 12
{
	set handicon 3
	ifhitspace
	{
		ifge credits 50
		{
			resetcount
			addvar countvar 1
			sound COINSLOT
			add botclip 1
			sub credits 50
			sub lastcredits 50
		}
		else quote 361
	}
}

ifvarg countvar 0
{
	stopsound DUKE_GRUNT
	stopsound B_SEARCH
	stopsound B_GRUNT2
	stopsound M_LIFTING
	stopsound M_HARDLAND1
	stopsound M_WHEREISIT
	stopsound DEA_WHEREISIT
	stopsound DEA_EFFORT1
	stopsound DEA_FALL1
	stopsound DEA_HMMM
	addvar countvar 1
	
	ifl countvarb 12
	ifvare countvar 34 { ife mtype VENDMACHINE sound DISPENSE_SNACK else sound DISPENSE_CAN }
	
	ifvare countvar 60 
	{
		ifl countvarb 12
		{
			ife mtype VENDMACHINE state EJECTSNACK else
			state EJECTCOLA
		}
		else set countvar 0
		
	}
}
ife myspawner -1
{
	quote 1084
	state broken_vender
	break
}
getwall[myspawner].picnum picnum
ifvarn picnum COLAMACHINE // if the wall has changed picnum, the machine is broken
ifvarn picnum VENDMACHINE
ifvarn picnum BLUEBALLVEND
ifvarn picnum SNACKVEND
ifvarn picnum ASIANVEND
ifvarn picnum REACTORVEND
ifvarn picnum BRAWNDOVEND
	state broken_vender
	

enda

defstate goosearch

	ifcount 3
	{
		resetcount
		headspritestat myvictim 1
		whilevarn myvictim -1
		{
			ife actorvar[myvictim].monstatus 1
			ife actorvar[myvictim].gooify 0
			{
				set xydist sprite[].xrepeat
				shiftl xydist 5
				ldist xydist2 THISACTOR myvictim
				ifl xydist2 xydist
				{
					set z sprite[].z
					sub z sprite[myvictim].z
					abs z
					ifl z 6144
					{
						add mtype 8
						set gooify 60
						set TMP_A dweaplevel[FREEZE_WEAPON]
						shiftl TMP_A 4
						add gooify TMP_A
						setav[myvictim].gooify gooify
						set temp sprite[myvictim].htextra
						add temp 4
						shiftr TMP_A 2
						add temp TMP_A
						seta[myvictim].htextra temp
						seta[myvictim].htowner player[].i
						seta[myvictim].htpicnum GOOBULLET
						soundonce GLOOPIMPACT
						
						espawn DRIPBLOOD
						seta[RETURN].cstat 16
						seta[RETURN].pal 126
						
						set z sprite[myvictim].z, sub z 8192
						setav[RETURN].myvictim myvictim
						setav[RETURN].myspawner myvictim
						setav[RETURN].startx sprite[myvictim].x
						setav[RETURN].starty sprite[myvictim].y
						setav[RETURN].float sprite[myvictim].z
						setsprite RETURN sprite[myvictim].x sprite[myvictim].y z
					}
				}
			}
			nextspritestat myvictim myvictim
		}
	}

ends

defstate spawngoogas
	espawn POISONGAS
	seta[RETURN].pal 126
	seta[RETURN].htpicnum GOOBULLET
ends

useractor notenemy BLOODPOOL2 0

ifmove 0
{
	ife sprite[].sectnum -1 killit
	ifvarand sector[].floorstat 1 killit
	move STOPPED
	rand angvar 2047
	seta[].ang angvar
	sizeat 16 16
	cstat 32
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	cstator 32768
	ifn mtype 1
	{
		ifawayfromwall
		{
			getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum temp temp z spriteid 192 CLIPMASK0
			ifge spriteid 49152
			{
				sub spriteid 49152
				ifvarand sprite[spriteid].cstat 32 nullop else killit
			}
			else
			ifn sector[].floorslope 0 killit
			
			insertspriteq
		}
		else 
		{ 
			spawn BLOODPOOL
			killit 
		}
	}
	else
	{
		ifawayfromwall
		{
			ifn sector[].floorslope 0 state spawngoogas
			else
			getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum temp temp z spriteid 192 CLIPMASK0
			ifge spriteid 49152
			{
				sub spriteid 49152
				ifvarand sprite[spriteid].cstat 32 nullop else
				state spawngoogas
			}
		}
		else state spawngoogas
	}
}

ifp ponground
ifpdistl 408
{
	setp[].footprintcount 10
	ifspritepal 0 setp[].footprintpal 2 else
	setp[].footprintpal sprite[].pal
	setp[].footprintshade sector[].floorshade
}

ifvarand sprite[].cstat 32768
iffloordistl 8 
{
	geta[].cstat temp
	xorvar temp 32768
	seta[].cstat temp
	cstator 32
}


ifn mtype 0 
{
	ife mtype 1
	sizeto 52 52 
	else
	{
		set x 52
		sub x mtype
		ifg sprite[].xrepeat x
			sizeto 1 1
		ifge mtype 48 killit
	}
	
	state goosearch
	
	add countvar 1
	ifg countvar 900
	{
		set temp countvar
		sub temp 900
		shiftl temp 1
		seta[].alpha temp
		ifg countvar 1027 killit
	}
}
else
sizeto 40 40
fall

enda

move GASDRIFTSLOW 16
move GASDRIFT 24
move GASDRIFTFAST 32
action GASFRAMES -1 5 1 1 18
useractor notenemy POISONGAS 0 GASFRAMES

ifmove 0
{
	ifrnd 85 move GASDRIFTSLOW randomangle
	else
	ifrnd 128 move GASDRIFT randomangle else
	move GASDRIFTFAST randomangle
	ife sprite[].htpicnum GOOBULLET
	{
		spriteflags 4
		seta[].shade -100
	}
	else
	ifn sprite[].htpicnum AIRSHOCK
	{
		geta[].z z
		sub z 4096
		seta[].z z
	}
	cstator 130
	ifrnd 128 cstator 4
	sizeat 24 48
	ifspritepal 0 spritepal 8
	seta[].xoffset 48
}
ifspritepal 126 
{
	state goosearch 
	
	ifn mtype 0 
	{
		set temp mtype
		shiftl temp 2
		seta[].alpha temp
		ifge mtype 48 killit
		
		state goosearch
	}
	add countvar 1
	ifg countvar 900
	{
		set temp countvar
		sub temp 900
		shiftl temp 1
		set tempb mtype
		shiftl tempb 2
		
		add temp tempb
		seta[].alpha temp
		ifg temp 250 killit
	}
	
	sizeto 80 160

	break 
}

sizeto 80 160


ifcount 90 cstator 512
ifcount 120 killit

ifspritepal 8
{
	ifcount 91 nullop else
	ifcount 10
	{
		ifpdistl 1024
		ifvarand player[].player_par 1
		{
			getp[].boot_amount temp
			ifg temp 0
			{
				subvar temp 1
				setp[].boot_amount temp
				ife player[].fta 0 quote 75
			}
			else
			{
				palfrom 50 20 63 24
				ifrnd 32 addphealth -1
			}
		}
	}
}

enda


action RAINFRAME 0 1 1 1 1
action RAINSPLASH -3188 9 1 1 3

move SNOWFALL 16 16
move SNOWFALL2 16 20

spritenopal RAINSPRITE
useractor notenemy RAINSPRITE 0




ifinwater killit 

ifaction RAINFRAME
{
	sizeto 16 64
	geta[].z savz
	fall
	ife sprite[].z savz action RAINSPLASH
	else
	iffloordistl 2
	{
		gets[].floorbunch temp
		ife temp -1
		action RAINSPLASH else
		ifvarvarg sprite[THISACTOR].z player[THISACTOR].posz
		ifpdistg 16384 killit
		
	}
	else
	ifnotmoving action RAINSPLASH

} else
ifaction RAINSPLASH 
{
	sizeat 16 16
	ifactioncount 9 killit
}
else
ifaction 0
{
	cstat 0
	cstator 8192
	setactor[THISACTOR].mdflags 1
	spriteflags 16
	
	// ifoutside nullop else killit
	action RAINFRAME
	sizeat 8 15
	setactor[THISACTOR].zvel 2048
	setactor[THISACTOR].pal rainpal
	ifvarand raincstat 512 cstator 514 else
	ifvarand raincstat 2 cstator 2
	ifg sprite[].pal 9
	{
		geta[].htflags temp
		orvar temp 4
		seta[].htflags temp
		seta[].shade -100
	}
}
else // does this ever happen??
{
	ifnotmoving killit
	iffloordistl 4 
	{
		gets[].floorbunch temp
		ife temp -1
		killit
	}
	ifvarvarg sprite[THISACTOR].z player[THISACTOR].posz
	ifpdistg 16384 killit
}

enda

action SCRAP2FALLFRAMES -26 4 1 1 8
action SCRAP4FALLFRAMES -18 4 1 1 8
action SCRAP5FALLFRAMES -14 3 1 1 8

move SCRAPBOUNCE  84

state scrapfallcode

	fall
	
	ifaction 0
	{
		cstator 8192
		setactor[THISACTOR].mdflags 1
		spriteflags 16
		ifrnd 84 action SCRAP2FALLFRAMES else
		ifrnd 128 action SCRAP4FALLFRAMES else
		action SCRAP5FALLFRAMES
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
		rand x 20 add x 20 { seta[].xrepeat x seta[].yrepeat x }
		setactor[THISACTOR].zvel 2048
		ifvarand raincstat 512 cstator 514 else
		ifvarand raincstat 2 cstator 2
	}
	else
	{
		// floor bouncing, killing conditions
		iffloordistl 2
		{
			ife mtype 0
			{
				set mtype 1
				move SCRAPBOUNCE randomangle
				geta[].z z
				sub z 1024
				seta[].zvel -2048
				ifpdistl 3072 ifrnd 96 { ifrnd 128 sound ROCKHIT else sound CRUMBLE }
			}
			else ifcount 20
			killit
		}
	}

ends

useractor notenemy SCRAPFALL 0 state scrapfallcode enda


spritenopal SNOWSPRITE
useractor notenemy SNOWSPRITE 0 

fall
ifinwater killit 

ifmove 0
{	
	cstat 0
	cstator 8192
	spriteflags 16
	setactor[THISACTOR].mdflags 1
	ifoutside nullop else killit
	ifrnd 128
	move SNOWFALL geth getv randomangle
	else move SNOWFALL2 geth getv randomangle
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	ifvarand raincstat 2 cstator 2
	randvar x 3 addvar x 3
	randvar y 3 addvar y 3
	setactor[THISACTOR].xrepeat x
	setactor[THISACTOR].yrepeat y
	seta[].pal rainpal
}
ifl framerate 50 ifrnd 4 killit
ifnotmoving killit
iffloordistl 4 killit
ifvarvarg sprite[THISACTOR].z player[THISACTOR].posz
ifpdistg 16384 killit

enda

spritenoshade GLAREANIM
spritenopal GLAREANIM
action GLAREFRAMES 0 13 1 1 8
useractor notenemy GLAREANIM 0 GLAREFRAMES

ifmove 0
{
	move STOPPED
	ifspawnedby LARGEMAN sizeat 36 36 else
	sizeat 16 16
	cstat 130
	seta[].blend 1
	seta[].alpha 96
	seta[].shade -50
}
ifactioncount 13 killit

enda

spritenoshade GLARESTAR
action GLSTARBACK 0 11 1 -1 3
action GLSTARFORW -11 11 1 1 3
useractor notenemy GLARESTAR 0 GLSTARBACK

ifmove 0
{
	move STOPPED
	cstat 130
	ifspawnedby TRANSPORTERSTAR sizeat 20 20 else
	{
		geta[].z z, sub z 8192, seta[].z z
		sizeat 26 26
	}
	seta[].blend 1
	seta[].alpha 96
	seta[].shade -50
}
ifaction GLSTARBACK
{
	ifactioncount 11
	action GLSTARFORW
} else
ifaction GLSTARFORW
{
	ifactioncount 11 killit
}

enda

useractor notenemy HIGHSOUND 0

	ifmove 0
	{
		move STOPPED
		set startx initflags
		sub startx 1024
		ifn mtype 0 ife initflags 0 set initflags SPRITELOTAG
	}
	
	ifl mtype 3 // distance based
	{
		findplayer xydist
		ifl xydist startx
		{
			ifactorsound THISACTOR SPRITELOTAG nullop else soundvar SPRITELOTAG
			resetcount
		}
		else
		ifg xydist initflags
		ifcount 20
		{
			ifactorsound THISACTOR SPRITELOTAG stopactorsound THISACTOR SPRITELOTAG
		}
	}
	else // activation based
	{
		// botclip 0 means not moving, initial state
		// botclip 1 means moving, play first sound
		
		ife botclip 0
		{
			gets[].floorz angvel
			gets[].ceilingz countvar
			ifn angvel lastangvel { set botclip 1 soundvar SPRITELOTAG } else
			ifn countvar countvarb { set botclip 1 soundvar SPRITELOTAG }
			set lastangvel angvel
			set countvarb countvar
		}
		else
		ife botclip 1
		{
			gets[].floorz angvel
			gets[].ceilingz countvar
			ife angvel lastangvel ife countvar countvarb
			{
				set botclip 0
				ifactorsound THISACTOR SPRITELOTAG stopactorsound THISACTOR SPRITELOTAG
				soundvar initflags
			}
			set lastangvel angvel
			set countvarb countvar
		}	
	}
enda

/*
ife actorvar[spriteid].countvar 0 // not currently active
{
	setav[spriteid].countvar 1
	// al SPRITELOTAG
	
	ife actorvar[spriteid].countvarb 0 // in starting position
		setav[spriteid].countvarc 1 // start animation forward
	else
		setav[spriteid].countvarc -1 // start animation backward
}
SPRITELOTAG activates this sprite
initflags matches hitag on the walls it animates when active
initsprite is channel it triggers when it is done
*/

state animopencode

	ifn countvar 0
	{
		ifg droptile 0 { sub droptile 1 break }
		ifg countvarc 0 
		{
			ife countvarc 1 screensound NEWDOOR1
			add countvarc 1
			set tempb countvarc
			modvar tempb 5
			ife tempb 0 // advance wall tile
			{
				set target 0
				set B 0
				whilevarvarn target NUMWALLS
				{
					ife wall[target].hitag initflags
					{
						getw[target].picnum picnum
						add picnum 1
						
						ifactor ANIMOPEN1 set temp 18700 else
						ifactor ANIMOPEN2 set temp 18709 else
						ifactor ANIMOPEN3 set temp 18718
						ifg picnum temp
						{
							ife botclip NO
								set botclip YES
						}
						else setw[target].picnum picnum
					}
					add target 1
				}
				ife botclip YES // finished forward animation
				{
					operateactivators initsprite 0
					operatemasterswitches initsprite
					operaterespawns initsprite
					
					set spriteid 0
					whilevarn spriteid 16384 // Numsprites
					{
						ife sprite[spriteid].picnum MULTIPLATE
						ife actorvar[spriteid].mtype 2
						ife actorvar[spriteid].SPRITELOTAG initsprite
						{
							setav[spriteid].droptile YES
						}
						
						ifge sprite[spriteid].picnum 18719
						ifle sprite[spriteid].picnum 18721
						{
							// al spriteid
							ife initsprite actorvar[spriteid].SPRITELOTAG
							ife actorvar[spriteid].countvar 0 // not currently active
							{
								setav[spriteid].countvar 1
								// al SPRITELOTAG
								
								ife actorvar[spriteid].countvarb 0 // in starting position
									setav[spriteid].countvarc 1 // start animation forward
								else
									setav[spriteid].countvarc -1 // start animation backward
							}
						}
						add spriteid 1
					}
					
					
					set countvarb countvarc
					set countvarc 0
					set countvar 0
					set botclip NO
				}
			}
		}
	}

ends

useractor notenemy ANIMOPEN1 0 state animopencode enda
useractor notenemy ANIMOPEN2 0 state animopencode enda
useractor notenemy ANIMOPEN3 0 state animopencode enda

eventloadactor NEWLASERLINE geta[].lotag SPRITELOTAG seta[].lotag 0 

geta[].cstat temp
ifvarand temp 1 xorvar temp 1
ifvarand temp 256 xorvar temp 256
orvar temp 2
seta[].blend 1
seta[].cstat temp

set initflags temp

enda

eventloadactor LASERWALL cstat 33041 geta[].lotag SPRITELOTAG seta[].lotag 0 enda
eventloadactor DEATHZONE
 cstat 32768 
 geta[].lotag SPRITELOTAG seta[].lotag 0 
 geta[].hitag starty seta[].hitag 0
 geta[].extra monstflags
 ifspritepal 1 set mtype 1
enda

useractor notenemy DEATHZONE

	ife mtype 0 // active
	{
		ife sprite[].sectnum player[].cursectnum
		{
			set temp 0
			ife hitag 0 set temp YES
			else
			{
				set tempb starty
				shiftl tempb 3
				set z sprite[].z
				sub z tempb
				ifge sprite[player[].i].z z set temp YES
			}
			ife temp YES ifp palive { set falldeath YES addphealth -1000 palfrom 63 63 }
		}
		headspritestat spriteid 1
		{
			ife sprite[spriteid].sectnum sprite[].sectnum
			ife actorvar[spriteid].monstatus 1
			{
				set temp 0
				ife hitag 0 set temp YES
				else
				{
					set tempb starty
					shiftl tempb 3
					set z sprite[].z
					sub z tempb
					ifge sprite[spriteid].z z set temp YES
				}
			}
			ife temp YES
			{
				seta[spriteid].htextra 1000
				seta[spriteid].htowner THISACTOR
				seta[spriteid].htpicnum SHOCKPROJ
			}
			nextspritestat spriteid spriteid
		}
	}
enda

useractor notenemy LASERWALL 9999

ife mtype 1
cstat 32768
else
{
	cstat 33041 
	// check for player boosting into it and reduce speed if necessary
}

ifn countvar 0
{
	ife countvar 1 // player damage
	{
		palfrom 48 63
		state playerpainsounds
		set spriteid player[].i
		geta[spriteid].htextra temp
		add temp 15
		seta[spriteid].htextra temp
		seta[spriteid].htpicnum FIRELASER3
		seta[spriteid].htowner THISACTOR
		rand temp 5
		ife temp 0 screensound SABCLSH1 else
		ife temp 1 screensound SABCLSH2 else
		ife temp 2 screensound SABCLSH3 else
		screensound SABCLSH4
	}
	add countvar 1
	ifge countvar 30 set countvar 0
}

ifhitweapon
{
	strength 1000
	ifrnd 128 sound SPARKING1 else sound SPARKING2
}

enda


defstate coinsget
  
  ifspritepal 8 set B 25 else
  ifspritepal 1 set B 125 else
  ifspritepal 23 set B 250 else
  ifspritepal 10 set B 500 else
  ifspritepal 49 set B 1250 else
  ifspritepal 99 set B 1000 else
  set B 125
  
  ifactor BIGSTACK mul B 3
  
  add credits B
  add totalcredits B
  set lastcredits credits
 
  
  set gotcoins 90
  
  ife pchar 4 
  {
	ifsound DEA_SMALLCHANGE nullop else ifsound DEA_USEFUL nullop else
	ifrnd 128 globalsound DEA_USEFUL else globalsound DEA_SMALLCHANGE
  }
  else
  {
	  rand temp 3
	  ife temp 0 globalsound DUKE_GETWEAPON1 else
	  ife temp 1 globalsound DUKE_GETWEAPON3 else
	  ife temp 2 globalsound DUKE_GETWEAPON5 else
	  ife temp 3 globalsound VACA_GETWEAP1
  }
  palfrom 20 0 32
  globalsound DUKE_GET
  globalsound GETCOINS
  killit
ends

eventloadactor SMALLSTACK geta[].lotag SPRITELOTAG seta[].lotag 0 cstat 32768 enda
eventloadactor SMALLSTACK geta[].lotag SPRITELOTAG seta[].lotag 0 cstat 32768 enda

spriteshadow SMALLSTACK
useractor notenemy SMALLSTACK 0

	ifl wave SPRITELOTAG break
	
	cstat 0
	fall
	sizeat 20 16
	ifp palive
	ifpdistl RETRIEVEDISTANCE
	ifcansee
	{
		state coinsget
	}

enda

spriteshadow BIGSTACK
useractor notenemy BIGSTACK 0

	ifl wave SPRITELOTAG break
	
	cstat 0
	fall
	sizeat 20 16
	ifp palive
	ifpdistl RETRIEVEDISTANCE
	ifcansee
	{
		state coinsget
	}

enda

defstate diskcut

	ifcount 3
	{
		set K NO
		resetcount
		set countvar 0
		headspritestat myvictim 1
		whilevarn myvictim -1
		{
			ifn actorvar[myvictim].monstatus 2
			ifvarand sprite[myvictim].cstat 256
			ife sprite[myvictim].htextra -1
			ifn myvictim myshelly
			{
				set z sprite[myvictim].z
				sub z sprite[].z
				abs z
				ifle z 10240
				{
					ldist xydist THISACTOR myvictim
					ifle xydist 1560
					{
						ifvarand novaupgrades[GROW_WEAPON] 1
						ife actorvar[myvictim].monstatus 1
						{
							set K YES
							ifg sprite[myspawner].xvel 64 // 192
							{
								set x sprite[myspawner].xvel
								div x 3
								seta[myspawner].xvel x
								set z sprite[myspawner].zvel
								div z 3
								seta[myspawner].zvel z
							}
						}
						seta[myvictim].htextra 8
						seta[myvictim].htowner player[].i
						seta[myvictim].htang sprite[].ang
						seta[myvictim].htpicnum RPG
						
						espawn SHOCKBALL_EXP
						setsprite RETURN sprite[myvictim].x sprite[myvictim].y sprite[].z
						seta[RETURN].htpicnum PLASDISKPROJ
						setav[RETURN].mtype 3
						ife countvar 0
						{
							espawn SOUNDPLATE
							setav[RETURN].SPRITELOTAG DISKIMPACT
							setav[RETURN].mtype 1
							set countvar 1
						}
					}
				}
				
			}
			nextspritestat myvictim myvictim
		}
		headspritestat myvictim 6
		whilevarn myvictim -1
		{
			ifvarand sprite[myvictim].cstat 256
			ife sprite[myvictim].htextra -1
			{
				set z sprite[myvictim].z
				sub z sprite[].z
				abs z
				ifle z 10240
				{
					ldist xydist THISACTOR myvictim
					ifle xydist 1560
					{
						seta[myvictim].htextra 9
						seta[myvictim].htowner player[].i
						seta[myvictim].htang sprite[].ang
						seta[myvictim].htpicnum RPG
						
						espawn SHOCKBALL_EXP
						setsprite RETURN sprite[myvictim].x sprite[myvictim].y sprite[].z
						seta[RETURN].htpicnum PLASDISKPROJ
						setav[RETURN].mtype 3

						ife countvar 0
						{
						espawn SOUNDPLATE
						setav[RETURN].SPRITELOTAG DISKIMPACT
						setav[RETURN].mtype 1
						set countvar 1
						}
	
					}
				}
				
			}
			nextspritestat myvictim myvictim
		}
		ifn monstatus 2
		ife K NO
		ifl sprite[myspawner].xvel 192
		ifvarand novaupgrades[GROW_WEAPON] 1
		{
			seta[myspawner].xvel 192
			set z sprite[myspawner].zvel
			mul z 2
			seta[myspawner].zvel z
		}
	}

ends

useractor notenemy PLASMADISK 0

ife monstatus 2
{
	geta[].alpha temp
	ifge temp 250 
	{
		stopactorsound THISACTOR DISKTRAVEL
		stopactorsound THISACTOR DISKIMPACT
		killit 
	}
	else
	{
		add temp 8, ifg temp 255 set temp 255
		seta[].alpha temp
		state diskcut
	}
	break
}

ifactorsound THISACTOR DISKTRAVEL nullop else sound DISKTRAVEL

ifn myspawner -1
{
	ifmove 0 
	{
		move STOPPEDFORPLAYER
		sizeat 56 56
		// seta[].blend 1
		// cstator 130
		seta[].shade -127
		
		
		geta[myspawner].xvel xvel
        geta[myspawner].zvel z    
        shiftr z 4
        getangle tempb z xvel
        sub tempb 512
        
        div tempb 16
        seta[].yoffset tempb
	}
	ifn sprite[myspawner].statnum 4 { set monstatus 2 seta[].mdflags 0 set myspawner -1 break } else
	setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
	state diskcut
}

enda

useractor notenemy SMARTBOMB 500

ifaction 0
{
	action ANULLACTION
	seta[].shade -100
	seta[].blend 1
	cstator 130
	sizeat 24 16
	spritepal 23
	espawn SMARTDISK
	setav[RETURN].myspawner THISACTOR
	setav[RETURN].team team
}
ife initsprite 0
sizeto 64 48 else
ife initsprite 1
{
	sizeto 4 4
	sizeto 4 4
}
ife monstatus 2 killit

enda

action ADISKBACK -1  2  1  1  3
useractor notenemy SMARTDISK 0

ife myspawner -1 break

ifaction 0
{
	action ADISKBACK
	cstat 34
	sizeat 255 255
	seta[].shade -100
}
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z

sizeto 0 0
sizeto 0 0
ifl sprite[].xrepeat 96 
{
	sizeto 0 0
	setav[myspawner].initsprite 1
}
ifl sprite[].xrepeat 64
{
	sizeto 0 0
	ifle sprite[].xrepeat 1 
	{
		setav[myspawner].monstatus 2
		espawn EXPLOSION2
		setav[RETURN].mtype WESBOT
		hitradius 6144 80 160 240 320
		ifpdistl 6145 seta[player[].i].htowner player[].i
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 1
			{
				dist xydist THISACTOR spriteid
				ifle xydist 6144 
				{
					seta[spriteid].htowner player[].i
					seta[spriteid].htpicnum RPG
				}
			}
			nextspritestat spriteid spriteid
		}
		killit
	}
}

	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		ife actorvar[spriteid].monstatus 1
		ifn actorvar[spriteid].team team
		ifn actorvar[spriteid].team 3
		{
			set target spriteid // actorvar[spriteid].mysignpost
			ifn target -1
			{
				dist xydist THISACTOR target
				ifl xydist 6144
				{
					canseespr THISACTOR target temp
					ife temp YES
					{
						geta[target].x x2
						geta[target].y y2
						sub x2 sprite[].x
						sub y2 sprite[].y
						getangle angvar x2 y2
							
						add angvar 1024
						cos xvel angvar
						sin yvel angvar
				  
						shiftvarr xvel 7
						shiftvarr yvel 7
						
						geta[].z z
						sub z sprite[target].z
						shiftr z 1
							
						ifg xydist 768
						movesprite target xvel yvel z CLIPMASK0 RETURN
						
						setav[target].bottarget -1
						rand angvar 2047
						seta[target].xvel 0	
					}
				}
			}
		}
		nextspritestat spriteid spriteid
	}

enda

eventloadactor POWERORB
geta[].lotag SPRITELOTAG
seta[].lotag 0
sizeat 37 30
enda

useractor notenemy BURNTPALM 50

	ifg burning 0 
	{
		state imonfire
		ifrnd 16
		{
			 espawn BIGSMOKE
			 seta[RETURN].htpicnum RECONWRECK
		}
	}
	ifhitweapon
	{
		ifdead 
		{
			spawn BIGSMOKE
			killit
		}
	}

enda

useractor notenemy POWERORB 0

ifn SPRITELOTAG 0
{
	ifn sprite[].pal 13
	{
		geta[].ang angvar
		add angvar 64
		seta[].ang angvar
	}
	break
}
ifaction 0
{
	action ANULLACTION
	sizeat 37 30
	spritepal 116
	seta[].shade -32
	ife VOLUME 4 ife LEVEL 10
	{	
		stopallsounds
		screensound DEA_BATTERY
		set subtitle_start 954
		set subtitle_numlines 2
		set subtitle_time 210
	}
}

state checkheldobject
ife heldobject THISACTOR
{
	set initsprite NO
	headspritestat myspawner 1
	whilevarn myspawner -1
	{
		ife sprite[myspawner].picnum POWERORB
		ifn myspawner THISACTOR
		ife sprite[myspawner].pal 13
		{
			dist xydist THISACTOR myspawner
			ifle xydist 512
			{
				getav[myspawner].SPRITELOTAG SPRITELOTAG
				ifn SPRITELOTAG 0
				{
					operateactivators SPRITELOTAG 0
					operatemasterswitches SPRITELOTAG
					operaterespawns SPRITELOTAG
					state AActivation
					seta[myspawner].pal 116
					set initsprite YES
					sound ORBIN sound ORBIN sound ORBIN sound ORBIN sound ORBIN 
				}
			}
		}
		nextspritestat myspawner myspawner
	}
	ife initsprite YES
	{
		set heldobject -1
		killit
	}
}
else fall

enda

useractor notenemy LAPTOP 10

ife SPRITELOTAG 0 break

fall

ifspritepal 33 { getlastpal resetcount }
ifcount 30 spritepal 33

ifhitweapon
{
	ifdead
	{
		debris SCRAP2 3
		debris SCRAP3 3
		debris SCRAP6 3
		lotsofglass 8
		sound VENT_BUST
		spawn BIGSMOKE
		operateactivators SPRITELOTAG 0
		operatemasterswitches SPRITELOTAG
		operaterespawns SPRITELOTAG
		state AActivation
		set bigmsg 1496
		set bigmsgcount 90
		globalsound NEWSWITCH3 globalsound NEWSWITCH3 globalsound NEWSWITCH3
		killit
	}
}

enda

useractor notenemy ICEBLOCK 200

ifvarand sprite[].cstat 32 break

ifaction 0
{
	action ANULLACTION
	cstator 257
	geta[].xrepeat startx
	geta[].yrepeat starty
}

state predamage

ife VOLUME 4 ife LEVEL 11
ife player[].gotweapon PISTOL_WEAPON NO
ifpdistl 3072
ifle subtitle_time 1
{
	set subtitle_start 1448
	set subtitle_numlines 2
	set subtitle_time 100
}

geta[].extra countvar
geta[].htextra bleeding, ife bleeding -1 set bleeding 0
ifhitweapon
{ 
	seta[].extra countvar
	set temp NO
	ifg burning 0 set temp YES
	ifge sprite[].htpicnum FLAMEPROJ2 ifle sprite[].htpicnum FLAMEPROJ set temp YES
	ife temp YES
	{
		shiftr bleeding 4
		ifl bleeding 1 set bleeding 1
		// add countvarb bleeding
		add countvarb 1
		set burning 0
	}
}

ifg countvarb 0
{
	espawn BIGSMOKE
	rand x 1024
	rand y 1024
	sub x 512
	sub y 512
	add x sprite[].x
	add y sprite[].y
	seta[RETURN].x x
	seta[RETURN].y y
		
	set temp player[].player_par
	modvar temp 6
	ife temp 0
	{
		sub startx 1
		sub starty 2
		sub countvarb 1
		ifl starty 2 set starty 2
		ifl startx 2 set startx 2
		seta[].xrepeat startx
		seta[].yrepeat starty
		
		ifle starty 2
			killit
	}
	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		switch sprite[spriteid].picnum
		case BURNING case BURNING2
			ifn actorvar[spriteid].initsprite 1
			{
				dist xydist THISACTOR spriteid
				ifl xydist 1280 setav[spriteid].initsprite 1
			}
		break
		endswitch
		nextspritestat spriteid spriteid
	}

}

enda

useractor notenemy TROOPHUGGED 20

	ifhitweapon
	{
		ifdead
		{
			spritepal 6
			guts JIBS3 4
			sound SLIM_DYING
			getlastpal
			espawn EDFTROOP
			seta[RETURN].pal sprite[].pal
			seta[RETURN].htextra 70
			seta[RETURN].htpicnum SHOTSPARK1
			seta[RETURN].htowner THISACTOR
			killit
		}
	}
	ifmove 0
	{
		cstator 256
		ifcansee ifpdistl 4096
		move STOPPEDFORPLAYER
		else break
		
	}
	
	ifcount 300
	ifrnd 1
	{
		sound SLIM_HATCH
		spawn GREENSLIME
		cactor 22457
	}

enda

useractor notenemy LEVELENDER 0

	ife SPRITELOTAG 0 // end when player in sector
	{
		ife sprite[].sectnum player[].cursectnum
		{
			setp[].timebeforeexit initsprite
			setp[].fta 150
			setp[].ftq 1447
			ife initflags 0 setuserdef[].display_bonus_screen YES else setuserdef[].display_bonus_screen NO
			killit
		}
	}
	else
	{
		checkactivatormotion SPRITELOTAG
			ife RETURN YES set monstatus 1
		ife monstatus 1
		{
			ifspritepal 2 { set SPRITELOTAG 0 break }
			setp[].timebeforeexit initsprite
			setp[].fta 150
			setp[].ftq 1447
			ife initflags 0 setuserdef[].display_bonus_screen YES else setuserdef[].display_bonus_screen NO
			set monstatus 0
			killit
		}
	}

enda

spritenoshade LASERFLASH
spritenopal LASERFLASH

useractor notenemy LASERFLASH 0
ifaction 0
{
	action ANULLACTION
	sizeat 80 64
	seta[].shade -126
	set temp player[].player_par
	modvar temp 4
	switch temp
		case 0 cstat 128 break
		case 1 cstat 132 break
		case 2 cstat 136 break
		case 3 cstat 140 break
	endswitch
}

ifcount 1 killit

enda

spritenoshade NEWTRANS
spritenopal NEWTRANS
action ANEWTRANSFRAMES 0  30  1  1  3
useractor notenemy NEWTRANS 0 ANEWTRANSFRAMES

ifmove 0
{
	// move DRIFTTOPLAYER fleeenemy
	move STOPPEDFORPLAYER
	// geta[].z z, sub z 12288, seta[].z z
	seta[].blend 1
	cstator 130
	seta[].shade -100
	ifrnd 128 cstator 4
	ife team 0 spritepal 21
	// sound MIA_REVERSE
	sound BOSSPORT
	
	
}
ife mtype 0 { sizeto 26 26 sizeto 26 26 } else
{
	sizeto 0 0
	seta[].blend 0
}

ifactioncount 30
{
	resetactioncount
	ife mtype 1 killit
	else set mtype 1
}

// geta[].alpha temp
// add temp 4, ifg temp 224 set temp 224
// ifg temp 20 seta[].blend 0
// seta[].alpha temp

enda

move LIGHTFALL 0 128

// good light strand sizes: 24 96, 64 128, 64 64, 64 255
useractor notenemy LIGHTSTRAND 0

ifmove 0
{
	move LIGHTFALL getv
	ifl sprite[].xrepeat 4
		sizeat 32 192
	cstat 130
	cstator 1024
	seta[].blend 1
	ife initsprite 0 set initsprite 6144
}

geta[].z z
add z initsprite
seta[].z z

geta[myspawner].z z2

add z2 131072
ifg z z2 killit

enda

defstate spawnstrand
	espawn LIGHTSTRAND
	set startx sprite[].x
	rand xydist 12282
	add startx xydist
	rand angvar 2047
	rotatepoint sprite[].x sprite[].y startx sprite[].y angvar newx newy
	set z sprite[].z
	sub z 524288
	set z2 FEMFALLDMG
	sub z FEMFALLDMG
	sub z FEMFALLDMG
	setsprite RETURN newx newy z
	setav[RETURN].myspawner THISACTOR
	setav[RETURN].initsprite FEMFALLDMG
	ifn mtype 0 seta[RETURN].pal 23
ends

// several spawns can happen during a wave
// countvarc is a countdown timer for how long until next spawn
// botclip is the total number of spawn events and decrements on spawn
// ikicked increments with each spawn in case they are different types of enemies

useractor notenemy PORTALMANAGER 0

ifmove 0
{
	move STOPPEDFORPLAYER
	cstat 32768
	set countvarb 16
	set FEMFALLDMG 6144
	ife initflags 1 set theportal THISACTOR
}

// ifhitspace ife mtype 0
// set mtype 1

ifg countvarc 0
{
	sub countvarc 1
	ife countvarc 0 set mtype 1
}

set portaltime mtype
ifg mtype 0
{
	ife mtype 45 globalsound PORTALSTART
	add mtype 1
	ife mtype 180 
	{
		quake 60
		// start enemy spawns
		ifg botclip 0
		{
			sub botclip 1
			cactor RESPAWN
			
			set savedvalue NO
			switch wave
				case 55
					ife botclip 2 set FEMKILLCOUNT 16 else
					ife botclip 1 set FEMKILLCOUNT 2 else
					set FEMKILLCOUNT 8
					set countvarc 1200
				break
				
				case 56
				case 57
					ife botclip 5 set FEMKILLCOUNT 6 else
					ife botclip 4 set FEMKILLCOUNT 3 else
					ife botclip 3 set FEMKILLCOUNT 3 else
					ife botclip 2 set FEMKILLCOUNT 10 else
					ife botclip 1 set FEMKILLCOUNT 18 else
					set FEMKILLCOUNT 12
					set countvarc 1200
				break
				case 58
					ife botclip 4 set FEMKILLCOUNT 50 else
					ife botclip 3 set FEMKILLCOUNT 6 else
					ife botclip 2 set FEMKILLCOUNT 18 else
					ife botclip 1 set FEMKILLCOUNT 20 else
					set FEMKILLCOUNT 4
					set countvarc 1200
				break
			endswitch
			
			whilevarvarn savedvalue FEMKILLCOUNT
			{
				set K NO // height flag
				set E 21 // pal flag
				set J 0 // additional monstflags
				switch wave
					case 55 
						ife botclip 2
						{
							ifvarand savedvalue 1 
							{
								set inithp ARMLIZJETPACK 
								ifl savedvalue 4 set E 15 else ifl savedvalue 8 set E 49 else
								ifl savedvalue 12 set E 12
								set K YES
							}
							else set inithp ARMPIG
						}
						else ife botclip 1 { set inithp BOSS2FLY set K YES }
						else 
						{
							ifvarand savedvalue 1 set inithp CYBERBEAST else set inithp NEWBEAST
						}
					break
					
					case 56
					case 57
						ife botclip 5 
						{
							ifl savedvalue 4 set inithp LIZELITE 
							else { set inithp LIZDOG set E 104 }
						}
						else ife botclip 4
						{
							set inithp NEWBATLORD
							set J 256
						}
						else ife botclip 3
						{
							set inithp BOSS3
						}
						else ife botclip 2
						{
							ifl savedvalue 6 set inithp SPACEBULL else
							set inithp MECHBRAIN
						}
						else ife botclip 1
						{
							ifl savedvalue 7 set inithp POLYMORPH else
							{
								set K YES
								ifl savedvalue 14 set inithp OCTABRAIN else
								set inithp MEGABRAIN
							}
						}
						else
						{
							set inithp ROBOSHARK
							ifl savedvalue 6 set E 0 else
							ifl savedvalue 10 set E 24 else
							set E 41
						}
					break
					case 58
						ife botclip 4 
						{ 
							set K YES
							ifl savedvalue 40 set inithp DRONE 
							else set inithp NEWSENTRY
						}
						else ife botclip 3
						{
							ifvarand savedvalue 1 set inithp NEWMECH else set inithp CANHEAD
						}
						else ife botclip 2
						{
							set K YES
							ifvarand savedvalue 1 set inithp COMMANDER else set inithp NEWCOMM
						}
						else ife botclip 1
						{
							ifl savedvalue 16 set inithp LIZBOSS else
							set inithp LIZMAN2
						}
						else
							set inithp QUEEN
					break
				endswitch
	
				espawnvar inithp
				seta[RETURN].pal E
				setav[RETURN].monstflags J
				set startx sprite[].x
				rand xydist 12282
				add startx xydist
				rand angvar 2047
				rotatepoint sprite[].x sprite[].y startx sprite[].y angvar newx newy
				
				set mysector sprite[].sectnum
				updatesector newx newy mysector
				ifvarn mysector -1
				{
					getflorzofslope mysector newx newy padmove
					ife K YES sub padmove 24576
					setsprite RETURN newx newy padmove
					sub padmove 16384
					espawn NEWTRANS 
					set y tiledata[inithp].ysize
					setav[RETURN].initsprite y
					setsprite RETURN newx newy padmove
				}
				add savedvalue 1
			}
			
			cactor PORTALMANAGER
			ife botclip 0 set countvarc 0
		}
	}
	ife mtype 150 globalsound PORTALVACUUM
	ife mtype 210 
	{
		globalsound PORTALARRIVE
		set countvarb 16 
		set mtype 0 
		set FEMFALLDMG 6144
		palfrom 63 63 63 31
	}
}

add countvar 1
ifge countvar countvarb
{
	ifg mtype 0 
	{
		sub countvarb 1
		set temp mtype
		clamp temp 8 128
		shiftl temp 8
		add temp 6144
		set FEMFALLDMG temp
	}
	
	set countvar 0
	
	state spawnstrand
	state spawnstrand
	
}

enda

move HEARTUPVEL 32 -14

useractor notenemy HEARTFLOAT 0
	ifmove 0
	{
		ifspawnedby LILY
		{
			geta[].z z sub z 4096 seta[].z z seta[].htbposz z
		}
		move HEARTUPVEL randomangle getv
		sizeat 16 16
		seta[].shade -100
		spriteflags 2048
	}
	ifcount 20 sizeto 0 0
	ifcount 40 killit
enda
