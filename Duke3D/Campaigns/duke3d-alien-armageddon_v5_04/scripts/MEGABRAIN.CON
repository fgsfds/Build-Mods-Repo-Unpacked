// MEGABRAIN
// sprites by sebabdukeboss20, code by Dan Gaskill
// version .90 February 19, 2018

// define MEGABRAIN 5811

spriteflags MEGABRAIN 5242880
move MEGAJIBMOVE 192
action MEGAJIBFRAMES 0 2 1 1 6

useractor notenemy MEGAJIB 0 MEGAJIBFRAMES

ifmove 0
{
	sizeat 32 32
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	seta[].zvel -3072
	move MEGAJIBMOVE geth randomangle
}

ifcount 5
	ifrnd 128
{
	randvar temp 3
	ife temp 0 cstat 0
	ife temp 1 cstat 4
	ife temp 2 cstat 8
	ife temp 3 cstat 12
}
fall
iffloordistl 2 killit

enda

spritenoshade BIGSHOCKER
spritenopal BIGSHOCKER
action ABIGSHOCKFRAMES -1 4 1 1 4
move BIGSHOCKVELS 464
useractor notenemy BIGSHOCKER 33 ABIGSHOCKFRAMES

ifmove 0
{
	sizeat 32 32
	cstat 256
	cstator 130
	move BIGSHOCKVELS faceplayerslow
	seta[].shade -100
	seta[].blend 1
	
	// ife rendmode 4
	// {
		// espawn SMALLSMOKE
		// seta[RETURN].lotag 49
		// seta[RETURN].hitag 10240
		// seta[RETURN].xrepeat 2
		// seta[RETURN].yrepeat 2
		// seta[RETURN].xvel 192 // R value
		// seta[RETURN].yvel 192 // G value
		// seta[RETURN].zvel 255 // B value
		// setav[RETURN].myspawner THISACTOR
		// geta[].picnum picnum
		// setav[RETURN].mtype picnum
		// seta[RETURN].picnum SECTOREFFECTOR
		// seta[RETURN].cstat 0
		// changespritestat RETURN 3
	// }
}

state targetsearch
ife bottarget -1 getp[].i bottarget


	geta[].z z
	subvar z 8192
	seta[].z z
	geta[bottarget].z tempb
	ifle tempb z set targethigher YES else set targethigher NO
	subvar tempb 8192
	seta[bottarget].z tempb
	canseespr THISACTOR bottarget seemytarget
	add z 8192
	seta[].z z
	add tempb 8192
	seta[bottarget].z tempb
	ife seemytarget YES set shootmytarget YES // temp stand in for hitscan verification
	dist targetdist THISACTOR bottarget
	ifn actorvar[bottarget].monstatus 2 set targetalive YES
	else set targetalive NO
		
	geta[bottarget].x x
	geta[bottarget].y y
	sub x sprite[].x
	sub y sprite[].y
	getangle angvar x y
	
	geta[].ang temp
	getincangle tempb angvar temp
	ifl tempb 0 add temp 24 else sub temp 24
	seta[].ang temp

ifactorsound THISACTOR MEGASHOCK nullop else sound MEGASHOCK
ifcount 240
{
	strength 0
	seta[].htextra 10
	seta[].htpicnum SHOTSPARK1
	seta[].htowner player[].i
}

ifhitweapon
{
	ifdead
	{
		// spawn ELEC_EXP
		spawn SHOCK_ACTOR
		spawn AIRSHOCK
		spawn AIRSHOCK
		sound MEGAIMPACT2
		hitradius 2048 24 28 32 36
		ifpdistl 2048 seta[player[].i].htowner THISACTOR
		stopactorsound THISACTOR MEGASHOCK
		killit
	}
}
ife seemytarget YES
ifl targetdist 1280
{
	spawn SHOCK_ACTOR
	spawn AIRSHOCK
	spawn AIRSHOCK
	sound MEGAIMPACT2
	hitradius 2048 24 28 32 36
	ifpdistl 1280
	ife pdown NO
	palfrom 63 56 56 60
	stopactorsound THISACTOR MEGASHOCK
	killit
}

findplayer xydist
setvar xydist2 4096
subvarvar xydist2 xydist
ifvarg xydist2 0
ifcansee
{
	shiftvarr xydist2 6
	addvar xydist2 16
	ifvarg xydist2 63 setvar xydist2 63
	ifvarvarl player[THISACTOR].pals_time xydist2
	setplayer[THISACTOR].pals_time xydist2
	setplayer[THISACTOR].pals 0 56
	setplayer[THISACTOR].pals 1 56
	setplayer[THISACTOR].pals 2 63
}

geta[bottarget].z zdist
sub zdist 8192
geta[].z z
sub zdist z
ifl zdist -2048
{
	ifceilingdistl 8 nullop else
	{
		sub z 644
		seta[].z z
	}
}
else ifg zdist 2048
{
	iffloordistl 8 nullop else
	{
		add z 644
		seta[].z z
	}
}

enda

action MEGAPROJFRAMES 9652 4 1 1 3
useractor notenemy MEGAPROJ MEGAPROJSTRENGTH MEGAPROJFRAMES enda

action AMEGAWALK    0   3   5   1   15
action AMEGASTAND     0   1   5   1   15
action AMEGAHIT    20   2   1   1   10
action AMEGASHOOT    15   1   5   1   10
action AMEGASHOCK    25   1   5   1   20
action AMEGADYING    20   5   1   1   20
action AMEGAFROZEN   0  1  5  1  10
move MEGAWALKVELS 128 -22
move MEGAUPVELS 128 -65
move MEGASTOPPED 0 -30
move MEGAINWATER 128 24

ai AIMEGAGETENEMY AMEGAWALK MEGAWALKVELS seekplayer
ai AIMEGASHOOTENEMY AMEGASHOOT MEGASTOPPED faceplayer
ai AIMEGASHOCK AMEGASHOCK MEGASTOPPED faceplayerslow
ai AIMEGAHIT AMEGAHIT MEGASTOPPED faceplayer
ai AIMEGASHRUNK AMEGAWALK SHRUNKVELS fleeenemy
ai AIMEGAGROW AMEGASTAND MEGASTOPPED faceplayerslow
ai AIMEGADYING AMEGADYING MEGASTOPPED geth

state megagetenemystate

  ifinwater nullop else
  ifmove MEGAWALKVELS
  ifnotmoving ifcount 30
  {
	  move MEGAUPVELS seekplayer getv
  }
  

  ife targethigher NO
  {
	  ifmove MEGAUPVELS ifcount 90 ifrnd 8
	  move MEGAWALKVELS seekplayer
  }
  ifmove MEGAUPVELS ifceilingdistl 96
  ifcount 60
	move MEGAWALKVELS seekplayer

  ife seemytarget YES
  {
    set temp NO
	ifge SKILL 4 { ifactioncount 16 set temp YES }
	else ifactioncount 28 set temp YES
	
    ife temp YES
    {
      ifrnd 48
        ife shootmytarget YES
		  ife targetalive YES
      {
	    findnearactor BIGSHOCKER 16384 spriteid
		ife spriteid -1
		ife shrunken 0
	    ifrnd 96 ai AIMEGASHOCK
		else
        ai AIMEGASHOOTENEMY
        break
      }
    }
  }

ends

state megashootenemystate
  ifcount 17
  {
    ifcount 18
      ai AIMEGAGETENEMY
  }
  else
    ifcount 16
	{
	  sound MEGAFIRE
      eshoot MEGAPROJ
	  ifspritepal 47 seta[RETURN].pal 23
	  eshoot MEGAPROJ
	  ifspritepal 47 seta[RETURN].pal 23
	  geta[RETURN].ang angvar
	  sub angvar 64
	  seta[RETURN].ang angvar
	  eshoot MEGAPROJ
	  ifspritepal 47 seta[RETURN].pal 23
	  geta[RETURN].ang angvar
	  add angvar 64
	  seta[RETURN].ang angvar
	}
  else
	ifcount 15
	ifspritepal 19
	{
		sound MEGAFIRE
		shoot MEGAPROJ
		eshoot MEGAPROJ
		geta[RETURN].ang angvar
		sub angvar 64
		seta[RETURN].ang angvar
		eshoot MEGAPROJ
		geta[RETURN].ang angvar
		add angvar 64
		seta[RETURN].ang angvar
	}
  else
    ifactioncount 5
      resetactioncount
ends

state checkmegahitstate
  ifwasweapon SHRINKSPARK
  {
    sound ACTOR_SHRINKING
	set shrunken 1
  }
  else
  {
    ifdead
    {
	  ifspritepal 47 { cstat 257 seta[].blend 0 }
      ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action AMEGAFROZEN
        strength 0
		state freezeme
        break
      }

      addkills 1 state enemy_death

        state rf
		spritepal 10
        ai AIMEGADYING
        seta[].mdflags 0
		seta[].htflags 4
		seta[].shade -127
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			setp[].sound_pitch 1560
			sound MEGADEATH
			setp[].sound_pitch 0
		}
		else
      sound MEGADEATH
    }
    else
    {
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
		
		
		ifactorsound THISACTOR MEGAPAIN nullop else 
		{
			sound MEGAPAIN
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setactorsoundpitch THISACTOR MEGAPAIN SHRUNKPITCH
			else
				setactorsoundpitch THISACTOR MEGAPAIN 1024
		}
		

	  ifn sprite[].htpicnum FREEZEBLAST
	  ifn sprite[].htpicnum SHRINKSPARK
	  ife shrunken 0
      spawn BLOOD
      ifai AIMEGAHIT nullop else
	  ifai AIMEGASHOOTENEMY nullop else
	  ifai AIMEGASHOCK nullop else
      ifrnd 32
	  ifn sprite[].htpicnum BURNING
	  ifn sprite[].htpicnum RADWOUND
	  {
        ai AIMEGAHIT
		ifspritepal 47 { cstat 257 seta[].blend 0 }
	  }
      else
      ifvarg stun 0
	  {
        ai AIMEGAHIT
		ifspritepal 47 { cstat 257 seta[].blend 0 }
	  }
    }
  }
  state random_wall_jibs
ends

state megashrunkstate
  ifcount SHRUNKDONECOUNT
    ai AIMEGAGETENEMY
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 19 sizeat 60 52 else
      sizeto 48 40
	}
  else
    state genericshrunkcode
ends

state megashockstate

ifl targetdist 1280
{
	ifn bottarget -1
	{
		geta[bottarget].htextra temp
		add temp 2
		seta[bottarget].htextra temp
		seta[bottarget].htowner THISACTOR
		seta[bottarget].htang sprite[].ang
		seta[bottarget].htpicnum BIGSHOCKER
		ife bottarget player[].i palfrom 56 56 56 60
	}
	else
	{
		palfrom 56 56 56 60
		addphealth -1
	}
}

ifspritepal 47
{
	soundonce ELEC_LOOP
	cstator 2
	seta[].blend 1
	
	ifactioncount 5
	{
		
		espawn SOUNDPLATE
		setav[RETURN].SPRITELOTAG IFRAMEHIT
		setav[RETURN].mtype 1
		
		setprojectile[POMPROJ].xrepeat 128
		setprojectile[POMPROJ].yrepeat 128
		setprojectile[POMPROJ].extra 12
		setprojectile[POMPROJ].range 40
		setprojectile[POMPROJ].spawns LASERHIT
		setprojectile[POMPROJ].hitradius 1024
		setprojectile[POMPROJ].pal 113
		
		set xvel 768
		state rpg_targetprep
		zshoot zdist POMPROJ
		
		set safecount 0
		rand angvel 2047
		geta[].z savz
		set starty savz
		whilevarn safecount 12
		{
			rand zdist 16384
			sub zdist 8192
			ezshoot zdist POMPROJ
			seta[RETURN].ang angvel
			sub starty 644
			seta[].z starty
			add safecount 1
			add angvel 170
		}
		seta[].z savz
		setprojectile[POMPROJ].xrepeat 18
		setprojectile[POMPROJ].yrepeat 18
		setprojectile[POMPROJ].pal 0
		setprojectile[POMPROJ].yrepeat 48
		setprojectile[POMPROJ].range 10
		setprojectile[POMPROJ].extra 8
		setprojectile[POMPROJ].hitradius 0
		setprojectile[POMPROJ].spawns -1
	}
	ifactioncount 20 { cstat 257 seta[].blend 0 stopactorsound THISACTOR ELEC_LOOP ai AIMEGAGETENEMY }
	break
}

ifcount 17
  {
    ifcount 18
      ai AIMEGAGETENEMY
  }
  else
    ifcount 16
	{
	  sound MEGAFIRE
      espawn BIGSHOCKER
	  geta[RETURN].z temp
	  sub temp 10240
	  seta[RETURN].z temp
	  setav[RETURN].team team
	}
  else
    ifcount 15
	ifspritepal 19
	{
		sound MEGAFIRE
		shoot MEGAPROJ
		eshoot MEGAPROJ
		geta[RETURN].ang angvar
		sub angvar 64
		seta[RETURN].ang angvar
		eshoot MEGAPROJ
		geta[RETURN].ang angvar
		add angvar 64
		seta[RETURN].ang angvar
	}
    ifactioncount 5
      resetactioncount
	  
ends

state megadyingstate
  ifcount 21 nullop else ifcount 20 
  {
	state standard_jibs
	geta[].z savz
	set temp savz
	sub temp 10240
	seta[].z temp
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	seta[].z savz
  }
  ifactioncount 5 killit
ends

useractor enemy MEGABRAIN MEGASTRENGTH

  fall
  state checksquished
  ifai 0
  {
	ifl sprite[].xrepeat 4 spawn GLARESTAR
    ai AIMEGAGETENEMY
	sound MEGARECOG
	clipdist 96
	ifspritepal 19 { sizeat 60 52 addstrength 400 } else
	ifspritepal 47 { sizeat 42 38 clipdist 64 ai AIMEGASHOCK } else
	sizeat 48 40
	cstat 257
	seta[].mdflags 16
  }
	
  state monsterai
  
  ifaction AMEGAFROZEN
  {
    ifcount THAWTIME
    {
      ai AIMEGAGETENEMY
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
      {
        ifactioncount 26
        {
          spawn WATERDRIP
          resetactioncount
        }
      }
    ifhitweapon
    {
      addkills 1 state enemy_death
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  lotsofglass 10
      state standard_jibs
	  geta[].z savz
	  set temp savz
	  sub temp 10240
	  seta[].z temp
	  espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	espawn MEGAJIB
	seta[RETURN].pal sprite[].pal
	  seta[].z savz
      ifrnd 84
        spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else
  {
    ifrnd 1
	{
	  	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		nullop
		else
		{
			ifactorsound THISACTOR MEGAROAM1 nullop else
			ifactorsound THISACTOR MEGAROAM2 nullop else
			{
				ifrnd 128 sound MEGAROAM1 else sound MEGAROAM2
			}
		}
	}

    ifai AIMEGAGETENEMY
      state megagetenemystate
    else
      ifai AIMEGAHIT
      {
	    ifvarg stun 0 resetcount
        ifcount 8
		{
		  ifrnd 64 ai AIMEGASHOCK
		  else
          ai AIMEGASHOOTENEMY
		}
      }
    else
      ifai AIMEGADYING
      {
        state megadyingstate
        break
      }
    else
      ifai AIMEGASHOOTENEMY
        state megashootenemystate
    else
	  ifai AIMEGASHOCK
	    state megashockstate
	else
      ifai AIMEGASHRUNK
      {
        state megashrunkstate
        break
      }
    else
      ifai AIMEGAGROW
        state genericgrowcode

    ifmove MEGAUPVELS nullop
    else
      ife targethigher YES
	  {	
	    ifceilingdistl 96 nullop else
		ifcount 20
        move MEGAUPVELS seekplayer
	  }
    else
      ifmove MEGAINWATER nullop
      else
        ifinwater
          move MEGAINWATER seekplayer
	 
	ife monstatus -1 break
	
	geta[].htextra temp
	ifg temp 0 ife sprite[].htpicnum RPG
	{
		mul temp 2
		seta[].htextra temp
	}
	
    ifhitweapon
      state checkmegahitstate
	else state checksquished
  }
enda

useractor enemystayput MEGABRAINSTAYPUT MEGASTRENGTH cactor MEGABRAIN enda

action AOCTBABANIM		0	2	5	1	20
action AOCTBABFROZE	    5	1	5	1	10
action AOCTBABDYING		10	5	1	1	8

move OCTBABVEL 64 -16
move OCTBABDOWNVEL 64 -4
move OCTBABUPVELS 64 -32
move OCTBABSTOPPED 0 -20
move OCTBABRUSH 280 -6

ai AIOCTBABSEEK	AOCTBABANIM OCTBABVEL seekplayer getv
ai AIOCTBABGROW AOCTBABANIM OCTBABSTOPPED geth
ai AIOCTBABRUSH AOCTBABANIM OCTBABRUSH geth getv

defstate octbabrushstate

	ifcount 30
	{
		ai AIOCTBABSEEK
		break
	}
	ifn bottarget -1
	{
		state makespeedblur
		ifcount 10 nullop else state turntotarget
		state zveltotarget
		
		ifl targetdist 896
		{
		  ai AIOCTBABSEEK
		  seta[bottarget].htpicnum FIRELASER
		  seta[bottarget].htowner THISACTOR
		  seta[bottarget].htextra 4
		  seta[bottarget].htang sprite[].ang
		  sound BEASTBITESND
		  setactorsoundpitch THISACTOR BEASTBITESND 644
		}
		
	}

ends

defstate octbabseekstate

	ifn bottarget -1
	ifcount 20
	ifrnd 8
	ifl targetdist 6144
	{
		geta[bottarget].z z
		geta[].z z2
		sub z z2
		abs z
		ifl z 16384
		{
			ai AIOCTBABRUSH
			sound MECHBRAIN_PAIN2
			setactorsoundpitch THISACTOR MECHBRAIN_PAIN2 768
			break
		}
	}
	
  // ifinwater nullop else
  
  ifmove OCTBABVEL
  {
	ifcount 30
	{
	  iffloordistl 64 nullop else
	  ife targethigher NO
	  {
		move OCTBABDOWNVEL seekplayer getv
		break
	  }
	  
	  ifnotmoving
		  move OCTBABUPVELS seekplayer getv
	  else
	  ife targethigher YES
	  ifrnd 16
		move OCTBABUPVELS seekplayer getv

	}  
  }
  else ifmove OCTBABUPVELS
  ifcount 30 
  {
	ife targethigher NO
	ifrnd 8
		move OCTBABVEL seekplayer getv
	
	ifnotmoving ifrnd 128
		move OCTBABVEL randomangle getv
	else
		ifceilingdistl 32
		move OCTBABDOWNVEL seekplayer getv
  }
  else ifmove OCTBABDOWNVEL
  ifcount 60 
  {
	ife targethigher YES
		move OCTBABVEL seekplayer getv
	else
	iffloordistl 48 move OCTBABVEL seekplayer getv
  }
  
  ifrnd 1
  ifrnd 128
  {
	sound MECHBRAIN_ROAM1
	setactorsoundpitch THISACTOR MECHBRAIN_ROAM1 1560
  }


ends

eventloadactor OCTOBABY sizeat 34 28 enda
useractor enemy OCTOBABY TROOPSTRENGTH AOCTBABANIM

	ifaction AOCTBABDYING
	{
		ifcount 10 nullop else
		ifcount 9
		{
			guts JIBS4 1
			guts JIBS5 1
			guts JIBS6 4
		}
		ifactioncount 5 
			killit
		break
	}
	fall
	state monsterai
	state checksquished
	ifaction AOCTBABFROZE
	{
		ifcount THAWTIME
		{
		  getlastpal
		  ai AIOCTBABSEEK
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}

		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			resetcount resetactioncount
			strength 0
			break
		  }
		  addkills 1 state enemy_death
		  lotsofglass 8
		  ifrnd 84
			spawn BLOODPOOL
		  state lite_jibs
		  sound GLASS_BREAKING
		  killit
		}
		ifp pfacing
		  ifpdistl FROZENQUICKKICKDIST
			state pkick_check
		break
	}
	ifai 0
	{
		// sound NEWCOMM_ALERT
		sizeat 34 28
		cstator 257
		ai AIOCTBABSEEK
		clipdist 16
		ifrnd 96 move OCTBABUPVELS seekplayer getv
	}
	ifai AIOCTBABGROW
    { state genericgrowcode break }
	
	ifai AIOCTBABSEEK state octbabseekstate else
	ifai AIOCTBABRUSH state octbabrushstate
	
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{
			sound ACTOR_SHRINKING
			// ai AIOCTASHRUNK
			set shrunken 1
		}
		ifdead
		{
			ifwasweapon FREEZEBLAST
		    {
				sound NEWFREEZE
				spritepal 1
				move 0
				action AOCTBABFROZE
				strength 0
				state freezeme
				break
		    }
			else
			ifwasweapon GROWSPARK
			{
				cstat 0
				sound ACTOR_GROWING
				ai AIOCTBABGROW
				break
			}
			
			state random_wall_jibs
			addkills 1 state enemy_death
			
			
			ifwasweapon RPG
			{
				sound SQUISHED
				state lite_jibs
				killit
			}
			else ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				state lite_jibs
				killit
			}
			
			state rf
			action AOCTBABDYING
			move STOPPED geth
			sound OCTA_DYING
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setactorsoundpitch THISACTOR OCTA_DYING 1560
			else
			setactorsoundpitch THISACTOR OCTA_DYING 1024
		}
		else
		{
			ifwasweapon GROWSPARK
            sound SPARKLESOUND
		}
	}

enda
