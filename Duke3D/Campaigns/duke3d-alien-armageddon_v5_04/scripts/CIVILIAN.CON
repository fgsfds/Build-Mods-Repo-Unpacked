// FEMALE CIVILIAN
// sprites by sebabdukeboss20, code by Dan Gaskill
// version .91 February 9, 2018

// define FEMCIV 5376
// define FEMCIVCROUCH 5421

// define STRIPPER 5430
// define STRIPPERSTAYPUT 5431
// define STRIPPERCROUCH 5475
// define KBABE 6270
// define KBABESTAYPUT 6271
// define KBABECROUCH 6315

// spriteshadow FEMCIV
// spriteshadow FEMCIVCROUCH
spriteflags FEMCIV 12582912 // 46145537
spriteflags FEMCIVCROUCH 12582912
spriteflags FEMCIV2 12582912
spriteflags FEMCIV2CROUCH 12582912
spriteflags FEMCIV3 12582912
spriteflags FEMCIV3CROUCH 12582912
spriteflags FEMCIV4 12582912
spriteflags FEMCIV5 12582912
spriteflags FEMCIV5DANCE 12582912
spriteflags STRIPPER 12582912
spriteflags STRIPPERSTAYPUT 12582912
spriteflags STRIPPERCROUCH 12582912
spriteflags KBABE 12582912
spriteflags KBABESTAYPUT 12582912
spriteflags KBABECROUCH 12582912
spriteflags HOOKER2 12582912
spriteflags HOOKERSTAYPUT2 12582912
spriteflags HOOKERCROUCH2 12582912
spriteflags HOOKER 12582912
spriteflags HOOKERSTAYPUT 12582912
spriteflags HOOKERCROUCH 12582912
spriteflags CHEERLEADER 12582912
spriteflags CHEERLEADERCROUCH 12582912
spriteflags CAVEGIRL 12582912
spriteflags CAVEGIRLCROUCH 12582912
spriteflags MALECIV1 12582912
spriteflags MALECIV1CROUCH 12582912
spriteflags MALECIV1POOP 12582912
spriteflags MALECIV2 12582912
spriteflags MALECIV2CROUCH 12582912
spriteflags MALECIV2POOP 12582912
spriteflags MALECIV3 12582912
spriteflags MALECIV3CROUCH 12582912
spriteflags MALECIV4 12582912
spriteflags MALECIV4CROUCH 12582912
spriteflags MALECIV5 12582912
spriteflags MALECIV5CROUCH 12582912
spriteflags MALECIV6 12582912
spriteflags MALECIV6CROUCH 12582912
spriteflags FATGUY1 12582912
spriteflags FATGUY2 12582912
spriteflags POLEDANCER 12582912
spriteflags NAKEDBABE 12582912
spriteflags NAKEDBABECROUCH 12582912
spriteflags BIKINIBABE 12582912
spriteflags BIKINIBABECROUCH 12582912
spriteflags BIKINIBABE2 12582912
spriteflags BIKINIBABECROUCH2 12582912
spriteflags SISTERS 12582912
spriteflags SISTERS2 12582912
spriteflags CRYSTAL 12582912
spriteflags CRYSTALCROUCH 12582912
spriteflags GRAVES 12582912
spriteflags SHOWERGIRL 12582912
spriteflags ALIENCIVFEM 12582912
spriteflags ALIENCIVFEMCROUCH 12582912
spriteflags ALIENCIVMALE 12582912
spriteflags ALIENCIVMALECROUCH 12582912
spriteflags PIGSTRIPPER 12582912
spriteflags MAIDNPC 12582912

gamevar FEMVAR 0 0
// gamevar FEMFALLDMG 0 2
// gamevar FEMKILLCOUNT 0 2

state checkfemsquished
  ifsquished
  {
    sound SQUISHED
    state standard_jibs
    state random_ooz
    killit
  }
ends

state facestartpos

	set x2 startx
	set y2 starty
	sub x2 sprite[].x
	sub y2 sprite[].y
	getangle angvar x2 y2
	seta[].ang angvar

ends

defstate corruption

	strength CORRUPTSTRENGTH
	cactor CORRUPT
	ai AICORRTRANSFORM
	clipdist 40
	set monstatus -1
	sound CORRUPT_TRANSFORM

ends

defstate crayruption

	strength CORRUPTSTRENGTH
	
	ai AICRAZTRANSFORM
	ifactor FEMCIV3 action ACRAZTRANSFORM1 else
	ifactor CRYSTAL action ACRAZTRANSFORM2 else
	ifactor FEMCIV5 action ACRAZTRANSFORM3
	cactor CRAZYLADY
	ifspritepal 0 spritepal 21
	clipdist 40
	set monstatus -1
	sound LADY_SCREAM
	sound SAWFLESH

ends


// SFLAG_SMOOTHMOVE + SFLAG_HURTSPAWNBLOOD + SFLAG_GREENSLIMEFOOD + SFLAG_WAKEUPBADGUYS

action AFEMCIVSTAND		0  1  5  1  10
action AFEMCIVWALK		5  4  5  1  18
action ACIVWALKFAST	    5  4  5  1  10
action AFEMCIVFROZEN	5  1  5  1  10
action AFEMCIVRUN		25 4  5  1  8
action AFEMCIVGROW		30 1  5  1  10
// action AFEMCIVCROUCH	45 1  5  1  10
action AFEMCIVCROUCH	0 1  5  1  10
action AFEMTHICFEAR		25 1 5  1  10
action AFEMCIVDIE		50 3  1  1  30
action AFEMCIVDEAD		52 1  1  1  10

action AFEMTHICDIE		30 4  1  1  20
action AFEMTHICDEAD		34 1  1  1  10

action ALIENDYING		50 6  1  1  16
action ALIENDEAD		55 1  1  1  10

action ACRYSTALSMOKE  53  2  5  1  90

action AFEM5ANIMF		53  4  5  1  10
action AFEM5ANIMB		68  4  5  -1  10

action AFEM5DANCEF		0  4  5  1  10
action AFEM5DANCEB		15  4  5  -1  10

move FEMSHRUNKVELS 32
move FEMWALKVELS 64
move FEMRUNVELS 180
move GUIDEWALKVELS 128
move GUIDERUNVELS 256
move FEMSTOPPED

ai AIFEMDYING AFEMCIVDIE FEMSTOPPED geth
ai AIFEMWAIT AFEMCIVSTAND FEMSTOPPED geth
ai AIFEMWALK AFEMCIVWALK FEMWALKVELS randomangle
ai AIFEMRUN AFEMCIVRUN FEMRUNVELS randomangle
ai AIFEMCROUCH AFEMCIVCROUCH FEMSTOPPED geth
ai AIFEMSHRUNK AFEMCIVRUN FEMSHRUNKVELS fleeenemy

state femdyingstate

state deadsquished
ifaction AFEMCIVDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 { action AFEMCIVDEAD cstat 0 strength 0 }
}
else
ifaction AFEMCIVDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}

ifaction AFEMTHICDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 4 { action AFEMTHICDEAD cstat 0 strength 0 }
}
else
ifaction ALIENDYING
{
	ifhitweapon state random_wall_jibs
	ifactioncount 5 { action ALIENDEAD cstat 0 strength 0 }
}
else
ifaction ALIENDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}

ends

state femfallcheck

	ifg sprite[].zvel 0
		add FEMFALLDMG 2
		
	ife globalnofall YES ifg FEMFALLDMG 8 set FEMFALLDMG 8
	
	iffloordistl 4
	{
		ifonwater set FEMFALLDMG 0
		ifinwater set FEMFALLDMG 0
		ifg FEMFALLDMG 0
		{
			sub FEMFALLDMG 30
			ifg FEMFALLDMG 0
			{
				sound SQUISHED
				guts JIBS6 8
				seta[].htextra FEMFALLDMG
				seta[].htpicnum SHOTSPARK1
				seta[].htowner THISACTOR
			}
			set FEMFALLDMG 0
		}
	}
	
	// drowning check
	ifinwater
	{
		ifrnd 16
		{
			spawn WATERBUBBLE
			ifrnd 128 spawn WATERBUBBLE
			seta[].htextra 1
			seta[].htpicnum SHOTSPARK1
			seta[].htowner THISACTOR
		}
	}

ends

state femshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIFEMWAIT
  else
    ifcount SHRUNKCOUNT
	{
	  ifactor ALIENCIVFEM
	  sizeto 26 24 else
	  ifactor FEMCIV4
	  sizeto 24 22 else
      sizeto 28 26
	}
  else
    state genericshrunkcode

ends

state aliencivsounds


	ife initflags 0
	{
		ife pchar 2 rand temp 3 else
		rand temp 4
		switch temp
		
		case 0 sound FEMALIEN_SURRENDER break
		case 1 sound FEMALIEN_UPCLOSE break
		case 2 sound FEMALIEN_YOUSTRONG break
		case 3 sound FEMALIEN_NOHARM break
		case 4 sound FEMALIEN_SAWYOURVICTORY break
		endswitch
	}
	else
	{
		rand temp 3
		switch temp
		case 0 sound FEMALIEN_HUMANSATTACK break
		case 1 sound FEMALIEN_HUMANSHERE break
		case 2 sound FEMALIEN_IMPERIALGUARDS break
		case 3 sound FEMALIEN_NOTHINGOFVALUE break
		endswitch
	}
		

ends

defstate girlpainsounds

	ifactorsound THISACTOR SG_PAIN1 break
	ifactorsound THISACTOR SG_PAIN2 break
	ifactorsound THISACTOR SG_PAIN3 break
	ifrnd 84 sound SG_PAIN1 else
	ifrnd 128 sound SG_PAIN2 else
	sound SG_PAIN3
	
ends

defstate civpainsound
	rand temp 2
	add temp 6
	add temp initflags
	soundoncevar temp
ends

defstate civdeathsound
	set temp initflags
	add temp 9
	sound temp
ends

state femwaitstate

	ife botclip 10
	{
		ife navmode 2
		{
			ifg mtype 0
			{
				sub mtype 1
				set navpoint -1
				set savedvalue -1
				break
			}
			ai AIFEMWALK
			move GUIDEWALKVELS geth
			break
		}
		ifn initflags 0 // paused to get player's attention
		{
			ifcount 30
			{
				set navmode 2
				ai AIFEMWALK
				move GUIDEWALKVELS geth
				set initflags NO
				set navpoint -1
				set savedvalue -1
				break
			}
		}
		ifg wave 1
		ifl tourgoal 5
		{
			ai AIFEMWALK
			move GUIDEWALKVELS geth
			set navmode 2
			ifn myspawner -1
			{
				setav[myspawner].monstatus 2
				set myspawner -1
			}
			set tourgoal 5
			set mtype 0
		}
		// tour guide code
		ifn myspawner -1
		{
			ife tourgoal 8
			ife sprite[myspawner].cstat 0
			ife mtype 0
			{
				sound ITEMPORT
				move STOPPED faceplayer
				set mtype 60
				seta[myspawner].cstat 32768
			}
			ifg mtype 0
			{
				sub mtype 1
				ife tourgoal 4 ife mtype 120 ife es_type 0
				{
					sound GUIDE_NEXTMATCH2
				}
				ife tourgoal 7 ife mtype 30
				{
					sound GUIDE_ONMYWAYTHEN
					set subtitle_time 90
					set subtitle_start 427
					set subtitle_numlines 1
				}
				ife tourgoal 8 ifn mtype 0
				{
					seta[].htflags 64
					seta[].htextra -1
					ifspritepal 33 spritepal 17 else spritepal 33
					ife mtype 1 { setav[myspawner].monstatus 2 killit }
				}
				ifn vendor_screen 0 ifl mtype 30 add mtype 1
				ifpdistg 7168 
				ifl tourgoal 8
				{
					set mtype 0
					stopsound GUIDESPEECH1
					stopsound GUIDE_SLARTY
					stopsound GUIDE_OGUS
					stopsound GUIDE_NEXTMATCH
					stopsound GUIDE_TELEPORTERS
				}
				ife mtype 0
				{
					// ai AIFEMWALK
					// move GUIDEWALKVELS geth
					set navmode 2
					setav[myspawner].monstatus 2
					set myspawner -1
					add tourgoal 1
					set mtype 20 // makes her pause to wait for paths to value up
				}
			}
			ifn myspawner -1
				setsprite myspawner sprite[].x sprite[].y sprite[].z // just in case it moved
			
			ifp pfacing
			ife seeplayer YES
			ifpdistl 1560
			ifhitspace
			ife mtype 0
			{
				ife tourgoal 1 
				{
					sound GUIDESPEECH1
					move STOPPED faceplayer
					set mtype 420
					
					set subtitle_time 420
					set subtitle_start 400
					set subtitle_numlines 5
					
				}
				ife tourgoal 2
				{
					sound GUIDE_SLARTY
					move STOPPED faceplayer
					set mtype 240
					set subtitle_time 240
					set subtitle_start 405
					set subtitle_numlines 3
				}
				ife tourgoal 3
				{
					sound GUIDE_OGUS
					move STOPPED faceplayer
					set mtype 240
					set subtitle_time 240
					set subtitle_start 408
					set subtitle_numlines 3
				}
				ife tourgoal 4
				{
					sound GUIDE_NEXTMATCH
					move STOPPED faceplayer
					ife es_type 0 { set subtitle_time 330 set mtype 330 } else { set subtitle_time 240 set mtype 240 }
					set subtitle_start 411
					ife es_type 0 set subtitle_numlines 4 else set subtitle_numlines 2
				}
				ife tourgoal 5
				{
					sound GUIDE_TELEPORTERS
					move STOPPED faceplayer
					set mtype 210
					set subtitle_time 210
					set subtitle_start 415
					set subtitle_numlines 3
				}
				ife tourgoal 6
				{
					sound GUIDE_QUARTERS
					move STOPPED faceplayer
					set mtype 300
					set subtitle_time 300
					set subtitle_start 423
					set subtitle_numlines 4
				}
				ife tourgoal 7
				{
					sound GUIDE_TERRORISTS
					move STOPPED faceplayer
					set mtype 500
					set subtitle_time 500
					set subtitle_start 418
					set subtitle_numlines 5
				}
				stopsound DUKE_GRUNT
				stopsound DUKE_SEARCH
				stopsound DUKE_SEARCH2
				stopsound B_GRUNT
				stopsound B_SEARCH
			}
		}
		
		break
	}
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
				
	// ifactorsound THISACTOR FC_COOLSHADES nullop else
	// ifactorsound THISACTOR FC_YOURCUTE nullop else
	// ifactorsound THISACTOR FC_YOUMORON nullop else
	// ifn sprite[].picnum ALIENCIVFEM
	// {
		// ifp pfacing
		// ifpdistl 4096
		// ifrnd 32
		// ife seeplayer YES
		// {
			// ifp pducking soundonce FC_YOUMORON else
			// ifp pjumping soundonce FC_YOUMORON
		// }
	// }
	
	ifaction ACRYSTALSMOKE nullop else
	ifactor CRYSTAL ifcount 15 ifrnd 16 { action ACRYSTALSMOKE resetcount }
	
	ifaction AFEM5ANIMF
	{
		ifactioncount 4
		action AFEM5ANIMB
		break
	}
	ifaction AFEM5ANIMB
	{
		ifactioncount 4
		{
			action AFEMCIVSTAND
		}
	}
	
	ifcount 80 nullop else
	ifactor FEMCIV5
	ifaction AFEMCIVSTAND
	ifrnd 2
	action AFEM5ANIMF

	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	ife lastangvel 0
	{
		ifn SPRITELOTAG 0 { state crayruption break }
		ifactor FEMCIV5
		ifaction AFEMCIVSTAND
			action AFEM5ANIMF
			
		resetcount
		state giftcheck
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		
		ifactor ALIENCIVFEM state aliencivsounds
		else
		{
			ifaction ACRYSTALSMOKE { action AFEMCIVSTAND resetcount }
			rand temp 2
			add temp initflags
			soundoncevar temp
			
			
			// ifg player[].actors_killed FEMKILLCOUNT sound FC_THANKS
			// else
			// {
				// ifrnd 128 sound FC_COOLSHADES else sound FC_YOURCUTE
				// ifaction ACRYSTALSMOKE { action AFEMCIVSTAND resetcount }
			// }
		}
		set lastangvel 120
	}
	
	ifcount 120
	ifrnd 8 
		ai AIFEMWALK

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch 0
		
	ifn SPRITELOTAG 0
	ifrnd 1 ifrnd 96
	ifpdistl 8192
	ife seeplayer YES
	{ state crayruption break }
	

ends

state femwalkstate

	ife botclip 10
	{
			
		ifn initflags 0
		{
			ife seeplayer YES
			{
				ifpdistl 3072
				{
					set navmode 0
					ai AIFEMWAIT
					ife es_type 0 ifrnd 64 soundonce GUIDE_PLAYERDITCHING else
					ifrnd 84 soundonce GUIDE_FOLLOW1 else
					ifrnd 128 soundonce GUIDE_FOLLOW2 else
					soundonce GUIDE_FOLLOW3
					ifle subtitle_start 434
					set subtitle_time 0
					break
				}
				else ifpdistl 8192
				{
					set navmode 0
					set navpoint -1
					set initflags 2
					state crumbcode
				}
			}
			else { set initflags 1 set navmode 2 }
		}
		else ifn tourgoal 8
		{
			ifpdistl 3072
			ifangdiffl 256
			ifp pfacing
			ife seeplayer YES
			{
				ai AIFEMRUN break
			}
			
		}
		ifmove GUIDEWALKVELS nullop else
		{
			move GUIDEWALKVELS geth
			action ACIVWALKFAST
		}
		
		ifpdistg 7168 ifn tourgoal 8 { ai AIFEMRUN break }
		ifn myspawner -1 // dialog bubble has been spawned for next check point
		{
			ai AIFEMWAIT
			move STOPPED faceplayer
			set navmode 0
		}
		break
	}
	
	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	ife lastangvel 0
	{
		ifn SPRITELOTAG 0
		{ state crayruption break }
		
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
		state giftcheck		
		getangletotarget FEMVAR
		seta[].ang FEMVAR	
		
		ifn SPRITELOTAG 0
		{ state crayruption break }
		
		ifactor ALIENCIVFEM state aliencivsounds
		else
		{	
			rand temp 2
			add temp initflags
			soundoncevar temp
			
			// ifg player[].actors_killed FEMKILLCOUNT sound FC_THANKS
			// else
			// {
				// ifrnd 128 sound FC_COOLSHADES else sound FC_YOURCUTE
			// }
		}
		ai AIFEMWAIT
		
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
				
		set lastangvel 120
	}

	ifcount 60
		ifrnd 6
			ife navmode 0
		ai AIFEMWAIT
	
	geta[].htmovflag temp
	ifn temp 0
	{
		// bumping something
		add temp 16384
		ifl temp 16384 ifg temp -1 // bumping a sprite
		{
			ife actorvar[temp].monstatus 0 ai AIFEMWAIT
		}
		else
		ai AIFEMWAIT
	}
	ifn SPRITELOTAG 0
	ifrnd 1 ifrnd 96
	ifpdistl 8192
	ife seeplayer YES
	{ state crayruption break }
ends

defstate startscream

	ifinwater break
	
	ifvarand monstflags 8192 break
	
	
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
			
	ifactor ALIENCIVFEM
	{
		ifactorsound THISACTOR FEMALIEN_HUMANSATTACK nullop else
		ifactorsound THISACTOR FEMALIEN_IMPERIALGUARDS nullop else
		ifactorsound THISACTOR FEMALIEN_RUNFORLIVES nullop else
		ifactorsound THISACTOR FEMALIEN_SCREAM nullop else
		ifactorsound THISACTOR FEMALIEN_WHYHAPPENING nullop else
		{
			randvar FEMVAR 4
			ife FEMVAR 0 soundonce FEMALIEN_HUMANSATTACK
			ife FEMVAR 1 soundonce FEMALIEN_IMPERIALGUARDS
			ife FEMVAR 2 soundonce FEMALIEN_RUNFORLIVES
			ife FEMVAR 3 soundonce FEMALIEN_SCREAM
			ife FEMVAR 4 soundonce FEMALIEN_WHYHAPPENING
		}
		break
	}
	ife botclip 10 break
	ifg initflags 20000
	{
		geta[].picnum picnum
		al picnum
	}
	set temp initflags
	add temp 3
	ifactorsound THISACTOR temp break
	add temp 1
	ifactorsound THISACTOR temp break
	add temp 1 
	ifactorsound THISACTOR temp break
	
	rand temp 2
	add temp 3
	add temp initflags
	soundoncevar temp
	
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0

ends

defstate stopscream

	stopactorsound THISACTOR FEMALIEN_SCREAM
	stopactorsound THISACTOR FEMALIEN_RUNFORLIVES
	stopactorsound THISACTOR FEMALIEN_WHYHAPPENING
	stopactorsound THISACTOR FEMALIEN_HUMANSATTACK
	stopactorsound THISACTOR FEMALIEN_IMPERIALGUARDS
	
	ife botclip 10 break
	set temp botclip, add temp 3
	stopactorsound THISACTOR temp
	add temp 1 stopactorsound THISACTOR temp
	add temp 1 stopactorsound THISACTOR temp
ends



state femrunstate

	ife botclip 10
	{
		ifmove GUIDERUNVELS nullop else
		move GUIDERUNVELS geth
		
		ifn initflags 0
		{
			ife seeplayer YES
			{
				ifpdistl 3072
				{
					set navmode 0
					ai AIFEMWAIT
					ife es_type 0 ifrnd 64 soundonce GUIDE_PLAYERDITCHING else
					ifrnd 84 soundonce GUIDE_FOLLOW1 else
					ifrnd 128 soundonce GUIDE_FOLLOW2 else
					soundonce GUIDE_FOLLOW3
					break
				}
				else ifpdistl 8192
				{
					set navmode 0
					set navpoint -1
					set initflags 2
					state crumbcode
				}
			}
			else { set initflags 1 set navmode 2 }
		}
		
		ifpdistl 3072
		ifangdiffl 256
		ifp pfacing
		ife seeplayer YES nullop else
		ifpdistl 5120 ai AIFEMWALK
		
		ifn myspawner -1 // dialog bubble has been spawned for next check point
		{
			ai AIFEMWAIT
			move STOPPED faceplayer
			set navmode 0
		}
		break
	}

	ifrnd 2
	state startscream
		
	ifnotmoving
	{
		ifvarand monstflags 8192 nullop else
		ifrnd 128
		{
			ai AIFEMCROUCH
			ifactor FEMCIV cactor FEMCIVCROUCH else
			ifactor FEMCIV2 cactor FEMCIV2CROUCH else
			ifactor FEMCIV3 cactor FEMCIV3CROUCH else
			ifactor FEMCIV5 cactor FEMCIV5CROUCH else
			ifactor NAKEDBABE cactor NAKEDBABECROUCH else
			ifactor BIKINIBABE cactor BIKINIBABECROUCH else
			ifactor BIKINIBABE2 cactor BIKINIBABECROUCH2 else
			ifactor CRYSTAL cactor CRYSTALCROUCH else
			ifactor ALIENCIVFEM cactor ALIENCIVFEMCROUCH
			ifactor FEMCIV4 action AFEMTHICFEAR
			state stopscream
			// state startcrouchsound
			break
		}
		ifawayfromwall
		move FEMRUNVELS randomangle
		else
		move FEMRUNVELS furthestdir
		
	}
	ifcount 300 ifrnd 8 ai AIFEMWALK
ends

state femcrouchstate

	// ifcount 90 nullop else
	// ifrnd 4
		// state startcrouchsound
	
	ifcount 300 ifrnd 8
	{
		ifactor FEMCIVCROUCH cactor FEMCIV else
		ifactor FEMCIV2CROUCH cactor FEMCIV2 else
		ifactor FEMCIV3CROUCH cactor FEMCIV3 else
		ifactor FEMCIV5CROUCH cactor FEMCIV5 else
		ifactor NAKEDBABECROUCH cactor NAKEDBABE else
		ifactor BIKINIBABECROUCH cactor BIKINIBABE else
		ifactor BIKINIBABECROUCH2 cactor BIKINIBABE2 else
		ifactor CRYSTALCROUCH cactor CRYSTAL else
		ifactor ALIENCIVFEMCROUCH cactor ALIENCIVFEM
		ai AIFEMWAIT
	}

ends

defstate femcivcode

	fall
	ifai 0
	{
		ai AIFEMWAIT
		ifactor ALIENCIVFEM ife droptile 1 
		{
			move STOPPED faceplayerslow
			set mtype ancients
		}
		ifspawnedby TROOPGROPE1
		{
			state startscream
			ai AIFEMRUN
		}
		ifspawnedby TROOPGROPE2
		{
			state startscream
			ai AIFEMRUN
		}
		clipdist 32 // 48
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		cstat 257
		ifactor FEMCIV4 sizeat 24 22 else
		sizeat 28 26
		// set monstatus 1
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
		ifactor ALIENCIVFEM { set team 3 set navmode 3 set monstatus 1 sizeat 26 24 } else
		ife VOLUME 4 ifge LEVEL 10 set team 3 else
		set team 3 // 1
		ife botclip 10 // tour guide
		{ 
			set monstatus 1
			geta[].htflags temp
			orvar temp 2097152
			seta[].htflags temp
		} 
		else 
		{
			set initflags 500
			rand temp 3
			ifn temp 0 mul temp 10
			add initflags temp
			ifvarand monstflags 8192 { set team 1 set monstatus 1 addstrength 30 seta[].htextra 2 seta[].htpicnum SHOTSPARK1 }
		}
		geta[].x startx
		geta[].y starty
	}
	ifactor ALIENCIVFEM 
	{
		ife droptile 1 
		{
			ifg ancients mtype
			{
				set mtype ancients
				findnearsprite DIALOGBUB 1024 spriteid
				ifn spriteid -1
				{
					seta[spriteid].cstat 0
					setav[spriteid].SPRITELOTAG actorvar[spriteid].mtype
					setav[spriteid].starty actorvar[spriteid].initsprite
					setav[spriteid].initflags 256
				}
			}
			break
		}
	}
	
	ife botclip 10
	{
		
		ifg wave 2 
		{
			ifn myspawner -1
			{
				setav[myspawner].monstatus 2
				set myspawner -1
			}
			killit // she is gone when you return from 2nd match
		}
		ife monstatus 66
		{
			ifn myspawner -1 setav[myspawner].monstatus 2
			set myspawner -1
			killit // failsafe
		}
		ifg navpoint 0
		ifg sprite[].xvel 0
		{
			headspritesect spriteid sprite[].sectnum
			whilevarn spriteid -1
			{
				ife sprite[spriteid].picnum SECTOREFFECTOR
				ife sprite[spriteid].lotag 24 
				{
					getincangle angvar sprite[].ang sprite[spriteid].ang
					ifl angvar 128 
					{ 
						 seta[].xvel 480
					}
				}
				nextspritesect spriteid spriteid
			}
		}
		
	}
	
	ifaction AFEMCIVFROZEN
	{
	  ifcount THAWTIME
		{
		  ai AIFEMWAIT
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			strength 0
			break
		  }

		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2

		  killit
		}
		// ifp pfacing
		  // ifpdistl FROZENQUICKKICKDIST
			// pkick
		break
	}
	ifaction AFEMCIVGROW
	{
		state genericgrowcode
		break
	}
	ifg lastangvel 0 
	{
		sub lastangvel 1
		ife sprite[].xvel 0
		{
			getangletotarget FEMVAR
			seta[].ang FEMVAR
		}
	}
	ifai AIFEMDYING { state femdyingstate break }
	
	state femfallcheck
	state checkfemsquished
	state monsterai ife monstatus -1 break
	
	ifai AIFEMSHRUNK { state femshrunkstate break }
	
	

	ifai AIFEMWAIT state femwaitstate else
	ifai AIFEMWALK state femwalkstate else
	ifai AIFEMRUN state femrunstate else
	ifai AIFEMCROUCH state femcrouchstate
	
	

ife botclip 10 ife tourgoal 1 seta[].htextra -1

ifhitweapon
  {
    ife botclip 10
	{
		strength 100
		ifle subtitle_start 434
		set subtitle_time 0
		stopsound GUIDESPEECH1
		stopsound GUIDE_SLARTY
		stopsound GUIDE_OGUS
		stopsound GUIDE_NEXTMATCH
		stopsound GUIDE_TELEPORTERS
		stopsound GUIDE_TERRORISTS
		stopsound GUIDE_ONMYWAYTHEN
		stopsound GUIDE_QUARTERS
		ifg mtype 60 set mtype 60		
		ifsound GUIDE_FUNNYGUARDS break
		ifsound GUIDE_DOUCHENUKEM break
		ifsound GUIDE_BOMBSHELLFACE break
		ifsound GUIDE_BETTERTODO break
		ifsound GUIDE_SERIOUSLYSTOP break
		ifsound GUIDE_BITEMYASS break
		
		add countvarc 1
		switch countvarc
		case 1	sound GUIDE_FUNNYGUARDS
				set subtitle_start 428
		break
		case 2  ife pchar 0 { sound GUIDE_DOUCHENUKEM set subtitle_start 433 } else { sound GUIDE_BOMBSHELLFACE set subtitle_start 434 }
		break
		case 3  sound GUIDE_BETTERTODO set subtitle_start 430
		break
		case 4  sound GUIDE_SERIOUSLYSTOP set subtitle_start 429
		break
		case 5  sound GUIDE_BITEMYASS set subtitle_start 432
		break
		case 6 sound GUIDE_CALLGUARDS set subtitle_start 431
			headspritestat startx 980
			whilevarn startx -1
			{
				dist xydist THISACTOR startx
				ifl xydist 16384
				ifrnd 96
				{
					espawn SPACEBULL
					seta[RETURN].pal 51
					setsprite RETURN sprite[startx].x sprite[startx].y sprite[startx].z
				}
				nextspritestat startx startx
			}
			globalsound ITEMPORT
			ai AIFEMWALK
			move GUIDEWALKVELS geth
			set navmode 2
			set team 0
			ifn myspawner -1
			{
				setav[myspawner].monstatus 2
				set myspawner -1
			}
			set tourgoal 8
			
		break
		endswitch
			set subtitle_numlines 1
		    set subtitle_time 120
			ife subtitle_start 434 { set subtitle_numlines 2 set subtitle_time 180 }
		break
	}
	ifvarand monstflags 8192 nullop else
    set team 3
	ifactor ALIENCIVFEM ife initflags 0 set initflags 1
	
    ifactor FEMCIVCROUCH cactor FEMCIV else
	ifactor FEMCIV2CROUCH cactor FEMCIV2 else
	ifactor FEMCIV3CROUCH cactor FEMCIV3 else
	ifactor FEMCIV5CROUCH cactor FEMCIV5 else
	ifactor NAKEDBABECROUCH cactor NAKEDBABE else
	ifactor BIKINIBABECROUCH cactor BIKINIBABE else
	ifactor BIKINIBABECROUCH2 cactor BIKINIBABE2 else
	ifactor CRYSTALCROUCH cactor CRYSTAL else
	ifactor ALIENCIVFEMCROUCH cactor ALIENCIVFEM
    ifdead
    {
	  ifn SPRITELOTAG 0 { state crayruption break }
	  state stopscream
      ifwasweapon GROWSPARK
      {
	    set monstatus 2
        cstat 0
        move FEMSTOPPED
        sound ACTOR_GROWING
        action AFEMCIVGROW
		set monstatus 2
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifai AIFEMSHRUNK
          break

            action AFEMCIVFROZEN
			move FEMSTOPPED
			spritepal 1
			strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  ifwasweapon RPG
	  { 
	     sound SQUISHED
         state standard_jibs
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
	     sound SQUISHED
         state standard_jibs
		 killit
	  }
	  ai AIFEMDYING
	  ifactor ALIENCIVFEM action ALIENDYING
	  ifactor FEMCIV4 action AFEMTHICDIE
	  set burning 0
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
			
      ifn botclip 10 state civdeathsound
	  
	  
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		ifn sprite[].picnum ALIENCIVFEM state civpainsound
		// set shrunken 1
        ai AIFEMSHRUNK
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  state random_wall_jibs
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
	  ifinwater nullop else
	  ifai AIFEMRUN { ifn sprite[].picnum ALIENCIVFEM state civpainsound } else 
	  { 
		ai AIFEMRUN 
		state startscream
		ifactor FEMCIV4
		{
			ai AIFEMCROUCH
			action AFEMTHICFEAR
		}
		ifg player[].actors_killed FEMKILLCOUNT
		getp[].actors_killed FEMKILLCOUNT
	  }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
    }
  }
  else ifn sprite[].picnum FEMCIVCROUCH ifn sprite[].picnum FEMCIV2CROUCH
  ifn sprite[].picnum FEMCIV3CROUCH
  ifn sprite[].picnum FEMCIV5CROUCH
  ifn sprite[].picnum NAKEDBABECROUCH
  ifn sprite[].picnum CRYSTALCROUCH
  ifn sprite[].picnum BIKINIBABECROUCH
  ifn sprite[].picnum BIKINIBABECROUCH2
  ifn sprite[].picnum ALIENCIVFEMCROUCH
  {
	ifai AIFEMRUN nullop else 
	{
		ife botclip 10 break
		set FEMVAR NO
		ife seeplayer YES
		ifp pfacing
		ifpdistl 6144 ifn player[].kickback_pic 0
			ifn player[].curr_weapon KNEE_WEAPON
		set FEMVAR YES
		else ifbulletnear set FEMVAR YES
		ife FEMVAR YES
		{
			ai AIFEMRUN
			state startscream
			ifactor FEMCIV4
			{
				ai AIFEMCROUCH
				action AFEMTHICFEAR
			}
			ifg player[].actors_killed FEMKILLCOUNT
			getp[].actors_killed FEMKILLCOUNT
		}
	}
  }
 ends
 
eventloadactor FEMCIV sizeat 28 26 enda
eventloadactor FEMCIV2 sizeat 28 26 enda
eventloadactor FEMCIV3 sizeat 28 26 geta[].extra botclip geta[].lotag SPRITELOTAG ifn SPRITELOTAG 0 add maxkills 1 seta[].lotag 0 enda
eventloadactor FEMCIV4 sizeat 24 22 enda
eventloadactor FEMCIV5 sizeat 28 26 geta[].lotag SPRITELOTAG ifn SPRITELOTAG 0 add maxkills 1 seta[].lotag 0 enda
eventloadactor BIKINIBABE sizeat 28 26 enda
eventloadactor BIKINIBABECROUCH sizeat 28 26 enda
eventloadactor BIKINIBABE2 sizeat 28 26 enda
eventloadactor BIKINIBABECROUCH2 sizeat 28 26 enda
eventloadactor HOOKER sizeat 28 26 enda
eventloadactor HOOKER2 sizeat 28 26 enda
eventloadactor HOOKERSTAYPUT sizeat 28 26 enda
eventloadactor HOOKERSTAYPUT2 sizeat 28 26 enda
eventloadactor NAKEDBABE sizeat 28 26 enda
eventloadactor MALECIV1 
	sizeat 28 26 
	geta[].lotag SPRITELOTAG ifn SPRITELOTAG 0 add maxkills 1
	seta[].lotag 0
enda
eventloadactor MALECIV1POOP sizeat 28 26 enda
eventloadactor MALECIV1CROUCH sizeat 28 26 enda
eventloadactor MALECIV2 sizeat 28 26 enda
eventloadactor MALECIV2POOP sizeat 28 26 enda
eventloadactor MALECIV2CROUCH sizeat 28 26 enda
eventloadactor MALECIV3 sizeat 28 26 enda
eventloadactor MALECIV3CROUCH sizeat 28 26 enda
eventloadactor MALECIV4 sizeat 28 26 enda
eventloadactor MALECIV4CROUCH sizeat 28 26 enda
eventloadactor MALECIV5 sizeat 28 26 enda
eventloadactor MALECIV5CROUCH sizeat 28 26 enda
eventloadactor MALECIV6 sizeat 28 26 enda
eventloadactor MALECIV6CROUCH sizeat 28 26 enda
eventloadactor FATGUY1 sizeat 28 26 enda
eventloadactor FATGUY2 sizeat 28 26 enda
eventloadactor ALIENCIVFEM sizeat 26 24 ifn sprite[].extra -1 geta[].extra droptile enda
eventloadactor ALIENCIVMALE sizeat 26 24 ifn sprite[].extra -1 geta[].extra droptile enda
eventloadactor POLEDANCER sizeat 28 27 enda
eventloadactor CRYSTAL sizeat 28 26 geta[].lotag SPRITELOTAG ifn SPRITELOTAG 0 add maxkills 1 seta[].lotag 0 enda
eventloadactor SHOWERGIRL sizeat 28 26 enda
// eventloadactor SHOWERGIRL2 sizeat 28 26 cstator 256 enda
// eventloadactor GUNGIRL sizeat 40 32 cstator 256 enda
eventloadactor PIGSTRIPPER sizeat 28 26 cstator 257 enda
eventloadactor STRIPPER2 sizeat 28 26 cstator 257 enda
spriteshadow STRIPPER2
eventloadactor MAIDNPC sizeat 26 24 cstator 257 enda
spriteshadow MAIDNPC

action ASTRIP2FLASH1  2  2  1  1 30
action ASTRIP2FLASH2  3  2  1  -1  30
useractor notenemy STRIPPER2 STRIPPERSTRENGTH ANULLACTION

  ifmove 0
  {
	sizeat 28 26
	cstator 256
	ifcansee nullop else break
    set monstatus 1
	set team 3
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	move STOPPED faceplayer
	
  }
  fall
  state monsterai
  ifhitweapon
  {
    set team 3
    state civpainsound
	ifdead
	{
		sound LADY_SCREAM
		sound SQUISHED
		state lite_jibs
		killit
	}
  }
  ifhitspace
  ifp pfacing
  ifpdistl 1280
  ife seeplayer YES
  {
	ifaction ANULLACTION 
	{
		stopsound DUKE_GRUNT
		stopsound DUKE_SEARCH
		stopsound DUKE_SEARCH2
		stopsound B_GRUNT
		stopsound B_SEARCH
		action ASTRIP2FLASH1
		state simpletip // defined in NOTENEMY.CON
	}
  }
  ifaction ASTRIP2FLASH1
  ifactioncount 2
  {
	action ASTRIP2FLASH2
  }
  ifaction ASTRIP2FLASH2
  ifactioncount 2
	action ANULLACTION
  

enda





// action AFEMCIVSTAND		0  1  5  1  10
// action ACIVWALKFAST	    5  4  5  1  10
// action AFEMCIVFROZEN		5  1  5  1  10
action AMAIDSHRUNK		5   4  5  1  10
action AMAIDGROW		0   1  5  1  10
action AMAIDCLEAN1		25  1  5  1  24
action AMAIDCLEAN2		30  2  5  1  16
action AMAIDCLEAN3		25  1  5  1  24
action AMAIDDIE			40  3  1  1  30
action AMAIDDEAD		42  1  1  1  10

move MAIDWALKVELS		80

useractor enemy MAIDNPC 70 AFEMCIVSTAND

  ifmove 0
  {
	// set monstatus 1 
	set team 3
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	move STOPPED geth
	sizeat 26 24
	cstator 256
	clipdist 24
	// getp[].max_actors_killed temp
	// sub temp 1
	// setp[].max_actors_killed temp
  }
  fall
  ifaction AFEMCIVFROZEN
  {
		ifcount THAWTIME
		{
		  action AFEMCIVSTAND
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			strength 0
			break
		  }
		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2

		  killit
		}
		break
  }
	
	ifaction AMAIDGROW
	{
		state genericgrowcode
		break
	}
	state monsterai
	ifaction AMAIDDIE	
	{
		ifactioncount 2 action AMAIDDEAD
		ifhitweapon ifwasweapon RPG { state standard_jibs killit } 
		strength 0
		break
	}
	ifaction AMAIDDEAD
	{
		ifhitweapon 
		{
			ifwasweapon RPG { state standard_jibs killit }
			ifwasweapon RADIUSEXPLOSION { state standard_jibs killit }
		}
		break
	}

  
  ifhitweapon
  {
	state random_wall_jibs
	
	ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
        action AMAIDSHRUNK
		move FEMSHRUNKVELS fleeenemy
        break
      }
      else
	  {
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
		state girlpainsounds
	  }
	
	ifdead
	{
	   move FEMSTOPPED geth
	   ifwasweapon GROWSPARK
      {
        cstat 0
		set monstatus 2
        move STOPPED geth
        sound ACTOR_GROWING
        action AMAIDGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifaction AMAIDSHRUNK
          break

        action AFEMCIVFROZEN
	    move STOPPED
		spritepal 1
		strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
	  
	   ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  
	  ifrnd 84
	  spawn BLOODPOOL
	  
	  
	  ifwasweapon RPG
	  { 
	     sound SQUISHED
         state standard_jibs
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
	     sound SQUISHED
         state standard_jibs
		 killit
	  }
	  stopactorsound THISACTOR SG_PAIN1
	  stopactorsound THISACTOR SG_PAIN2
	  stopactorsound THISACTOR SG_PAIN3
	  
	  rand temp 3
	  ife temp 0 sound SG_DEATH1 else
	  ife temp 1 sound SG_DEATH2 else
	  ife temp 2 sound SG_DEATH3 else
	  sound SG_DEATH4
	  sound SQUISHED
	  action AMAIDDIE
	  set monstatus 2
	  ifrnd 128 cstat 0 else cstat 4
	  
	}
  }
  
  ifaction AMAIDSHRUNK 
  {
	ifcount SHRUNKDONECOUNT
    {
	  sizeat 26 24
	  action AFEMCIVSTAND
	  move STOPPED geth
	}
	  else
		ifcount SHRUNKCOUNT
		  sizeto 26 24
	  else
		state genericshrunkcode
	break 
  }
  
  ifaction AFEMCIVSTAND
  {
	ifcount 20
	{
		set myvictim -1
		ifcansee
		ifpdistl 8192
		{
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				geta[spriteid].z z
				sub z sprite[].z
				abs z
				ife actorvar[spriteid].monstatus 109
				ifl z 8192
				{
					ldist temp THISACTOR spriteid
					ifl temp 8192
					{
						canseespr THISACTOR spriteid temp
						ife temp YES
							set myvictim spriteid
					}
				}
				nextspritestat spriteid spriteid
			}
		}
		ife myvictim -1
		{
			action ACIVWALKFAST
			move MAIDWALKVELS randomangle
		}
		else
		{
			action ACIVWALKFAST
			move MAIDWALKVELS geth
		}
	}
  }
  
  ifaction AMAIDCLEAN1
  {
	ifactioncount 1 action AMAIDCLEAN2
  }
  
  ifaction AMAIDCLEAN2
  {	
	ifactioncount 12
	{
		ifn myvictim -1
		{
			ife sprite[myvictim].statnum 1024 set myvictim -1 else
			ifn actorvar[myvictim].monstatus 109 set myvictim -1 else
			{
				ldist xydist THISACTOR myvictim
				ifl xydist 640
				{
					sound SPARKLESOUND
					setav[myvictim].monstatus 0
					seta[myvictim].cstat 32768
					set myvictim -1
				}
			}
		}
		action AMAIDCLEAN3
	}
  }
  
  ifaction AMAIDCLEAN3
  {	
	ifactioncount 1 action AFEMCIVSTAND
  }
  
  ifaction ACIVWALKFAST
  {
     ifg myvictim -1
	 {
		ife sprite[myvictim].statnum 1024 set myvictim -1
		else
		ifn actorvar[myvictim].monstatus 109 set myvictim -1
		
		ifg myvictim -1
		{
			set spriteid myvictim
			state facesprite
			
			ldist temp THISACTOR myvictim
			ifl temp 384 { action AMAIDCLEAN1 move STOPPED geth break }
			
			canseespr THISACTOR spriteid temp
			ife temp NO set myvictim -1
		}
	 }
	 ifnotmoving 
	 ifcount 12
	 {
		move STOPPED geth
		action AMAIDCLEAN1
		set myvictim -1
	 }
	 else ifcount 30 ifrnd 1
	 ife myvictim -1
	 {
		move STOPPED geth
		action AMAIDCLEAN1
	 }
  }
  
enda

action AGUNGIRLSHOOT 2  1  1

state gungirlcode

ife monstatus 0 
  {
	set monstatus 1 set team 1
	move STOPPED faceplayer
	// sizeat 40 32
	cstator 256
  }
  state monsterai
  ifhitweapon
  {
    set team 3
    ifaction ANULLACTION action AGUNGIRLSHOOT
    state civpainsound
	ifdead
	{
		sound LADY_SCREAM
		sound SQUISHED
		state lite_jibs
		killit
	}
  }
  ifhitspace
  ifp pfacing
  ifpdistl 1792
  ife seeplayer YES
  {
	ifaction ANULLACTION action AGUNGIRLSHOOT
  }
  ifaction AGUNGIRLSHOOT
  {
	ifcount 4
	{
		resetcount
		shoot SHOTSPARK1
		sound SHELLYFIRE
		add countvarc 1
	}
	ifg countvarc 15 { set countvarc 0 action ANULLACTION }
  }

ends

useractor notenemy GUNGIRL STRIPPERSTRENGTH ANULLACTION state gungirlcode enda
useractor notenemy SHOWERGIRL2 STRIPPERSTRENGTH ANULLACTION state gungirlcode enda
 

eventloadactor ASUKA geta[].extra initsprite enda

useractor notenemy ASUKA STRIPPERSTRENGTH ANULLACTION

  ife initsprite 1 break
  ife monstatus 0 
  {
	set monstatus 1 set team 3
	move STOPPED faceplayer
	cstator 256
	set initflags 520
  }
  state monsterai
  ifhitweapon
  {
    state civpainsound
	ifdead
	{
		sound LADY_SCREAM
		sound SQUISHED
		state lite_jibs
		killit
	}
 
  }

enda

action AGIRLWASH  0  2  5  1  20
useractor notenemy SHOWERGIRL FEMCIVSTRENGTH AGIRLWASH

  ife monstatus 0 
  {
	set monstatus 1 set team 3
	move STOPPED
	sizeat 28 26
	cstator 256
	set initflags 520
  }
  state monsterai
  fall
  ifhitweapon
  {
    state civpainsound
	ifdead
	{
		sound LADY_SCREAM
		sound SQUISHED
		state lite_jibs
		killit
	}
  }

enda

useractor enemy FEMCIV FEMCIVSTRENGTH state femcivcode enda

useractor enemy BIKINIBABE FEMCIVSTRENGTH state femcivcode enda

useractor enemy BIKINIBABECROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy BIKINIBABE2 FEMCIVSTRENGTH state femcivcode enda

useractor enemy BIKINIBABECROUCH2 FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIVCROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV2 FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV2CROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy NAKEDBABE FEMCIVSTRENGTH state femcivcode enda

useractor enemy NAKEDBABECROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV3 FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV4 FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV5 FEMCIVSTRENGTH state femcivcode enda
useractor enemy FEMCIV5CROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy FEMCIV3CROUCH FEMCIVSTRENGTH state femcivcode enda

useractor enemy ALIENCIVFEM FEMCIVSTRENGTH state femcivcode enda

useractor enemy ALIENCIVFEMCROUCH FEMCIVSTRENGTH state femcivcode enda

// ******************** HOOKER *************************

action AHOOKERSTAND		0  2  5  1  40
action AHOOKERSMOKE		30 2  5  1  90
action AHOOKERWALK		10  4  5  1  18
action AHOOKERFROZEN	10  1  5  1  10
action AHOOKERRUN		45 4  5  1  8
action AHOOKERGROW		45 1  5  1  10
action AHOOKERFLASH		65 2  1  1  40
action AHOOKERCROUCH	0 1  5  1  10
action AHOOKERDIE		67 3  1  1  30
action AHOOKERDEAD		69 1  1  1  10
action AHOOKERSHOWASS1	1974  2  1  1  20
action AHOOKERSHOWASS2  1976  1  1  1  100
action AHOOKERSHOWASS3  1976  2  1  -1  20

ai AIHOOKERDYING AHOOKERDIE FEMSTOPPED geth
ai AIHOOKERWAIT AHOOKERSTAND FEMSTOPPED geth

ai AIHOOKERWALK AHOOKERWALK FEMWALKVELS randomangle
ai AIHOOKERRUN AHOOKERRUN FEMRUNVELS randomangle
ai AIHOOKERCROUCH AHOOKERCROUCH FEMSTOPPED geth
ai AIHOOKERSHRUNK AHOOKERRUN FEMSHRUNKVELS fleeenemy

state hookerdyingstate

ifaction AHOOKERDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 { action AHOOKERDEAD cstat 0 strength 0 }
}
else
ifaction AHOOKERDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}

ends

state hookershrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIHOOKERWAIT
  else
    ifcount SHRUNKCOUNT
      sizeto 28 26
  else
    state genericshrunkcode

ends

state hookerwaitstate

	
	ifaction AHOOKERFLASH
	{
		ifactioncount 2 action AHOOKERSTAND
		break
	}

	ifaction AHOOKERFLASH nullop else
	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	ifcount 30 // 90
	{
		state simpletip // defined in NOTENEMY.CON
		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR		
		action AHOOKERFLASH
		break
	}
	
	ifaction AHOOKERSTAND
	{
		ifactioncount 10
		action AHOOKERSMOKE
	}
	ifaction AHOOKERSMOKE
	{
		ifactioncount 4
		action AHOOKERSTAND
	}
	
	ifaction AHOOKERSTAND
	ifcount 20
	ifangdiffl 192 ife seeplayer YES
	ifpdistl 6144
	ifactor HOOKER
	{
		action AHOOKERSHOWASS1
		break
	}
	ifaction AHOOKERSHOWASS1
	{
		ifactioncount 2
		action AHOOKERSHOWASS2
		break
	}
	ifaction AHOOKERSHOWASS2
	{
		ifactioncount 1
		action AHOOKERSHOWASS3
		break
	}
	ifaction AHOOKERSHOWASS3
	{
		ifactioncount 2
		{
			action AHOOKERSTAND
			resetcount
		}
	}
	
	ifactornotstayput
	ifcount 90
	ifrnd 2 ai AIHOOKERWALK
	

ends

state hookerwalkstate

	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	{	
		state simpletip // defined in NOTENEMY.CON
		getangletotarget FEMVAR
		seta[].ang FEMVAR	
		ai AIHOOKERWAIT
		action AHOOKERFLASH
		break
	}

	ifcount 60
		ifrnd 6
	ai AIHOOKERWAIT
	
	ifnotmoving ai AIHOOKERWAIT

ends

state hookerrunstate

	ifrnd 2
	state startscream
		
	
	ifnotmoving
	{
		ifvarand monstflags 8192 nullop else
		ifrnd 128
		{
			ai AIHOOKERCROUCH
			ifactor HOOKER
			cactor HOOKERCROUCH
			ifactor HOOKER2
			cactor HOOKERCROUCH2
			state stopscream
			// state startcrouchsound
			break
		}
		move FEMRUNVELS randomangle
		
	}
	ifcount 300 ifrnd 8 ai AIHOOKERWALK
ends

state hookercrouchstate

	// ifcount 90 nullop else
	// ifrnd 4
		// state startcrouchsound
	
	ifcount 300 ifrnd 8
	{
		ifactor HOOKERCROUCH
		cactor HOOKER
		ifactor HOOKERCROUCH2
		cactor HOOKER2
		ai AIHOOKERWAIT
	}

ends

defstate hookercode

	fall
	ifai 0
	{
		clipdist 48
		ai AIHOOKERWAIT 
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		cstat 257
		sizeat 28 26
		// set monstatus 1
		ife VOLUME 4 ifge LEVEL 10 set team 3 else
		set team 3 // 1
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
		ifvarand monstflags 8192
		{
			set team 1
			set monstatus 1
			addstrength 30 seta[].htextra 2 seta[].htpicnum SHOTSPARK1
		}
		geta[].x startx
		geta[].y starty
		set initflags 500
		rand temp 3
		ifn temp 0 mul temp 10
		add initflags temp
	}
	ifaction AHOOKERFROZEN
	{
	  ifcount THAWTIME
		{
		  ai AIHOOKERWAIT
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			strength 0
			break
		  }
			ifn initsprite 0
			{
				seta[].hitag initsprite
				respawnhitag
			}
		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  money 10
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2

		  killit
		}
		// ifp pfacing
		  // ifpdistl FROZENQUICKKICKDIST
			// pkick
		break
	}
	ifaction AHOOKERGROW
	{
		state genericgrowcode
		break
	}

	ifai AIHOOKERDYING { state hookerdyingstate break }
	
	state monsterai ife monstatus -1 break
	
	ifai AIHOOKERSHRUNK { state hookershrunkstate break }

	ifai AIHOOKERWAIT state hookerwaitstate else
	ifai AIHOOKERWALK state hookerwalkstate else
	ifai AIHOOKERRUN state hookerrunstate else
	ifai AIHOOKERCROUCH state hookercrouchstate
	
	state femfallcheck
	state checkfemsquished

ifhitweapon
  {
    ifvarand monstflags 8192 nullop else
    set team 3
	ifactor HOOKERCROUCH
    cactor HOOKER
	ifactor HOOKERCROUCH2
	cactor HOOKER2
    ifdead
    {
      ifwasweapon GROWSPARK
      {
	    set monstatus 2
        cstat 0
        move FEMSTOPPED
        sound ACTOR_GROWING
        action AHOOKERGROW
		ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifai AIHOOKERSHRUNK
          break

            action AHOOKERFROZEN
			move FEMSTOPPED
			spritepal 1
			strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
		ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  money 10
	  ifwasweapon RPG
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
	  ai AIHOOKERDYING
	  set burning 0
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	   state civdeathsound
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		state civpainsound
		// set shrunken 1
        ai AIHOOKERSHRUNK
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  state random_wall_jibs
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
	  ifn sprite[].htactorstayput -1
		{
			state startscream
			ifactor HOOKER
			cactor HOOKERCROUCH
			ifactor HOOKER2
			cactor HOOKERCROUCH2
			ai AIHOOKERCROUCH
			break
		}
	  ifinwater nullop else
	  ifai AIHOOKERRUN state civpainsound else 
	  { 
		ai AIHOOKERRUN state startscream 
		ifg player[].actors_killed FEMKILLCOUNT
		getp[].actors_killed FEMKILLCOUNT
	  }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
    }
  }
  else ifactor HOOKER
  {
	ifai AIHOOKERRUN nullop else 
	ifactornotstayput
	{
		set FEMVAR NO
		ife seeplayer YES
		ifpdistl 6144 ifp pfacing ifn player[].kickback_pic 0
			ifn player[].curr_weapon KNEE_WEAPON
		set FEMVAR YES
		else ifbulletnear set FEMVAR YES
		ife FEMVAR YES
		{
			ai AIHOOKERRUN
			state startscream
			ifg player[].actors_killed FEMKILLCOUNT
			getp[].actors_killed FEMKILLCOUNT
		}
	}
  }
 ends
 
useractor enemy HOOKER FEMCIVSTRENGTH state hookercode enda
useractor enemy HOOKERCROUCH FEMCIVSTRENGTH state hookercode enda

useractor enemy HOOKER2 FEMCIVSTRENGTH state hookercode enda
useractor enemy HOOKERCROUCH2 FEMCIVSTRENGTH state hookercode enda

useractor enemy HOOKERSTAYPUT FEMCIVSTRENGTH
	geta[].sectnum temp
	seta[].htactorstayput temp
	cactor HOOKER 
enda

useractor enemy HOOKERSTAYPUT2 FEMCIVSTRENGTH
	geta[].sectnum temp
	seta[].htactorstayput temp
	cactor HOOKER2 
enda


// ******************** STRIPPER ***********************

action ASTRIPPERSTAND		0  1  5  1  10
action ASTRIPPERWALK		5  4  5  1  18
action ASTRIPPERFROZEN	5  1  5  1  10
action ASTRIPPERRUN		25 4  5  1  8
action ASTRIPPERGROW		30 1  5  1  10
// action ASTRIPPERCROUCH	45 1  5  1  10
action ASTRIPPERCROUCH	0  1  5  1  10
action ASTRIPPERDIE		130 3  1  1  30
action ASTRIPPERDEAD	132 1  1  1  10
action ATITTYSHOW1		133  1  1  1  24
action ATITTYLOOP		136  2  1  1  16
action ADANCE1			50  5  5  1  12
action ADANCE1B			70  5  5  -1  12
action ADANCE2			75  6  5  1  14
action ADANCE3			105 5  5  1  18
action ADANCE3B			125 5  5  -1 18

action APIGDANCE1A		45  3  5  1  12
action APIGDANCE1B		55  3  5  -1  12

action APIGDANCE2		60  1  1  1  10
action APIGDANCE3		63  1  1  1  10

action APIGSTRIPDIE		67	5  1  1  18
action APIGSTRIPDEAD	71  1  1  1  10

action AKDANCE1			50  4  5  1  6
action AKDANCE1B		65  4  5  -1  6
action AKDANCE2			70  5  5  1  8
action AKDANCE2B		90  5  5  -1  8
action AKBABEDIE		95  3  1  1  30	
action AKBABEDEAD		97  1  1  1  10
action AKSHOW			1595  8  1  1  6

action ACAVEDANCE		53  6  5  1  8
action ACAVEWORSHIPF	83  4  5  1  12
action ACAVEWORSHIPB	98  4  5  -1  12
action ACAVEGIRLDIE		50  3  1  1  30
action ACAVEGIRLDEAD	52  1  1  1  10

ai AISTRIPPERDYING ASTRIPPERDIE FEMSTOPPED geth
ai AISTRIPPERWAIT ASTRIPPERSTAND FEMSTOPPED geth
ai AISTRIPPERDANCE ADANCE1 FEMSTOPPED geth
ai AISTRIPPERWALK ASTRIPPERWALK FEMWALKVELS randomangle
ai AISTRIPPERRUN ASTRIPPERRUN FEMRUNVELS randomangle
ai AISTRIPPERCROUCH ASTRIPPERCROUCH FEMSTOPPED geth
ai AISTRIPPERSHRUNK ASTRIPPERRUN FEMSHRUNKVELS fleeenemy

state stripperdyingstate

ifaction AKBABEDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 { action AKBABEDEAD cstat 0 strength 0 }
	break
}
else
ifaction AKBABEDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
	break
}

ifaction APIGSTRIPDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 4 { action APIGSTRIPDEAD cstat 0 strength 0 }
	break
}
else
ifaction APIGSTRIPDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
	break
}

ifaction ACAVEGIRLDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 { action ACAVEGIRLDEAD cstat 0 strength 0 }
	break
}
else
ifaction ACAVEGIRLDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
	break
}

ifaction ASTRIPPERDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 { action ASTRIPPERDEAD cstat 0 strength 0 }
}
else
ifaction ASTRIPPERDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}

ends

state strippershrunkstate

  ifcount SHRUNKDONECOUNT
    ai AISTRIPPERWAIT
  else
    ifcount SHRUNKCOUNT
      sizeto 28 26
  else
    state genericshrunkcode

ends


state stripperwaitstate

	ifaction ACAVEWORSHIPF
	{
		ifactioncount 4
		action ACAVEWORSHIPB
		break
	}
	ifaction ACAVEWORSHIPB
	{
		ifactioncount 4
		{
			add countvarc 1
			ifg countvarc 3
			{
				set countvarc 0
				ai AISTRIPPERWALK
				break
			}
			else
			action ACAVEWORSHIPF
		}
		break
	}
	ifaction ATITTYSHOW1
	{
		ifactioncount 1
			action  ATITTYLOOP
		break
	}
	ifaction ATITTYLOOP
	{
		ifactioncount 8
			action ASTRIPPERSTAND
		break
	}
	ifaction AKSHOW
	{
		ifactioncount 24
			action ASTRIPPERSTAND
		break
	}
	ifp pfacing
	ifpdistl 1560
	ifhitspace
	{

		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		
		state simpletip // defined in NOTENEMY.CON
		  
		ifactor CAVEGIRL
			action ACAVEWORSHIPF
		else
		ifactor STRIPPER
			action ATITTYSHOW1
		else ifactor KBABE
		{
			action AKSHOW
			resetcount
			// ai AISTRIPPERDANCE
			// ifrnd 128 action AKDANCE1
			// else action AKDANCE2
		}
		else ifactor PIGSTRIPPER
		{
			soundonce PIGSTRIPTALK
			ai AISTRIPPERDANCE
			ifrnd 84 action APIGDANCE1A else
			ifrnd 128 action APIGDANCE2 else
			action APIGDANCE3
		}
		break
	}
	
	ifcount 20
	ifrnd 4 ai AISTRIPPERWALK
	
ends

state stripperwalkstate

	ifp pfacing
	ifpdistl 1560
	ifhitspace
	{
		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		
		state simpletip // defined in NOTENEMY.CON
		  
		ifactor CAVEGIRL
		{
			ai AISTRIPPERWAIT
			action ACAVEWORSHIPF
		} else
		ifactor STRIPPER
		{
			ai AISTRIPPERWAIT
			action ATITTYSHOW1
		} else
		ifactor KBABE
		{
			ai AISTRIPPERWAIT
			action AKSHOW
		} else
		ifactor PIGSTRIPPER
		{
			soundonce PIGSTRIPTALK
			ai AISTRIPPERDANCE
			ifrnd 84 action APIGDANCE1A else
			ifrnd 128 action APIGDANCE2 else
			action APIGDANCE3
		}
		break
	}

	ifcount 60
	{
		ife seeplayer YES ifp pfacing
		ifpdistl 8192
		ifrnd 12
		{
			ai AISTRIPPERDANCE
			ifactor CAVEGIRL
			{
				action ACAVEDANCE
			}
			else
			ifactor KBABE
			{
				ifrnd 128 action AKDANCE1
				else action AKDANCE2
			}
			else
			ifactor PIGSTRIPPER
			{
				action APIGDANCE1A
			}
			else
			ifrnd 170
			{
				ifrnd 128 action ADANCE2
				else action ADANCE3
			}
			break
		}
		ifrnd 6
		ai AISTRIPPERWAIT
	}
	
	ifnotmoving 
	{
		ifactornotstayput
			ai AISTRIPPERWAIT
		else 
		{
			ifrnd 128 ai AISTRIPPERWAIT
			else
			state facestartpos
		}
	}
	ifactornotstayput nullop else
	{
		ife sprite[].sectnum sprite[].htactorstayput
		{
			ifawayfromwall
			{
				geta[].x initflags
				geta[].y mtype
			}
			else
			{
				ifrnd 128
				{
					state facestartpos
					break
				}

				seta[].x initflags
				seta[].y mtype
				ai AISTRIPPERDANCE
				ifactor CAVEGIRL
				{
					action ACAVEDANCE
				}
				else
				ifactor KBABE
				{
					ifrnd 128 action AKDANCE1
					else action AKDANCE2
				}
				else
				ifactor PIGSTRIPPER
				{
					ifrnd 84 action APIGDANCE1A else
					ifrnd 128 action APIGDANCE2 else
					action APIGDANCE3
				}
				else
				ifrnd 170
				{
					ifrnd 128 action ADANCE2
					else action ADANCE3
				}
			}
		}
		else
		{
			changespritesect THISACTOR sprite[].htactorstayput
			seta[].x initflags
			seta[].y mtype
		}
	}

ends

state stripperdancestate

	ifaction ADANCE1
	{
		ifactioncount 5
		{
			resetactioncount
			action ADANCE1B
		}
	}
	ifaction ADANCE1B
	{
		ifactioncount 5
		{
			resetactioncount
			ifrnd 32 action ADANCE2
			else
			action ADANCE1
		}
	}
	ifaction ADANCE2
	{
		ifactioncount 6
		{
			resetactioncount
			ifrnd 32 action ADANCE3
		}
	}
	ifaction ADANCE3
	{
		ifactioncount 5
		{
			resetactioncount
			action ADANCE3B
		}
	}
	ifaction ADANCE3B
	{
		ifactioncount 5
		{
			resetactioncount
			ifrnd 32 action ADANCE1
			else
			action ADANCE3
		}
	}
	ifaction AKDANCE1
	{
		ifactioncount 4
		{
			resetactioncount
			action AKDANCE1B
		}
	}
	ifaction APIGDANCE1A
	{
		ifactioncount 3
		{
			resetactioncount
			action APIGDANCE1B
		}
	}
	ifaction APIGDANCE1B
	{
		ifactioncount 3
		{
			ifcount 150 ifrnd 64
				ai AISTRIPPERWALK
			else
			{
				resetactioncount
				action APIGDANCE1A
			}
		}
	}
	ifaction APIGDANCE2
	ifcount 150 ifrnd 8
	{
		ai AISTRIPPERWALK
	}
	ifaction APIGDANCE3
	ifcount 150 ifrnd 8
	{
		ai AISTRIPPERWALK
	}
	ifaction AKDANCE1B
	{
		ifactioncount 4
		{
			resetactioncount
			ifrnd 32 action AKDANCE2
			else
			ifrnd 16 
			{ 
			    ifactornotstayput
					ai AISTRIPPERWALK 
				else
				ife seeplayer YES ifpdistl 4096 
				{
					ai AISTRIPPERWALK
					state facestartpos
				}
				 
			}
			else action AKDANCE1
		}
	}
	ifaction AKDANCE2
	{
		ifactioncount 5
		{
			resetactioncount
			action AKDANCE2B
		}
	}
	ifaction AKDANCE2B
	{
		ifactioncount 5
		{
			resetactioncount
			ifrnd 32 action AKDANCE1
			else
			ifrnd 16 { ai AISTRIPPERWALK break }
			else action AKDANCE2
		}
	}
	ifaction ACAVEDANCE
	ifactornotstayput
	ifcount 300 ifrnd 8
	{
		ai AISTRIPPERWALK
	}
	
	ifp pfacing
	ifpdistl 1560
	ifhitspace
	{
		ifactor PIGSTRIPPER
		ifcount 30
		{
			ifaction APIGDANCE1A action APIGDANCE2 else
			ifaction APIGDANCE1B action APIGDANCE2 else
			ifaction APIGDANCE2 action APIGDANCE3 else
			ifaction APIGDANCE3 action APIGDANCE1A
		}
		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		
		state simpletip // defined in NOTENEMY.CON
		  
		ifactor STRIPPER
		{
			ai AISTRIPPERWAIT
			action ATITTYSHOW1
		}
		ifactor CAVEGIRL
		{
			ai AISTRIPPERWAIT
			action ACAVEWORSHIPF
		}
		break
	}

ends

state stripperrunstate

	ifrnd 4
	{
		ifactor PIGSTRIPPER { ifrnd 128 soundonce PIGSTRIPRUN1 else soundonce PIGSTRIPRUN2 } 
		// else
		// state startscream
	}
		
	ifnotmoving
	{
		ifvarand monstflags 8192 nullop else
		ifrnd 128
		{
			ifactor PIGSTRIPPER { ai AISTRIPPERWAIT break }
			ai AISTRIPPERCROUCH
			ifactor STRIPPER cactor STRIPPERCROUCH
			ifactor CAVEGIRL cactor CAVEGIRLCROUCH
			ifactor KBABE cactor KBABECROUCH
			state stopscream
			break
		}
		move FEMRUNVELS randomangle
		
	}
	ifcount 300 ifrnd 8 ai AISTRIPPERWALK
ends

state strippercrouchstate

	ifcount 240 ifrnd 8
	{
		ifactor STRIPPERCROUCH cactor STRIPPER
		ifactor KBABECROUCH cactor KBABE
		ifactor CAVEGIRLCROUCH cactor CAVEGIRL
		ai AISTRIPPERWAIT
	}

ends

defstate strippercode

	fall
	ifai 0
	{
		ifactornotstayput
		{
			ifactor PIGSTRIPPER
			{
				ai AISTRIPPERDANCE
				ifrnd 84 action APIGDANCE1A
				else ifrnd 128 action APIGDANCE2 else
				action APIGDANCE3
			}
			else
			ai AISTRIPPERWAIT 
			
			ifspawnedby LIZGROPE
			{
				state startscream
				ai AIFEMRUN
			}
		}
		else 
		{
			ai AISTRIPPERDANCE
			ifactor CAVEGIRL
			{
				action ACAVEDANCE
			}
			else
			ifactor KBABE
			{
				ifrnd 128 action AKDANCE1
				else action AKDANCE2
			}
			else
			ifactor PIGSTRIPPER
			{
				ifrnd 84 action APIGDANCE1A
				else ifrnd 128 action APIGDANCE2 else
				action APIGDANCE3
			}
			else
			{
				ifrnd 170
				{
					ifrnd 128 action ADANCE2
					else action ADANCE3
				}
			}
		}
		clipdist 48
		cstat 257
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		sizeat 28 26
		// set monstatus 1 
		ife VOLUME 4 ifge LEVEL 10 set team 3 else
		set team 3 // 1
		ifactor PIGSTRIPPER set team 3
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
		ifvarand monstflags 8192 { set team 1 set monstatus 1 addstrength 30 seta[].htextra 2 seta[].htpicnum SHOTSPARK1 }
		geta[].x startx
		geta[].y starty
		set initflags 500
		rand temp 3
		ifn temp 0 mul temp 10
		add initflags temp
	}
	ifaction ASTRIPPERFROZEN
	{
	  ifcount THAWTIME
		{
		  ai AISTRIPPERWAIT
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
		    resetcount resetactioncount
			strength 0
			break
		  }
			ifn initsprite 0
			{
				seta[].hitag initsprite
				respawnhitag
			}
		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  ifn sprite[].picnum CAVEGIRL
		  money 5
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2
		  
		  killit
		}
		// ifp pfacing
		  // ifpdistl FROZENQUICKKICKDIST
			// pkick
		break
	}
	ifaction ASTRIPPERGROW
	{
		state genericgrowcode
		break
	}

	ifai AISTRIPPERDYING { state stripperdyingstate break }
	
	state monsterai ife monstatus -1 break
	
	ifai AISTRIPPERSHRUNK { state strippershrunkstate break }

	ifai AISTRIPPERWAIT state stripperwaitstate else
	ifai AISTRIPPERWALK state stripperwalkstate else
	ifai AISTRIPPERRUN state stripperrunstate else
	ifai AISTRIPPERDANCE state stripperdancestate else
	ifai AISTRIPPERCROUCH state strippercrouchstate
	
	state femfallcheck
	state checkfemsquished

ifhitweapon
  {
    ifvarand monstflags 8192 nullop else
    set team 3
	ifactor STRIPPERCROUCH cactor STRIPPER
	ifactor KBABECROUCH cactor KBABE
	ifactor CAVEGIRLCROUCH cactor CAVEGIRL
    ifdead
    {
      ifwasweapon GROWSPARK
      {
	    ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
		set monstatus 2
        cstat 0
		ifn sprite[].picnum CAVEGIRL
		money 5
        move FEMSTOPPED
        sound ACTOR_GROWING
        action ASTRIPPERGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifai AISTRIPPERSHRUNK
          break

            action ASTRIPPERFROZEN
			move FEMSTOPPED
			spritepal 1
			strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
      set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  ifn sprite[].picnum CAVEGIRL
	  money 5
	  ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
	  ifwasweapon RPG
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
	  ai AISTRIPPERDYING
	  set burning 0
	  ifactor PIGSTRIPPER action APIGSTRIPDIE
	  ifactor KBABE action AKBABEDIE
	  ifactor CAVEGIRL action ACAVEGIRLDIE
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	  ifactor PIGSTRIPPER soundonce PIGSTRIPHURT else
	   state civdeathsound
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		ifactor PIGSTRIPPER nullop else
		state civpainsound
		// set shrunken 1
        ai AISTRIPPERSHRUNK
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  state random_wall_jibs
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
	  ifinwater nullop else
	  ifai AISTRIPPERRUN 
	  {
	    ifactor PIGSTRIPPER soundonce PIGSTRIPHURT else
		state civpainsound
	  }
	  else 
	  {
	    ifactor PIGSTRIPPER { ifrnd 128 soundonce PIGSTRIPRUN1 else soundonce PIGSTRIPRUN2 } else
	    state startscream
		ifg player[].actors_killed FEMKILLCOUNT
		getp[].actors_killed FEMKILLCOUNT
		ifactornotstayput
			ai AISTRIPPERRUN
		else
		ifn sprite[].picnum PIGSTRIPPER
		{
			ifactor STRIPPER cactor STRIPPERCROUCH
			ifactor KBABE cactor KBABECROUCH
			ifactor CAVEGIRL cactor CAVEGIRLCROUCH
			ai AISTRIPPERCROUCH
		}
	  }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
    }
  }
  else ifn sprite[].picnum STRIPPERCROUCH ifn sprite[].picnum KBABECROUCH
  ifn sprite[].picnum CAVEGIRLCROUCH
  ifn sprite[].picnum PIGSTRIPPER
  {
	ifai AISTRIPPERRUN nullop else 
	{
		set FEMVAR NO
		ife seeplayer YES
		ifp pfacing
		ifpdistl 6144 ifn player[].kickback_pic 0 
		ifn player[].curr_weapon KNEE_WEAPON set FEMVAR YES
		else ifbulletnear set FEMVAR YES
		ife FEMVAR YES
		{
			state startscream
			ifg player[].actors_killed FEMKILLCOUNT
			getp[].actors_killed FEMKILLCOUNT
			ifactornotstayput
				ai AISTRIPPERRUN
			else
			{
				ifactor STRIPPER cactor STRIPPERCROUCH
				ifactor KBABE cactor KBABECROUCH
				ifactor CAVEGIRL cactor CAVEGIRLCROUCH
				ai AISTRIPPERCROUCH
			}
		}
	}
  }
 ends
 

useractor enemy STRIPPER STRIPPERSTRENGTH state strippercode enda
useractor enemy STRIPPERSTAYPUT STRIPPERSTRENGTH
	geta[].sectnum temp
	seta[].htactorstayput temp
	cactor STRIPPER 
enda

useractor enemy STRIPPERCROUCH STRIPPERSTRENGTH state strippercode enda

useractor enemy KBABE STRIPPERSTRENGTH state strippercode enda
useractor enemy KBABESTAYPUT STRIPPERSTRENGTH
	geta[].sectnum temp
	seta[].htactorstayput temp
	cactor KBABE 
enda


useractor enemy CAVEGIRLCROUCH STRIPPERSTRENGTH state strippercode enda

useractor enemy CAVEGIRL STRIPPERSTRENGTH state strippercode enda
useractor enemy CAVEGIRLSTAYPUT STRIPPERSTRENGTH
	geta[].sectnum temp
	seta[].htactorstayput temp
	cactor CAVEGIRL 
enda

useractor enemy KBABECROUCH STRIPPERSTRENGTH state strippercode enda

useractor enemy PIGSTRIPPER PIGCOPSTRENGTH state strippercode enda

action ACHEER1  50  7  5  1  16
action ACHEER1PAUSE  80  1  5  1  24
action ACHEER2FORW  115  6  5  1  7
action ACHEER2BACK  140  6  5  -1  7
action ACHEER3FORW  91  4  5  1  9
action ACHEER3BACK  106  4  5  -1  9
action ACHEERDIE  111  3  1  1  30
action ACHEERDEAD 113  1  1  1  10

move CHEERJUMPVELS 0 -30


ai AICHEERWAIT AFEMCIVSTAND FEMSTOPPED geth
ai AICHEERRUN AFEMCIVRUN FEMRUNVELS randomangle
ai AICHEERWALK AFEMCIVWALK FEMWALKVELS geth
ai AICHEERDYING ACHEERDIE FEMSTOPPED geth
ai AICHEERSHRUNK AFEMCIVRUN FEMSHRUNKVELS fleeenemy
ai AICHEERCROUCH AFEMCIVCROUCH FEMSTOPPED geth
ai AICHEERPERFORM ACHEER2FORW FEMSTOPPED geth

state cheerperformstate

	ifaction ACHEER2FORW
	{
		ifactioncount 6 { action ACHEER2BACK add FEMKILLCOUNT 1 }
	}
	ifaction ACHEER2BACK
	{
		ifactioncount 6
		{
			ifge FEMKILLCOUNT 4 { action ACHEER3FORW set FEMKILLCOUNT 0 } else
			action ACHEER2FORW
		}
	}
	ifaction ACHEER3FORW
	{
		ifactioncount 4 { action ACHEER3BACK add FEMKILLCOUNT 1 }
	}
	ifaction ACHEER3BACK
	{
		ifactioncount 4
		{
			seta[].mdflags 16
			ife FEMKILLCOUNT 1 cstat 261 else ife FEMKILLCOUNT 3 cstat 261
			else cstat 257
			ifge FEMKILLCOUNT 4 { action ACHEER1 set FEMKILLCOUNT 0 } 
			else
			action ACHEER3FORW
		}
	}
	ifaction ACHEER1
	{
		ifmove FEMSTOPPED
			ifactioncount 3 
		move CHEERJUMPVELS geth getv
		ifmove CHEERJUMPVELS
			ifactioncount 5
		move FEMSTOPPED geth
		ifactioncount 7 { resetactioncount add FEMKILLCOUNT 1 }
		ifge FEMKILLCOUNT 2 action ACHEER1PAUSE
	}
	ifaction ACHEER1PAUSE
	{
		ifactioncount 1 { action ACHEER2FORW set FEMKILLCOUNT 0 }
	}

ends

state cheerwaitstate

	ife countvarb -1
	{
		ifcount 30 { ai AICHEERWALK move FEMWALKVELS randomangle }
		break
	}
	ife countvar 0
	{
		ife seeplayer YES 
		{
			set countvar 1
			set spriteid 0
			whilevarvarn spriteid Numsprites
			{
				ife sprite[spriteid].picnum CHEERLEADER
				ifn sprite[spriteid].statnum 1024
				{
					ifg actorvar[spriteid].countvar countvar getav[spriteid].countvar countvar else
					ife sprite[spriteid].statnum 0 changespritestat spriteid 1
				}
				add spriteid 1
			}
		}
		else break
	}
	add countvar 1
	ifge countvar countvarb { ai AICHEERPERFORM set countvar 0 }

ends

state cheerdyingstate

	ifaction ACHEERDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 2 { action ACHEERDEAD cstat 0 strength 0 }
	}
	else
	ifaction ACHEERDEAD
	{
		ifhitweapon
		{
			sound SQUISHED
			state standard_jibs
			killit
		}
	}

ends

state cheerwalkstate

ifcount 10
	ifnotmoving ai AICHEERWAIT
	
ifcount 240 ai AICHEERWAIT

ends

state cheerrunstate

	ifrnd 2
	state startscream
		
	
	ifnotmoving
	{
		ifvarand monstflags 8192 nullop else
		ifrnd 128
		{
			ai AICHEERCROUCH
			cactor CHEERLEADERCROUCH
			state stopscream
			// state startcrouchsound
			break
		}
		move FEMRUNVELS randomangle
		
	}
	ifcount 300 ifrnd 8 ai AICHEERWALK

ends

state cheercrouchstate

	ifcount 300 ifrnd 8
	{
		cactor CHEERLEADER
		ai AICHEERWAIT
	}

ends

state cheercode

	fall
	ifai 0
	{
		clipdist 48
		ai AICHEERWAIT
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
		cstat 257
		sizeat 28 26
		// set monstatus 1 
		ife VOLUME 4 ifge LEVEL 10 set team 3 else
		set team 3 // 1
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
		ifvarand monstflags 8192 { set team 1 set monstatus 1 addstrength 30 seta[].htextra 2 seta[].htpicnum SHOTSPARK1 }
		geta[].x startx
		geta[].y starty
		set countvarb 150
		set initflags 500
		rand temp 3
		ifn temp 0 mul temp 10
		add initflags temp
	}
	
	// ifcount 10
	// ifhitspace
	// {
		// resetcount
		// ifaction AFEMCIVSTAND action AFEMCIVWALK else
		// ifaction AFEMCIVWALK action AFEMCIVRUN else
		// ifaction AFEMCIVRUN action ACHEER1 else
		// ifaction ACHEER1 action ACHEER2FORW else
		// ifaction ACHEER2FORW action ACHEER3FORW else
		// ifaction ACHEER3FORW action ACHEERDIE else
		// ifaction ACHEERDIE action ACHEERDEAD else
		// ifaction ACHEERDEAD action AFEMCIVSTAND
	// }
	
	ifaction ASTRIPPERFROZEN
	{
	  ifcount THAWTIME
		{
		  ai AICHEERWAIT
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
		    resetcount resetactioncount
			strength 0
			break
		  }
			ifn initsprite 0
			{
				seta[].hitag initsprite
				respawnhitag
			}
		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2
		  
		  killit
		}

		break
	}
	ifaction ASTRIPPERGROW
	{
		state genericgrowcode
		break
	}

	ifai AICHEERDYING { state cheerdyingstate break }
	state monsterai ife monstatus -1 break
	ifai AICHEERSHRUNK 
	{ 
	  ifcount SHRUNKDONECOUNT
		ai AICHEERWAIT
	  else
		ifcount SHRUNKCOUNT
		  sizeto 28 26
	  else
		state genericshrunkcode
		break 
	}

	ifai AICHEERWAIT state cheerwaitstate else
	ifai AICHEERWALK state cheerwalkstate else
	ifai AICHEERRUN state cheerrunstate else
	ifai AICHEERPERFORM state cheerperformstate else
	ifai AICHEERCROUCH state cheercrouchstate
	
	state femfallcheck
	state checkfemsquished

ifhitweapon
  {
    ifvarand monstflags 8192 nullop else
    set team 3
	set countvarb -1 // will not perform again
	cactor CHEERLEADER
	cstat 257
	set FEMKILLCOUNT 0
    ifdead
    {
      ifwasweapon GROWSPARK
      {
	    ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
		set monstatus 2
        cstat 0
		money 5
        move FEMSTOPPED
        sound ACTOR_GROWING
        action ASTRIPPERGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifai AICHEERSHRUNK
          break

            action ASTRIPPERFROZEN
			move FEMSTOPPED
			spritepal 1
			strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
	  ifwasweapon RPG
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
         state standard_jibs
		 state stopscream
		 killit
	  }
	  ai AICHEERDYING
	  set burning 0
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	  state civdeathsound
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
    {
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		state civpainsound
		// set shrunken 1
        ai AICHEERSHRUNK
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  state random_wall_jibs
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
	  ifinwater nullop else
	  ifai AICHEERRUN state civpainsound else 
	  { 
	    state startscream
		ifactornotstayput
			ai AICHEERRUN
		else
		{
			cactor CHEERLEADERCROUCH
			ai AICHEERCROUCH
		}
	  }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
    }
  }

ends

useractor enemy CHEERLEADER STRIPPERSTRENGTH state cheercode enda
useractor enemy CHEERLEADERCROUCH STRIPPERSTRENGTH state cheercode enda


action AMALECIVSTAND	0  1  5  1  10
action AMALECIVWALK		5  4  5  1  18
action AMALECIVWALK1	5  3  5  1  18
action AMALECIVWALK2	10  1  5  1  18
action AMALECIVFROZEN	5  1  5  1  10
action AMALECIVRUN		20 4  5  1  8
action AMALECIVRUN2		25 4  5  1  8
action AMALECIVGROW		25 1  5  1  10
action AFATCOWER		25 1  5  1  30
action AMALECIVSIT		0 1  5  1  10
action AMALECIVCROUCH	5 1  5  1  10
action AMALECIVDIE		50 3  1  1  30
action AMALECIVDEAD		52 1  1  1  10
action AMALECIVDIE2		55 3  1  1  30
action AMALECIVALIENDIE 50 5  1  1  20
action AFATGUYDIE		30 5  1  1  20
action AFATGUYDEAD 		34 1  1  1  10
action AMALECIVALIENDEAD 55 1 1  1  10
action AMALECIVDEAD2	57 1  1  1  10
action AMALECIVSMOKE	53 2  5  1  70
action AMALECIVSMOKE2	58 2  5  1  70
action AMALEALIENTEXT   56 1  5  1  60

ai AIMALEDYING AMALECIVDIE FEMSTOPPED geth
ai AIMALEWAIT AMALECIVSTAND FEMSTOPPED geth
ai AIMALEWALK AMALECIVWALK FEMWALKVELS randomangle
ai AIMALERUN AMALECIVRUN FEMRUNVELS randomangle
ai AIMALECROUCH AMALECIVCROUCH FEMSTOPPED geth
ai AIMALESHRUNK AMALECIVRUN FEMSHRUNKVELS fleeenemy

defstate maledyingstate

ifaction AMALECIVDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 
	{ 
		action AMALECIVDEAD
		cstat 0 strength 0 
	}
}
else ifaction AMALECIVDIE2
{
	ifhitweapon state random_wall_jibs
	ifactioncount 2 
	{ 
		action AMALECIVDEAD2
		cstat 0 strength 0 
	}
}
else
ifaction AMALECIVALIENDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 5
	{ 
		action AMALECIVALIENDEAD
		cstat 0 strength 0 
	}
}
ifaction AFATGUYDIE
{
	ifhitweapon state random_wall_jibs
	ifactioncount 4
	{ 
		action AFATGUYDEAD
		cstat 0 strength 0 
	}
}
else
ifaction AMALECIVDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}
else
ifaction AMALECIVDEAD2
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}
else
ifaction AMALECIVALIENDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}
else
ifaction AFATGUYDEAD
{
	ifhitweapon
	{
		sound SQUISHED
		state standard_jibs
		killit
	}
}

ends

state maleshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIMALEWAIT
  else
    ifcount SHRUNKCOUNT
	{
	  ifactor ALIENCIVMALE sizeto 26 24
	  else
      sizeto 28 26
	}
  else
    state genericshrunkcode

ends

state malecomment

	ifactor ALIENCIVMALE
	{
		ife initflags 0
		{
			ife pchar 2 rand temp 4 else
			rand temp 6
			switch temp
			case 0 sound MUDOKON_FIREAWAY break
			case 1 sound MUDOKON_SOMUCHTOLEARN break
			case 2 sound MUDOKON_UPCLOSE break
			case 3 sound MUDOKON_YOUTALKHOLE break
			case 4 sound MUDOKON_SURRENDER break
			case 5 sound MUDOKON_BETBIGWONSLAVE break
			case 6 sound MUDOKON_SAWINARENA break
			endswitch
		}
		else
		{
			rand temp 5
			switch temp
			case 0 sound MUDOKON_THIRDUNCLE break
			case 1 sound MUDOKON_SURRENDER break
			case 2 sound MUDOKON_RIDLEYSCOTT break
			case 3 sound MUDOKON_MYANCESTORS break
			case 4 sound MUDOKON_LEAVEMEALONE break
			case 5 sound MUDOKON_NOHARM break
			case 6 sound MUDOKON_FIREAWAY break
			endswitch
		}
		break
	}
	rand temp 2
	add temp initflags
	soundoncevar temp
ends

defstate malewaitstate

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
				
	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	ife lastangvel 0
	{
		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR

		ifn SPRITELOTAG 0 { state corruption break }
		set lastangvel 120
		state malecomment
		state giftcheck
		action AMALECIVSTAND
	}
	
	ifcount 120
	ifrnd 4 ai AIMALEWALK
	
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch 0
		
	
	ifn SPRITELOTAG 0
	ifrnd 1 ifrnd 96
	ifpdistl 8192
	ife seeplayer YES
	{ state corruption break }
	

ends

defstate malewalkstate

	// MALECIV1 has a 3 frame walk cycle and requires special kludge code!
	ifactor MALECIV1
	{
	ifaction AMALECIVWALK action AMALECIVWALK1
	ifaction AMALECIVWALK1 ifactioncount 3 action AMALECIVWALK2 else
	ifaction AMALECIVWALK2 ifactioncount 1 action AMALECIVWALK1
	}

	ifp pfacing
	ife seeplayer YES
	ifpdistl 1560
	ifhitspace
	ife lastangvel 0
	{
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
				
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		action AMALECIVSTAND
		
		ifn SPRITELOTAG 0 { state corruption break }
		set lastangvel 120
		state malecomment
		state giftcheck
		ai AIMALEWAIT
		
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
	}

	ifcount 120
		ifrnd 6
	{
		ai AIMALEWAIT
		ifrnd 128 
		{
			ifactor ALIENCIVMALE
			action AMALEALIENTEXT
			else
			ifactor MALECIV1
			action AMALECIVSMOKE
			else
			ifactor MALECIV2
			action AMALECIVSMOKE2
		}
		break
	}
	
	geta[].htmovflag temp
	ifn temp 0
	{
		// bumping something
		add temp 16384
		ifl temp 16384 ifg temp -1 // bumping a sprite
		{
			ife actorvar[temp].monstatus 0 ai AIMALEWAIT
		}
		else
		ai AIMALEWAIT
	}
	
	ifai AIMALEWAIT 
	{
		ifrnd 128 
		{
			ifactor ALIENCIVMALE
			action AMALEALIENTEXT
			else
			ifactor MALECIV1
			action AMALECIVSMOKE
			else
			ifactor MALECIV2
			action AMALECIVSMOKE2
		}
	}
	
	ifn SPRITELOTAG 0
	ifrnd 1 ifrnd 96
	ifpdistl 8192
	ife seeplayer YES
	{ state corruption break }

ends

defstate startmalescream

	ifinwater break
	ifvarand monstflags 8192 break
	ifactor ALIENCIVMALE
	{
		stopactorsound THISACTOR MUDOKON_BETBIGWONSLAVE
		stopactorsound THISACTOR MUDOKON_FIREAWAY
		stopactorsound THISACTOR MUDOKON_LEAVEMEALONE
		stopactorsound THISACTOR MUDOKON_MYANCESTORS
		stopactorsound THISACTOR MUDOKON_NOHARM
		stopactorsound THISACTOR MUDOKON_RIDLEYSCOTT
		stopactorsound THISACTOR MUDOKON_SAWINARENA
		stopactorsound THISACTOR MUDOKON_SOMUCHTOLEARN
		stopactorsound THISACTOR MUDOKON_SURRENDER
		stopactorsound THISACTOR MUDOKON_THIRDUNCLE
		stopactorsound THISACTOR MUDOKON_UPCLOSE
		stopactorsound THISACTOR MUDOKON_YOUTALKHOLE
	
		ifactorsound THISACTOR MUDOKON_GETTOCOVER break
		ifactorsound THISACTOR MUDOKON_DONWANNADIE break
		ifactorsound THISACTOR MUDOKON_IMPERIALGUARD break
		ifactorsound THISACTOR MUDOKON_RUNFORLIVES break
		rand temp 3
		ife temp 0 sound MUDOKON_GETTOCOVER else
		ife temp 1 sound MUDOKON_DONWANNADIE else
		ife temp 2 sound MUDOKON_IMPERIALGUARD else
		ife temp 3 sound MUDOKON_RUNFORLIVES
		break
	}
	
	stopactorsound THISACTOR initflags
	set temp initflags add temp 1
	stopactorsound THISACTOR initflags
	add temp 1
	stopactorsound THISACTOR initflags
	
	set temp initflags
	add temp 3
	ifactorsound THISACTOR temp break
	add temp 1
	ifactorsound THISACTOR temp break
	add temp 1 
	ifactorsound THISACTOR temp break
	
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	rand temp 2
	add temp 3
	add temp initflags
	soundoncevar temp
	
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0

ends

state stopmalescream

	stopactorsound THISACTOR MUDOKON_GETTOCOVER
	stopactorsound THISACTOR MUDOKON_DONWANNADIE
	stopactorsound THISACTOR MUDOKON_IMPERIALGUARD
	stopactorsound THISACTOR MUDOKON_RUNFORLIVES
	
	set temp initflags
	add temp 3
	stopactorsound THISACTOR temp
	add temp 1
	stopactorsound THISACTOR temp
	add temp 1
	stopactorsound THISACTOR temp
	

ends

state malerunstate

	ifactor MALECIV4 ifaction AMALECIVRUN action AMALECIVRUN2
	ifactor MALECIV5 ifaction AMALECIVRUN action AMALECIVRUN2
	ifactor MALECIV6 ifaction AMALECIVRUN action AMALECIVRUN2
	
	// ifrnd 4
	// state startmalescream
		
	ifnotmoving
	{
		ifvarand monstflags 8192 nullop else
		ifrnd 128
		{
			ai AIMALECROUCH
			ifactor MALECIV1 { cactor MALECIV1CROUCH action AMALECIVSIT } else
			ifactor MALECIV2 { cactor MALECIV2CROUCH action AMALECIVSIT } else
			ifactor MALECIV3 { cactor MALECIV3CROUCH action AMALECIVSIT } else
			ifactor MALECIV4 { cactor MALECIV4CROUCH action AMALECIVSIT } else
			ifactor MALECIV5 { cactor MALECIV5CROUCH action AMALECIVSIT } else
			ifactor MALECIV6 { cactor MALECIV6CROUCH action AMALECIVSIT } else
			ifactor ALIENCIVMALE { cactor ALIENCIVMALECROUCH action AMALECIVSIT }
			// state startmalescream
			break
		}
		move FEMRUNVELS randomangle
		
	}
	ifcount 300 ifrnd 8 ai AIMALEWALK
	
	ifn SPRITELOTAG 0
	ifrnd 1 ifrnd 96
	ifpdistl 8192
	ife seeplayer YES
	{ state corruption break }
	
ends

state malecrouchstate

	ifcount 300 ifrnd 8
	{
		ifactor MALECIV1CROUCH cactor MALECIV1 else
		ifactor MALECIV2CROUCH cactor MALECIV2 else
		ifactor MALECIV3CROUCH cactor MALECIV3 else
		ifactor MALECIV4CROUCH cactor MALECIV4 else
		ifactor MALECIV5CROUCH cactor MALECIV5 else
		ifactor MALECIV6CROUCH cactor MALECIV6 else
		ifactor ALIENCIVMALECROUCH cactor ALIENCIVMALE
		ai AIMALEWAIT
		ifrnd 128 
		{
			ifactor ALIENCIVMALE
			action AMALEALIENTEXT
			else
			ifactor MALECIV1
			action AMALECIVSMOKE
			else
			ifactor MALECIV2
			action AMALECIVSMOKE2
		}
	}

ends

defstate malecivcode

ifai 0
{
	ai AIMALEWAIT
	clipdist 32 // 48
	// ifactor MALECIV1 ifn SPRITELOTAG 0 nullop else
	// {
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
	// }
	ifrnd 128 
	{
		ifactor ALIENCIVMALE
		action AMALEALIENTEXT
		else
		ifactor MALECIV1
		action AMALECIVSMOKE
		else
		ifactor MALECIV2
		action AMALECIVSMOKE2
	}
	sizeat 28 26
	// set monstatus 1 
	ifactor ALIENCIVMALE { set team 3 set navmode 3 sizeat 26 24 } else
	set team 3 // 1
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	ifvarand monstflags 8192 { set team 1 addstrength 30 seta[].htextra 2 seta[].htpicnum SHOTSPARK1 }
	geta[].x startx
	geta[].y starty
	cstator 257
	// assign starting sound # as initflags
	switch sprite[].picnum
		case MALECIV1 
		case MALECIV2 
		case MALECIV3 
		case MALECIV4 
		case MALECIV5 
		case MALECIV6 
		case FATGUY1 
		case FATGUY2 
			set initflags 550
			rand temp 3
			ifn temp 0 mul temp 10
			add initflags temp
		break
	endswitch
}
fall

ifaction AMALECIVFROZEN
{
  ifcount THAWTIME
	{
	  ai AIMALEWAIT
	  ifrnd 128 
		{
			ifactor ALIENCIVMALE
			action AMALEALIENTEXT
			else
			ifactor MALECIV1
			action AMALECIVSMOKE
			else
			ifactor MALECIV2
			action AMALECIVSMOKE2
		}
	  getlastpal
	}
	else
	  ifcount FROZENDRIPTIME
	{
	  ifactioncount 26
	  {
		spawn WATERDRIP
		resetactioncount
	  }
	}
	ifhitweapon
	{
	  ifwasweapon FREEZEBLAST
	  {
		strength 0
		break
	  }

	  ifrnd 84
		spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
	  sound GLASS_BREAKING
	   ifrnd 128
		sound DUKE_HIT_STRIPPER1
	  else
		sound DUKE_HIT_STRIPPER2

	  killit
	}
	// ifp pfacing
	  // ifpdistl FROZENQUICKKICKDIST
		// pkick
	break
}
ifaction AMALECIVGROW
{
	state genericgrowcode
	break
}
ifg lastangvel 0 
	{
		sub lastangvel 1
		ife sprite[].xvel 0
		{
			getangletotarget FEMVAR
			seta[].ang FEMVAR
		}
	}
ifai AIMALEDYING { state maledyingstate break }

state monsterai ife monstatus -1 break

	ifai AIMALESHRUNK { state maleshrunkstate break }

	ifai AIMALEWAIT state malewaitstate else
	ifai AIMALEWALK state malewalkstate else
	ifai AIMALERUN state malerunstate else
	ifai AIMALECROUCH state malecrouchstate
	
	state femfallcheck
	state checkfemsquished


ifhitweapon
  {
    ifdead
    {
	  ifactor MALECIV1CROUCH cactor MALECIV1 else
	  ifactor MALECIV2CROUCH cactor MALECIV2 else
	  ifactor MALECIV3CROUCH cactor MALECIV3 else
	  ifactor MALECIV4CROUCH cactor MALECIV4 else
	  ifactor MALECIV5CROUCH cactor MALECIV5 else
	  ifactor MALECIV6CROUCH cactor MALECIV6 else
	  ifactor ALIENCIVMALECROUCH cactor ALIENCIVMALE
	  ifn sprite[].htpicnum RPG
	  ifactor MALECIV1
	  ifn SPRITELOTAG 0 { state corruption break }
	  state stopmalescream
      ifwasweapon GROWSPARK
      {
	    set monstatus 2
        cstat 0
        move FEMSTOPPED
        sound ACTOR_GROWING
        action AMALECIVGROW
        break
      }
      else ifwasweapon FREEZEBLAST
      {
        ifai AIMALESHRUNK
          break

            action AMALECIVFROZEN
			move FEMSTOPPED
			spritepal 1
			strength 0

        sound NEWFREEZE
		state freezeme
        break
      }
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  ifwasweapon RPG
	  { 
		 sound SQUISHED
         state standard_jibs
		 killit
	  }
      ifwasweapon RADIUSEXPLOSION
	  { 
	     sound SQUISHED
         state standard_jibs
		 killit
	  }
	  ai AIMALEDYING
	  ifactor MALECIV2 action AMALECIVDIE2
	  ifactor MALECIV3 action AMALECIVALIENDIE
	  ifactor FATGUY1 action AFATGUYDIE
	  ifactor FATGUY2 action AFATGUYDIE
	  ifactor ALIENCIVMALE action AMALECIVALIENDIE
	  set burning 0
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	  ifactor ALIENCIVMALE
	  {
		ifrnd 84 sound MUDOKON_DIE1 else
		ifrnd 128 sound MUDOKON_DIE2 else
		sound MUDOKON_DIE3
	  }
	  else state civdeathsound

	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
    {
	  ifactor ALIENCIVMALE ife initflags 0 set initflags 1
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
		ifactor MALECIV1CROUCH cactor MALECIV1 else
	    ifactor MALECIV2CROUCH cactor MALECIV2 else
		ifactor MALECIV3CROUCH cactor MALECIV3 else
		ifactor MALECIV4CROUCH cactor MALECIV4 else
		ifactor MALECIV5CROUCH cactor MALECIV5 else
		ifactor MALECIV6CROUCH cactor MALECIV6 else
		ifactor ALIENCIVMALECROUCH cactor ALIENCIVMALE
        ai AIMALESHRUNK
		ifactor FATGUY1 action AMALECIVWALK
		ifactor FATGUY2 action AMALECIVWALK
        break
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  state random_wall_jibs
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setp[].sound_pitch SHRUNKPITCH
	  ifinwater nullop else
	  ifai AIMALERUN 
	  {
	    ifactor ALIENCIVMALE
		{
			rand temp 3
			ife temp 0 sound MUDOKON_PAIN1 else
			ife temp 1 sound MUDOKON_PAIN2 else
			ife temp 2 sound MUDOKON_PAIN3 else
			ife temp 3 sound MUDOKON_PAIN4	
		}
		else state civpainsound
		
	  }
	  else 
	  { 
	    ifactor MALECIV1CROUCH cactor MALECIV1 else
	    ifactor MALECIV2CROUCH cactor MALECIV2 else
		ifactor MALECIV3CROUCH cactor MALECIV3 else
		ifactor MALECIV4CROUCH cactor MALECIV4 else
		ifactor MALECIV5CROUCH cactor MALECIV5 else
		ifactor MALECIV6CROUCH cactor MALECIV6 else
		ifactor ALIENCIVMALECROUCH cactor ALIENCIVMALE
		
		ifactor FATGUY1 { ai AIMALEWAIT action AFATCOWER state civpainsound break }
		ifactor FATGUY2 { ai AIMALEWAIT action AFATCOWER state civpainsound break }
		ai AIMALERUN 
		
		state startmalescream 
		ifg player[].actors_killed FEMKILLCOUNT
		getp[].actors_killed FEMKILLCOUNT
		ifactor MALECIV2 action AMALECIVRUN2
		ifactor MALECIV3 action AMALECIVRUN2
		ifactor MALECIV6 action AMALECIVRUN2
	  }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
    }
  }
  else ifn sprite[].picnum MALECIV1CROUCH
  ifn sprite[].picnum MALECIV2CROUCH
  ifn sprite[].picnum ALIENCIVMALECROUCH
  ifn sprite[].picnum FATGUY1
  ifn sprite[].picnum FATGUY2
  {
	ifai AIMALERUN nullop else 
	{
		set FEMVAR NO
		ife seeplayer YES
		ifp pfacing
		ifpdistl 6144 ifn player[].kickback_pic 0 
		ifn player[].curr_weapon KNEE_WEAPON set FEMVAR YES
		else ifbulletnear set FEMVAR YES
		ife FEMVAR YES
		{
		    ifactor MALECIV1CROUCH cactor MALECIV1 else
		    ifactor MALECIV2CROUCH cactor MALECIV2 else
			ifactor MALECIV3CROUCH cactor MALECIV3 else
			ifactor MALECIV4CROUCH cactor MALECIV4 else
			ifactor MALECIV5CROUCH cactor MALECIV5 else
			ifactor MALECIV6CROUCH cactor MALECIV6 else
			ifactor ALIENCIVMALECROUCH cactor ALIENCIVMALE
			ai AIMALERUN
			state startmalescream
			ifg player[].actors_killed FEMKILLCOUNT
			getp[].actors_killed FEMKILLCOUNT
			ifactor MALECIV2 action AMALECIVRUN2
			ifactor MALECIV3 action AMALECIVRUN2
			ifactor MALECIV6 action AMALECIVRUN2
		}
	}
  }

ends

useractor enemy MALECIV1 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV1CROUCH STRIPPERSTRENGTH state malecivcode enda

useractor enemy MALECIV1POOP STRIPPERSTRENGTH
	sizeat 28 26
	cstator 257
	ifrnd 2 sound FART
	ifcount 90
  {
    sound FLUSH_TOILET
	ifg sprite[].htextra 5 spawn FECES
    operate
    cactor MALECIV1
  }
	ifg sprite[].htextra 0 count 100
	
enda

useractor enemy MALECIV2 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV2CROUCH STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV2POOP STRIPPERSTRENGTH
	sizeat 28 26
	cstator 257
	ifrnd 2 sound FART
	ifcount 90
  {
    sound FLUSH_TOILET
	ifg sprite[].htextra 5 spawn FECES
    operate
    cactor MALECIV2
  }
	ifg sprite[].htextra 0 count 100
	
enda

useractor enemy MALECIV3 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV3CROUCH STRIPPERSTRENGTH state malecivcode enda

useractor enemy MALECIV4 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV4CROUCH STRIPPERSTRENGTH state malecivcode enda

useractor enemy MALECIV5 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV5CROUCH STRIPPERSTRENGTH state malecivcode enda

useractor enemy MALECIV6 STRIPPERSTRENGTH state malecivcode enda
useractor enemy MALECIV6CROUCH STRIPPERSTRENGTH state malecivcode enda

useractor enemy FATGUY1 80 state malecivcode enda
useractor enemy FATGUY2 80 state malecivcode enda


action AGIANTWALK		5  4  5  1  30
ai AIGIANTWALK	AGIANTWALK SHIPMOVE3 randomangle

useractor enemy GIANTGUY 32000

ifai 0
{
	ai AIMALEWAIT
	clipdist 255
	
	// getp[].max_actors_killed temp
	// sub temp 1
	// setp[].max_actors_killed temp
	set team 3
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	geta[].x startx
	geta[].y starty
	cstator 257
	sizeat 224 192
}
ifai AIMALEWALK ai AIGIANTWALK
fall

// state monsterai 

	ifai AIMALEWAIT 
	{ 
		ifcount 120
		ifrnd 4 
			ai AIGIANTWALK
	}
	else
	ifai AIGIANTWALK
	{
		ifactioncount 2
		{
			resetactioncount
			sound THUD
		}
		ifcount 120
		ifrnd 6
		{
			ai AIMALEWAIT
			break
		}
		
		ifnotmoving
			ai AIMALEWAIT
	}
	
seta[].htextra -1
ifhitweapon
{

	ifwasweapon GROWSPARK
	  sound SPARKLESOUND

	ai AIMALEWAIT action AFATCOWER 
}

enda

useractor enemy ALIENCIVMALE STRIPPERSTRENGTH state malecivcode enda
useractor enemy ALIENCIVMALECROUCH STRIPPERSTRENGTH state malecivcode enda

action APOLEDANCE1 0 1 1
action APOLEDANCE2 4 1 1
action APOLEDANCE3 1269 1 1
action APOLECROUCH 10 1 5 1 10
action APOLEFLASH1 15 1 1 1 40
action APOLEFLASH2 15 1 1 1 40
action APOLEFLASHLOOP 16 1 1 1 20
action APOLEDANCEDYING 18 3 1 1 30
action APOLEDANCEDEAD 20 1 1 1 10
action APOLEDANCEFROZEN 15 1 1 1 10
action APOLEDANCEGROW 17 1 1 1 10
action APOLEDANCESHRINK 4 1 1

ai AIPOLEDANCE APOLEDANCE1 FEMSTOPPED geth
ai AIPOLEFLASH APOLEFLASH1 FEMSTOPPED geth

useractor enemy POLEDANCER STRIPPERSTRENGTH
	fall
	ifaction APOLEDANCEDYING
	{
		ifactioncount 3 action APOLEDANCEDEAD
		strength 0
		break
	}
	ifaction APOLEDANCEDEAD
	{
		ifhitweapon
		{
			sound SQUISHED
			 state standard_jibs
			 state stopscream
			 sizeat 40 40 cstat 0
			 money 10
			 cactor BARBROKE
		 }
		 break
	}
	ifaction APOLEDANCEFROZEN
	{
	  ifcount THAWTIME
		{
		  action APOLEDANCE1
		  getlastpal
		}
		else
		  ifcount FROZENDRIPTIME
		{
		  ifactioncount 26
		  {
			spawn WATERDRIP
			resetactioncount
		  }
		}
		ifhitweapon
		{
		  ifwasweapon FREEZEBLAST
		  {
			strength 0
			break
		  }

		  ifrnd 84
			spawn BLOODPOOL
		  lotsofglass 30
		  state lite_jibs
		  sound GLASS_BREAKING
		   ifrnd 128
			sound DUKE_HIT_STRIPPER1
		  else
			sound DUKE_HIT_STRIPPER2
		  geta[].cstat temp
	      xorvar temp 257
	      seta[].cstat temp
		  cactor BARBROKE
		  ifn initsprite 0
			{
				seta[].hitag initsprite
				respawnhitag
			}
		}
		
		break
	}
	ifaction APOLEDANCESHRINK
	{
	  ifcount SHRUNKDONECOUNT
		action APOLEDANCE1
	  else
		ifcount SHRUNKCOUNT
		  sizeto 28 26
	  else
		state genericshrunkcode
	  break
	}
	ifaction APOLEDANCEGROW
	{
		state genericgrowcode
		break
	}
	
	ifaction 0
	{
		// ifmove 0
		// {
			// getp[].max_actors_killed temp
			// sub temp 1
			// setp[].max_actors_killed temp
		// }
		clipdist 48
		move FEMSTOPPED geth
		ifrnd 84
		action APOLEDANCE1 else
		ifrnd 128
		action APOLEDANCE2 else
		action APOLEDANCE3
		
		cstator 257
		sizeat 28 27
		set monstatus 1 
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
		ife VOLUME 4 ifge LEVEL 10 set team 3 else
		set team 3 // 1
		ife startx 0 ife starty 0
		{
			geta[].x startx
			geta[].y starty
		}
		set initflags 500
		rand temp 3
		ifn temp 0 mul temp 10
		add initflags temp
	}
	seta[].x startx
	seta[].y starty
	
    ifaction APOLECROUCH { ifcount 240 ifrnd 8 { action APOLEDANCE1 resetcount } } else
	ifaction APOLEDANCE1 { ifcount 240 ifrnd 8 { action APOLEDANCE2 resetcount } } else
	ifaction APOLEDANCE2 { ifcount 180 ifrnd 8 { action APOLEDANCE3 resetcount } } else
	ifaction APOLEDANCE3 { ifcount 180 ifrnd 8 { action APOLEDANCE1 resetcount } }
	
	ifaction APOLECROUCH nullop else
	ifaction APOLEFLASHLOOP nullop else
	ifaction APOLEFLASH1 nullop else
	ifp pfacing
		ifcansee
			ifpdistl 1560
				ifhitspace
	{
		resetcount
		action APOLEFLASH1
		state simpletip // defined in NOTENEMY.CON
	}
	
	ifaction APOLEFLASH1 ifactioncount 1 action APOLEFLASHLOOP
	ifaction APOLEFLASHLOOP ifactioncount 8 action APOLEFLASH2
	ifaction APOLEFLASH2 ifactioncount 1 action APOLEDANCE1
  
	
  ifhitweapon
  {
    set team 3
    ifaction APOLEDANCESHRINK nullop else
    ifwasweapon SHRINKSPARK
    {
	  sound ACTOR_SHRINKING
	  state civpainsound
	  // set shrunken 1
	  action APOLEDANCESHRINK
	  resetcount
	  break
    }
    state startscream
    ifdead
    {
      ifwasweapon GROWSPARK
      {
	    set monstatus 2
        geta[].cstat temp
	    xorvar temp 257
	    seta[].cstat temp
        sound ACTOR_GROWING
        action APOLEDANCEGROW
		resetcount
        break
      }
       
	  ifwasweapon FREEZEBLAST
      {
        
		resetcount
        action APOLEDANCEFROZEN
		state stopscream
		spritepal 1
		strength 0
        sound NEWFREEZE
		state freezeme
        break
      }
		ifn initsprite 0
		{
			seta[].hitag initsprite
			respawnhitag
		}
	  set monstatus 2
      ifrnd 128
        sound DUKE_HIT_STRIPPER1
      else
        sound DUKE_HIT_STRIPPER2
	  spawn BLOODPOOL
	  state random_wall_jibs
	  ifwasweapon RPG
	  { 
	     sound SQUISHED
         state standard_jibs
		 state stopscream
		 sizeat 40 40 cstat 0
		 money 10
		 cactor BARBROKE
	  }
      ifwasweapon RADIUSEXPLOSION
	  {
	     sound SQUISHED
         state standard_jibs
		 state stopscream
		 sizeat 40 40 cstat 0
		 money 10
		 cactor BARBROKE
	  }
	  action APOLEDANCEDYING
	  set burning 0
	  geta[].cstat temp
	  xorvar temp 257
	  seta[].cstat temp
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
	  state civdeathsound
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0

    }
    else
	{
      action APOLECROUCH
	  resetcount
	}
    
  }

enda

action SISTERANIMF 0 3 8 1 50
action SISTERANIMB 16 3 8 -1 50
useractor notenemy SISTERS TOUGH SISTERANIMF

  cstator 256
  ifaction ANULLACTION action SISTERANIMF
  ifaction SISTERANIMF
  {
	ifactioncount 3 action SISTERANIMB
  }
  else
  ifaction SISTERANIMB
  {
    ifactioncount 3 action SISTERANIMF
  }
  state tipme
  state femcode
enda

useractor notenemy SISTERS2 TOUGH FEMANIMATE
  state tipme
  state femcode
enda

useractor enemy CRYSTAL STRIPPERSTRENGTH 
  state femcivcode
enda

useractor enemy CRYSTALCROUCH STRIPPERSTRENGTH 
  state femcivcode
enda

eventloadactor GRAVES 
sizeat 32 24 
cstator 257 
geta[].lotag SPRITELOTAG
ifg SPRITELOTAG 0 cstat 32768
seta[].lotag 0
enda
useractor notenemy GRAVES 100 AFEMCIVSTAND

	ifg SPRITELOTAG 0 ifl LEVEL SPRITELOTAG break
	ifmove 0
	{
		move STOPPED
		sizeat 32 24
		cstat 257
	}
	fall
	set monstatus 1 set team 1
	state checkfemsquished
	ifhitweapon spawn BLOOD
	
	// ifsound GRAVES01 break
	// ifsound GRAVES02 break
	// ifsound GRAVES03 break
	// ifsound GRAVES04 break
	ifp pfacing
	ifcansee
	ifpdistl 1560
	ifhitspace
	{
		resetcount
		getangletotarget FEMVAR
		seta[].ang FEMVAR
		rand temp 3
		// ife temp 0 sound GRAVES01 else
		// ife temp 1 sound GRAVES02 else
		// ife temp 2 sound GRAVES03 else
		// ife temp 3 sound GRAVES04
		state giftcheck
	}
	

enda

// action AFEM5DANCEF		0  4  5  1  10
// action AFEM5DANCEB		15  4  5  -1  10

spriteshadow FEMCIV5DANCE
useractor notenemy FEMCIV5DANCE STRIPPERSTRENGTH

fall
ifaction 0
{
	action AFEM5DANCEF
	clipdist 32 // 48	
	cstat 257
	sizeat 28 26
	set monstatus 1
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	set team 3
}

ifaction AFEM5DANCEF
{
	ifactioncount 4 action AFEM5DANCEB
}
else
ifaction AFEM5DANCEB
{
	ifactioncount 4 action AFEM5DANCEF
}

state predamage
ifge sprite[].htextra 0
{
	cactor FEMCIV5
	ai AIFEMWAIT
}
enda

state moneyprize

	// espawn BIGSTACK
	// seta[RETURN].pal 10
	add credits 2000
	set lastcredits credits
	set gotcoins 90
	rand temp 3
	ife temp 0 globalsound DUKE_GETWEAPON1 else
	ife temp 1 globalsound DUKE_GETWEAPON3 else
	ife temp 2 globalsound DUKE_GETWEAPON5 else
	ife temp 3 globalsound VACA_GETWEAP1
	palfrom 20 0 32
	globalsound DUKE_GET
	globalsound GETCOINS

ends

eventloadactor MANDONPC
geta[].extra team
ife team -1 set temp 0
enda

spriteshadow MANDONPC
action AMANDNPC  0  1  5  1  1
useractor notenemy MANDONPC 0 AMANDNPC

ifmove 0
{
	move STOPPEDFORPLAYER
	sizeat 26 24
	cstator 257
	count 90
}

fall
ife team 1
{
	ifcount 90
	ifpdistl 1408
	ifp pfacing 
	ifhitspace
	{
		resetcount
		sound M_SURETHINGBOSS
	}
}
else
ifcount 300 ifrnd 4 { sound FINGERSNAP resetcount } 

enda

useractor notenemy NURGLENPC 0 AMANDNPC ifmove 0 move STOPPED faceplayerslow enda

useractor notenemy ALIENTEXTING 0 AMANDNPC enda

move BUBADJUST 32

useractor notenemy DIALOGBUB 0

	ifmove 0
	{
		ife SPRITELOTAG 1605 ife VOLUME 4 ifg LEVEL 13 killit
		ife initflags 0 set initflags -1
		ifn SPRITELOTAG 0 move STOPPED
		else
		ifspawnedby GUNVENDOR 
		{
			sizeat 22 22 
			move BUBADJUST geth
		}
		else
		{
			sizeat 16 16
			move STOPPED
		}
	}
	
	ifmove BUBADJUST ifcount 4 move STOPPED
	
	ifn SPRITELOTAG 0
	{
		ifvarand lastang 536870912 nullop else
		ifcount 10
		ife subtitle_time 0
		ifp pfacing
		ifpdistl 1408
		ifhitspace
		ifcansee
		{
			set subtitle_time 10
			lockplayer 10
			
			ife gametype -1
			ifl wave startx // not advanced enough
			{
				set subtitle_start 1370
				set subtitle_numlines 2
				set subtitle_time 120
				lockplayer 30
				setp[].horiz 100
				break
			}
			else
			{
			
				// quest givers
				ife initflags 2 // brother questgiver
					orvar fetchquest 1
				ife initflags 8 // ogrechef questgiving
					orvar fetchquest 4
				ife initflags 32 // Claire questgiving
					orvar fetchquest 16
				ife initflags 128 // Jenene questgiving
					orvar fetchquest 64
				ife initflags 256
				{
					// operateactivators 13 0
					set initflags 0
				}
				
				ife initflags 2048 // Zaxtor mission questgiving
				{
					orvar fetchquest 1024
				}
				ifvarvarand initflags fetchquest
				ifn initflags -1
				{
					set subtitle_start mtype
					set subtitle_numlines initsprite
					set SPRITELOTAG mtype
					set starty initsprite
					ife initflags 1 // brother meds quest reward
					{
						ifvarand fetchquest 2 nullop // do nothing if quest already completed
						else
						{
							orvar fetchquest 2
							state moneyprize
						}
					}
					ife initflags 8 // death mug quest reward
					ife monstatus 0
					{
						set monstatus 1
						state moneyprize
						findnearsprite OGRECHEF 1024 spriteid
						ifn spriteid -1 setav[spriteid].mtype 1
					}
					ife initflags 32 // Claire tacos quest reward
					ife monstatus 0
					{
						set team 3
						set monstatus 1
						state moneyprize
					}
					ife initflags 128 // Jenene clothes quest reward
					ife monstatus 0
					{
						set team 3
						set monstatus 1
						state moneyprize
						findnearsprite 7805 1024 spriteid
						ifn spriteid -1 setav[spriteid].countvar 1		
					}
					ife initflags 512 // AKASU holobagger upgrade reward
					ife monstatus 0
					{
						set team 3
						set monstatus 1
						state moneyprize
						state moneyprize
					}
					ife initflags 1024
					ife monstatus 0
					{
						orvar fetchquest 2048
						findnearsprite ZAXTOR 1024 spriteid
						ifn spriteid -1
							setav[spriteid].monstatus 2
					}
					ife initflags 2048 // Zaxtor reward
					ife monstatus 0
					{
						set team 3
						set monstatus 1
						state moneyprize
						state moneyprize
						state moneyprize
					}
				}
				else
				{
					set subtitle_start SPRITELOTAG
					set subtitle_numlines starty
					
					ife SPRITELOTAG 563
					{
						findnearsprite 13034 6144 spriteid
						ifn spriteid -1 setav[spriteid].mtype 1 // ready for ritual
					}
					ife SPRITELOTAG 603
					{
						findnearsprite 13034 6144 spriteid
						ifn spriteid -1 
						{ 
							setav[spriteid].mtype 1 // ready for ritual
							setav[spriteid].initsprite 2 // 2nd ancients fight
						}
					}
					ife SPRITELOTAG 633
					{
						findnearsprite 13034 6144 spriteid
						ifn spriteid -1 
						{ 
							setav[spriteid].mtype 1 // ready for ritual
							setav[spriteid].initsprite 3 // 3rd ancients fight
						}
					}
				}
			}
			sound MONITOR_ACTIVE
			resetcount
		}

		ifn subtitle_time 0
		ife subtitle_start SPRITELOTAG
		{
			ife subtitle_start 1370 al THISACTOR
			stopsound DUKE_GRUNT
			stopsound DUKE_SEARCH
			stopsound DUKE_SEARCH2
			stopsound B_GRUNT
			stopsound B_SEARCH
			
			ifvarand lastang 536870912 nullop else
			ifhitspace ifcount 10
			{
				cstator 514
				lockplayer 1
				set subtitle_time 0
				set subtitle_start 0
				resetcount
				ife SPRITELOTAG 569 // end first ancients visit
				{
					ifn vrtrip 0
					{
						set vrscreen_time 32
						set vrscreen VR1
					}
				}
				ife SPRITELOTAG 609 // end second ancients visit
				{
					ifn vrtrip 0
					{
						set vrscreen_time 32
						set vrscreen VR1
					}
				}
				ife SPRITELOTAG 642 // end third ancients visit
				{
					ifn vrtrip 0
					{
						set vrscreen_time 32
						set vrscreen VR1
					}
				}
				ife SPRITELOTAG 847 // smoking collaborator speech
				{
					findnearsprite MALECIV2SMOKING 2048 spriteid
					ifn spriteid -1 
						setav[spriteid].myspawner THISACTOR // ready to die
					cstat 32768
				}
				
				ife SPRITELOTAG 863 // smoking collaborator speech
				{
					findnearsprite MALECIV2SMOKING 2048 spriteid
					ifn spriteid -1 
						setav[spriteid].myspawner THISACTOR // ready to die
					cstat 32768
				}
				break
			}
			set subtitle_time 10
			lockplayer 10
			setp[].horiz 100
		}
	}
	set lastang bits
	ife monstatus 2 killit

enda

eventloadactor BOSSHOLOGRAM cstator 2 sizeat 28 28 enda
useractor notenemy BOSSHOLOGRAM 0 FL3FRAME 

ifmove 0
{
	move STOPPED faceplayerslow
	sizeat 28 28
	seta[].blend 1
	cstator 2
	seta[].shade -50
	geta[].x startx geta[].y starty
}
seta[].x startx
seta[].y starty

enda

useractor notenemy ALIENSURRENDER STRIPPERSTRENGTH FL3FRAME

ifmove 0
{
	move STOPPED faceplayerslow
	sizeat 28 26
	cstator 257
	clipdist 40
	geta[].x startx geta[].y starty
}
fall
seta[].x startx
seta[].y starty
ifn myspawner -1
{
	ifhitweapon
	{
		ifdead
		{
			espawn ALIENCIVMALE
			seta[RETURN].pal sprite[].pal
			seta[RETURN].htextra 100
			seta[RETURN].htpicnum sprite[].htpicnum
			setav[myspawner].monstatus 2
			killit
		}
	}
}
else ifhitweapon { strength STRIPPERSTRENGTH }

enda

// 847 lotag on dialogbub
action ASLOWSMOKE -6  2  5  1  60
useractor notenemy MALECIV2SMOKING STRIPPERSTRENGTH ASLOWSMOKE

ifmove 0
{
	move STOPPED
	sizeat 28 26
	cstator 257
	clipdist 40
	geta[].x startx
	geta[].y starty
}
fall
seta[].x startx
seta[].y starty
ifn myspawner -1
{
	ifhitweapon
	{
		ifdead
		{
			espawn MALECIV2
			seta[RETURN].pal sprite[].pal
			seta[RETURN].htextra 100
			seta[RETURN].htpicnum sprite[].htpicnum
			setav[myspawner].monstatus 2
			killit
		}
	}
}
else ifhitweapon { strength STRIPPERSTRENGTH }


enda

eventloadactor NOCNITSA geta[].lotag SPRITELOTAG seta[].lotag 0 enda
useractor notenemy NOCNITSA 0 AFEMCIVSTAND

state floatcode
ife monstatus 2 killit

enda

useractor notenemy OGRECHEF

	ife mtype 0 break
	ife subtitle_time 0 add mtype 1
	ifg mtype 20
	{
		ife mtype 21 
		{
			globalsound COMM_DYING
			findnearsprite DIALOGBUB 1024 spriteid
			ifn spriteid -1 setav[spriteid].monstatus 2
		}
		set burning 2048
		state imonfire
		guts JIBS6 1
		ifg sprite[].yrepeat 4 
		{
			ifvarand player[].player_par 1 { geta[].yrepeat y sub y 1 seta[].yrepeat y }
		}
		else 
		{ 
			sound SQUISHED state standard_jibs 
			killit 
		}
	}

enda

useractor notenemy 7805 0

	ife countvar 0 break
	ife subtitle_time 0 add countvar 1
	ifg countvar 20
	{
		espawn FEMCIV
		seta[RETURN].pal 18
		setav[RETURN].navmode 3
		findnearsprite DIALOGBUB 1024 spriteid
		ifn spriteid -1 setav[spriteid].monstatus 2
		
		killit
	}

enda

useractor notenemy DEATHMUG 0

	ife initflags -1 break
	ifvarand fetchquest 4 nullop else break
	
	ifpdistl 1024
	ifp pfacing
	ifp palive
	ifcansee
	{
		globalsound DUKE_GET
		palfrom 32 0 32
		orvarvar fetchquest initflags
		killit
	}
enda

useractor notenemy TACOS 0

	ife initflags -1 break
	ifvarand fetchquest 16 nullop else break
	ifvarand fetchquest 32 break
	
	ifpdistl 1024
	ifp pfacing
	ifp palive
	ifcansee
	{
		globalsound DUKE_GET
		palfrom 32 0 32
		orvarvar fetchquest initflags
		killit
	}
enda

spriteshadow SUITCASE
useractor notenemy SUITCASE 0

	ife initflags -1 break
	ifvarand fetchquest 64 nullop else break
	
	ifpdistl 1024
	ifp pfacing
	ifp palive
	ifcansee
	{
		globalsound DUKE_GET
		palfrom 32 0 32
		orvarvar fetchquest initflags
		killit
	}
enda

// define DUMCIV 12079
sizeat 22 20
action AFIGHTFANIM 9517 4  5  1  10
action AFIGHTMANIM 9656 4  5  1  10
action ACIVGUY1ANIM 8501 4  5  1  10
action ACIVGIRL1ANIM 8150 4  5  1  10
action ACIVGUY2ANIM 7241 4  5  1  10
action ACIVGIRL2ANIM 5981 4  5  1  10
action ACIVGUY3ANIM 3471 4  5  1  10
action ACIVGIRL3ANIM -1643 4  5  1  10
action ACIVGUY4ANIM	 -6023  3  5  1  16
action ACIVGIRL4ANIM -6698  4  5  1  10

spriteshadow DUMCIV
action ATROOPANIM	-10399  4  5  1   12
action ALIZANIM     -9959   4  5  1  14
action ABOSS3ANIM   -9319  4  5  1  18
action ABOSS1ANIM   -9449  4  5  1  12
action ABOSS2ANIM   -9369  4  5  1  20
action ARANGERANIM  -6809  4  5  1  14
action ASTRIPPERANIM	-6644  4  5  1  14
action ATERMANIM	-5925  4  5  1  14
action ASHELLYANIM	-5418  4  5  1  14
action AMORPHANIM	-4859  4  5  1  8	
action ACRYSTALANIM	-3854  4  5  1  14
action AEDFANIM	-3360  4  5  1  16
action AELITEANIM	-2602  4  5  1  14
action ASCHOOLGIRLANIM	-2602  4  5  1  14
action AFEMBOUNTYANIM	-1114  4  5  1  14
action ADOOMGUYANIM		-1094  4  5  1  14
action ASITHANIM	209  4  5  1  16
action ATROLLANIM	401  4  5  1  15
action ADRONEANIM	421  1  5  1  10
action AGORILANIM   426  4  5  1  15
action ATROOP2ANIM	1506  4  5  1   12
action ALIZ2ANIM     1526   4  5  1  14
action ABOSS1BANIM   1546  4  5  1  12




move DUMMYWALK 112 // 128

state startdummyanim
	rand pal 13
	add pal 10
	seta[].pal pal
	
	ife VOLUME 4
	{
		ifspritepal 20 spritepal 37
		ifspritepal 19 spritepal 43
		ifspritepal 17 spritepal 49
		sizeat 26 24
		rand temp 9
		switch temp
		case 0 action AFIGHTFANIM sizeat 24 22 break
		case 1 action AFIGHTMANIM sizeat 24 22 break
		case 2 action ACIVGUY1ANIM sizeat 24 22 break
		case 3 action ACIVGUY2ANIM sizeat 24 22 break
		case 4 action ACIVGUY3ANIM break
		case 5 action ACIVGUY4ANIM break
		case 6 action ACIVGIRL1ANIM break
		case 7 action ACIVGIRL2ANIM break
		case 8 action ACIVGIRL3ANIM break
		case 9 action ACIVGIRL4ANIM  sizeat 28 26 break
		endswitch
	}
	else
	{
		sizeat 20 20
		rand temp 22
		switch temp
		case 0 action ATROOPANIM sizeat 40 40 break
		case 1 action ALIZANIM sizeat 28 26 break
		case 2 action ABOSS3ANIM sizeat 30 28 break
		case 3 action ABOSS1ANIM sizeat 30 28 break
		case 4 action ABOSS2ANIM sizeat 30 28 break
		case 5 action ARANGERANIM sizeat 30 28 break
		case 6 action ASTRIPPERANIM spritepal 17 sizeat 28 26 break
		case 7 action ATERMANIM sizeat 28 24 break
		case 8 action ASHELLYANIM spritepal 17 sizeat 28 26 break
		case 9 sizeat 32 32 action AMORPHANIM break
		case 10 action ACRYSTALANIM spritepal 19 sizeat 28 26 break
		case 11 action AEDFANIM sizeat 25 22 break
		case 12 action AELITEANIM sizeat 23 23 break
		case 13 action ASCHOOLGIRLANIM sizeat 28 26 break
		case 14 action ASITHANIM sizeat 29 27 break
		case 15 
				action AFEMBOUNTYANIM sizeat 28 26
				rand pal 5 
				ife pal 0 spritepal 7 
				ife pal 1 spritepal 19 
				ife pal 2 spritepal 20
				ife pal 3 spritepal 46 
				ife pal 4 spritepal 50 
				ife pal 5 spritepal 60
			 break
		case 16 action ADOOMGUYANIM sizeat 29 27 break
		case 17 action ATROLLANIM sizeat 30 27 break
		case 18 action ADRONEANIM sizeat 24 24 break
		case 19 action AGORILANIM sizeat 28 28 break
		case 20 action ATROOP2ANIM sizeat 40 40 break
		case 21 action ALIZ2ANIM sizeat 40 40 break
		case 22 action ABOSS1BANIM sizeat 36 36 break
		endswitch
	}
ends

useractor notenemy DUMCIV 0

ife monstatus 2 killit
sleeptime 0
ifmove 0
{
	cstator 257
	move DUMMYWALK geth
	state startdummyanim
	set monstatus 1
	geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	set team 3 // 1
	set navmode 3
	clipdist 16
}
fall
ifcansee set seeplayer YES else set seeplayer NO
	ifg redtimer 0 sub redtimer 1
	ifg crumbwait 0 
	{
		sub crumbwait 1
		ife crumbwait 0 move DUMMYWALK geth
		break
	}
	ifg navpoint 0
	{
		geta[].z z
		sub z 4096
		cos mycos sprite[].ang
		sin mysin sprite[].ang
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
		
		headspritesect spriteid sprite[].sectnum
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum SECTOREFFECTOR
			ife sprite[spriteid].lotag 24 
			{
				getincangle angvar sprite[].ang sprite[spriteid].ang
				ifl angvar 128 { set navpoint -1 set crumbwait 8 move DUMMYWALK randomangle }
			}
			nextspritesect spriteid spriteid
		}
		
		ifnotmoving 
		iffloordistl 8
		{
			geta[].htmovflag temp
			ifn temp 0
			ifrnd 10 // don't immediately give up on goal
			{
				add temp 16384
				ifl temp 16384 ifg temp -1 
				{
					ifn actorvar[temp].monstatus 1
					{
						set navpoint -1
						set crumbwait 8
						state setfarang
					}
				}
				else
				{
					set navpoint -1
					set crumbwait 8
					state setfarang
				}
			}
		}
		ifn hitsprite -1
		ifg sprite[].xvel 0
		ife actorvar[hitsprite].monstatus 1
		ife actorvar[hitsprite].team team
		{
			getincangle temp sprite[hitsprite].ang sprite[].ang
			abs temp
			ifg temp 768
			{
				geta[].ang angvar
				add angvar 512
				state moveatangvar
			}
		}
		ifg sprite[].xvel 0
			state strafecheck
	}
	
	ifrnd 1
	{
		ifpdistg 16384
		{
			ife seeplayer YES nullop else state startdummyanim
		}
	}
	state getanode
	state defaultwandercode
			
enda

action GD_FLY 0  1  5  1  10
move DRONEGOVEL 0 -14
useractor notenemy GUIDEDRONE 0

ifmove 0
{
	set B SPRITELOTAG
	sub B 1
	// ife gametype -1 
	ifl tourgoal B break
	cstat 257
	sizeat 28 26
	action GD_FLY
	move DRONEGOVEL geth getv
	orvar monstflags YES
	set team 1
	set botclip 10
}
	set navmode 2
	set initflags 0
	set zdist 0
	iffloordistl 16 set zdist -256 else
	ifceilingdistl 64 set zdist 256
	ifg countvar 0 sub countvar 1
	
	ifn zdist 0
	movesprite THISACTOR 0 0 zdist CLIPMASK0 RETURN
	fall
	ifl tourgoal SPRITELOTAG break
	ifn award_time 0 break
	
	ife award_screen 0 
	ife mtype 0
	{
		set mtype 1
		soundonce DRN_FOLLOW
		set subtitle_time 90
		set subtitle_start 455
		set subtitle_numlines 1
		set navpoint -1
		set savedvalue 0
	}
	ifhitweapon { }
	state monsterai
	
	// ifhitspace
	// {
		// al THISACTOR
		// getp[].player_par tempd
		// al tempd
		// al dodgetime
		// al bottarget
		// al navpoint
		// ifg navpoint 0
		// {
			// getav[navpoint].redflagval redflagval
			// al redflagval
		// }
	// }
	
	ifpdistg 8192 ifl dodgetime 10 set dodgetime 10
	ifg tourgoal 13 killit

	ifn dodgetime 0 
	{
		ife dodgetime 60
		{
			ife tourgoal 13
			{
				sound DRN_ARRIVE
				set subtitle_time 180
				set subtitle_start 458
				set subtitle_numlines 2
			}
		}
		ifpdistg 4096 ifl dodgetime 30 set dodgetime 30
		ife navpoint mynavsprite set navpoint -1
		set temp NO
		ife navpoint -1 { ifl dodgetime 30 set dodgetime 30 set temp YES }
		
		ifg navpoint -1 ifl actorvar[navpoint].redflagval 10000 set temp YES
		
		ife temp YES
		{
			ife tourgoal 11 ife dodgetime 60 
			{ 
				sound DRN_BUTTON set countvar 300
				set subtitle_time 90
				set subtitle_start 456
				set subtitle_numlines 1
			}
			ife tourgoal 12 ife dodgetime 60 
			{
				sound DRN_SECRET set countvar 300 
				set subtitle_time 90
				set subtitle_start 457
				set subtitle_numlines 1
			}
			ifl dodgetime 30 
			{
				set dodgetime 30 
				ife tourgoal 11 ife countvar 0 
				{ 
					soundonce DRN_BUTTON set countvar 300 
					set subtitle_time 90
					set subtitle_start 456
					set subtitle_numlines 1
				}
				ife tourgoal 12 ife countvar 0 
				{ 
					soundonce DRN_SECRET set countvar 300 
					set subtitle_time 90
					set subtitle_start 457
					set subtitle_numlines 1
				}
			}
			
		}
		break 
	}
	
	cos xvel angvel
	sin yvel angvel
	shiftr xvel 6
	shiftr yvel 6
	ifpdistg 2048 { shiftr xvel 1 shiftr yvel 1 }
	
	ifg navpoint -1 
	{
		geta[navpoint].z zdist
		sub zdist sprite[].z
		
		ldist xydist THISACTOR navpoint
		ife xydist 0 set xydist 1
		shiftl zdist 8
		div zdist xydist
		set spriteid navpoint
		state facesprite
		
		findnearspritez NPCNAV 512 16384 spriteid
		ifn spriteid -1
		{
			ife actorvar[spriteid].SPRITELOTAG SPRITELOTAG
			ife tourgoal SPRITELOTAG
			{
				add SPRITELOTAG 1
				add tourgoal 1
				set dodgetime 90
				break
			}
		}
		
	}
	else
	ifn bottarget -1
	{
		geta[bottarget].z zdist
		sub zdist 6144
		sub zdist sprite[].z
		geta[bottarget].x x
		geta[bottarget].y y
		sub x sprite[].x
		sub y sprite[].y
		getangle angvel x y
		
		ifl targetdist 4096 { shiftr xvel 1 shiftr yvel 1 add angvel 1024 } else
		ifl targetdist 8192 { set xvel 0 set yvel 0 }
		
	}
	else
	{
		set zdist 0
		iffloordistl 32 set zdist -256 else
		ifceilingdistl 64 set zdist 256
	}
	
	ifl zdist -3072 set zdist -3072
	ifg zdist 3072 set zdist 3072
	
	// failsafe to prevent movement when destination node has no value
	ifg navpoint -1
	{
		getav[navpoint].redflagval temp
		ifl temp 10000 { set xvel 0 set yvel 0 ifrnd 8 set navpoint -1 }
		ifg dodgetime 0 { set xvel 0 set yvel 0 }
	}
	movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
	
	// ifhitspace 
	// {
		// al navpoint
		// ifg navpoint -1
		// {
			// getav[navpoint].redflagval temp
			// al temp
		// }
	// }
	

enda

action AJOG  0  4  5  1  10
useractor notenemy ARUNNER 0 AJOG

enda

action AFREEHUMAN -10  1  1

state collaredstate

ifmove 0
{
	move STOPPEDFORPLAYER
	ifl sprite[].xrepeat 4 sizeat 28 26
	cstator 257
	clipdist 32
	seta[].htflags 3
}
fall

ifaction 0
ifpdistl 1280
ifp pfacing
ifp palive
ifhitspace
{
	ifactor COLLARED5 sound FC_THANKS
	ifactor COLLARED6 sound FC_THANKS
	sound ENDSEQVOL2SND3
	action AFREEHUMAN
	set value 10
	state spawnimpcoins
	set subtitle_numlines 3
	set subtitle_start 902
	set subtitle_time 240
}

ends

useractor notenemy ZAXTOR 0

ife monstatus 2
{
	ife subtitle_time 0
	{
		ife countvar 0 
		{
			set countvar 1 
			sound ITEMPORT
			seta[].htflags 68
			seta[].shade -10
			findnearsprite DIALOGBUB 1024 spriteid
			ifn spriteid -1
				setav[spriteid].monstatus 2
		}
		else
		add countvar 1
		
		ifspritepal 33 getlastpal else spritepal 33
		
		geta[].alpha temp add temp 4 seta[].alpha temp
		ifge countvar 60 killit
	}
	
}

enda

useractor notenemy COLLARED1 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED2 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED3 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED4 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED5 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED6 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED7 STRIPPERSTRENGTH state collaredstate enda
useractor notenemy COLLARED8 STRIPPERSTRENGTH state collaredstate enda

eventloadactor AAXON sizeat 24 22 enda
useractor notenemy AAXON 0 AMALECIVSTAND

ifmove 0
{
	move STOPPEDFORPLAYER faceplayerslow
	cstat 257
	sizeat 24 22
}
fall
state alphapulse

ife subtitle_start 985
ifg subtitle_time 0
{
	soundonce AXON_RELICS1
	seta[].blend 1
	cstator 514
	seta[].alpha 0
}
// else
// ife subtitle_start 991
// ifg subtitle_time 0
// {
	// soundonce AXON2
	// seta[].blend 1
	// cstator 514
	// seta[].alpha 0
// }
else 
{
	ifsound AXON_RELICS1 stopsound AXON_RELICS1
	// ifsound AXON2 stopsound AXON2
	cstat 257
	seta[].blend 0
}

enda

useractor notenemy SCIENTIST1 0 AFEMCIVSTAND fall enda
useractor notenemy SCIENTIST2 0 AFEMCIVSTAND fall enda

