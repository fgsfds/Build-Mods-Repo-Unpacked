// NAAMAH
// summoned by Wes (replaces holoduke)

action ANAMASTAND  0  2  5  1  50
action ANAMASPELL1  10  3  5  1  16
action ANAMAFLOATF  25  3  5  1  10
action ANAMAFLOATB  35  3  5  -1  10
action ANAMASPELL2  40  3  5  1  20
action ANAMAGRAB1 50  1  5  1  10
action ANAMAGRAB2 45  1  5  1  10
action ANAMASITF  65  3  5  1  10
action ANAMASITB  75  3  5  -1  10 
action ANAMAKICK1 80  3  8  1  16
action ANAMAKICK2 96  1  8  1  10
action ANAMAKICK3 88  1  8  1  30
action ANAMASPELL3 104  6  1  1  16
action ANAMADOWN 110  1  5  1  30

move NAMAVEL1	192 -8
move NAMAVEL2	420 -8
move NAMABACK	-128 -8
move NAMAFLOAT  0  -8
move NAMARISE	0  -256

ai AINAMASTAND ANAMASTAND STOPPEDFORPLAYER geth
ai AINAMAFLOAT ANAMAFLOATF NAMAFLOAT faceplayerslow getv
ai AINAMAKICK ANAMAKICK1 NAMAVEL2 geth
ai AINAMASPELL1 ANAMASPELL1 STOPPEDFORPLAYER geth
ai AINAMASPELL2 ANAMASPELL2 STOPPEDFORPLAYER geth
ai AINAMADOWN ANAMADOWN STOPPEDFORPLAYER geth

ai AINAMARUSH ANAMAFLOATF NAMAVEL2 geth

defstate namadie

	ifl sprite[].alpha 254
	{
		ife sprite[].alpha 0
		{
			screensound FLAMEDRAWIN
			spritepal 4
		}
		geta[].alpha temp
		add temp 16
		ifg temp 240
		{
			set mynaamah -1
			killit
		}
		seta[].alpha temp
	}

ends

defstate deathtaunt

	rand temp 4
	ife temp 0 sound NA_CANTGETUP else
	ife temp 1 sound NA_NIGHTNIGHT else
	ife temp 2 sound NA_WASITGOOD else
	ife temp 3 sound NA_DOESITHURT else
	ife temp 4 sound NA_HOWSTHAT

ends

defstate namadodgesound

	ifrnd 84 sound NA_HAHA else
	ifrnd 128 sound NA_HAHAHA else
	sound NA_NOPE

ends

defstate naamahrevive
	
	set bottarget -1
	set spriteid player[].i
	state facesprite
	addphealth 1
	addstrength -12
	ife sprite[].extra 30 setav[spriteid].ikicked -96

	ife sprite[player[].i].extra 10
		globalsound NA_CANTGETUP
					
	palfrom 16 0 10 63
	espawn CAPTUREPLUS
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	seta[RETURN].z sprite[player[].i].z
	seta[RETURN].pal 1
	rand x 512
	sub x 256
	add x sprite[].x
	seta[RETURN].x x
	rand y 512
	sub y 256
	add y sprite[].y
	seta[RETURN].y y
ends

defstate namaspellstate
	ifaction ANAMASPELL1
	{ 
		ifactorsound THISACTOR SPELL1FIRE nullop else sound SPELL1FIRE
		
		ifn bottarget -1
		ifcount 3
		{
			resetcount
			eshoot FREEZEBOLT
			addstrength -8
			ifvarand inven_upgrades 4 addstrength 2
			// ifn RETURN -1
			// {
			// setav[RETURN].bottarget bottarget
			// rand angvar 48, sub angvar 24
			// add angvar sprite[RETURN].ang
			// seta[RETURN].ang angvar
			// rand zdist 2048, sub zdist 1024
			// add zdist sprite[RETURN].zvel
			// seta[RETURN].zvel zdist
			// }
		}
		ifactioncount 3 
		{
			ai AINAMAFLOAT
			ifn bottarget -1 set myvictim bottarget
		}
	}
	
	ifaction ANAMASPELL2
	{
		ifactorsound THISACTOR SPELL2FIRE nullop else sound SPELL2FIRE
		ifcount 2
		{
			resetcount
			espawn BATPROJ
			addstrength -8
			ifvarand inven_upgrades 4 addstrength 2
			ifn RETURN -1
			{
			setav[RETURN].team team
			setav[RETURN].bottarget bottarget
			setav[RETURN].myspawner THISACTOR
			rand angvar 384, sub angvar 192
			add angvar sprite[RETURN].ang
			seta[RETURN].ang angvar
			geta[].z z, sub z 3072
			rand z2 5120
			sub z z2
			seta[RETURN].z z
			seta[RETURN].htbposz z
			ifrnd 84 seta[RETURN].pal 11
			}
		}
		ifactioncount 3 
		{
			ai AINAMAFLOAT
			ifn bottarget -1 set myvictim bottarget
		}
	}

ends

defstate namakickstate

	ife bottarget -1 
	{ 
		ife mtype 0
		{
			ai AINAMAFLOAT 
			set dodgetime 0
			break 
		}
	}
	else
	{
		set spriteid bottarget
		state facesprite
	}
	
	getflorzofslope sprite[].sectnum sprite[].x sprite[].y z2
	ifn bottarget -1 set z sprite[bottarget].z
	else set z z2
	sub z 6144
	sub z sprite[].z
	set z2 targetdist
	shiftr z2 9, ifl z2 1 set z2 1
	div z z2
	movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
	

	ifaction ANAMAKICK1
	{
		ifactorsound THISACTOR KICKOUT nullop else
		ifactioncount 1 sound KICKOUT
		
		ifactioncount 2 action ANAMAKICK2
		// else ifl targetdist 1280 action ANAMAKICK2
	}
	
	ifaction ANAMAKICK2
	{
		set dodgetime 3
		ife mtype 0
		{
			ldist xydist THISACTOR bottarget
			
			ife targetalive YES
			ife shootmytarget YES
			ifl targetdist 1760
			ifn bottarget -1
			{
				ifl xydist 844
				{
					move STOPPEDFORPLAYER geth getv
					geta[bottarget].htextra temp
					rand tempb 10
					add tempb 100
					add temp tempb
					addstrength -80
					ifvarand inven_upgrades 4 addstrength 20
					seta[bottarget].htextra temp
					seta[bottarget].htpicnum KICKPROJ
					ife mynaamah THISACTOR
					seta[bottarget].htowner player[].i
					seta[bottarget].htang sprite[].ang
					sound KICKIMPACT
					getav[bottarget].stun temp
					add temp 20
					setav[bottarget].stun temp
					set mtype 1
					resetactioncount
					set dodgetime 10
					set myvictim bottarget
					geta[].ang angvel
				}
			}
			
			set B NO
			ife mtype 0
			{
				ifnotmoving ifl xydist 1024 set B YES
				ifl xydist 384 set B YES
			}
			ife B YES
			{
				set mtype 0
				ai AINAMAFLOAT
				set dodgetime 0
			}
		}
		else
		{	
			seta[].ang angvel
			ifcount 8
			{
				set dodgetime 10
				set mtype 0
				action ANAMAKICK3
			}
		}
	}
	
	ifaction ANAMAKICK3
	{
		ifactioncount 1
		{
			ai AINAMAFLOAT
			set dodgetime 0
			ifn myvictim -1
			ife actorvar[myvictim].monstatus 2
			{
				ifrnd 128
				{
					state deathtaunt
					action ANAMASITF
				}
				set myvictim -1
			}
		}
	}
	
	

ends

defstate namarushstate

	ife myvictim -1 { ai AINAMAFLOAT }
	ifaction ANAMAGRAB2
	{
		ife actorvar[myvictim].monstatus 1 
		{
			set dodgetime 10
			setav[myvictim].stun 10
			seta[myvictim].xvel 0
			seta[].x startx
			seta[].y starty
			seta[myvictim].x init_xrepeat
			seta[myvictim].y init_yrepeat
			seta[].ang angvel
			set z sprite[].z
			ifge countvar 60 add z 4096
			seta[myvictim].z z
			
			add countvar 1
			ifg countvar 9
			{
				ifge countvar 300
				{
					ifge countvar 320
					{
						cstat 257
						ai AINAMAFLOAT
						set dodgetime 0
						set countvar 0
						geta[myvictim].cstat temp
						ifvarand temp 8 xorvar temp 8
						seta[myvictim].cstat temp
						ife actorvar[myvictim].monstatus 2
						{
							state deathtaunt
							action ANAMASITF
						}
						
						set myvictim -1
						break
					}
				}
				else
				ifge countvar 60 // flip over
				{
					ife countvar 60
					{
						rand temp 2
						ife temp 0 sound NA_THISWILLENDYOU else
						ife temp 1 sound NA_NOWYOUDIE else
						ife temp 2 sound NA_GOTOHELL
					}
					cstat 265
					seta[myvictim].cstat 265
					fall
					ifmove STOPPEDFORPLAYER nullop else move STOPPEDFORPLAYER geth
					getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum temp temp z spriteid 192 CLIPMASK0 
		
					set B NO
					set tempb z
					sub z sprite[].z
					ifl z 1024 set B YES
					else iffloordistl 4 set B YES
					
					ife B YES
					{
						sound LBM_STEP4
						ifpdistl 3072 quake 15
						seta[myvictim].htpicnum HEADJIB1
						seta[myvictim].htextra 500
						addstrength -300
						ifvarand inven_upgrades 4 addstrength 60
						ife mynaamah THISACTOR
						seta[myvictim].htowner player[].i
						else seta[myvictim].htowner THISACTOR
						seta[myvictim].htang sprite[].ang
						getav[myvictim].stun temp
						add temp 20
						setav[myvictim].stun temp
						set countvar 300
					}
					else ifg countvar 180 set countvar 300
				}
				else
				{
					ifmove NAMARISE nullop else move NAMARISE geth getv
					seta[myvictim].zvel -644
					seta[].zvel -644
					// movesprite THISACTOR 0 0 -1280 CLIPMASK0 RETURN
					getceilzofslope sprite[].sectnum sprite[].x sprite[].y z
					geta[].z zdist
					sub zdist z
					ifl zdist 10240 set countvar 59
				}
			}
		}
		else
		{
			ifn myvictim -1
			ife sprite[myvictim].statnum 1024 
			{
				cstat 257
				set countvar 0
				set dodgetime 0
				set myvictim -1
				ai AINAMAFLOAT
				state deathtaunt
				action ANAMASITF
				break
			}
			ifl countvar 300 set countvar 300
			add countvar 1
			ifge countvar 320
			{
				cstat 257
				ai AINAMAFLOAT
				set countvar 0
				set dodgetime 0
				ifn myvictim -1
				{
					geta[myvictim].cstat temp
					ifvarand temp 8 xorvar temp 8
					seta[myvictim].cstat temp
					ife actorvar[myvictim].monstatus 2
					{
						state deathtaunt
						action ANAMASITF
					}
				}
				
				set myvictim -1
				break
			}
		}
		break
	}
	ife bottarget -1  
	{ 
		ai AINAMAFLOAT set countvar 0 set dodgetime 0 
		ifn myvictim -1
		{
			geta[myvictim].cstat temp
			ifvarand temp 8 xorvar temp 8
			seta[myvictim].cstat temp
		}
		set myvictim -1 
		break 
	}
	
	
	ifaction ANAMAGRAB1 nullop else
	ifl targetdist 3072 action ANAMAGRAB1
	
	
	set z sprite[bottarget].z
	sub z sprite[].z
	set z2 targetdist
	shiftr z2 9, ifl z2 1 set z2 1
	div z z2
	movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
	
	ifaction ANAMAGRAB1
	{
		ldist xydist THISACTOR bottarget
		ifl xydist 844 
		{
			ifrnd 128 sound NA_DANCEFORME else sound NA_GOFORARIDE
			action ANAMAGRAB2
			move STOPPEDFORPLAYER geth
			set myvictim bottarget
			seta[myvictim].htextra 3
			seta[myvictim].htowner THISACTOR
			seta[myvictim].htpicnum SHOTSPARK1
			seta[myvictim].htang sprite[].ang
			seta[].z sprite[myvictim].z
			set spriteid myvictim
			state facesprite
			set angvel sprite[].ang
			set angvar sprite[].ang
			add angvar 1024
			
			setvarvar x2 sprite[myvictim].x
			addvar x2 384
			rotatepoint sprite[myvictim].x sprite[myvictim].y x2 sprite[myvictim].y angvar startx starty
			setactor[].x startx
			setactor[].y starty
			set dodgetime 10
			set init_xrepeat sprite[myvictim].x
			set init_yrepeat sprite[myvictim].y
		}
		else ifnotmoving { set myvictim -1 set countvar 0 set dodgetime 0 ai AINAMAFLOAT }
	}
	
	state makespeedblur
ends

defstate namattack
	
	ife sprite[bottarget].pal 1 { sound NA_ATTK2 ai AINAMAKICK }
	
	ldist TMP_A THISACTOR bottarget
	
	rand TMP_B 4096
	add TMP_A TMP_B
	
	ifl TMP_A 3072
	{
		rand temp 5
		ife temp 0 ai AINAMASPELL1 else
		ife temp 1 ai AINAMASPELL2 else
		ifle temp 3 ai AINAMARUSH else
		{ sound NA_ATTK2 ai AINAMAKICK }
	}
	else
	ifl TMP_A 10240
	{
		rand temp 5
		ifle temp 1 ai AINAMASPELL1 else
		ifle temp 3 ai AINAMASPELL2 else
		ife temp 4 ai AINAMARUSH else
		{ sound NA_ATTK2 ai AINAMAKICK }
	}
	else
	{
		ifrnd 128 ai AINAMASPELL1 else ai AINAMASPELL2
	}
	
ends

defstate namafollowplayer

	ife pdown YES
	ife mynaamah THISACTOR
	{
		ifpdistg 2048 // 2048
		ifcount 30
		ifrnd 8
		{
			set B -1
			headspritestat spriteid 999
			whilevarn spriteid -1
			{
				ldist tempc player[].i spriteid
				ifg tempc 2560 
				ifl tempc 6144
				{
					canseespr spriteid player[].i temp
					ife temp YES
					set B spriteid
				}
				nextspritestat spriteid spriteid
				ifn B -1 set spriteid -1
			}
			ifn B -1
			{
				setsprite THISACTOR sprite[B].x sprite[B].y sprite[B].z
				spritepal 33
				seta[].alpha 224
				screensound PORTALIN
				set bottarget -1
				resetcount
			}
		
		}
		
		ldist temp THISACTOR player[].i
		ifl temp 1280
		ifpdistl 2048
		{
			state naamahrevive
			ifpdistl 1280 move STOPPEDFORPLAYER faceplayer getv
			break
		}
		ife bottarget -1
		{
			set spriteid player[].i
			state facesprite
		}
	}
	
	ifn bottarget -1 break
	
	ifcansee
	{
		ifpdistg 16384
		{
			setsprite THISACTOR player[].posx player[].posy player[].posz
			screensound PORTALIN
			changespritesect THISACTOR player[].cursectnum
			spritepal 33
			seta[].alpha 224
		}
		else
		ifpdistl 2560
		ife bottarget -1
		ife pdown NO
		{
			ifmove NAMAFLOAT nullop else
				move NAMAFLOAT faceplayerslow getv
		}
	}
	else ifpdistg 8192
	{
		setsprite THISACTOR player[].posx player[].posy player[].posz
		screensound PORTALIN
		changespritesect THISACTOR player[].cursectnum
		spritepal 33
		seta[].alpha 224
	}
	
		set spriteid player[].i
		set z2 0
		set z sprite[spriteid].z
		ife pdown YES add z 4096
		
		sub z sprite[].z
		ifg z 10240 set z2 768 else
		ifl z -10240 set z2 -768
		movesprite THISACTOR 0 0 z2 CLIPMASK0 RETURN
ends

useractor enemy NAAMAH 1000

// fall
ifai 0
{
	ai AINAMAFLOAT
	cstat 257
	clipdist 32
	// getp[].max_actors_killed temp
	// sub temp 1
	// setp[].max_actors_killed temp
	sizeat 26 24
	set team 1
	screensound PORTALIN
	geta[].htflags temp
	orvar temp 4194304
	orvar temp 8192
	orvar temp 4
	seta[].htflags temp
	spritepal 33
	seta[].alpha 254
	seta[].shade -127
	
	ife THISACTOR mynaamah
	seta[].extra player[].holoduke_amount
	ifl player[].holoduke_amount 0 setp[].holoduke_amount 0
	set navmode 3
}

ife THISACTOR mynaamah set shellyinmap 10

ifvarand player[].gm 8 set monstatus 2
ifspritepal 0 ifvarand inven_upgrades 4 spritepal 25

ifl sprite[].extra 0 set monstatus 2
ife THISACTOR mynaamah
{
	ife pchar 2
	{
		setp[].holoduke_amount sprite[].extra
		ifl player[].holoduke_amount 0 setp[].holoduke_amount 0
	}
	else 
	ife switchboost 0 set monstatus 2
}
ifai AINAMARUSH nullop else
ife monstatus 2
{
	state namadie
	break
}

ifaction ANAMAFLOATF { ifactioncount 3 action ANAMAFLOATB } else
ifaction ANAMAFLOATB { ifactioncount 3 action ANAMAFLOATF } else
ifaction ANAMASITF { ifactioncount 3 action ANAMASITB } else
ifaction ANAMASITB { ifactioncount 3 action ANAMASITF }

ifai AINAMAFLOAT
{
	iffloordistl 8
		movesprite THISACTOR 0 0 -512 CLIPMASK0 RETURN
	else
	{
		set temp player[].player_par
		shiftl temp 5
		add temp THISACTOR
		sin tempb temp
		shiftr tempb 6
		geta[].z z
		sub z tempb
		seta[].z z
	}
	iffloordistl 32 nullop else
	ifvarand player[].player_par 1 fall
	state namafollowplayer
	ifn myvictim -1
	ife actorvar[myvictim].monstatus 2
	{
		ifrnd 64
		{
			state deathtaunt
			action ANAMASITF
		}
		set myvictim -1
	}
	
	ifg sprite[].alpha 0
	ifn monstatus 2
	{
		geta[].alpha temp
		sub temp 12, ifl temp 0 set temp 0
		seta[].alpha temp
		ife temp 0
		{
			ifvarand inven_upgrades 4 spritepal 25 else
			spritepal 0
			ife monstatus 0
			{
				ifrnd 84 sound NA_LOVELY else
				ifrnd 128 sound NA_MAKETHISFUN else
				sound NA_ICANPLAY
			}
			
		}
		
		break
	}
	
	ifcount 30
	{
		ife bottarget -1
		{
			ifpdistg 5120 { ifmove NAMAVEL2 nullop else move NAMAVEL2 geth getv }
			else
			ifpdistg 3072 { ifmove NAMAVEL1 nullop else move NAMAVEL1 geth getv }
		}
		else
		{
			ife pdown YES set bottarget -1
			else
			{
				ldist xydist THISACTOR bottarget
				ifl xydist 2048
				{
					ifmove NAMABACK nullop else
					move NAMABACK geth getv
				}
				else
				ifl xydist 8192
				{
					ifmove NAMAFLOAT nullop else
					move NAMAFLOAT geth getv
				}
				else
				{
					ifmove NAMAVEL1 nullop else
					move NAMAVEL1 geth getv
				}
			}
		}
	}
	ifmove NAMAFLOAT nullop else ifcount 30 ifaction ANAMASITF
	action ANAMAFLOATF
	
	ifn bottarget -1
	{
		set z2 0
		set z sprite[bottarget].z
		
		sub z sprite[].z
		ifg z 10240 set z2 768 else
		ifl z -10240 set z2 -768
		movesprite THISACTOR 0 0 z2 CLIPMASK0 RETURN
	
	}
	
	ifn myspawner -1
	{
		state incoming_eval
		set dodgetime 6
		set crumbwait 8
		set myspawner -1
		state namadodgesound
	
	}
	ifg dodgetime 0
	{
		cos xvel angvel
		sin tempb angvel
		shiftr xvel 5
		shiftr tempb 5
		movesprite THISACTOR xvel tempb 0 CLIPMASK0 RETURN
	}
	
}
else set myspawner -1

	state monsterai
	ifai AINAMAFLOAT ife pdown YES set bottarget -1
	
	ifoutside seta[].shade sector[].ceilingshade else seta[].shade sector[].floorshade
	
	ifn bottarget -1
	ife sprite[bottarget].picnum BATBOSS ifn actorvar[bottarget].countvarc 0 
	{
		set bottarget -1
		ifn myvictim -1
		{
			geta[myvictim].cstat temp
			ifvarand temp 8 xorvar temp 8
			seta[myvictim].cstat temp
		}
		set myvictim -1
	}
	
	ifn bottarget -1
	{
		ifai AINAMAFLOAT
		ifl targetdist 16384
		{
			ifrnd 4
				state namattack
			else
			ife sprite[bottarget].pal 1 { sound NA_ATTK2 ai AINAMAKICK }
			else
			ifvarand inven_upgrades 4
			ifrnd 8
				state namattack
		}
	}
	
	ifn sprite[].htowner -1
	{
		ife actorvar[sprite[].htowner].team team
		seta[].htextra -1
	}
	ifhitweapon 
	{
		ifdead 
		{
			ai AINAMADOWN
			set monstatus 2
			sound NA_DEATH
			ifn myvictim -1
			{
				geta[myvictim].cstat temp
				ifvarand temp 8 xorvar temp 8
				seta[myvictim].cstat temp
			}
		}
		else
		{
			ifsound NA_PAIN1 nullop else
			ifsound NA_PAIN2 nullop else
			ifsound NA_PAIN3 nullop else
			{
				ifrnd 84 sound NA_PAIN1 else
				ifrnd 128 sound NA_PAIN2 else
				sound NA_PAIN3
			}
			ifai AINAMAFLOAT
			ifn bottarget -1
				state namattack
		}
	}
	
	ifai AINAMARUSH state namarushstate else
	ifai AINAMAKICK state namakickstate else
	ifai AINAMASPELL1 state namaspellstate else
	ifai AINAMASPELL2 state namaspellstate

enda

action ABATFLAPF  0  3  5  1  8
action ABATFLAPMID  5  1  5  1  8
action ABATFLAPB  10  3  5  -1  8

move BATPROJVEL 392 -4
move BATPROJVEL2 320 -4
move BATPROJVEL3 464 -4

move BATPROJDASH 1024 -4

spriteflags BATPROJ 8260

useractor notenemy BATPROJ 20 ABATFLAPF

ifmove 0
{
	ifrnd 84
	move BATPROJVEL geth getv else
	ifrnd 128 move BATPROJVEL2 geth getv else
	move BATPROJVEL3 geth getv
	ifspawnedby BATBOSS { sizeat 38 38 spritepal 11 } else
	{ sizeat 24 24 spritepal 2 }
	cstat 2 // 130
	cstator 256
	
	seta[].blend 1
	seta[].shade -100
	seta[].mdflags 16
}

ifaction ABATFLAPF { ifactioncount 3 action ABATFLAPB } else
ifaction ABATFLAPB { ifactioncount 3 action ABATFLAPF }

ifrnd 8 sound BATSOUND

ifn bottarget -1
{
	ife sprite[bottarget].statnum 1024 set bottarget -1 else
	ife actorvar[bottarget].monstatus 2 set bottarget -1
}

ifspritepal 11 ifcount 60
{
	ifmove BATPROJDASH nullop else
	{
		move BATPROJDASH geth getv
		sound DASHSOUND
		action ABATFLAPMID
		ifn bottarget -1
		{
			set spriteid bottarget
			state facesprite
		}
	}
}
		
ifmove BATPROJDASH
{
	state makespeedblur
	ifn bottarget -1
	{
		dist targetdist THISACTOR bottarget
		set z sprite[bottarget].z
		sub z sprite[].z
		set z2 targetdist
		ifg z2 512 sub z2 512
		div z2 768 ifl z2 1 set z2 1
		div z z2
		movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
		
		ifl targetdist 1280
		{
			ifrnd 128 sound SMALLEXP1 else sound SMALLEXP2
			hitradius 2048 6 8 10 12
			spawn AIRSHOCK
			seta[bottarget].htowner myspawner
			killit
		}
	}
}
else
{
	ifn bottarget -1
	{
		set z2 0
		ifl sprite[bottarget].z sprite[].z set z2 -1792
		else
		{
			geta[bottarget].z z
			sub z 10240
			ifg z sprite[].z set z2 1792
		}
		movesprite THISACTOR 0 0 z2 CLIPMASK0 RETURN
	}

	ifn bottarget -1
	ifrnd 48
	{
		ife sprite[bottarget].statnum 1024 set bottarget -1 else
		ife actorvar[bottarget].monstatus 2 set bottarget -1 else
			state turntotarget
	}
	else
	{
		rand angvar 32 sub angvar 16
		add angvar sprite[].ang
		seta[].ang angvar
		rand zdist 512 sub zdist 256
		movesprite THISACTOR 0 0 zdist CLIPMASK0 RETURN
	}
}

geta[].htmovflag spriteid
ifn spriteid 0
{			
	add spriteid 16384
	ifl spriteid 16384 ifg spriteid -1 // bumping a sprite
	{
		ifn team actorvar[spriteid].team
		{
			geta[spriteid].htextra temp
			rand tempb 3
			add tempb 8
			add temp tempb
			seta[spriteid].htextra temp
			seta[spriteid].htpicnum FLAKPROJ
			ife myspawner mynaamah seta[spriteid].htowner player[].i
			else seta[spriteid].htowner myspawner
			seta[spriteid].htang sprite[].ang
			
			ifspritepal 2
			{
				getav[spriteid].burning temp
				add temp 40
				setav[spriteid].burning temp
			}
			ifspritepal 11
			{
				getav[spriteid].bleeding temp
				add temp 6
				setav[spriteid].bleeding temp
				sound RADFRY
			}
		}
	}
	espawn PART_EXP
	seta[RETURN].htpicnum FLAKPROJ
	geta[].z z, add z 2048, seta[RETURN].z z
	seta[RETURN].htbposz z
	ifspritepal 11
	{
		ifrnd 128 sound SMALLEXP1 else sound SMALLEXP2
		hitradius 1280 8 10 12 14
		spawn AIRSHOCK
	}
	killit
}

ifhitweapon 
{
	espawn PART_EXP
	seta[RETURN].htpicnum FLAKPROJ
	geta[].z z, add z 2048, seta[RETURN].z z
	seta[RETURN].htbposz z
	ifspritepal 11
	{
		ifrnd 128 sound SMALLEXP1 else sound SMALLEXP2
		hitradius 1280 4 6 8 10
		spawn AIRSHOCK
		killit
	}
	hitradius 1024 2 2 2 2
	killit
}

enda
