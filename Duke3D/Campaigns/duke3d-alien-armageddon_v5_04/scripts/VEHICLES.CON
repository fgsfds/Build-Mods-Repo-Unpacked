

// define PIGSUV 6374
// define PIGSUVPOINT 6396
define PIGSUVSTRENGTH 400

move PIGSUVSPEED1 320
move PIGSUVSPEED2 448
move PIGSUVSPEED3 576
move PIGSUVSPEED4 704
move PIGSUVSTOPPED

action APIGSUV1  0  1  5  1  10
action APIGSUVDYING  0  1  5  1  10
action APIGSUV2  5  1  5  1  58
action APIGSUV3  10 1  5  1  30
action APIGSUVDEAD 15  1  1  1  1

ai AISUVDYING APIGSUVDYING PIGSUVSPEED2 geth

eventloadactor LOCATORS
	// cactor PIGSUVPOINT
	ife clearrecons YES { set clearrecons 0 set highrecon 0 }
	// geta[].lotag SPRITELOTAG
	// seta[].lotag 0
	// seta[].hitag 0
	// ifg SPRITELOTAG highrecon set highrecon SPRITELOTAG
	// setarray reconpoints[SPRITELOTAG] THISACTOR
	// cstat 32768
enda

eventloadactor RECON

	ife clearrecons YES { set clearrecons 0 set highrecon 0 }
	cactor NEWRECON // PATROLSENTRY
	sizeat 24 24
	cstat 257
	
	ife highrecon 0
	{
		set TMP_A 0
		whilevarn TMP_A 16384
		{
			ife sprite[TMP_A].picnum LOCATORS
			ifn sprite[TMP_A].statnum 1024
			{
				// changespritestat TMP_A 0
				// seta[TMP_A].picnum PIGSUVPOINT
				// seta[TMP_A].cstat 32768
				// geta[TMP_A].lotag SPRITELOTAG
				// ifg SPRITELOTAG highrecon set highrecon SPRITELOTAG
				// setav[TMP_A].SPRITELOTAG SPRITELOTAG
				// setarray reconpoints[SPRITELOTAG] TMP_A
				// seta[TMP_A].lotag 0
				
				espawn PIGSUVPOINT
				geta[TMP_A].lotag SPRITELOTAG
				setav[RETURN].SPRITELOTAG SPRITELOTAG
				setsprite RETURN sprite[TMP_A].x sprite[TMP_A].y sprite[TMP_A].z
				seta[RETURN].cstat 32768
				seta[RETURN].statnum 0
				ifg sprite[TMP_A].lotag highrecon set highrecon sprite[TMP_A].lotag
				setarray reconpoints[SPRITELOTAG] RETURN
				
			}
			ife sprite[TMP_A].picnum RECON
			ifn sprite[TMP_A].statnum 1024
			{
				setav[TMP_A].SPRITELOTAG sprite[TMP_A].hitag
				seta[TMP_A].hitag 0
			}
			add TMP_A 1
		}
	}
enda

eventloadactor PIGSUVPOINT
	ife clearrecons YES { set clearrecons 0 set highrecon 0 }
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	seta[].hitag 0
	ifg SPRITELOTAG highrecon set highrecon SPRITELOTAG
	setarray reconpoints[SPRITELOTAG] THISACTOR
	cstat 32768
enda

eventloadactor CARPOINTR
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	seta[].hitag 0
	cstat 32768
	changespritestat THISACTOR 975
enda

eventloadactor CARPOINTL
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	seta[].hitag 0
	cstat 32768
	changespritestat THISACTOR 976
enda

eventloadactor COPTERPATH
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	geta[].ang angvel
	cstat 32768
enda

eventloadactor PIGSUV
	set SPRITELOTAG -1
	seta[].lotag 0
	seta[].hitag 0
	cstat 257
	sizeat 48 40
	set NEWENEMIES NO
enda

useractor notenemy RECONWRECK 100

	ifmove 0
	{
		move STOPPED
		sizeat 64 60
		ifrnd 128 cstator 4
		ifrnd 128
		{
			ifrnd 128 spawn BURNING else spawn BURNING2
		}
	}
	fall
	ifrnd 8
		spawn BIGSMOKE
	
	ifcount 240 cstator 256
	ifhitweapon
	{
		debris SCRAP1 2
		debris SCRAP2 2
		debris SCRAP5 2
		ifdead
		{
			set countvarb 0
			whilevarn countvarb 8
			{
			   rand z 3072
			   add z 2048
			   mul z -1
			   ezshoot z CARXPLODE
			   randvar temp  2047
			   setactor[RETURN].ang temp 
			   randvar temp  192 add temp 128
			   setactor[RETURN].xvel temp 
			   add countvarb 1
			}
			sound VENT_BUST
			killit
		}
	}

enda

state tankwreckcode
	ifmove 0
	{
		move STOPPED
		ifl sprite[].xrepeat 8
		sizeat 60 60
		ifrnd 128 cstator 4
	}
	fall
	ifrnd 8
	{
		geta[].z savz
		sub savz 4096
		seta[].z savz
		espawn BIGSMOKE
		seta[RETURN].htpicnum TANKWRECK
		add savz 4096
		seta[].z savz
	}
	
	ifactor TANKWRECK
	{	ifcount 240 cstator 256 }
	else
	cstator 256
	
	ifhitweapon
	{
		debris SCRAP1 1
		debris SCRAP2 1
		debris SCRAP5 1
		ifdead
		{
			set countvarb 0
			whilevarn countvarb 3
			{
			   rand z 3072
			   add z 2048
			   mul z -1
			   ezshoot z CARXPLODE
			   randvar temp  2047
			   setactor[RETURN].ang temp 
			   randvar temp  192 add temp 128
			   setactor[RETURN].xvel temp 
			   add countvarb 1
			}
			state tech_debris
			sound VENT_BUST
			ifactor TANKWRECK2 state lite_jibs
			killit
		}
	}
ends

useractor notenemy TANKWRECK 150 state tankwreckcode enda
useractor notenemy TANKWRECK2 150 state tankwreckcode enda
useractor notenemy TANKWRECK3 120 state tankwreckcode enda

// RIOT TANK with Pigcop inside

action ATANKSPIN   0 2 5 1 4
action ATANKSHOOTING 10 2 5 1 10
action ATANKWAIT 0 1 5 1 1
action ATANKDESTRUCT 0 1 5 1 1
action ATANKDEAD 0 1 5 1 1
// move TANKVEL horiz vert
move TANKFORWARD 112
move TANKSTOP

eventloadactor TANK sizeat 60 60 enda

useractor enemy TANK TANKSTRENGTH 

  fall

  state monsterai
  ifn myspawner -1
  {
    ifg sprite[myspawner].statnum 2 set myspawner -1 else
	ifl sprite[myspawner].extra 1 set myspawner -1 else
	ife actorvar[myspawner].monstatus 2 set myspawner -1 else
	ifn sprite[myspawner].picnum PIGCOP ifn sprite[myspawner].picnum PIGCOPDIVE set myspawner -1
	
	ifn myspawner -1
	ifg sprite[myspawner].xvel 0
	// ifge sprite[].z sprite[myspawner].z
	{
		geta[].ang angvar
		add angvar 1024
		set x2 sprite[].x
		add x2 1024
		rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
		geta[myspawner].x temp
		sub x temp
		div x 20
		geta[myspawner].x x2
		add x2 x
		// seta[myspawner].x x2
		geta[myspawner].y temp
		sub y temp
		div y 20
		geta[myspawner].y y2
		add y2 y
		// seta[myspawner].y y2
		setsprite myspawner x2 y2 sprite[].z	
		// updatesectorz x y sprite[].z temp
		// ifn temp -1 setsprite myspawner x y sprite[].z
	}
  }
  // else findnearspritez PIGCOP 6144 16384 myspawner
  
  
  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  action ATANKDEAD
  
  ifg shrunken 0
  {
	clipdist 16
	cstat 256
	set temp shrunken
	add temp 1
	ife temp SHRUNKCOUNT { clipdist 100 cstator 257 }
  }
  ifaction 0
  {
    sizeat 60 60
    action ATANKWAIT
    cstat 257
    clipdist 100
	ifspritepal 24 orvar initflags 32
  }
  else
    ifaction ATANKSPIN
  {
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		ifactorsound THISACTOR TANK_ROAM nullop else sound TANK_ROAM
		setactorsoundpitch THISACTOR TANK_ROAM SHRUNKPITCH
	}
	else
    ifactorsound THISACTOR TANK_ROAM nullop else sound TANK_ROAM
	
	set temp NO
	ifge SKILL 4 { ifactioncount 10 set temp YES }
	else ifactioncount 20 set temp YES
	
	ife temp YES
    {
      ifrnd 16
        ife seemytarget YES
          ife shootmytarget YES
      {
        move TANKSTOP geth
        action ATANKSHOOTING
        stopactorsound THISACTOR TANK_ROAM
      }
    }

    ifrnd 16
      move TANKFORWARD seekplayer
  }
  else
    ifaction ATANKSHOOTING
  {
    ifactioncount 22
    {
      ifg targetdist 4096 ife shrunken 0
      {
        sound BOS1_ATTACK2
		ifvarand initflags 32
		{
			eshoot RPG
			setav[RETURN].mtype CANHEAD
		}
		else
        shoot MORTER
      }
      resetcount
      move 0 action ATANKWAIT
    }
    else
      ifactioncount 2
    {
      ife seemytarget YES
      {
        ifl targetdist 16384
        {
		  add countvar 1
          ifge countvar 2
          {
		    set countvar 0
		    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			{
				sound PISTOL_FIRE
				shoot SHRUNKBULLET
				setactorsoundpitch THISACTOR PISTOL_FIRE SHRUNKPITCH
			}
			else
			{
				espawn SOUNDPLATE
				setav[RETURN].SPRITELOTAG HEAVY_RIFLE
				setav[RETURN].mtype 1
				ifn bottarget -1
				{
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					geta[].z savz
					set z savz
					sub z 10240
					seta[].z z
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					seta[].z savz
				}
				else
				{
					shoot SHOTSPARK1
					shoot SHOTSPARK1
				}
			}
            
          }
        }
        else
        {
		  add countvar 1
          ifge countvar 4
		  {
		    ife countvarb 1 set countvarb 0 else set countvarb 1
			
		    set countvar 0
			  // ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				// {
					// sound PRED_ATTACK
					// setactorsoundpitch THISACTOR PRED_ATTACK SHRUNKPITCH
				// }
				// else
			  // {
			    // espawn SOUNDPLATE
				// setav[RETURN].SPRITELOTAG LASERSHOOT
				// setav[RETURN].mtype 1
			  // }
			  sound TERMLASER
			  eshoot GREENLASER 
			  set zdist sprite[RETURN].z
			  rand z 5120
			  sub zdist z
			  seta[RETURN].z zdist
			  
			  set angvar sprite[].ang
			  ife countvarb 0 add angvar 384 else sub angvar 384
			  set x2 sprite[].x
			  add x2 320
			  rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
				
			  setsprite RETURN x y zdist
		
			  // shoot FIRELASER
		  }
        }
      }
      else
      {
        move TANKFORWARD seekplayer
        action ATANKSPIN
      }
    }

    ifrnd 16
    {
      stopactorsound THISACTOR TANK_ROAM
      move TANKSTOP faceplayerslow
    }
  }
  else
    ifaction ATANKWAIT
  {
    ifactioncount 32
    {
      move TANKFORWARD seekplayer
      action ATANKSPIN
    }
  }
  else
    ifaction ATANKDESTRUCT
  {
    	
    ifactioncount 64
      action ATANKDEAD
    else
      ifactioncount 56
       sound LASERTRIP_ARMING
     else
       ifactioncount 48
         sound LASERTRIP_ARMING
     else
       ifactioncount 32
         sound LASERTRIP_ARMING
     else
       ifactioncount 16
         sound LASERTRIP_ARMING
		 
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	setactorsoundpitch THISACTOR LASERTRIP_ARMING SHRUNKPITCH
	
     break
  }
  else
    ifaction ATANKDEAD
  {
    addkills 1 state enemy_death
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	hitradius 1024 3 3 3 3
	else
	{
		hitradius 6144 TOUGH TOUGH TOUGH TOUGH
		ifpdistl 6144 seta[player[].i].htowner THISACTOR
	}
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		soundonce LASERTRIP_EXPLODE
		setactorsoundpitch THISACTOR LASERTRIP_EXPLODE SHRUNKPITCH
	}
	else
    sound LASERTRIP_EXPLODE
    debris SCRAP1 8
    spawn EXPLOSION2
	ife mtype 1
	{
		espawn TANKWRECK3
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	else
    ifrnd 160 
	{
		espawn PIGCOP 
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT 
		{
			setav[RETURN].shrunken 50 
			seta[RETURN].xrepeat 12
			seta[RETURN].yrepeat 10
		}
		else setav[RETURN].burning 120
		
		espawn TANKWRECK
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	else 
	{
		espawn TANKWRECK2
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	
	set countvarb 0
	whilevarn countvarb 2
	{
	   rand z 3072
	   add z 2048
	   mul z -1
	   ezshoot z CARXPLODE
	   randvar temp  2047
	   setactor[RETURN].ang temp 
	   randvar temp  192 add temp 128
	   setactor[RETURN].xvel temp 
	   add countvarb 1
	}
	state tech_debris
	state tech_debris
    killit
  }

 
  ife monstatus -1 break
  ifhitweapon
  {
    ifwasweapon SHRINKSPARK
	{
		sound ACTOR_SHRINKING
		set shrunken 1
	}
	else
    ifdead
	{
      action ATANKDEAD
	  ife padang 1337 {  addkills 1 state enemy_death state spawnoutline killit }
	}
    else
    {
      debris SCRAP1 1
      ifaction ATANKSHOOTING break

      ifrnd 192
      {
        move TANKSTOP geth
        action ATANKSHOOTING
        stopactorsound THISACTOR TANK_ROAM
      }
    }
  }

  set temp inithp
  div temp 2
  ifle sprite[].extra temp
  ifrnd 8
  {
	espawn BIGSMOKE
	seta[RETURN].pal 4
	seta[RETURN].htpicnum PIGSUV
  }
  
  ifpdistl 1280
    ifhitspace
      ifp pfacing
        ifangdiffl 512
		{
		  // ifstrength 400
		  ifle sprite[].extra temp
		  {
			  action ATANKDESTRUCT
			  set mtype 1
		  }
		  else
		  {
			  addphealth -2
			  soundonce SHORT_CIRCUIT
			  palfrom 32 63 63 63
		  }
		}
enda

state suvdyingstate

ifaction APIGSUVDEAD
{
	ifrnd 8
	{
		espawn BIGSMOKE
		seta[RETURN].pal 4
	}
	ifg sprite[].htextra 0
		ifspawnedby PIGCOP seta[].htextra -1
	
	ifhitweapon
	{
		debris SCRAP1 2
		debris SCRAP2 2
		debris SCRAP5 2
		ifdead
		{
			debris SCRAP1 6
			debris SCRAP2 6
			debris SCRAP5 6
			spawn SUVWHEEL
			spawn SUVWHEEL
			spawn SUVWHEEL
			killit
		}
	}
	break
}

espawn BIGSMOKE
seta[RETURN].pal 4

ifaction APIGSUVDYING
ifnotmoving
{
	stopactorsound THISACTOR SUVLOOP
	addkills 1
	set monstatus 2
	ifrnd SPAWNAMMOODDS spawn ATOMICHEALTH
	espawn MINEEXP
	set countvarb 0
	whilevarn countvarb 4
	{
	   rand z 3072
	   add z 2048
	   mul z -1
	   ezshoot z CARXPLODE
	   randvar temp  2047
	   setactor[RETURN].ang temp 
	   randvar temp  192 add temp 128
	   setactor[RETURN].xvel temp 
	   add countvarb 1
	}
	// also spawn debris and license plate
	globalsound ROBOT_EXPLODE
	ifpdistl 8192 quake 20
	state enemy_death
	action APIGSUVDEAD
	ifhitweapon { }
	strength 150
	move PIGSUVSTOPPED geth
	ifrnd 128 spawn BURNING else spawn BURNING2
	espawn PIGCOP
	seta[RETURN].pal sprite[].pal
	
	ifrnd 128
	{
		espawn PIGCOP
		seta[RETURN].pal sprite[].pal
	}
	else state standard_jibs
	ifrnd 128
	{
		espawn PIGCOP
		seta[RETURN].pal sprite[].pal
	}
	else state standard_jibs
}

ends

defstate forceforward

ifnotmoving // force to move forward slightly
{
	set x sprite[].x
	add x 64
	rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang y y2
	geta[].sectnum tempb
	updatesectorz y y2 sprite[].z tempb
	ifvarn tempb -1 setsprite THISACTOR y y2 sprite[].z
}

ends

defstate spritecollision

	geta[].htmovflag spriteid
	add spriteid 16384
	ifl spriteid 16384 ifg spriteid -1 // hit a sprite
	{
		// ifmove PIGSUVSPEED1 { ifai AISUVDYING nullop else move PIGSUVSTOPPED geth } else
		ifmove PIGSUVSPEED2 { move PIGSUVSPEED1 geth } else
		ifmove PIGSUVSPEED3 { move PIGSUVSPEED2 geth } else
		ifmove PIGSUVSPEED4 { move PIGSUVSPEED3 geth }
		seta[spriteid].htextra 30
		seta[spriteid].htpicnum RPG
		seta[spriteid].htowner THISACTOR
		seta[spriteid].htang lastang
		ife sprite[spriteid].picnum APLAYER sound SQUISHED
		
	}
	

ends

state pigsuvcode

	seta[].ang lastang
	state spritecollision
	ifai AISUVDYING { state suvdyingstate break }
	
	ife SPRITELOTAG -1
	{
		seta[].mdflags 16
		geta[].ang lastang
		// find first waypoint
		// get the nearest one, then use the NEXT one as target
		set xydist 99999
		set hitag highrecon
		add hitag 1
		set B 0
		whilevarvarn B hitag
		{
			set spriteid reconpoints[B]
			ldist temp THISACTOR spriteid
			ifl temp xydist { set xydist temp set SPRITELOTAG B }
			add B 1
		}
		add SPRITELOTAG 1
		ifg SPRITELOTAG highrecon set SPRITELOTAG 0
		move PIGSUVSPEED2 geth
		sound SUVACCEL
	}
	ifmove PIGSUVSTOPPED
	{
		add countvar 1
		ifg countvar 30 move PIGSUVSPEED1 geth
	}
	else
	{
		set target reconpoints[SPRITELOTAG]
		geta[target].x x2
		geta[target].y y2
		geta[].x x
		geta[].y y
		sub x2 x
		sub y2 y
		getangle angvar x2 y2
		
		getincangle tempd angvar lastang	
			
		set tempe tempd
		ifl tempe 0 mul tempe -1 
		ifl tempe 24 
		{
			ifmove PIGSUVSPEED3 nullop else 
			ifmove PIGSUVSPEED4 nullop else 
				ifcount 10 move PIGSUVSPEED3 geth 
			ifmove PIGSUVSPEED3 ifcount 10 move PIGSUVSPEED4 geth
			ifactorsound THISACTOR SUVACCEL nullop else sound SUVACCEL
		} 
		else
		ifl tempe 72
		{
			ifmove PIGSUVSPEED2 nullop else 
			{
				ifcount 10 move PIGSUVSPEED2 geth 
				else // squeel tires and drift
				{
					// ifactorsound THISACTOR TIRESLIP nullop else ifg tempe 48 sound TIRESLIP
				}
			}
		}
		else
		{
			ifmove PIGSUVSPEED1 nullop else move PIGSUVSPEED1 geth
			ifcount 20 nullop else // squeel tires and drift
			{

				ifactorsound THISACTOR TIRESLIP nullop else
				{
					ifg tempe 96
					sound TIRESLIP
				}
				spawn BIGSMOKE
				
			}
		}

		ifl tempd 24 ifvarg tempd -24
		set lastang angvar
		else
		{
			div tempd 12
			ifg tempd 0 sub lastang tempd else
			ifl tempd 0 { mul tempd -1 add lastang tempd }
		}
		seta[].ang lastang
		ldist xydist THISACTOR target

		ifg xydist 844 ifl xydist 1280 { set lastang angvar seta[].ang angvar }
		
		ifvarl xydist 844
		{
			add SPRITELOTAG 1
			ifg SPRITELOTAG highrecon set SPRITELOTAG 0
		}
	}
	ifactorsound THISACTOR SUVLOOP nullop else sound SUVLOOP
	
	state targetsearch
	ife bottarget player[].i set bottarget -1
	ifn bottarget -1
	{
		geta[].z z
		subvar z 8192
		seta[].z z
		geta[bottarget].z tempb
		ifle tempb z set targethigher YES else set targethigher NO
		subvar tempb 8192
		seta[bottarget].z tempb
		canseespr THISACTOR bottarget seemytarget
		add z 8192
		seta[].z z
		add tempb 8192
		seta[bottarget].z tempb
		ife seemytarget YES set shootmytarget YES // temp stand in for hitscan verification
		dist targetdist THISACTOR bottarget
		ifn actorvar[bottarget].monstatus 2 set targetalive YES
		else set targetalive NO
	}
	else // assume player is target
	{
		ifp phigher set targethigher YES else set targethigher NO
		ife team 1 // player ally
		{
			set seemytarget NO
			set shootmytarget NO
			set targetdist 99999
			set targetalive NO
		}
		else
		{
			ifcansee set seemytarget YES else set seemytarget NO
			ifcanshoottarget set shootmytarget YES else set shootmytarget NO
			findplayer targetdist
			ifp palive set targetalive YES else set targetalive NO
		}
	}
	
	ifaction APIGSUV1
	{
		ife targetalive YES
		ifl targetdist 16384
			ife seemytarget YES
				ifactioncount 4
		{
			action APIGSUV2
			set countvar 15
			 ifrnd 128
			{
			  ifrnd 32
				soundonce PIG_ROAM
			  else
				ifrnd 64
				  soundonce PIG_ROAM2
			  else
				soundonce PIG_ROAM3
			}
		}
	}
	ifaction APIGSUV2
	{
		add countvar 1
		ife countvar 13 sound SHOTGUN_COCK
		ifactioncount 2
		{
			ife targetalive YES
			ifl targetdist 16384
				ife seemytarget YES
					ife shootmytarget YES
			{
				action APIGSUV3
				ifn bottarget -1 set spriteid bottarget else
				getp[].i spriteid
				state facesprite
				ifn bottarget -1
				{
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
				}
				else
				{
					shoot SHOTSPARK1 shoot SHOTSPARK1
					shoot SHOTSPARK1 shoot SHOTSPARK1
					shoot SHOTSPARK1 shoot SHOTSPARK1
					shoot SHOTSPARK1 shoot SHOTSPARK1
				}
				seta[].ang lastang
				sound PIG_ATTACK
				set countvar 0
			}
			else
			action APIGSUV1
		}
	}
	ifaction APIGSUV3
	{
		ifactioncount 1
		{
			action APIGSUV2
			set countvar 0
		}
	}
	
	ifstrength 200 ifrnd 12 { espawn BIGSMOKE seta[RETURN].pal 4 }
	ifstrength 150 ifrnd 12 { espawn BIGSMOKE seta[RETURN].pal 4 }
	ifstrength 100  ifrnd 12 { espawn BIGSMOKE seta[RETURN].pal 4 }
	ifstrength 50  ifrnd 12 { espawn BIGSMOKE seta[RETURN].pal 4 }
	
	ifg sprite[].htextra 0
	{
		geta[].htextra temp
		ifwasweapon RADIUSEXPLOSION { mul temp 2 seta[].htextra temp }
		ifwasweapon RPG { mul temp 2 seta[].htextra temp }
	}
	ifhitweapon
	{
		ifdead
		{
			ai AISUVDYING
			spawn SUVPLATE
			sound TIRESLIP
			sound PIG_DYING
			rand temp 128
			sub temp 64
			add lastang temp
			seta[].ang lastang
			// add custom debris later
			debris SCRAP1 3
			debris SCRAP2 3
			debris SCRAP6 3
			spawn SUVWHEEL
			
		}
	}
	
ends

useractor enemy PIGSUV PIGSUVSTRENGTH
	ifaction 0 
	{
		action APIGSUV1
		set SPRITELOTAG -1
		cstat 257
		sizeat 48 40
		clipdist 80
		geta[].htflags temp
		orvar temp 8192
		seta[].htflags temp
	}
	fall
	cstat 257
	sizeat 48 40
	state monsterai
	state pigsuvcode
enda

move WHEELROLL 256
useractor notenemy SUVWHEEL 0

ifmove 0
{
	sizeat 40 40
	getplayer[].i spriteid
	state facesprite
	set temp 512
	rand tempb 256
	sub tempb 128
	add temp tempb
	ifrnd 128 add angvar temp else sub angvar temp
	seta[].ang angvar
	move WHEELROLL geth
	seta[].mdflags 16
	rand countvarb 2
}
fall

ifmove WHEELROLL
{
	ife countvar 0 cstat 16 else
	ife countvar 1 cstat 20 else
	ife countvar 2 cstat 24 else
	ife countvar 3 cstat 28
	
	add countvar 1
	ife countvar 4 set countvar 0
	
	ifnotmoving
	{
		add countvarb 1
		
		geta[].htmovflag spriteid
		add spriteid 16384
		ifl spriteid 16384 ifg spriteid -1 // hit a sprite
		{
			geta[spriteid].ang angvar
			geta[].ang temp
			getincangle tempc angvar temp
			sub angvar tempc
			add angvar 1024
			setactor[].ang angvar // bouncing angle
		}
		else
		ifle spriteid 0 // hit wall
		{
			add spriteid 16384
			getw[spriteid].x x
			getw[spriteid].y y
			getw[spriteid].point2 B
			getw[B].x x2
			getw[B].y y2
			sub x2 x
			sub y2 y
			getangle angvar x2 y2
			geta[].ang temp
			getincangle tempc angvar temp
			sub angvar tempc
			setactor[].ang angvar // bouncing angle
		}

		ifg countvarb 2
		{
			cstat 32
			move PIGSUVSTOPPED
		}
	}
}

ifmove WHEELROLL
ifcount 240
{
	cstat 32
	move PIGSUVSTOPPED
}
enda

action PLATEALTFRAME 1 1 1 1 1
move PLATEVELS 768
useractor notenemy SUVPLATE 0
	ifaction 0
	{
		seta[].z player[].posz
		ifrnd 128 action PLATEALTFRAME
		move PLATEVELS faceplayer
		cstat 17
		sizeat 36 36
	}
	ifcount 10 nullop else ifcount 9 move PLATEVELS geth
	
	ifmove PLATEVELS
	{
		ifcount 40 
		{
			fall
			iffloordistl 4
			{
				cstat 32
				sizeat 20 20
				move PIGSUVSTOPPED
			}
		}
		ifnotmoving
		{
			cstat 32
			sizeat 20 20
			move PIGSUVSTOPPED
		}
	}
	ifmove PIGSUVSTOPPED
	fall
	
	
enda

// EDF DRONE

action ADRONEHOVER  0  1  7  1  10
action ADRONESHOOT  7  1  7  1  10
action ADRONEHURT   14 1  7  1  10

move EDFHOVERVEL 0 -14
move EDFDRONEDIEVEL 256 2

ai AIDRONEDYING ADRONEHURT EDFDRONEDIEVEL geth getv

state edfdronedead
  stopactorsound THISACTOR HOVERLOOP
  ifrnd 128 sound ROBOT_EXPLODE else sound EXPLODE4
  spawn EXPLOSION2
  debris SCRAP1 6
  debris SCRAP2 3
  debris SCRAP3 5
  state tech_debris
  ifactor NEWSENTRY
  {
	ifrnd 128 state drop_ammo else state drop_battery
  }
  killit

ends

state dronediestate

	ifrnd 16
	{
		stopactorsound THISACTOR HOVERLOOP
		ifrnd 128
		{
			sound HOVERLOOP
			rand temp 512
			add temp 768
			setactorsoundpitch THISACTOR HOVERLOOP temp
		}
	}
	ifrnd 64 { espawn BIGSMOKE seta[RETURN].htpicnum RECONWRECK }
	espawn SPARKFALL
	rand x 256 sub x 128
	rand y 256 sub y 128
	geta[RETURN].x x2 add x2 x seta[RETURN].x x2
	geta[RETURN].y y2 add y2 y seta[RETURN].y y2
	espawn SPARKFALL
	rand x 256 sub x 128
	rand y 256 sub y 128
	geta[RETURN].x x2 add x2 x seta[RETURN].x x2
	geta[RETURN].y y2 add y2 y seta[RETURN].y y2
	geta[].ang angvar
	add angvar 48
	seta[].ang angvar
	
	iffloordistl 8 state edfdronedead else
	ifcount 300 state edfdronedead else
	ifnotmoving state edfdronedead

ends

state droneclimbcheck

	ifceilingdistl 96 break
	geta[].z z
	sub z 4096
	
	cos mycos sprite[].ang
	sin mysin sprite[].ang
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 1024
		{
			sub z 8192
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 1560 { movesprite THISACTOR 0 0 -3072 CLIPMASK0 RETURN }
		}
	}
ends

state edfdronestartwander

	resetcount
	set B 0
	set xydist 2048
	whilevarn B 32
	{
		add B 1
		geta[].z z
		sub z 4096
		rand angvar 2047
		cos mycos angvar
		sin mysin angvar
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz clipmask
		
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		
		ifg startx xydist set B 32
	}
	seta[].ang angvar
	set angvel angvar

ends

state dronemovement

	ifactor NEWSENTRY ifn mtype 0
	{
		ifn bottarget -1 set mtype 0
		else break
	}
	ifn thiscam -1 break
	ifn padmove 0 { set angvel padang break }
	
	ifg countvarc 0 sub countvarc 1

	ifn myspawner -1
	{
		ife dodgetime 0
		ife countvarc 0
		{
			set navpoint -1
			set dodgetime 10
			set countvarc 30
			geta[myspawner].x x
			geta[myspawner].y y
			sub x sprite[].x
			sub y sprite[].y
			getangle angvel x y
			ifrnd 128 sub angvel 512 else add angvel 512
		}
		set myspawner -1
	}
	cos xvel angvel
	sin yvel angvel
	shiftr xvel 6
	shiftr yvel 6
	
	ifg navpoint -1 
	{
		geta[navpoint].z zdist
		ife sprite[navpoint].picnum BLUEFLAG sub zdist 4096 else
		ife sprite[navpoint].picnum REDFLAG sub zdist 4096 else
		ifactor NEWSENTRY ife gametype 0 sub zdist 6144 else
		sub zdist 3072
		sub zdist sprite[].z
		
		ldist xydist THISACTOR navpoint
		ife xydist 0 set xydist 1
		shiftl zdist 8
		div zdist xydist
	} 
	else
	ife dodgetime 0
	{
		ifn bottarget -1
		{
			geta[bottarget].z zdist
			sub zdist 6144
			ife gametype 0 sub zdist 2048 else
			ife gametype SURVIVAL sub zdist 2048
			sub zdist sprite[].z
			geta[bottarget].x x
			geta[bottarget].y y
			sub x sprite[].x
			sub y sprite[].y
			getangle angvel x y
			
			ifl targetdist 4096 { shiftr xvel 1 shiftr yvel 1 add angvel 1024 } else
			ifl targetdist 8192 { set xvel 0 set yvel 0 }
			
		}
		else
		{
			// random wandering
			set zdist 0
			ifactor NEWSENTRY ife gametype 0 iffloordistl 64 set zdist -256 else
			iffloordistl 32 set zdist -256 else
			ifceilingdistl 64 set zdist 256
			
			ifcount 150 state edfdronestartwander
		}
	}
	
	ifl zdist -3072 set zdist -3072
	ifg zdist 3072 set zdist 3072
	
	ifactor NEWSENTRY ife gametype 0 shiftr zdist 1
	
	movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
	
	ifn RETURN 0
	{
		// bumping something
		sub RETURN 49152
		ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
		{
			set B NO
			ife actorvar[RETURN].monstatus 1 
				ife actorvar[RETURN].team team set B YES
			ifg gametype 0 ifg navpoint -1
			ifvarand sprite[RETURN].cstat 16 set B YES
			ife B YES
			{
				set x2 sprite[].x
				add x2 128
				rotatepoint sprite[].x sprite[].y x2 sprite[].y angvel x y
				setsprite THISACTOR x y sprite[].z
				seta[].htmovflag 0
			}
		}
		else ifg navpoint -1 { set navpoint -1 count 160 set crumbwait 8 }
	}

	
	
	ifg zdist -257 state droneclimbcheck

ends

state edfdroneshoot

	ifaction ADRONESHOOT action ADRONEHOVER
	add countvar 1
	ifg countvar 2
	{
		set target bottarget
		state friendlyfirecheck
		ife target -1 
		{ 
			set bottarget -1 
			add B 1024
			set angvar B
			state moveatangvar
			break 
		}
		
		set countvar 0
		state hitscan_targetprep
		zshoot zdist SHOTSPARK1
		sound DPCHAINGUN
		action ADRONESHOOT
	}

ends

state newdronecode

	ifaction 0
	{
		action ADRONEHOVER
		cstator 257
		ifactor NEWSENTRY sizeat 22 20 else
		sizeat 34 32
		clipdist 42
		move EDFHOVERVEL geth getv
		orvar monstflags YES
		ifactor EDFDRONE
		{
			set team 1
			set navmode 3
		}
		ifactor NEWSENTRY
		{
			set team 0
			set navmode 4
		}
		ifactor EDFDRONE
		ife gametype SURVIVAL addstrength EDFDRONESTRENGTH
	}
	fall
	ife thiscam -1 add countvarb 1
	ife countvarb 35 { sound HOVERLOOP set countvarb 0 }
	// ifactorsound THISACTOR HOVERLOOP nullop else sound HOVERLOOP
	ifai AIDRONEDYING { state dronediestate break }
	
	state monsterai
	ife team 1 state summondespawn
	ife bottarget -1 add FEMFALLDMG 1 else
	set FEMFALLDMG 0
	ifg FEMFALLDMG 300
	ifp palive
	ifg FEMKILLCOUNT 0
	{
		set FEMFALLDMG 0
		sound TELEPORTER
		spawn GLARESTAR
		state spawnoutline
		setsprite THISACTOR player[].posx player[].posy player[].posz
		sound TELEPORTER
		spawn GLARESTAR
		state spawnoutline
	}
	set stun 0
	
	ife bottarget -1 ife dodgetime 0 seta[].ang angvel
	state dronemovement
	
	ife padmove 0
	{
		set zdist 0
		iffloordistl 32 set zdist -256 else
		ifceilingdistl 64 set zdist 256
		
		ifn zdist 0
		movesprite THISACTOR 0 0 zdist CLIPMASK0 RETURN
	}
	
	ifn bottarget -1 state edfdroneshoot
	else ifaction ADRONESHOOT action ADRONEHOVER
	
	ifsquished	
	{ geta[].htextra temp add temp 20 seta[].htextra temp seta[].htpicnum RPG }
	ifstrength 40 ifrnd 8
	{
		espawn BIGSMOKE
		seta[RETURN].htpicnum TANKWRECK
	}
	ifhitweapon 
	{
		ifdead 
		{
			state enemy_death
			ai AIDRONEDYING
			set monstatus 2
			ifvarand monstflags YES xorvar monstflags YES
			set bottarget -1
			stopactorsound THISACTOR HOVERLOOP
			sound HOVERLOOP
			rand temp 512
			add temp 768
			setactorsoundpitch THISACTOR HOVERLOOP 256
			ife padang 1337 { ifrnd 128 state drop_ammo else state drop_battery state spawnoutline killit }
			break
		}
	}

ends

useractor notenemy EDFDRONE EDFDRONESTRENGTH

	state newdronecode
	// state summondespawn

enda

useractor enemy NEWSENTRY EDFDRONESTRENGTH

	state newdronecode

enda

eventloadactor PATROLSENTRY sizeat 32 30 add maxkills 1 enda

useractor notenemy PATROLSENTRY EDFDRONESTRENGTH

	ifaction 0
	{
		action ADRONEHOVER
		cstat 257
		sizeat 32 30
		geta[].ang lastang
		set SPRITELOTAG -1
	}
	
	ife thiscam -1 add countvarb 1
	ife countvarb 35 { sound HOVERLOOP set countvarb 0 }
	
	seta[].ang lastang
		
	ife SPRITELOTAG -1
	{
		// seta[].mdflags 16

		// find first waypoint
		// get the nearest one, then use the NEXT one as target
		set xydist 99999
		set hitag highrecon
		add hitag 1
		set B 0
		whilevarvarn B hitag
		{
			set spriteid reconpoints[B]
			ldist temp THISACTOR spriteid
			ifl temp xydist { set xydist temp set SPRITELOTAG B }
			add B 1
		}
		add SPRITELOTAG 1
		ifg SPRITELOTAG highrecon set SPRITELOTAG 0
		move PIGSUVSPEED2 geth
	}
	

	ifmove PIGSUVSTOPPED
	{
		add countvar 1
		ifg countvar 30 move PIGSUVSPEED1 geth
	}
	else
	{
		set target reconpoints[SPRITELOTAG]
		geta[target].x x2
		geta[target].y y2
		geta[].x x
		geta[].y y
		sub x2 x
		sub y2 y
		getangle angvar x2 y2
		
		getincangle tempd angvar lastang	
			
		set tempe tempd
		ifl tempe 0 mul tempe -1 
		ifl tempe 24 
		{
			ifmove PIGSUVSPEED3 nullop else 
			ifmove PIGSUVSPEED4 nullop else 
				ifcount 10 move PIGSUVSPEED3 geth 
			ifmove PIGSUVSPEED3 ifcount 10 move PIGSUVSPEED4 geth
		} 
		else
		ifl tempe 72
		{
			ifmove PIGSUVSPEED2 nullop else 
			{
				ifcount 10 move PIGSUVSPEED2 geth 
			}
		}
		else
		{
			ifmove PIGSUVSPEED1 nullop else move PIGSUVSPEED1 geth
		}

		ifl tempd 36 ifvarg tempd -36 // 24
		set lastang angvar
		else
		{
			div tempd 9 // 12
			ifg tempd 0 sub lastang tempd else
			ifl tempd 0 { mul tempd -1 add lastang tempd }
		}
		seta[].ang lastang
		ldist xydist THISACTOR target
	
		ifvarl xydist 844
		{
			add SPRITELOTAG 1
			ifg SPRITELOTAG highrecon set SPRITELOTAG 0
		}
		else
		{
			geta[target].z z
			sub z sprite[].z
			mul z sprite[].xvel
			div z xydist
			movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
		}
	}
	ifg sprite[].htextra 0
	{
		set SPRITELOTAG 0
		cactor NEWSENTRY
		action ADRONEHOVER
		cstator 257
		clipdist 42
		move EDFHOVERVEL geth getv
		orvar monstflags YES
		set navmode 4
		// getp[].max_actors_killed temp
		// add temp 1
		// setp[].max_actors_killed temp
	}

enda

action ARECONFLY  0  1  7  1  10
action ARECONTILT 7  1  7  1  10

move RECONDYINGVELS 256 12
move RECONSPEED1 160
move RECONSPEED2 192
move RECONSPEED3 256
move RECONSPEED4 320
move RECONSPEED5 400

ai AIRECONDYING ARECONTILT RECONDYINGVELS geth getv
ai AIRECONSHOOT ARECONFLY STOPPED geth getv

defstate checkreconhit
	ifhitweapon
	{
		ifactorsound THISACTOR PIG_PAIN nullop else sound PIG_PAIN
		ifdead
		{
			ife padang 1337 { state enemy_death state spawnoutline killit }
			strength 1 
			ai AIRECONDYING
			set countvar 0
			set countvarb 0
			whilevarn countvarb 4
			{
			   rand z 3072
			   add z 2048
			   mul z -1
			   ezshoot z CARXPLODE
			   randvar temp  2047
			   setactor[RETURN].ang temp 
			   randvar temp  192 add temp 128
			   setactor[RETURN].xvel temp 
			   add countvarb 1
			}
			set countvarb 0
		}
		else ifai AIRECONSHOOT nullop else
		ifn bottarget -1
		ife bottarget sprite[].htowner
		ifl targetdist MAXRANGE
			ai AIRECONSHOOT
	}
ends

state recondead
  addkills 1
  
	espawn NEWPIG
	setav[RETURN].monstflags monstflags
	setav[RETURN].team team
	setactor[RETURN].pal sprite[THISACTOR].pal
  
  state enemy_death
  debris SCRAP1 12
  debris SCRAP2 6
  debris SCRAP3 10
  spawn EXPLOSION2
  sound RPG_EXPLODE
  state tech_debris
  hitradius 1560 5 10 15 20
  ifpdistl 1560 seta[player[].i].htowner THISACTOR
  espawn RECONWRECK
  seta[RETURN].pal sprite[].pal
  killit
  
ends

defstate recondying

	fall
	strength 1
	getactor[].htextra temp
	ifvarl temp 50 
	{
	   setactor[].htextra -1
	   setactor[].htpicnum 0
	}
	else state recondead
	
	add initflags 1

	iffloordistl 8 state recondead else
	ifnotmoving state recondead else
	ifg initflags 300 state recondead
	
	addvar countvar 1
	ifvarg countvar 5
	{
		addvar countvarb 1
		ifvarg countvarb 3 setvar countvarb 0
		ifrnd 64 sound RPG_EXPLODE
		espawn EXPLOSION2
		ifvarn countvarb 0 
		setactor[RETURN].htflags 256
		espawn BIGSMOKE
		seta[RETURN].htpicnum PIGSUV
		setvar countvar 0
	}

	  getactor[THISACTOR].ang angvar
	  subvar angvar 48
	  setactor[THISACTOR].ang angvar

ends

state reconshootstate

	state predamage
	state monsterai
	state targetsearch
	add countvarc 1
	ifge countvarc 150 ifrnd 2 set bottarget -1
	ife bottarget -1
	{
		move PIGSUVSTOPPED
		set SPRITELOTAG -1
		set mtype 0
		set countvarc 0
		set botclip 0
		ai 0
		ifvarand monstflags 1 xorvar monstflags 1
		break
	}
	
	geta[bottarget].x x2
	geta[bottarget].y y2
	sub x2 sprite[].x
	sub y2 sprite[].y
	getangle tempb x2 y2
	
	getincangle temp sprite[].ang tempb
	
	ifn sprite[].ang tempb
	{
		set B MAXRECONTURN
		mul B -1
		ifg temp B ifl temp MAXRECONTURN
			seta[].ang tempb
		else
		{
			geta[].ang angvar
			ifl temp 0 sub angvar MAXRECONTURN
			else add angvar MAXRECONTURN
			seta[].ang angvar
		}
		set lastang sprite[].ang
	}
	abs temp
	
	ifl temp 64 // temp is angle difference
	{
		ifcount 3
		{
			resetcount
			add botclip 1
			ifg botclip 0
			{
				ifge botclip 10 ifrnd 64
				{
					set botclip -20
					eshoot MORTER2
					seta[RETURN].ang sprite[].ang
					sound GRENADE_SHOOT
				}
				else
				{
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					sound SHELLYFIRE
					spawn SHELL
					geta[].z savz, add savz 7680 seta[].z savz
					geta[].x savx geta[].y savy
					
					set x savx
					add x 240
					geta[].ang angvar, sub angvar 368
					rotatepoint savx savy x savy angvar x2 y2
					setsprite THISACTOR x2 y2 savz
					spawn AIRFLASH1
					set x savx
					addvar x 240
					geta[].ang angvar, add angvar 368
					rotatepoint savx savy x savy angvar x2 y2
					setsprite THISACTOR x2 y2 savz
					spawn AIRFLASH1
					
					sub savz 7680
					setsprite THISACTOR savx savy savz
				}
			}
		}
	}
	
	ifn myspawner -1
	{
		ife dodgetime 0
		ifrnd 32
		{
			set dodgetime 10
			geta[myspawner].x x
			geta[myspawner].y y
			sub x sprite[].x
			sub y sprite[].y
			getangle angvel x y
			ifrnd 128 sub angvel 512 else add angvel 512
		}
		set myspawner -1
	}

	ife dodgetime 0 { set xvel 0 set yvel 0 }
	else
	{
		cos xvel angvel
		sin yvel angvel
		shiftr xvel 7
		shiftr yvel 7
	}

	geta[bottarget].z z
	sub z sprite[].z
	shiftr z 8
	movesprite THISACTOR xvel yvel z CLIPMASK0 RETURN
	
	state checkreconhit

ends

useractor enemy NEWRECON RECONSTRENGTH ARECONFLY

	ifmove 0
	{
		move PIGSUVSTOPPED
		cstat 257
		sizeat 24 24
		spriteflags 2107392 // 10240
		
		geta[].ang lastang
		set SPRITELOTAG -1
		set initflags 0
	}
	
	ifai AIRECONDYING { setvar bottarget -1 state recondying break }
	ifactorsound THISACTOR DUKE_JETPACK_IDLE nullop else sound DUKE_JETPACK_IDLE

	ifai AIRECONSHOOT { state reconshootstate break }
	
	seta[].ang lastang
		
	ife SPRITELOTAG -1
	{
		// seta[].mdflags 16

		// find first waypoint
		// get the nearest one, then use the NEXT one as target
		set xydist 99999
		set hitag highrecon
		add hitag 1
		set B 0
		whilevarvarn B hitag
		{
			set spriteid reconpoints[B]
			ldist temp THISACTOR spriteid
			ifl temp xydist { set xydist temp set SPRITELOTAG B }
			add B 1
		}
		add SPRITELOTAG 1
		ifg SPRITELOTAG highrecon set SPRITELOTAG 0
		move RECONSPEED2 geth
	}
	
	state recontilt
	set temp mtype
	abs temp
	ifmove PIGSUVSTOPPED action ARECONFLY else
	ifl temp 160 action ARECONFLY else
	action ARECONTILT

	ifmove PIGSUVSTOPPED
	{
		add countvar 1
		ifg countvar 30 move RECONSPEED1 geth
	}
	else
	{
		set target reconpoints[SPRITELOTAG]
		geta[target].x x2
		geta[target].y y2
		geta[].x x
		geta[].y y
		sub x2 x
		sub y2 y
		getangle angvar x2 y2
		
		getincangle tempd angvar lastang

		set tempe tempd
		ifl tempe 0 mul tempe -1 
		ifl tempe 24 
		{
			ifmove RECONSPEED3 nullop else 
			ifmove RECONSPEED4 nullop else 
			ifmove RECONSPEED5 nullop else
				ifcount 10 move RECONSPEED3 geth 
				
			ifmove RECONSPEED3 ifcount 10 move RECONSPEED4 geth
			else
			ifmove RECONSPEED4 ifcount 20 move RECONSPEED5 geth
		} 
		else
		ifl tempe 72
		{
			ifmove RECONSPEED2 nullop else 
			{
				ifcount 10 move RECONSPEED2 geth 
			}
		}
		else
		{
			ifmove RECONSPEED1 nullop else move RECONSPEED1 geth
		}

		ifl tempd 36 ifvarg tempd -36 // 24
		set lastang angvar
		else
		{
			div tempd 9 // 12
			ifg tempd 0 sub lastang tempd else
			ifl tempd 0 { mul tempd -1 add lastang tempd }
		}
		seta[].ang lastang
		ldist xydist THISACTOR target
	
		ifvarl xydist 844
		{
			add SPRITELOTAG 1
			ifg SPRITELOTAG highrecon set SPRITELOTAG 0
			set target reconpoints[SPRITELOTAG]
		}
		else
		{
			geta[target].z z
			sub z sprite[].z
			mul z sprite[].xvel
			div z xydist
			movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
			ifn RETURN 0 add float 1
		}
		ifnotmoving add float 1
		else set float 0
		ifge float 30
		{
			set float 0
			seta[].ang angvar
			setsprite THISACTOR sprite[target].x sprite[target].y sprite[target].z
		}
	}
	state predamage
	state monsterai
	state targetsearch
	state checkreconhit
	
	ifai AIRECONSHOOT break
	ifangdiffl 512 nullop else
	ifn bottarget -1
	ifrnd 1
	{
		ai AIRECONSHOOT
		orvar monstflags YES
	}

enda

// EDFTANK size 60 47
// EDFTANKTURRET size 56 56
// turret distance from ground 14336

eventloadactor EDFTANK
	sizeat 60 47
	cstat 257
	espawn EDFTANKTURRET
	set myspawner RETURN
	setav[RETURN].myspawner THISACTOR
	seta[RETURN].pal sprite[].pal
	geta[].z z, sub z 14336, seta[RETURN].z z
	seta[RETURN].xrepeat 56
	seta[RETURN].yrepeat 56
	geta[].ang lastangvel
	geta[].hitag initflags
	seta[].hitag 0
enda

eventloadactor EDFTANKWRECK
	sizeat 60 47
	cstat 257
	
enda

move TANKSTOPPED
move TANKVEL0 48
move TANKVEL1 96
move TANKVEL2 208
move TANKVEL3 320

action AEDFTANKSIT
action AEDFTANKRUN  0  2  1  1  8
action AEDFTANKWRECK  3  1  1  1  1

defstate tankgofaster

	ifmove TANKSTOPPED move TANKVEL0 geth 
	else
	ifmove TANKVEL0 move TANKVEL1 geth else
	ifmove TANKVEL1 move TANKVEL2 geth else
	ifn navmode -1
	ifmove TANKVEL2 move TANKVEL3 geth
	

ends

defstate tankgoslower

	ifmove TANKVEL3 move TANKVEL2 geth else
	ifmove TANKVEL2 move TANKVEL1 geth else
	ifmove TANKVEL1 move TANKVEL0 geth else
	ifmove TANKVEL0 move TANKSTOPPED geth

ends

defstate ptankride

ldist TMP_A THISACTOR player[].i
ifp phigher
{
	ifl TMP_A 2048
	{
		set z sprite[].z
		sub z player[].posz
		ifg z 28672
			cstat 256
		else cstat 257
	}
	else cstat 257	
}
else cstat 257

ifp pjetpack nullop else
ifp palive ifp phigher
ifpdistl 2560
ife player[].poszv 0
{
	sub startx sprite[].x
	sub starty sprite[].y
	getp[].posx x, sub x startx, setp[].posx x
	getp[].posy y, sub y starty, setp[].posy y
}

ends

useractor enemy EDFTANK 1500

ifaction AEDFTANKWRECK
{
	fall
	ifcount 3 state tankgoslower
	ldist TMP_A THISACTOR player[].i
	ifp phigher
	{
		ifl TMP_A 2048
		{
			set z sprite[].z
			sub z player[].posz
			ifg z 28672
				cstat 256
			else cstat 257
		}
		else cstat 257	
	}
	else cstat 257
	break
}

ifaction 0
{
	// getp[].max_actors_killed temp
	// sub temp 1
	// setp[].max_actors_killed temp
	geta[].htflags temp, orvar temp 8192, orvar temp 1048576, seta[].htflags temp
	cstat 257
	sizeat 60 47
	clipdist 128
	ife initflags 0
	{
		action AEDFTANKRUN
		move TANKVEL1 geth
		set navmode 1
		seta[].mdflags 16
	}
	else 
	{
		action AEDFTANKSIT
		move TANKSTOPPED
		set navmode -1
	}
	set team 1
	
	ife myspawner -1
	{
		espawn EDFTANKTURRET
		set myspawner RETURN
		seta[RETURN].pal sprite[].pal
		setav[RETURN].myspawner THISACTOR
		geta[].z z, sub z 14336, seta[RETURN].z z
		seta[RETURN].xrepeat 56
		seta[RETURN].yrepeat 56
		
	}
}
seta[].ang lastangvel

ifle initflags 0
{
	ifactorsound THISACTOR TANK_RUN nullop else 
	{
		sound TANK_RUN
		set temp sprite[].xvel, shiftl temp 2
		setactorsoundpitch THISACTOR TANK_RUN temp
	}
}

fall
state ptankride
ifg sprite[].zvel 0 add FEMFALLDMG 1 else
ifg FEMFALLDMG 0
{
	ifg FEMFALLDMG 5
	{
		sound CYBOSS_LAND
		setactorsoundpitch THISACTOR CYBOSS_LAND -384
		ifpdistl 6144 quake 20
		shiftl FEMFALLDMG 3
		seta[].htextra FEMFALLDMG
		seta[].htowner THISACTOR
		seta[].htpicnum RPG
	}
	set FEMFALLDMG 0
}

ifaction AEDFTANKSIT
{
	ife initflags GRAVESNPC
	{
		set monstatus 0
		set team 3
		strength 2000
		set inithp 2000
		set mlevel 10
		seta[].htextra -1
	}
	ife initflags -22460 // activated by Graves
	{
		action AEDFTANKRUN
		move TANKVEL1 geth
		seta[].mdflags 16
		set team 1
		set monstatus 1
	}
}

state monsterai
ifle initflags 0
{
	ifn sprite[].ang lastangvel
	{
		ifcount 8
		{
			getincangle temp sprite[].ang lastangvel
			abs temp
			ifge temp 13
			state tankgoslower
		}
		ifaction AEDFTANKSIT action AEDFTANKRUN
	}
	else
	{
		ifcount 12
		{
			ifn bottarget -1
			{
				ifle targetdist 7168
				{
					ifle targetdist 3072 state tankgofaster
					else
					state tankgoslower
				}
				else ifg targetdist 7168
				{
					state tankgofaster
				}
			}
			else
			ifg navpoint -1
			{
				state tankgofaster
			}
		}
		ifmove TANKSTOPPED action AEDFTANKSIT else
		ifaction AEDFTANKSIT action AEDFTANKRUN
	}
	ife navmode -1
	{
	
		ife navpoint -1
		{
			
			// get the nearest one
			set xydist 99999
			set hitag highrecon
			add hitag 1
			set SPRITELOTAG 0
			whilevarvarn SPRITELOTAG hitag
			{
				set spriteid reconpoints[SPRITELOTAG]
				ldist temp THISACTOR spriteid
				ifl temp xydist { set xydist temp set navpoint spriteid }
				add SPRITELOTAG 1
			}
			sub SPRITELOTAG 1
			
		}
		else
		{
			geta[navpoint].x x2
			geta[navpoint].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle tempb x2 y2
			
			getincangle temp sprite[].ang tempb
			
			ifn sprite[].ang tempb
			{
				set B MAXTANKTURN
				mul B -1
				ifg temp B ifl temp MAXTANKTURN
					seta[].ang tempb
				else
				{
					geta[].ang angvar
					ifl temp 0 sub angvar MAXTANKTURN
					else add angvar MAXTANKTURN
					seta[].ang angvar
				}
			}
			ldist xydist THISACTOR navpoint
			ifl xydist 512 // 384
			{
				add SPRITELOTAG 1
				ifg SPRITELOTAG highrecon set SPRITELOTAG 0
				set navpoint reconpoints[SPRITELOTAG]
			}
		}
	
	}
}

geta[].ang lastangvel
geta[].x startx
geta[].y starty

ifn mysignpost -1 // Graves is driving the tank
{
	ife sprite[].htextra -1
	ifvarand player[].player_par 1
	{
		ifge SKILL 4 seta[].htextra 2 else
		seta[].htextra 1
		ifn player[].access_incs 0 seta[].htextra 200
		seta[].htpicnum SHOTSPARK1
	}
}
ifhitweapon
{
	ifdead 
	{
		ifn mysignpost -1
		{
			setav[mysignpost].initsprite 10
			set mysignpost -1
		}
		set monstatus 2
		stopactorsound THISACTOR TANK_RUN
		action AEDFTANKWRECK
		espawn EXPLOSION6
		seta[RETURN].xrepeat 36
		seta[RETURN].yrepeat 30
		sound EXPLODE4
		debris SCRAP1 12
	    debris SCRAP2 6
	    debris SCRAP3 10
		sizeat 30 30
	    state tech_debris
		sizeat 60 47
		cstat 0
		ifn myspawner -1
		{
			setav[myspawner].myspawner -1
			set myspawner -1
		}
	}
}

ifg sprite[].xvel 32
{
	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		ife sprite[spriteid].picnum EDFTANK
		ifn spriteid THISACTOR
		{
			ldist xydist THISACTOR spriteid
			ifl xydist 7168
			{
				geta[spriteid].x x2
				geta[spriteid].y y2
				sub x2 sprite[].x
				sub y2 sprite[].y
				getangle angvar x2 y2
				getincangle angle2 sprite[].ang angvar
				abs angle2
				ifl angle2 512
					state tankgoslower
			}
		}
		nextspritestat spriteid spriteid
	}
	
}

ifnotmoving
{
	geta[].htmovflag spriteid
	add spriteid 16384
	ifl spriteid 16384 ifg spriteid -1 // hit a sprite
	{
		ifp phigher
		ife spriteid player[].i nullop else
		{
		seta[spriteid].htextra 30
		seta[spriteid].htpicnum RPG
		seta[spriteid].htowner THISACTOR
		seta[spriteid].htang lastangvel
		ife sprite[spriteid].picnum APLAYER soundonce SQUISHED
		}
		ifmove TANKVEL0 nullop else
		state tankgoslower
	}
}
enda

action AEDFTURRETAIM 2  1  1  1  1
action AEDFTURRETFIRE1 1  1  1  1  20
action AEDFTURRETFIRE2

defstate tankfirerox
	set savx sprite[].x
	set savy sprite[].y
	
	geta[].ang angvar, add angvar 256
	set x sprite[].x
	add x 512
	rotatepoint sprite[].x sprite[].y x sprite[].y angvar y y2
	
	seta[].x y
	seta[].y y2
	ezshoot -3072 RPG
	
	seta[].x savx
	seta[].y savy
	
	
	setav[RETURN].mtype TARGETLOCK
	setav[RETURN].bottarget bottarget
	seta[RETURN].extra 100
	
	rand angvar 512, sub angvar 256
	add angvar sprite[RETURN].ang
	seta[RETURN].ang angvar
	
	sub botclip 1
	ife botclip 0 set botclip -240
ends


useractor notenemy EDFTANKTURRET 2000

ife myspawner -1 
{
	espawn EXPLOSION5
	seta[RETURN].xrepeat 20
	seta[RETURN].yrepeat 20
	sound ROBOT_EXPLODE
	debris SCRAP1 6
	debris SCRAP2 4
	debris SCRAP3 5
	sizeat 16 16
	state tech_debris
	killit
}

set z sprite[myspawner].z
sub z 14336
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z

ife actorvar[myspawner].initflags GRAVESNPC { seta[].htextra -1 break }

ifaction 0
{
	action AEDFTURRETAIM
	cstat 256
	set team 1
	set botclip 3
}


set dodgetime 3 // prevents instant turning
	state monsterai
	ife bottarget -1 ifn actorvar[myspawner].bottarget -1
	set bottarget actorvar[myspawner].bottarget
	
	ifn bottarget -1
	{
		geta[bottarget].x x2
		geta[bottarget].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle tempb x2 y2
		
		getincangle temp sprite[].ang tempb
		
		ifn sprite[].ang tempb
		{
			ifg temp -16 ifl temp 16
			{
				seta[].ang tempb
				ifcount 10
				ifactorsound THISACTOR TANK_TURRET stopactorsound THISACTOR TANK_TURRET
				
				ifg countvar 120
				{
					sound TANK_SHOOT sound TANK_SHOOT sound TANK_SHOOT sound TANK_SHOOT
					geta[].z savz, add savz 2048, seta[].z savz
					eshoot TANKSHELL 
					sub savz 2048, seta[].z savz
					ifg sprite[bottarget].z sprite[].z
					ifl targetdist 10240
					{
						geta[RETURN].zvel z, add z 512, seta[RETURN].zvel z
					}
					action AEDFTURRETFIRE1
					set countvar 0
					resetcount
				}
			}
			else
			{
				
				geta[].ang angvar
				ifl temp 0 sub angvar 16
				else add angvar 16
				seta[].ang angvar
				
				abs temp
				ifl temp 512
				ife botclip 3
				ifaction AEDFTURRETAIM
				{
					action AEDFTURRETFIRE2
					sound THREEROX
					
					state tankfirerox
				}
				
				
				ifactorsound THISACTOR TANK_TURRET nullop else sound TANK_TURRET
				resetcount
			}
			
		}
		else
		ifcount 10
		ifactorsound THISACTOR TANK_TURRET stopactorsound THISACTOR TANK_TURRET

	}
	set lastang sprite[].ang
	ifl botclip 0 
	{ 
		add botclip 1 ife botclip 0 
		{
			set botclip 3 
			ifaction AEDFTURRETFIRE2 action AEDFTURRETAIM
		}
	}
	
	ifaction AEDFTURRETFIRE1 ifactioncount 1 
	{
		ife botclip 3
		action AEDFTURRETAIM
		else
		action AEDFTURRETFIRE2
	}
	add countvar 1
	
	ifle botclip 2
	ifg botclip 0
	{
		add countvarc 1
		ifge countvarc 4
		{
			state tankfirerox
			set countvarc 0
		}
	}

	geta[].htextra B
	ifg B 0
	ifg B sprite[myspawner].htextra
	ifn sprite[].htowner myspawner
	ifn sprite[].htowner THISACTOR
	{
		seta[myspawner].htextra B
		seta[myspawner].htpicnum sprite[].htpicnum
		seta[myspawner].htowner sprite[].htowner
		seta[myspawner].htang sprite[].htang
	}
	ifhitweapon strength 2000

enda

eventloadactor BLACKHAWK
	sizeat 120 100
	cstat 257
	espawn HELIBLADES
	set myspawner RETURN
	setav[RETURN].myspawner THISACTOR
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	geta[].hitag initsprite
	seta[].hitag 0
	geta[].extra initflags
	ife initflags -1 set initflags 0
	ife initflags 1 cstat 32768 // start invisible and wait for lotag
	// hitag 0 = do nothing
	// hitag 1 = start up and then idle only
	// hitag 2 = take off, fly straight up then hover
	// hitag 3 = take off, fly up and then fly forward until it hits something then disappear
	// hitag 4 = take off, then follow tagged waypoints
	// lotag is set only if startup is triggered by this channel; if lotag not set it starts up right away
enda

move BLACKHAWKUP	0  -48
move BLACKHAWKDOWN	0  40
move BHDOWNSLOW 0 16
move BLACKHAWKHOVER 0  -15
move BLACKHAWKFORW  128 -15
useractor notenemy BLACKHAWK 800

ifn SPRITELOTAG 0 ife initflags 1 break

ifmove 0
{
	move TANKSTOPPED geth
	sizeat 120 100
	clipdist 128
	cstat 257
	geta[].htflags temp, orvar temp 8193, seta[].htflags temp
	ife myspawner -1
	{
		espawn HELIBLADES
		set myspawner RETURN
		setav[RETURN].myspawner THISACTOR
	}
	ife initflags 1 set countvar 89
}
fall

geta[].htflags temp
iffloordistl 1024 orvar temp 1 else
ifvarand temp 1 xorvar temp 1
seta[].htflags temp

ifn SPRITELOTAG 0 break
ife startmode -1 break
ife initsprite 0 break

// BLACKHAWK_START is 12.2 seconds long, about 370 tics
ife countvar 0 sound BLACKHAWK_START
add countvar 1

setav[myspawner].countvar countvar

ife countvar 90
{
	espawn HELIBLADES
	setav[RETURN].myspawner THISACTOR
	setav[RETURN].countvar countvar
	setav[RETURN].mtype 1
	ife initflags 1 { set countvar 560 move BLACKHAWKFORW geth getv set initsprite 4 } 
}

// spawn dust
ifoutside
iffloordistl 256
ifge countvar 120
{
	set countvarb 0
	// set angvel sprite[].ang
	rand angvel 2047
	set temp player[].player_par
	modvar temp 8
	mul temp 16
	add angvel temp
	set z2 sector[].floorz
	sub z2 2048
	set countvarc 1280
	whilevarn countvarb 4
	{
		espawn BIGSMOKE
		set x2 sprite[].x
		add x2 countvarc
		rotatepoint sprite[].x sprite[].y x2 sprite[].y angvel x y
		setsprite RETURN x y z2
		seta[RETURN].ang angvel
		seta[RETURN].htbposx x
		seta[RETURN].htbposy y
		add angvel 512
		add countvarb 1
		add countvarc 192
	}
}

ifge countvar 370
{
	soundonce BLACKHAWK_LOOP
	ifge initsprite 2
	{
		ifmove TANKSTOPPED
			move BLACKHAWKUP geth getv
		
		ife initsprite 2
		{
			ifmove BLACKHAWKUP
			{
				geta[].zvel z
				set temp countvar
				sub temp 370
				shiftl temp 4
				sub z temp
				seta[].zvel z
				
				ifge countvar 640 move BLACKHAWKHOVER geth getv
			}
			 
		}
		else
		ife initsprite 3
		{
			ifmove BLACKHAWKUP
			{
				geta[].zvel z
				set temp countvar
				sub temp 370
				shiftl temp 4
				sub z temp
				seta[].zvel z
				
				ifge countvar 500 move BLACKHAWKHOVER geth getv
			}
			else
			ifmove BLACKHAWKHOVER
			{
				ifge countvar 560
				move BLACKHAWKFORW geth getv
				geta[].htflags temp
				orvar temp 2048
				seta[].htflags temp
				cstat 256
			}
			else
			ifmove BLACKHAWKFORW
			{
				geta[].xvel xvel
				set temp countvar
				sub temp 560
				shiftl temp 1
				add xvel temp
				seta[].xvel xvel
				ifnotmoving 
				{
					setav[myspawner].monstatus 2
					set monstatus 2
					stopactorsound THISACTOR BLACKHAWK_LOOP
					killit
				}
				// state forceforward
				// set mysector sprite[].sectnum
				// updatesectorz sprite[].x sprite[].y sprite[].z mysector
				// ife mysector -1 killit
			}
			 
		}
		else
		ife initsprite 4 // follows waypoints
		{
			ife mysignpost -1
			{
				headspritestat spriteid 978 // statnum unique to copter path
				whilevarn spriteid -1
				{
					ife actorvar[spriteid].SPRITELOTAG 1
					{
						set mysignpost spriteid
						set savedvalue 1
					}
						
					nextspritestat spriteid spriteid
				}
			}
			else
			{
				dist xydist THISACTOR mysignpost
				set z sprite[mysignpost].z
				sub z sprite[].z // negative if above you
				set z2 z, abs z2
				
				// turn to match angle of destination waypoint
				set TMP_B 6
				ife bottarget -1
				{
					
					getincangle temp actorvar[mysignpost].angvel sprite[].ang
					
					ifn temp 0
					{
						set B TMP_B
						mul B -1
						ifg temp B ifl temp TMP_B
							seta[].ang actorvar[mysignpost].angvel
						else
						{
							geta[].ang angvar
							ifl temp 0 add angvar TMP_B
							else sub angvar TMP_B
							seta[].ang angvar
							
						}
						set lastang sprite[].ang
					}
				}
				
				ifl xydist 384
				ifl z2 1024 // has reached the next waypoint
				{
					ife sprite[mysignpost].pal 8 // END OF JOURNEY
					{
						move STOPPEDFORPLAYER geth
						set initsprite 5 // does nothing
						ife initflags 1 setp[].timebeforeexit 30
					}
					else
					{
						ife sprite[mysignpost].pal 2 set savedvalue 1 else
						{
							set savedvalue actorvar[mysignpost].SPRITELOTAG
							add savedvalue 1
						}
						
						headspritestat spriteid 978 // statnum unique to copter path
						whilevarn spriteid -1
						{
							ife actorvar[spriteid].SPRITELOTAG savedvalue
								set mysignpost spriteid
								
							nextspritestat spriteid spriteid
						}
					}
				}
				else // has a waypoint and has not reached it
				{
					ifl z2 844 // already at correct height
					{
						ifmove BLACKHAWKHOVER nullop else move BLACKHAWKHOVER geth getv
						
					}
					else
					{
						ifl z 0 // waypoint above
						{
							ifmove BLACKHAWKUP nullop else move BLACKHAWKUP geth getv
						}
						else // waypoint below
						{
							ifl z2 20480
							{
								ifmove BHDOWNSLOW nullop else move BHDOWNSLOW geth getv
							}
							else
							{
								ifmove BLACKHAWKDOWN nullop else move BLACKHAWKDOWN geth getv
							}
						}
					}
					
					ldist xydist2 THISACTOR mysignpost
					ifg xydist2 256
					{
						geta[mysignpost].x x2
						geta[mysignpost].y y2
						sub x2 sprite[].x
						sub y2 sprite[].y
						getangle angvar x2 y2
						
						cos xvel angvar
						sin yvel angvar
						  
						shiftvarr xvel 6
						shiftvarr yvel 6
						
						movesprite THISACTOR xvel yvel 0 CLIPMASK0 RETURN
					}
					
				}

			}
		}
	}
}

enda

// 24 page ups, 100x100

useractor notenemy HELIBLADES 0

ife myspawner -1 break
ife sprite[myspawner].cstat 32768 { cstat 32768 break }

ifmove 0
{
	move STOPPED
	sizeat 76 76
	cstat 32
	ife mtype 1 
	{
		cstator 514
		geta[].ang angvar
		sub angvar 32
		seta[].ang angvar
	}
}
geta[myspawner].z z
sub z 24576
ife mtype 1 sub z 512

setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z

ife mtype 1 add countvar 1
ifge countvar 1
{
	geta[].ang angvar
	set temp countvar
	ifg temp 127 set temp 127 // 509
	add angvar temp
	
	seta[].ang angvar
}
ife monstatus 2 killit
ife sprite[myspawner].statnum 1024 killit
ife actorvar[myspawner].monstatus 2 killit
ifn sprite[myspawner].picnum BLACKHAWK killit

enda

eventloadactor BMWM4
cstat 257
sizeat 60 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor COPCAR
cstat 257
sizeat 64 50
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
enda

eventloadactor 19203
cactor COPCAR
cstat 257
sizeat 64 50
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
enda

eventloadactor TAXI
cstat 257
sizeat 64 50
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor CRUZ
cstat 257
sizeat 56 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor CIVIC
cstat 257
sizeat 56 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor PORSCHE
cstat 257
sizeat 58 42
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor LAMBO90
cstat 257
sizeat 58 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor HUMMER
cstat 257
sizeat 60 46
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor BEETLE
cstat 257
sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor JEEP
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor LIMO
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor PICKUP
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor PICKUP2
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor VAN1
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor VAN2
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor DODGECAR
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor FORD
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor ROVER
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor BMWM3
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor NANOCOOPER
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor TOWTRUCK
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor CHEVELLE
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor OLDCAR
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor MICROBUS
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car
enda

eventloadactor AUDIA
cstat 257
// sizeat 48 40
geta[].lotag SPRITELOTAG
seta[].lotag 0
geta[].hitag mtype
seta[].hitag 0
geta[].z initsprite
  ifn sprite[].extra -1 geta[].extra countvarb // set to 1 for driving car

enda

eventloadactor WRECKEDCAR
cstat 257
enda

defstate carcheckactor

	cstator 257
	headspritesect spriteid sprite[].sectnum
	set TMP_B NO
	whilevarn spriteid -1
	{
		ifge actorvar[spriteid].monstatus 1 ifle actorvar[spriteid].monstatus 2
		{
			ldist TMP_A THISACTOR spriteid
			ifle TMP_A 1280
			{
				set z sprite[].z
				sub z sprite[spriteid].z
				ifg z 16384
				{
					cstat 256
					set TMP_B YES
				}
			}
		}
		nextspritesect spriteid spriteid
		ife TMP_B YES set spriteid -1
	}

ends

defstate carsmoke
	geta[].x savx
	geta[].y savy
	
	set x sprite[].x
	rand x2 1024
	add x x2 
	rand angvar 2048
	rotatepoint sprite[].x sprite[].y x sprite[].y angvar tempb tempc
	seta[].x tempb
	seta[].y tempc
	
	
	geta[].z savz
	sub savz 4096
	seta[].z savz
	espawn BIGSMOKE
	seta[RETURN].htpicnum TANKWRECK
	add savz 4096
	seta[].x savx
	seta[].y savy
	seta[].z savz
ends

defstate parkedflycar

state floatcode


ldist TMP_A THISACTOR player[].i
ifp phigher
{
	ifl TMP_A 1280
	{

		set z sprite[].z
		sub z player[].posz

		ifg z 20480
		{
			geta[].cstat temp
			orvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
		}
		else 
			cstator 257

	}
	else 
		state carcheckactor
}
else 
	state carcheckactor
	

state predamage
ifhitweapon
{
	ifdead
	{
		sound ROBOT_EXPLODE
		sound EXPLODE4
		espawn EXPLOSION6
		seta[RETURN].xrepeat 22
		seta[RETURN].yrepeat 20
		sizeat 32 32
		state tech_debris
		set countvarb 0
		whilevarn countvarb 8
		{
		   rand z 3072
		   add z 2048
		   mul z -1
		   ezshoot z CARXPLODE
		   randvar temp  2047
		   setactor[RETURN].ang temp 
		   randvar temp  192 add temp 128
		   setactor[RETURN].xvel temp 
		   add countvarb 1
		}
		spawn BURNING
		spawn BURNING2
		hitradius 4096 40 60 80 100
		// killit
		geta[].xrepeat x, mul x 3 div x 2, seta[].xrepeat x
		geta[].yrepeat y, mul y 3 div y 2, seta[].yrepeat y
		strength 2000
		cactor WRECKEDCAR
		move 0
	}
}

ifstrength 250
ifrnd 10	
	state carsmoke

ends

// move SHIPMOVE0 128
// move SHIPMOVE1 224
// move SHIPMOVE2 320
// move SHIPMOVE3 416
// move SHIPMOVE4 512
// move SHIPMOVE5 608
// move SHIPMOVE6 704
// move SHIPMOVE7 800
// move SHIPMOVE8 896

defstate carslowdown

ifcount 8
{
	ifmove SHIPMOVE5 move SHIPMOVE4 geth else
	ifmove SHIPMOVE4 move SHIPMOVE3 geth else
	ifmove SHIPMOVE3 move SHIPMOVE2 geth else
	ifmove SHIPMOVE2 move SHIPMOVE1 geth else
	ifmove SHIPMOVE1 move SHIPMOVE0 geth
}

ends

defstate carslowdown2

ifcount 3
{
	ifmove SHIPMOVE5 move SHIPMOVE4 geth else
	ifmove SHIPMOVE4 move SHIPMOVE3 geth else
	ifmove SHIPMOVE3 ifl x2 6144 move SHIPMOVE2 geth else
	ifl x2 5120
	{
		ifmove SHIPMOVE2 move SHIPMOVE1 geth else
		ifl x2 4096
		{
			ifmove SHIPMOVE1 move SHIPMOVE0 geth else
			ifmove SHIPMOVE0 ifl x2 2048 move SHRUNKVELS geth
		}
	}
}

ends

defstate carspeedup

ifcount 10
{
	ifmove SHRUNKVELS move SHIPMOVE0 geth else
	ifmove SHIPMOVE0 move SHIPMOVE1 geth else
	ifmove SHIPMOVE1 move SHIPMOVE2 geth else
	ifmove SHIPMOVE2 move SHIPMOVE3 geth else
	ifmove SHIPMOVE3 move SHIPMOVE4 geth else
	ifmove SHIPMOVE4 move SHIPMOVE5 geth
}

ends

defstate movecar

	ife myvictim -1
	{
		geta[].ang lastang
		set xydist 99999
		ife countvarb 1 headspritestat spriteid 975 else
		headspritestat spriteid 976
		whilevarn spriteid -1
		{
			ldist temp THISACTOR spriteid
			ifl temp xydist 
			{ 
				// geta[spriteid].x x2
				// geta[spriteid].y y2
				// sub x2 sprite[].x
				// sub y2 sprite[].y
				// getangle angvar x2 y2
				// getincangle temp angvar lastang
				// abs temp
				// ifl temp 512
				// {
					set xydist temp 
					set myvictim spriteid 
				// }
			}
			
			nextspritestat spriteid spriteid
		}
		move SHIPMOVE1 geth
		ifn myvictim -1
		{
			geta[myvictim].x x2
			geta[myvictim].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvar x2 y2
			seta[].ang angvar
		}
	}
	ife myvictim -1 killit
	
	geta[].z z, sub z 6144
	geta[].ang angvar
	// rand angle2 16 sub angle2 8 add angvar angle2
	cos mycos angvar
	sin mysin angvar
	
	cstat 32768
	geta[].xrepeat A
	geta[].yrepeat B
	sizeat 1 1
	// set x sprite[].x
	// add x 1024
	// rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x y
	
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 mysector temp bottarget x2 y2 zdist 4294901809
	seta[].xrepeat A
	seta[].yrepeat B
	cstat 257
	
	sub x2 sprite[].x
	sub y2 sprite[].y
	mul x2 x2
	mul y2 y2
	add x2 y2
	sqrt x2 x2
	ifn bottarget -1 ifn actorvar[bottarget].countvarb 0 ifn actorvar[bottarget].countvarb countvarb set x2 16384
	ifnotmoving 
	{
		set x2 100
		geta[].htmovflag spriteid
		add spriteid 16384
		ifl spriteid 16384 ifg spriteid -1 // hit a sprite
		{
			seta[spriteid].htextra 5
			seta[spriteid].htpicnum RPG
			seta[spriteid].htowner THISACTOR
			seta[spriteid].htang sprite[].ang
			ife sprite[spriteid].picnum APLAYER sound SQUISHED
		}
	}
	
	ifl x2 8192 
		state carslowdown2

	set bottarget -1
	
	set spriteid myvictim
	geta[spriteid].x x2
	geta[spriteid].y y2
	sub x2 sprite[].x
	sub y2 sprite[].y
	getangle tempb x2 y2
	
	getincangle temp lastang tempb
	
	ifn sprite[].ang tempb
	{
		set B MAXRECONTURN
		mul B -1
		ifg temp B ifl temp MAXRECONTURN
		{
			seta[].ang tempb
			state carspeedup
		}
		else
		{
			geta[].ang angvar
			ifl temp 0 sub angvar MAXRECONTURN
			else add angvar MAXRECONTURN
			seta[].ang angvar
			
			state carslowdown
		}
	}
	
	ldist xydist THISACTOR myvictim
	ifle xydist 384
	{
		getav[myvictim].SPRITELOTAG botclip
		add botclip 1
		ife sprite[myvictim].pal 6 set botclip 0
		set target -1
		
		ife countvarb 1 headspritestat spriteid 975 else
		headspritestat spriteid 976
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].SPRITELOTAG botclip set target spriteid
			nextspritestat spriteid spriteid
			ifn target -1 set spriteid -1
		}
		ifn target -1 set myvictim target
	}
	geta[].ang lastang

ends

defstate parkedcar

	ifn mtype 1
	fall

	ldist TMP_A THISACTOR player[].i
	ifg TMP_A 2048 ife sprite[].zvel 0 geta[].z initsprite
	ifp phigher
	{
		ifl TMP_A 1280
		{
			// set z initsprite
			// add z 2048
			// seta[].z z
			set z sprite[].z
			sub z player[].posz
			
			ifactor VAN1 sub z 8192 else
			ifactor VAN2 sub z 8192 else
			ifactor ROVER sub z 8192 else
			ifactor PICKUP sub z 8192 else
			ifactor PICKUP2 sub z 8192 else
			ifactor TOWTRUCK sub z 8192 else
			ifactor EDFTANKWRECK sub z 6144 else
			ifactor MICROBUS sub z 6144 else
			ifactor JEEP sub z 6144
			
			ifg z 20480
			{
				geta[].cstat temp
				orvar temp 256
				ifvarand temp 1 xorvar temp 1
				seta[].cstat temp
			}
			else 
			{
				cstator 257
				ifsound LANDCAR ife SPRITELOTAG 1 soundonce CARALARM
			}
			
		}
		else 
		{
			// cstat 257
			state carcheckactor
			// seta[].z initsprite
		}
			
		
	}
	else 
	{
		state carcheckactor
		// cstat 257
		// seta[].z initsprite
	}

	state predamage
	ifhitweapon
	{
		ife SPRITELOTAG 1 soundonce CARALARM
		ife SPRITELOTAG 2 strength 800 else
		ifdead
		{
			sound ROBOT_EXPLODE
			sound EXPLODE4
			espawn EXPLOSION6
			seta[RETURN].xrepeat 22
			seta[RETURN].yrepeat 20
			sizeat 32 32
			state tech_debris
			set countvarb 0
			whilevarn countvarb 8
			{
			   rand z 3072
			   add z 2048
			   mul z -1
			   ezshoot z CARXPLODE
			   randvar temp  2047
			   setactor[RETURN].ang temp 
			   randvar temp  192 add temp 128
			   setactor[RETURN].xvel temp 
			   add countvarb 1
			}
			spawn BURNING
			spawn BURNING2
			hitradius 4096 40 60 80 100
			// killit
			geta[].xrepeat x, mul x 3 div x 2, seta[].xrepeat x
			geta[].yrepeat y, mul y 3 div y 2, seta[].yrepeat y
			ifactor EDFTANKWRECK nullop else
			{
				strength 2000
				cactor WRECKEDCAR
				move 0
				break
			}
		}
	}

	ifstrength 250
	ifrnd 10	
		state carsmoke
		
	ifg countvarb 0 state movecar

ends

useractor notenemy WRECKEDCAR 2000

fall
cstator 257
ifhitweapon 
{
	ifdead
	{
		sound BREAK_MET2
		set countvarb 0
		whilevarn countvarb 6
		{
		   rand z 3072
		   add z 2048
		   mul z -1
		   ezshoot z CARXPLODE
		   randvar temp  2047
		   setactor[RETURN].ang temp 
		   randvar temp  192 add temp 128
		   setactor[RETURN].xvel temp 
		   add countvarb 1
		}
		state tech_debris
		killit
	}
}

enda

useractor notenemy BMWM4 600 state parkedcar enda
useractor notenemy COPCAR 600 state parkedcar enda
useractor notenemy TAXI 600 state parkedcar enda
useractor notenemy CRUZ 600 state parkedcar enda
useractor notenemy CIVIC 600 state parkedcar enda
useractor notenemy PORSCHE 600 state parkedcar enda
useractor notenemy LAMBO90 600 state parkedcar enda
useractor notenemy BEETLE 600 state parkedcar enda
useractor notenemy BEETLE 600 state parkedcar enda
useractor notenemy JEEP 600 state parkedcar enda
useractor notenemy LIMO 600 state parkedcar enda
useractor notenemy PICKUP 600 state parkedcar enda
useractor notenemy PICKUP2 600 state parkedcar enda
useractor notenemy VAN1 600 state parkedcar enda
useractor notenemy VAN2 600 state parkedcar enda
useractor notenemy DODGECAR 600 state parkedcar enda
useractor notenemy FORD 600 state parkedcar enda
useractor notenemy ROVER 600 state parkedcar enda
useractor notenemy BMWM3 600 state parkedcar enda
useractor notenemy NANOCOOPER 600 state parkedcar enda
useractor notenemy TOWTRUCK 600 state parkedcar enda
useractor notenemy CHEVELLE 600 state parkedcar enda
useractor notenemy OLDCAR 600 state parkedcar enda
useractor notenemy HUMMER 1000 state parkedcar enda
useractor notenemy EDFTANKWRECK 1000 state parkedcar enda
useractor notenemy MICROBUS 700 state parkedcar enda
useractor notenemy AUDIA 600 state parkedcar enda
useractor notenemy FLYCAR_SPAWNER 0

ifcount 70 ifrnd 4
{
	ifn SPRITELOTAG 0 espawnvar SPRITELOTAG
	else
	{
		ifrnd 84 espawn FLYCAR_1 else
		ifrnd 128 espawn FLYCAR_2 else
		espawn FLYCAR_3
	}
	rand pal 6
	ifn pal 0 add pal 10
	ife pal 6 set pal 21
	seta[RETURN].pal pal
	setav[RETURN].mtype 1
	ifn initsprite 0 setav[RETURN].countvar countvar
	resetcount
}

enda

move CAR_FLY 700

defstate simpleflycar

	ifmove 0
	{
		move CAR_FLY geth
		cstat 257
		sizeat 60 60
		ifrnd 128 set initsprite 1
	}
	else
	ifnotmoving killit
	ifg countvar 0
	{
		sub countvar 1
		ife countvar 0 killit
	}
	
	set xydist2 8192
	
	ife initflags YES break // played flyby sound again
	
	ifn player[].cursectnum -1
	ifvarand sector[player[].cursectnum].ceilingstat 1
		mul xydist2 2
	
	ldist startx THISACTOR player[].i
	ifl startx xydist2
	{
		set TMP_A NO
		
		ifn starty 0 
		{
			ifg startx starty set TMP_A YES else
			ifl startx starty
			{
				ifl startx 1560 set TMP_A YES
				else
				{
					getincangle angvar sprite[].ang player[].ang
					abs angvar
					shiftr angvar 3
					
					set xydist startx
					mul xydist angvar
					ifl xydist 800000 set TMP_A YES
				}
			}
		}
		
		ife TMP_A YES
		{
			ife initsprite 0
			{
				sound CAR_FLYBY1
				set initflags YES
			}
			else
			{
				sound CAR_FLYBY2
				set initflags YES
			}
		}
		set starty startx
	}

ends

useractor notenemy FLYCAR_1 600

ife mtype 1 state simpleflycar

enda

useractor notenemy FLYCAR_2 600

ife mtype 1 state simpleflycar

enda

useractor notenemy FLYCAR_3 600

ife mtype 1 state simpleflycar

enda

useractor notenemy FLYCAR_PARKED1 600 state parkedflycar enda
useractor notenemy FLYCAR_PARKED2 600 state parkedflycar enda
useractor notenemy FLYCAR_PARKED3 600 state parkedflycar enda
useractor notenemy FLYCAR_PARKED4 600 state parkedflycar enda

eventloadactor SPYPATH 
	cstat 32768 
	geta[].z z
	sub z 12288
	gets[].ceilingz z2
	add z2 4096
	ifl z z2 set z z2
	seta[].z z
	
enda

eventloadactor SPYDRONE cstat 256 sizeat 8 8 geta[].z z sub z 8192 seta[].z z enda

state spyexplode

	ife padang 1337 { state spawnimpcoins state spawnoutline killit }
	espawn EXPLOSION3
	seta[RETURN].xrepeat 24
	seta[RETURN].yrepeat 24
	// hitradius PIPEBOMBRADIUS 15 30 45 60
	spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL spawn SPARKFALL 
	state tech_debris
	
	rand value 5
	add value 1
	state spawnimpcoins
	
	killit

ends

useractor notenemy SPYDRONE 30

	ifmove 0
	{
		move STOPPED geth
		sizeat 8 8
		cstat 257
		clipdist 8
	}
	
	ife mysignpost -1
	{
		set xydist 65536
		headspritestat spriteid 977 // statnum unique to spy path
		whilevarn spriteid -1
		{
			ldist temp THISACTOR spriteid
			ifl temp xydist
			{
				canseespr THISACTOR spriteid tempb
				ife tempb YES
				{
					set mysignpost spriteid
					set xydist temp
				}
			}

			nextspritestat spriteid spriteid
		}
	}
	else
	{
		ldist xydist2 THISACTOR mysignpost
		ifmove PIGSUVSTOPPED set xydist2 512
		ifmove STOPPEDFORPLAYER set xydist2 128
		ifg xydist2 192
		{
			geta[mysignpost].x x2
			geta[mysignpost].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvar x2 y2
			
			ifmove PIGSUVSTOPPED 
			{
				set x sprite[].x
				add x 128
				rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang y y2
				geta[].sectnum tempb
				updatesectorz y y2 sprite[].z tempb
				ifvarn tempb -1 setsprite THISACTOR y y2 sprite[].z
				set angvar angvel
				ifcount 40 move STOPPED geth
			}
			
			cos xvel angvar
			sin yvel angvar
			
			seta[].ang angvar
			  
			shiftvarr xvel 8
			shiftvarr yvel 8
			
			// set z player[].posz
			set z sprite[mysignpost].z
			sub z sprite[].z
			div z 8
			clamp z -2048 2048
			ifg countvar 0 ifg z 0 set z 0
			movesprite THISACTOR xvel yvel z CLIPMASK0 RETURN
			// ifceilingdistl 48 nullop else
			ifn RETURN 0 
			{
				set temp RETURN
				add temp 16384
				ifl temp 16384 ifg temp -1
				ife sprite[temp].picnum SPYDRONE
				{
					getincangle tempc sprite[temp].ang sprite[].ang
					abs tempc
					ifg tempc 768
					{
						geta[].ang angvar
						add angvar 512
						state moveatangvar
					}
				}
				else
				{
					add countvar 1
					ifge countvar 30
					{
						
						move PIGSUVSTOPPED geth
						set countvar 0
						state setfarang
						ifn bossang -6666 set angvel bossang
						
					}
					else
					movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
				}
			}
		}
		else
		{
			ifmove STOPPED move STOPPEDFORPLAYER faceplayerslow
			ifcount 90
			{
				// MAXELEMENTS
				set TMP_A 0 // number of candidates found
				headspritestat spriteid 977 // statnum unique to spy path
				whilevarn spriteid -1
				{
					ifn spriteid mysignpost
					{
						ldist xydist THISACTOR spriteid
						ifg xydist 512
						ifl xydist 20480
						{
							canseespr THISACTOR spriteid tempb
							ife tempb YES
							{
								setarray tmpar1[TMP_A] spriteid
								add TMP_A 1
							}	
						}
					}
					nextspritestat spriteid spriteid
					ifge TMP_A MAXELEMENTS set spriteid -1
				}
				ifg TMP_A 0
				{
					sub TMP_A 1
					rand temp TMP_A
					set mysignpost tmpar1[temp]
					move STOPPED geth
					set countvar 0
				}
				else
				{
					move PIGSUVSTOPPED geth
					set countvar 0
					state setfarang
					ifn bossang -6666 set angvel bossang
				}
			}
		}
	}
	ifhitweapon
	{
		ifdead state spyexplode
	}

enda


