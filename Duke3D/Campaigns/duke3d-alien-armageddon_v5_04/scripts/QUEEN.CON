// New Alien Queen

action AQUEENWALK		0  4  5  1  12
action AQUEENCHARGE 	20  4  5  1  12
action AQUEENCHARGEPREP	40 1  5  1  60
action AQUEENSHOULDER 	40  1  5  1  60
action AQUEENLAYF		65  3  5  1  70
action AQUEENLAYB		75  3  5  -1 40
action AQUEENSHOOT1		80  1  5  1  30
action AQUEENSHOOT2		85  1  5  1  40
action AQUEENCLAW1		90  2  5  1  18
action AQUEENCLAW2		100 1  5  1  30
action AQUEENSTAND		105  1  5  1  10
action AQUEENFROZEN		105  1  5  1  10
action AQUEENFLASH		110  2  5  1  8

action AQUEENHURT		55  2  5  1  24
action AQUEENGROW		55  2  5  1  18
action AQUEENDIE		45  10  1  1  24
action AQUEENDEAD		54  1  1  1  10

move QUEENWALKVELS 256
move QUEENSHRUNKVELS 32
move QUEENCHARGEVELS 408
move QUEENCHARGEFAST 700

ai AIQUEENWAIT AQUEENSTAND STOPPED geth
ai AIQUEENSEEK AQUEENWALK QUEENWALKVELS seekplayer
ai AIQUEENCHARGE AQUEENCHARGEPREP STOPPED geth
ai AIQUEENLAY AQUEENLAYF STOPPED geth
ai AIQUEENSHOOT AQUEENSHOOT1 STOPPED geth
ai AIQUEENSHRUNK AQUEENCHARGE QUEENSHRUNKVELS fleeenemy
ai AIQUEENFROZEN ANOSHLDFROZEN STOPPED geth
ai AIQUEENGROW AQUEENGROW STOPPED geth
ai AIQUEENHIT AQUEENHURT STOPPED geth
ai AIQUEENDYING AQUEENDIE STOPPED geth
ai AIQUEENSHOCK AQUEENSTAND STOPPED geth
ai AIQUEENCLAW AQUEENCLAW1 STOPPED geth

state queenroamsounds

	ifrnd 84 soundonce QUEENATTK2 else
	ifrnd 128 soundonce QUEENROAM else
	soundonce QUEENALERT
	
ends

state queenlaystate

	ifaction AQUEENLAYF
	{
		ifactioncount 3
		{
			// sound
			action AQUEENLAYB
			ifinwater ifrnd 128 espawn DEVOURER else
			ifspritepal 42 espawn CYBERBEAST else
			espawn NEWBEASTHANG
			setav[RETURN].team team
			ifspritepal 0 seta[RETURN].pal 3 else
			seta[RETURN].pal sprite[].pal
			ifspritepal 21 ifrnd 160 seta[RETURN].pal 2
			ife sprite[RETURN].picnum DEVOURER seta[RETURN].pal 19
			
			ifspritepal 42 spawn CYBERBEAST
			
			sound SQUISHED
			sub botclip 1
		}
	}
	ifaction AQUEENLAYB
	{
		ifactioncount 3 ai AIQUEENSEEK
	}

ends

state queenshockstate

	ifaction AQUEENSTAND
	{
		ifactioncount 4 action AQUEENFLASH
	}
	else
	{
		ifcount 1
		{
			resetcount
			rand zdist 4096
			sub zdist 4096 ifspritepal 42 sub zdist 2048
			ife targethigher YES sub zdist 1024
			ezshoot zdist RADPROJ
			rand angvar 2047
			seta[RETURN].ang angvar
			seta[RETURN].pal 0
			
			set temp NO
			ifspritepal 0 set temp YES
			ifspritepal 42 set temp YES
			
			ife temp YES
			{
				rand zdist 4096
				sub zdist 6144
				ife targethigher YES sub zdist 2048
				ezshoot zdist RADPROJ
				rand angvar 2047
				seta[RETURN].ang angvar
				seta[RETURN].pal 0
			}
		}
		ifactorsound THISACTOR ELECTRICLOOP nullop else sound ELECTRICLOOP
		ifactioncount 20 
		{
			stopactorsound THISACTOR ELECTRICLOOP
			ai AIQUEENSEEK
		}
	}

ends

state queenclawstate

	ifaction AQUEENCLAW1
	{
		ifactorsound THISACTOR LBM_HAMMERSWING nullop else sound LBM_HAMMERSWING
		ifactioncount 2 
		{
			action AQUEENCLAW2
			state meleedamage

		}
	}
	ifaction AQUEENCLAW2
	ifactioncount 1 ai AIQUEENSEEK

ends

state queenhurtstate

	// ifvarg stun 0 resetcount
	ifcount 30
	{
		set stun 0
		ife targetalive YES
		{
			ifrnd 96
			{
				sound QUEENATTK
				ai AIQUEENSHOOT
				set initsprite 0
			}
			else
			ifrnd 96
			ife seemytarget YES
				ifg targetdist 3072
					ifl targetdist 16384
					{
						// height check
						ifn bottarget -1
						{
							geta[].z z
							geta[bottarget].z z2
							sub z z2
							abs z
							ifl z 32768 ai AIQUEENCHARGE
						}
						else
						ai AIQUEENCHARGE
						soundonce QUEENPAIN2
					}
			else
				ai AIQUEENSEEK	
		}
		else
			ai AIQUEENSEEK	
	}

ends

state queenfrozenstate 

	ifsquished	
	{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

    ifcount THAWTIME
    {
      ai AIQUEENWAIT
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }

      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 40
      state standard_jibs
      sound GLASS_BREAKING
      killit
    }
	ifn team actorvar[player[].i].team
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state queendyingstate 

	strength 0
	ifaction AQUEENDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 9
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			globalsound THUD
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			cstat 0
			action AQUEENDEAD
			ifspritepal 0
			{
				ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
				{
					endofgame 60
					set cutscene_ready 1255
					ife attmode YES set attmid 4
				}
			}
			else
			ifspritepal 42
			{
				setp[].timebeforeexit 150
				ife VOLUME 4
				ife LEVEL 24
				{
					set donepart1 YES
					savegamevar donepart1
					save 9
					stopallsounds
					starttrackslot 4 31
					set midscreen 20479
					set cutend totalclock
					set cutstart totalclock
					add cutend 7000
					set cutmash 3
					startscreen
				}
			}

			break
		}
	}
	ifaction AQUEENDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				state beast_armslegs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				state beast_armslegs
				killit
			}
		}
		
	}


ends

state queenshrunkstate 

  ifcount SHRUNKDONECOUNT
    ai AIQUEENWAIT
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 0 sizeto 96 80 else sizeto 42 40
	}
  else
    state genericshrunkcode

ends

state queengrowstate 

	state genericgrowcode

ends

state queenfire

	sound CBFIRE
	
	ifspritepal 21
	{
		set xvel 832
	    state rpg_targetprep
	    sub zdist 1560
        zshoot zdist ARCBALLPROJ
		break
	}
	
	ifspritepal 42 ifrnd 84
	{
		set xvel 832
	    state rpg_targetprep
	    sub zdist 1560
        zshoot zdist ARCBALLPROJ
		
		set xvel 832
	    state rpg_targetprep
	    sub zdist 1560
        ezshoot zdist ARCBALLPROJ
		geta[RETURN].ang angvar, sub angvar 96, seta[RETURN].ang angvar
		
		set xvel 832
	    state rpg_targetprep
	    sub zdist 1560
        ezshoot zdist ARCBALLPROJ
		geta[RETURN].ang angvar, add angvar 96, seta[RETURN].ang angvar
		
		break
	}
	
	
	set xvel 844
	state rpg_targetprep
	ezshoot zdist SWIRLPROJ
	ifspritepal 42 seta[RETURN].pal 11
	
	set xvel 844
	state rpg_targetprep
	ezshoot zdist SWIRLPROJ
	ifspritepal 42 seta[RETURN].pal 11
	geta[RETURN].ang angvar, sub angvar 96, seta[RETURN].ang angvar
	
	set xvel 844
	state rpg_targetprep
	ezshoot zdist SWIRLPROJ
	ifspritepal 42 seta[RETURN].pal 11
	geta[RETURN].ang angvar, add angvar 96, seta[RETURN].ang angvar
	
	set temp NO
	ifspritepal 0 set temp YES
	ifspritepal 42 set temp YES
	ife temp YES
	{
	
		set xvel 844
		state rpg_targetprep
		ife targethigher YES { mul zdist 3 div zdist 2 } else
		div zdist 2
		ezshoot zdist SWIRLPROJ
		ifspritepal 42 seta[RETURN].pal 11
		
		set xvel 844
		state rpg_targetprep
		ife targethigher YES { mul zdist 3 div zdist 2 } else
		div zdist 2
		ezshoot zdist SWIRLPROJ
		ifspritepal 42 seta[RETURN].pal 11
		geta[RETURN].ang angvar, sub angvar 96, seta[RETURN].ang angvar
		
		set xvel 844
		state rpg_targetprep
		ife targethigher YES { mul zdist 3 div zdist 2 } else
		div zdist 2
		ezshoot zdist SWIRLPROJ
		ifspritepal 42 seta[RETURN].pal 11
		geta[RETURN].ang angvar, add angvar 96, seta[RETURN].ang angvar
	
	}
	

ends

state queenshootstate

	ife initsprite 0 // ready shooting frame
	{
		ifactioncount 1
		{
			state queenfire
			action AQUEENSHOOT2
			set initsprite 1
			move STOPPED geth
		}
	}
	ife initsprite 1 // firing frame
	{
		ifactioncount 1
		{
			action AQUEENSHOOT1
			set initsprite 2
		}
	}
	ife initsprite 2 // done firing frame
	{
		ifactioncount 1
		{
			ife seemytarget YES ife shootmytarget YES
			ifrnd 96
			{
				ifn bottarget -1 move STOPPED geth
				else
				move STOPPED faceplayer
				action AQUEENSHOOT1
				set initsprite 0
				break
			}
			set initsprite 0
			ai AIQUEENSEEK
		}
	}

ends

state queenseekstate

	ifspritepal 0
	state squish_bot else
	ifspritepal 42 state squish_bot

	ife shootmytarget YES 
	{
		ifl targetdist 1560 ifrnd 32 { ai AIQUEENCLAW break }
		ifl targetdist 1280 { ai AIQUEENCLAW break }
	}
	
	ifactioncount 4
	{
		sound BIGSTEP
		resetactioncount
	}
	
	
	ifrnd 1 state queenroamsounds
	
	ifcount 60 
		ifrnd 8
	{
		ai AIQUEENWAIT
		break
	}
	else
	ifcount 30
		ifrnd 6
	ifg botclip 15
	{
		ai AIQUEENWAIT
		break
	}
	
	ife seemytarget YES ife shootmytarget YES
		ife targetalive YES
	{
		ifl targetdist 2560 
		ifcount 10
		{ 
			ifrnd 84 ai AIQUEENCHARGE
			else
			{
				ai AIQUEENSHOOT
				set initsprite 0
			}
			break 
		}
		ifcount 60 ifrnd 16
		{
			ifl targetdist 16384
			ifrnd 96 
			{
				// height check
				ifn bottarget -1
				{
					geta[].z z
					geta[bottarget].z z2
					sub z z2
					abs z
					ifl z 32768 ai AIQUEENCHARGE
				}
				else
				ai AIQUEENCHARGE
				ifai AIQUEENCHARGE break 
			}
			else
			{
				ai AIQUEENSHOOT
				set initsprite 0
			}
		}
	}
	
	

ends

state queenwaitstate 

	ifn thiscam -1 break
	ife gametype SURVIVAL ifl player[].player_par 150 break
	ife shootmytarget YES ifl targetdist 1560 { ai AIQUEENCLAW break }
	ifcount 20 
	{
		
		ifrnd 128
		ifg botclip 0
		ife seeplayer YES
		{
			ai AIQUEENLAY
			sound BOS4_LAY
			ifspritepal 42 globalsound BOS4_LAY
		}
		else
		ifl targetdist 16384
		{
			ai AIQUEENSHOCK
			sound QUEENALERT
			ifvarand monstflags 1024 nullop else
			{
				espawn HEALINGAURA
				setav[RETURN].myspawner THISACTOR
				set myspawner RETURN
			}
		}
		else
		ai AIQUEENSEEK
	}
	
ends

state queensmokecircle

set countvar 0
whilevarn countvar 2048
{
	espawn BIGSMOKE
	set x2 sprite[].x
	add x2 2048 ifspritepal 42 add x2 3072
	rotatepoint sprite[].x sprite[].y x2 sprite[].y countvar temp tempb
	updatesectorz temp tempb sprite[].z mysector
	ifn mysector -1 setsprite RETURN temp tempb sprite[].z
	add countvar 128
}

ends

state queenshoulderhitplayer

	ife bottarget player[].i set target player[].i else
	ife bottarget -1 set target player[].i else
	set target bottarget
	ifpdistl 5120 quake 12
	sound HUGESTEP3
	setactorsoundpitch THISACTOR HUGESTEP3 -384
	sound CYBOSS_LAND
	ife target player[].i
	{
		sound SQUISHED
		wackplayer
		getp[].ang temp
		add temp 1024
		cos tempb temp
		sin tempc temp
		shiftvarl tempb 9
		shiftvarl tempc 9
		ifspritepal 42 
		{
			shiftvarl tempb 1
			shiftvarl tempc 1
		}
		setp[].posxv tempb
		setp[].posyv tempc
	}
	else
	{
		seta[target].xvel -1024
		getav[target].stun temp
		add temp 30 ifspritepal 42 add temp 60
		setav[target].stun temp
		geta[target].z z
		sub z 1024
		seta[target].z z
		geta[target].zvel z
		sub z 2048
		seta[target].zvel z
	}
	
	geta[target].htextra tempc
	rand tempb 20
	add tempb 80
	ifspritepal 0 add tempb 150 else
	ifspritepal 42 add tempb 300
	add tempc tempb
	seta[target].htextra tempc
	seta[target].htpicnum RPG
	seta[target].htowner THISACTOR
	seta[target].htang sprite[].ang
	
	action AQUEENSHOULDER
	move STOPPED geth
	ifspritepal 0 state queensmokecircle else
	ifspritepal 42 state queensmokecircle

ends


state queenshoulderhitobject

	ifpdistl 6144 quake 12
	debris SCRAP1 4
	debris SCRAP2 4
	debris SCRAP3 4
	debris SCRAP4 4
	debris SCRAP5 4
	debris SCRAP6 4
	sound HUGESTEP3
	setactorsoundpitch THISACTOR HUGESTEP3 -384
	sound CYBOSS_LAND
	ifspritepal 0
	{
		hitradius 5120 20 40 60 100
		// seta[].htextra -1
		ifpdistl 5120 seta[player[].i].htowner THISACTOR
	}
	else
	ifspritepal 42
	{
		hitradius 7168 20 40 60 100
		// seta[].htextra -1
		ifpdistl 7168 seta[player[].i].htowner THISACTOR
	}
	else
	{
		hitradius 3500 15 25 40 80
		// seta[].htextra -1
		ifpdistl 3500 seta[player[].i].htowner THISACTOR
	}
	action AQUEENSHOULDER
	move STOPPED geth
	ifspritepal 0 state queensmokecircle

ends

state queenchargestate
	
	ifspritepal 0
	state squish_bot else
	ifspritepal 42 state squish_bot
	
	set temp player[].player_par
	modvar temp 8
	ife temp 0 sound BIGSTEP
	
	ife bottarget -1
	{
		ai AIQUEENSEEK
		break
	}
	
	ife seemytarget YES ifl targetdist 4096
	set crumbwait 2 else
	ifmove QUEENCHARGEFAST set crumbwait 2
	else ifn team actorvar[player[].i].team
	{ set crumbwait 0 state crumbcode }
	
	ifaction AQUEENCHARGEPREP
	{
		ifg targetdist 1280
		state strafechecklong
		ifactioncount 1
		{
			// sound PIGBROAR2
			action AQUEENCHARGE
			ifn bottarget -1 move QUEENCHARGEVELS geth
			else
			ifn team actorvar[player[].i].team
			move QUEENCHARGEVELS faceplayersmart
			else
			move QUEENCHARGEVELS geth
		}
	}
	ifaction AQUEENCHARGE
	{
		ifg targetdist 1280
		state strafechecklong
		
		ifmove QUEENCHARGEVELS
		ifcount 30
		{
			move QUEENCHARGEFAST geth
			ifn bottarget -1
			{
				set spriteid bottarget
				state facesprite
			}
			geta[].ang float
		}
		ifmove QUEENCHARGEFAST
		{
			espawn SPEEDBLUR
			setav[RETURN].mtype sprite[].htdispicnum
			seta[RETURN].xrepeat sprite[].xrepeat
			seta[RETURN].yrepeat sprite[].yrepeat
			ifg sprite[].alpha 96 seta[RETURN].alpha sprite[].alpha else
			seta[RETURN].alpha 80
			seta[RETURN].pal sprite[].pal
			seta[RETURN].mdflags 16
				
			seta[].ang float
			
			ifspritepal 21 { ifrnd 128 spawn BURNING else spawn BURNING2 }
		}
		ifl targetdist 1560
			state queenshoulderhitplayer
		ifnotmoving
			state queenshoulderhitobject
		ifaction AQUEENCHARGE // did not hit anything
		ifcount 60
		{
			action AQUEENSHOULDER
			move STOPPED geth
		}
		
	}
	else
	ifaction AQUEENSHOULDER
	{
		seta[].ang float
		ifactioncount 1
			ai AIQUEENSEEK
	}

ends

eventloadactor QUEEN ifspritepal 0 sizeat 96 80 else sizeat 42 40 cstator 257 enda

useractor enemy QUEEN QUEENSTRENGTH AQUEENSTAND

ifai 0
{
	ai AIQUEENWAIT
	ifspritepal 0 { set botclip 20 sizeat 96 80 } else
	ifspritepal 42 { set botclip 20 sizeat 124 120 strength 12288 clipdist 192 } else
	{ sizeat 42 40 strength 2000 set botclip 8 }
	cstat 257
	globalsound QUEENALERT
	
	ifinwater nullop else
	ifspritepal 42
	{
		set navmode 3
		geta[].htflags temp
		orvar temp 16384
		orvar temp 134217728
		seta[].htflags temp
	}
}

fall

ifai AIQUEENFROZEN { state queenfrozenstate break }
ifai AIQUEENDYING { state queendyingstate break }
ifspritepal 21 set burning 0
state monsterai
ifg sprite[].zvel 0 add FEMFALLDMG 1
else
{
	ifg FEMFALLDMG 30
	{
		ifpdistl 3072 quake 12
		sound HUGESTEP3
		setactorsoundpitch THISACTOR HUGESTEP3 -384
	}
	set FEMFALLDMG 0
} 

ifai AIQUEENSHRUNK state queenshrunkstate else
ifai AIQUEENGROW state queengrowstate else
ifai AIQUEENWAIT state queenwaitstate else
ifai AIQUEENSEEK state queenseekstate else
ifai AIQUEENSHOOT state queenshootstate else
ifai AIQUEENCHARGE state queenchargestate else
ifai AIQUEENLAY state queenlaystate else
ifai AIQUEENSHOCK state queenshockstate else
ifai AIQUEENCLAW state queenclawstate else
ifai AIQUEENHIT state queenhurtstate

ifai AIQUEENSHOCK nullop else ifn myspawner -1
{
	setav[myspawner].myspawner -1
	set myspawner -1
}
ife monstatus -1 break
geta[].htextra safecount
ifspritepal 42 div safecount 2
ifhitweapon
{
	ifn sprite[].pal 0
	ifn sprite[].pal 42
    ifwasweapon SHRINKSPARK
	{
	  sound ACTOR_SHRINKING
	  ai AIQUEENSHRUNK
	  set shrunken 1
	  break
	}
	else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	state random_wall_jibs
	ifdead
	{
		ifn sprite[].pal 0
		ifn sprite[].pal 42
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  ai AIQUEENFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  ai AIQUEENGROW
			  break
			}
		}
		addkills 1
		getp[].player_par ikicked
		state enemy_death

		ai AIQUEENDYING
		spritepal 6
		guts JIBS6 16
		getlastpal
		
		globalsound QUEENDEATH

		break
	}
	ifrnd 4
	{
		spritepal 6
		spawn BLOODPOOL
		getlastpal
	}
	ifactorsound THISACTOR QUEENPAIN1 nullop else
	ifactorsound THISACTOR QUEENPAIN2 nullop else
	{
		ifrnd 128
		{
			sound QUEENPAIN1 
			ifspritepal 42 globalsound QUEENPAIN1 
		}
		else
		{
			sound QUEENPAIN2
			ifspritepal 42 globalsound QUEENPAIN2
		}
	}
	
	ifai AIQUEENLAY set stun 0 else
	ifai AIQUEENCHARGE set stun 0 else
	ifai AIQUEENSHRUNK set stun 0 else
	ifai AIQUEENHIT set stun 0 else
	ifai AIQUEENSHOCK set stun 0 else
	{
		ifrnd 64
		ifg safecount 100
		{ 
			ai AIQUEENHIT 
			break 
		}
		else ifg stun 0 
		{ 
			
			ai AIQUEENHIT 
			break 
		}
		
	}
}

enda
