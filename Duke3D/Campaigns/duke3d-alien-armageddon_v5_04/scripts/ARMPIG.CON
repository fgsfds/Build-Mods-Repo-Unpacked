// ARMORED PIG
// sprites by sebabdukeboss20, code by Dan Gaskill
// version .90 February 16, 2018
// define ARMPIG 5632
// define ARMPIGSTAYPUT
// define EDFPIG 5712

action AARMPIGSTAND 	32  1  8  1  10
action AARMPIGWALK		0	4  8  1  16
action AARMPIGSHOOT1	32  1  8  1  30
action AARMPIGSHOOT2	40  1  8  1  20
action ADAMPIGWALK		0  4  8  1  16

action ADAMPIGSHOOT1	32  1  8  1  30
action ADAMPIGSTAND		32  1  8  1  10
action ADAMPIGSHOOT2	40  1  8  1  20

action ANOSHLDPIGSTAND  155 1  5  1  10
action ANOSHLDPIGWALK	80  4  5  1  14
action ANOSHLDSHOOT1	100  1  5  1  30
action ANOSHLDSTAND		100  1  5  1  10
action ANOSHLDFROZEN	100  1  5  1  10
action ANOSHLDSHOOT2	105  1  5  1  20
action AARMPIGJUMP1		110  1  5  1  20
action AARMPIGJUMP2		115  1  5  1  20

action APIGCHARGEPREP	115 2  5  1  40
action AARMPIGCHARGE 	130  4  5  1  12
action AARMPIGSHOULDER 	150  1  5  1  60
action AARMPIGHURT		166  1  1  1  10
action AARMPIGGROW		166  1  1  1  10
action AARMPIGDIE		166  5  1  1  30
action AARMPIGDEAD		170  1  1  1  10
action AARMPIGDIE3		48  6  1  1  20
action AARMPIGDEAD3		53	1  1  1  10
action AARMPIGDIE2		54 6  1  1  20
action AARMPIGDEAD2		59 1  1  1  10
action AARMPIGHEADSHOT  60 6  1  1  16
action AARMPIGHEADDEAD  65 1  1  1  10

move ARMPIGWALKVELS 84
move ARMPIGWALK2VELS 128
move ARMPIGSHRUNKVELS 32
move ARMPIGCHARGEVELS 288
move ARMPIGCHARGEFAST 512
move ARMPIGJUMPVELS 288

ai AIARMPIGWAIT AARMPIGSTAND STOPPED geth
ai AIARMPIGSEEK AARMPIGWALK ARMPIGWALKVELS seekplayer
ai AIARMPIGCHARGE APIGCHARGEPREP STOPPED faceplayerslow
ai AIARMPIGJUMP AARMPIGJUMP1 ARMPIGJUMPVELS jumptoplayer
ai AIARMPIGSHOOT AARMPIGSHOOT1 STOPPED faceplayer
ai AIARMPIGSHRUNK AARMPIGCHARGE ARMPIGSHRUNKVELS fleeenemy
ai AIARMPIGFROZEN ANOSHLDFROZEN STOPPED geth
ai AIARMPIGGROW AARMPIGGROW STOPPED geth
ai AIARMPIGHIT AARMPIGHURT STOPPED faceplayer
ai AIARMPIGDYING AARMPIGDIE STOPPED geth

state armpigroamsounds

	ifactorsound THISACTOR PIG_ROAM break
	ifactorsound THISACTOR PIG_ROAM2 break
	ifactorsound THISACTOR PIG_ROAM3 break

	getp[].sound_pitch tempb
	setp[].sound_pitch -384
	rand temp 2
	ife temp 0 sound PIG_ROAM
	ife temp 1 sound PIG_ROAM2
	ife temp 2 sound PIG_ROAM3

	setp[].sound_pitch tempb

ends

defstate armpig_drop
	ifrnd 128
	{
		ifrnd 192
		{
			ifn pchar 1 ifn pchar 2
			spawn DBSPRITE
			else
			spawn BOWSPRITE
			
		}
		else ife es_type 0
		spawn SHIELD
	}
	else
	ifrnd 96 spawn SHOTGUNSPRITE
ends

state armpighurtstate

	ifvarg stun 0 resetcount
	ifcount 12
	{
		ife targetalive YES
		ifn bottarget -1
		{
			ifrnd 96
			{
				ai AIARMPIGSHOOT
				action ANOSHLDSHOOT1
				set initsprite 0
			}
			else
			ifrnd 96
			ife seemytarget YES
				ifg targetdist 3072
					ifl targetdist 16384
					{
						// height check
						ifn bottarget -1
						{
							geta[].z z
							geta[bottarget].z z2
							sub z z2
							abs z
							ifl z 20480 ai AIARMPIGCHARGE
						}
						else
						ai AIARMPIGCHARGE
					}
			else
			{
				ai AIARMPIGSEEK
				action ANOSHLDPIGWALK
			}
		}
		else
		{
			ai AIARMPIGSEEK
			action ANOSHLDPIGWALK
		}
	}

ends

state armpigfrozenstate 

	ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

    ifcount THAWTIME
    {
      ai AIARMPIGWAIT
	  action ANOSHLDSTAND
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  ifn gametype SURVIVAL
	  state armpig_drop
	  
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  set B YES
	  state armpig_body_jibs
      state lite_jibs
      sound GLASS_BREAKING
      killit
    }
	ifn team 1
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state armpigdyingstate 

	strength 0
	state deadsquished
	ifaction AARMPIGHEADSHOT
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			sound THUD
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action AARMPIGHEADDEAD
			break
		}
	}
	ifaction AARMPIGDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 4
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			sound THUD
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action AARMPIGDEAD
			break
		}
	}
	ifaction AARMPIGDIE2
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			globalsound THUD
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action AARMPIGDEAD2
			break
		}
	}
	ifaction AARMPIGDIE3
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			globalsound THUD
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action AARMPIGDEAD3
			break
		}
	}
	set temp NO
	ifaction AARMPIGDEAD set temp YES
	ifaction AARMPIGDEAD2 set temp YES
	ifaction AARMPIGDEAD3 set temp YES
	ifaction AARMPIGHEADDEAD set temp YES
	ife temp YES
	{
		state deadsquished
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				ifaction AARMPIGHEADDEAD set B NO else set B YES
				ifaction AARMPIGDEAD3 set mtype 987
				state armpig_body_jibs
				state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				ifaction AARMPIGHEADDEAD set B NO else set B YES
				ifaction AARMPIGDEAD3 set mtype 987
				state armpig_body_jibs
				state lite_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength PIGCOPSTRENGTH
		  // ai AIARMPIGWAIT
		  // action ANOSHLDSTAND
		// }
	}


ends

state armpigshrunkstate 

  ifcount SHRUNKDONECOUNT
  {
    ai AIARMPIGWAIT
	action ANOSHLDSTAND
  }
  else
    ifcount SHRUNKCOUNT
      sizeto 26 24
  else
    state genericshrunkcode

ends

state armpiggrowstate 

	state genericgrowcode

ends

state transitiontoseek

	ai AIARMPIGSEEK
	ife mtype 1 action ADAMPIGWALK
	ife mtype 2 
	{
		action ANOSHLDPIGWALK
		move ARMPIGWALK2VELS seekplayer
	}

ends

state transitiontowait

	ai AIARMPIGWAIT
	ife mtype 1 action ADAMPIGSTAND
	ife mtype 2 action ANOSHLDSTAND

ends

state transitiontoshoot

	ai AIARMPIGSHOOT
	ife mtype 1 action ADAMPIGSHOOT1
	ife mtype 2 action ANOSHLDSHOOT1
	set initsprite 0 // tracks shooting frames
	
ends

state armpigshootstate

	ife initsprite 0 // ready shooting frame
	{
		ifactioncount 1
		{
			
			ifn bottarget -1
			{
				ifvarand initflags 32
				{
					ifrnd 84
					{
						sound FLAMER_SHOOT sound FLAMER_SHOOT sound FLAMER_SHOOT 
						shoot FIREBOLT shoot FIREBOLT shoot FIREBOLT
						shoot FIREBOLT shoot FIREBOLT shoot FIREBOLT
					}
					else
					ifrnd 128
					{
						sound GRENADE_SHOOT
						set xvel 768
						state rpg_targetprep
						ezshoot zdist STICKYPROJ
						ezshoot zdist STICKYPROJ
						ezshoot zdist STICKYPROJ
						ezshoot zdist STICKYPROJ
					}
					else
					{
						sound DBFIRE
						state hitscan_targetprep
						zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
						zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
						zshoot zdist SHOTGUN zshoot zdist SHOTGUN
						zshoot zdist SHOTGUN zshoot zdist SHOTGUN
					}
				}
				else
				{
					sound DBFIRE
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
					zshoot zdist SHOTSPARK1 zshoot zdist SHOTSPARK1
					zshoot zdist SHOTGUN zshoot zdist SHOTGUN
					zshoot zdist SHOTGUN zshoot zdist SHOTGUN
				}
			}
			else
			{
				sound DBFIRE
				shoot SHOTSPARK1 shoot SHOTSPARK1
				shoot SHOTSPARK1 shoot SHOTSPARK1
				shoot SHOTGUN shoot SHOTGUN
				shoot SHOTGUN shoot SHOTGUN
			}
			ife mtype 0 action AARMPIGSHOOT2
			ife mtype 1 action ADAMPIGSHOOT2
			ife mtype 2 action ANOSHLDSHOOT2
			set initsprite 1
			move STOPPED geth
		}
	}
	ife initsprite 1 // firing frame
	{
		ifactioncount 1
		{
			ife mtype 0 action AARMPIGSHOOT1
			ife mtype 1 action ADAMPIGSHOOT1
			ife mtype 2 action ANOSHLDSHOOT1
			set initsprite 2
		}
	}
	ife initsprite 2 // done firing frame
	{
		ifactioncount 1
		{
			ife seemytarget YES ife shootmytarget YES
			ifn bottarget -1
			ifrnd 96
			{
				ifn bottarget player[].i move STOPPED geth
				else
				move STOPPED faceplayer
				ife mtype 0 action AARMPIGSHOOT1
				ife mtype 1 action ADAMPIGSHOOT1
				ife mtype 2 action ANOSHLDSHOOT1
				set initsprite 0
				break
			}
			set initsprite 0
			state transitiontoseek
		}
	}

ends

state armpigseekstate

	// ifhitspace ife mtype 2
	// { ai AIARMPIGJUMP break }
	
	ifrnd 1 state armpigroamsounds

	ifnotmoving ifrnd 32 operate
	
	ifaction AARMPIGWALK
	{
		ife mtype 1 action ADAMPIGWALK
		ife mtype 2 action ANOSHLDPIGWALK
	}
	ifaction ADAMPIGWALK
		ife mtype 2 action ANOSHLDPIGWALK
		
	ife mtype 2 ifmove ARMPIGWALKVELS
		move ARMPIGWALK2VELS seekplayer
	
	
	ifcount 60 
		ifrnd 8
		  ifl navpoint 0
	state transitiontowait
	
	ife seemytarget YES ife shootmytarget YES
		ife targetalive YES
		ifn bottarget -1
	{
		ifl targetdist 2048 
		ifcount 10
		{ 
			ife mtype 2 ifrnd 84 ai AIARMPIGCHARGE
			else
			state transitiontoshoot 
			break 
		}
		ifcount 30 ifrnd 16
		{
			ife mtype 2 ifl targetdist 16384
			ifrnd 96 
			{ 
				// height check
				ifn bottarget -1
				{
					geta[].z z
					geta[bottarget].z z2
					sub z z2
					abs z
					ifl z 20480 ai AIARMPIGCHARGE
				}
				else
				ai AIARMPIGCHARGE
				ifai AIARMPIGCHARGE break 
			}
			state transitiontoshoot
		}
	}
	ife mtype 2
	{
		ifceilingdistl 128 nullop else
		ifnotmoving ifrnd 32 
		{
			ai AIARMPIGJUMP
			ifg navpoint 0
			{
				set spriteid navpoint
				state facesprite
			}
		}
		ifai AIARMPIGJUMP nullop
		else ifcount 30
		{
			ife targethigher YES // ifp ponground
			ifl targetdist 6144 ai AIARMPIGJUMP
			
		}
	}

ends

state armpigwaitstate 

	ifaction AARMPIGSTAND 
	{
		ife mtype 1 action ADAMPIGSTAND
		ife mtype 2 action ANOSHLDSTAND
	}
	ifaction ADAMPIGSTAND
		ife mtype 2 action ANOSHLDSTAND
	
	
	ifcount 20 state transitiontoseek
	
ends

state shoulderhitplayer

	ife bottarget player[].i set target player[].i else
	ife bottarget -1 set target player[].i else
	set target bottarget
	ifpdistl 3072 quake 12
	sound CYBOSS_LAND
	
	ife target player[].i
	{
		ifphealthl 60 sound SQUISHED
		wackplayer
		getp[].ang temp
		add temp 1024
		cos tempb temp
		sin tempc temp
		shiftvarl tempb 9
		shiftvarl tempc 9
		setp[].posxv tempb
		setp[].posyv tempc
	}
	else
	{
		seta[target].xvel -1024
		getav[target].stun temp
		add temp 30
		setav[target].stun temp
		ifl actorvar[target].inithp 1000
		{
			geta[target].z z
			sub z 2048
			seta[target].z z
		}
		geta[target].zvel z
		sub z 2048
		seta[target].zvel z
		
	}
	
	geta[target].htextra tempc
	rand tempb 10
	add tempb 40
	add tempc tempb
	seta[target].htextra tempc
	seta[target].htpicnum RPG
	seta[target].htowner THISACTOR
	seta[target].htang sprite[].ang
	
	action AARMPIGSHOULDER
	move STOPPED geth

ends


state shoulderhitobject

	ifpdistl 6144 quake 12
	debris SCRAP1 4
	debris SCRAP2 4
	debris SCRAP3 4
	debris SCRAP4 4
	debris SCRAP5 4
	debris SCRAP6 4
	sound CYBOSS_LAND
	
	hitradius 2048 10 20 30 40
	// seta[].htextra -1
	ifpdistl 2048 seta[player[].i].htowner THISACTOR
	action AARMPIGSHOULDER
	move STOPPED geth

ends

state armpigchargestate

	ife bottarget -1 ife team 1
	{
		ai AIARMPIGSEEK
		action ANOSHLDPIGWALK
		break
	}
	
	ife seemytarget YES ifl targetdist 4096
	set crumbwait 2 else
	ifmove ARMPIGCHARGEFAST set crumbwait 2
	else ifn team actorvar[player[].i].team
	{ set crumbwait 0 state crumbcode }
	
	ifaction APIGCHARGEPREP
	{
		ifg targetdist 1024
		state strafechecklong
		ifactioncount 2
		{
			sound PIGBROAR2
			action AARMPIGCHARGE
			ifn bottarget player[].i move ARMPIGCHARGEVELS geth
			else
			ifn team actorvar[player[].i].team
			move ARMPIGCHARGEVELS faceplayersmart
			else
			move ARMPIGCHARGEVELS geth
		}
	}
	ifaction AARMPIGCHARGE
	{
		ifg targetdist 1024
		state strafechecklong
		
		ifn bottarget -1
		{
			geta[].z z
			sub z sprite[bottarget].z
		
			ifl targetdist 6144
			{
				ifg z 8192
				{
					ai AIARMPIGJUMP
					move ARMPIGCHARGEFAST jumptoplayer
					break
				}
			}
			else
			ifg z 16384
			ife sector[].floorslope 0
			{
				ai AIARMPIGSEEK
				break
			}
		}
		ifmove ARMPIGCHARGEVELS
		ifcount 30
		{
			move ARMPIGCHARGEFAST geth
			ifn bottarget -1
			{
				set spriteid bottarget
				state facesprite
			}
			geta[].ang float
		}
		ifmove ARMPIGCHARGEFAST
		{
			ifge SKILL 4
			{
				geta[].xvel xvel, add xvel 32 seta[].xvel xvel
			}
			// spawn FRAMEEFFECT1
			state makespeedblur
			seta[].ang float
		}
		ifl targetdist 1280
			state shoulderhitplayer
		ifnotmoving
			state shoulderhitobject
		ifaction AARMPIGCHARGE // did not hit anything
		ifcount 60
		{
			action AARMPIGSHOULDER
			move STOPPED geth
		}
		
	}
	else
	ifaction AARMPIGSHOULDER
	{
		seta[].ang float
		ifactioncount 1
		{
			ai AIARMPIGSEEK
			action ANOSHLDPIGWALK
		}
	}

ends

state armpigjumpstate

	ifg navpoint 0
	{
		set spriteid navpoint
		state facesprite
	}

	ifaction AARMPIGJUMP1
	{
		ifactioncount 3
			action AARMPIGJUMP2
	}
	ifaction AARMPIGJUMP2
	{
		iffloordistl 8
		{
			ai AIARMPIGSEEK
			action ANOSHLDPIGWALK
		}
	}
	ifcount 8
	ifl targetdist 1280
	ife seemytarget YES ife shootmytarget YES
	  ife targetalive YES
	{
		geta[].ang float
		set crumbwait 2
		ai AIARMPIGCHARGE
		state shoulderhitplayer
	}

ends

useractor enemy ARMPIG ARMPIGSTRENGTH

ifaction 0
{
	ai AIARMPIGWAIT
	sizeat 26 24
	cstat 257
	clipdist 56 // 80
	getp[].sound_pitch tempb
	setp[].sound_pitch -384
	sound PIG_RECOG
	setp[].sound_pitch tempb
}

fall

ifai AIARMPIGFROZEN { state armpigfrozenstate break }
ifai AIARMPIGDYING { state armpigdyingstate break }

state monsterai 

ifg sprite[].zvel 0 add FEMFALLDMG 1
else
{
	ifg FEMFALLDMG 30
	{
		ifpdistl 3072 quake 12
		sound CYBOSS_LAND
	}
	set FEMFALLDMG 0
}

ifai AIARMPIGSHRUNK state armpigshrunkstate else
ifai AIARMPIGGROW state armpiggrowstate else
ifai AIARMPIGWAIT state armpigwaitstate else
ifai AIARMPIGSEEK state armpigseekstate else
ifai AIARMPIGSHOOT state armpigshootstate else
ifai AIARMPIGCHARGE state armpigchargestate else
ifai AIARMPIGJUMP state armpigjumpstate else
ifai AIARMPIGHIT state armpighurtstate


ife monstatus -1 break
ifhitweapon
{
	ifwasweapon HEADJIB1
	{
		sound SQUISHED
		guts JIBS2 2
		state enemy_death
		ifn gametype SURVIVAL
		state armpig_drop
		state random_wall_jibs
		addkills 1
		strength 0
		ai AIARMPIGDYING
		action AARMPIGHEADSHOT
		rand zdist 1024
		sub zdist 3500
		ezshoot zdist PIGHEADPROJ
		rand angvar 512
		sub angvar 256
		add angvar sprite[].htang
		seta[RETURN].ang angvar
		seta[RETURN].pal sprite[].pal
		ifrnd 128 cstat 0 else cstat 4
		break
	}
    ifwasweapon SHRINKSPARK
	{
		ife mtype 2 // shield down
		{
		  sound ACTOR_SHRINKING
		  ai AIARMPIGSHRUNK
		  set shrunken 1
		  break
		}
	}
	else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	ife mtype 2 state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		{
		  sound NEWFREEZE
		  spritepal 1
		  ai AIARMPIGFROZEN
		  strength 0
		  state freezeme
		  break
		}
		ifwasweapon GROWSPARK
		{
		  cstat 0
		  sound ACTOR_GROWING
		  ai AIARMPIGGROW
		  break
		}
		addkills 1
		state enemy_death
		ifn gametype SURVIVAL
		state armpig_drop
		ai AIARMPIGDYING
		ifrnd 128 cstat 256 else cstat 260
		ifrnd 84
		action AARMPIGDIE2
		else
		ifrnd 128
		{
			action AARMPIGDIE3
			rand zdist 1024
			sub zdist 2048
			ezshoot zdist ARMPIGARMPROJ
			rand angvar 2047
			seta[RETURN].ang angvar
			seta[RETURN].pal sprite[].pal
			seta[RETURN].xvel 128
		}
		guts JIBS6 8
		ifwasweapon RPG
		{
			sound SQUISHED
			spawn BLOODPOOL
			ifaction AARMPIGHEADDEAD set B NO else set B YES
			state armpig_body_jibs
			state lite_jibs
			killit
        }
		ifwasweapon RADIUSEXPLOSION
		{
			sound SQUISHED
			spawn BLOODPOOL
			ifaction AARMPIGHEADDEAD set B NO else set B YES
			state armpig_body_jibs
			state lite_jibs
			killit
        }
		sound PIG_DYING
		setactorsoundpitch THISACTOR PIG_DYING -512
		break
	}
	ife mtype 2 ifrnd 4
		spawn BLOODPOOL
	ife mtype 2 sound PIG_PAIN
	
	// ife mtype 0 ifstrength DAMPIGSTRENGTH set mtype 1
	set temp inithp, mul temp 75, div temp 100
	ife mtype 0 ifl sprite[].extra temp set mtype 1
	
	ife mtype 1 // ifstrength EDFPIGSTRENGTH
	{
		set mtype 2
		spriteflags 4194304
		geta[].z savz
		set temp savz
		sub temp 10240
		seta[].z temp
		// shield destruction code
		spawn SHLDPART1
		spawn SHLDPART2
		spawn SHLDPART2
		spawn SHLDPART3
		spawn SHLDPART3
		spawn SHLDPART4
		spawn SHLDPART5
		spawn SHLDPART6
		spawn SHLDPART6
		seta[].z savz
		ifstrength 200 strength 200
		seta[].htactorstayput -1 // edfpig cannot be stayput
		ai AIARMPIGCHARGE
	}
	
	ifai AIARMPIGCHARGE nullop else
	ifai AIARMPIGJUMP nullop else
	ifai AIARMPIGSHRUNK nullop else
	ife  mtype 2
	{
	    ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
		ifrnd 16 { sound PIG_PAIN ai AIARMPIGHIT break }
		else ifg stun 0 { sound PIG_PAIN ai AIARMPIGHIT break }
		
	}

}
ife mtype 2 state checksquished
else ifsquished { geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

enda

define SHLDPART1 5805
define SHLDPART2 5806
define SHLDPART3 5807
define SHLDPART4 5808
define SHLDPART5 5809
define SHLDPART6 5810

move SHLDPRTVELS 160

state shldpartcode

ifmove 0
{
	sizeat 46 40
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	seta[].zvel -1560
	move SHLDPRTVELS geth randomangle
}

ifcount 5
	ifrnd 128
{
	randvar temp 3
	ife temp 0 cstat 0
	ife temp 1 cstat 4
	ife temp 2 cstat 8
	ife temp 3 cstat 12
}
geta[].zvel temp
add temp 42
seta[].zvel temp
fall
iffloordistl 2 killit

ends

useractor enemy EDFPIG EDFPIGSTRENGTH
	set mtype 2
	spriteflags 4194304
	cactor ARMPIG
	seta[].htpicnum sprite[].picnum
	sizeat 26 24
	clipdist 80
	cstat 257
	ai AIARMPIGSEEK
	action ANOSHLDPIGWALK
	getp[].sound_pitch tempb
	setp[].sound_pitch -384
	sound PIG_RECOG
	setp[].sound_pitch tempb
enda

useractor enemystayput ARMPIGSTAYPUT ARMPIGSTRENGTH
	cactor ARMPIG
enda

useractor notenemy SHLDPART1 0 state shldpartcode enda
useractor notenemy SHLDPART2 0 state shldpartcode enda
useractor notenemy SHLDPART3 0 state shldpartcode enda
useractor notenemy SHLDPART4 0 state shldpartcode enda
useractor notenemy SHLDPART5 0 state shldpartcode enda
useractor notenemy SHLDPART6 0 state shldpartcode enda

appendevent EVENT_LOADACTOR

ifactor ARMPIG sizeat 26 24
ifactor ARMPIGSTAYPUT sizeat 26 24
ifactor EDFPIG sizeat 26 24

endevent
