// PIGCOP BOSS version .96, February 9, 2018
// sprites by sebabdukeboss20, code by Dan Gaskill
// thanks to JBlade for extracting and arranging sprites

// define PIGBOSS 5120
// define PIGBOSSSTAYPUT 5121

spriteshadow PIGBOSSGUN

spriteflags PIGBOSS 5251072
// HURTSPAWNBLOOD + NODAMAGEPUSH + SMOOTHMOVE

gamevar PIGBVAR 0 0
gamevar PIGBVAR2 0 0
gamevar PIGBVAR3 0 0
gamevar PIGBRESET 0 2

action APIGBOSSSTAND	0  1 5 1 10
action APIGBOSSWALK     0  4 5 1 20
action APIGBOSSRUN      0  4 5 1 11
action APIGBOSSSHOOT1    20 1 5 1 17
action APIGBOSSSHOOT2    25 1 5 1 17
action APIGBOSSSHOOT3    30 1 5 1 17
action APIGBOSSSHOOT4    35 1 5 1 17
// action APIGBOSSMELEE	40 3 1 1 15
action APIGBOSSMELEE	4096 3 5 1 15
action APIGBOSSTAUNTFORW    9444 3 5 1 6
action APIGBOSSTAUNTBACK    9454 3 5 -1 12
action APIGBOSSROAR		9476 3 5 1 60
action APIGBOSSSTOMP1	9491 2 5 1 20
action APIGBOSSSTOMP2   9501 1 5 1 30
action APIGBOSSDYING    51 6 1 1 20
action APIGBOSSHIT      13777 1 5 1 12 // 51
action APIGBOSSDEAD     56 1 1 1 1
action APIGBOSSFROZEN    20 1 5 1 10
action APIGBOSSGROW      51 1 5 1 10

move PIGBOSSWALKVELS 112
move PIGBOSSRUNVELS 212
move PIGBOSSRUNBOT 212
move PIGBOSSRUNFACEVELS 288
move PIGBOSSSRUNKVELS 32

ai AIPIGBOSSWAIT APIGBOSSSTAND STOPPED geth
ai AIPIGBOSSROAR APIGBOSSROAR STOPPEDFORPLAYER geth
ai AIPIGBOSSSEEK APIGBOSSWALK PIGBOSSWALKVELS seekplayer
ai AIPIGBOSSSHOOT APIGBOSSSHOOT1 STOPPEDFORPLAYER faceplayer
ai AIPIGBOSSCHARGE APIGBOSSRUN PIGBOSSRUNVELS seekplayer
ai AIPIGBOSSMELEE APIGBOSSMELEE PIGBOSSWALKVELS faceplayer
ai AIPIGBOSSTAUNT APIGBOSSTAUNTFORW STOPPEDFORPLAYER geth

ai AIPIGBOSSDYING APIGBOSSDYING STOPPED geth
ai AIPIGBOSSFROZEN APIGBOSSFROZEN STOPPED geth
ai AIPIGBOSSSHRUNK APIGBOSSWALK PIGBOSSSRUNKVELS fleeenemy
ai AIPIGBOSSGROW APIGBOSSGROW STOPPED geth
ai AIPIGBOSSHIT APIGBOSSHIT STOPPEDFORPLAYER faceplayer

state spawngunstate
	espawn PIGBOSSGUN
	setactor[RETURN].xrepeat sprite[].xrepeat
	setactor[RETURN].yrepeat sprite[].yrepeat
	setav[RETURN].init_xrepeat sprite[].xrepeat
	setav[RETURN].init_yrepeat sprite[].yrepeat
	setactor[RETURN].pal sprite[THISACTOR].pal
	getactor[RETURN].z PIGBVAR
	subvar PIGBVAR 6144
	setactor[RETURN].z PIGBVAR
	
	ifrnd 128 spawn SHIELD
ends

state pigbosshitstate

	ifg stun 0 resetactioncount
	ifactioncount 2 
	{
		ifrnd 32
		{
			ai AIPIGBOSSROAR
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			ifrnd 128 sound PIGBROAR else sound PIGBROAR2
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
		}
		else
		{
			ifn bottarget -1 geta[bottarget].z z2 else geta[player[].i].z z2
			add z2 8192
		
			ifactornotstayput
			ifg z2 sprite[].z
				ai AIPIGBOSSCHARGE
			else
			{
				set target -1
				ifn bottarget -1 set target bottarget else
				ifn team 1 set target player[].i
				
				ifn target -1
				{
					ife sprite[target].sectnum sprite[].sectnum ai AIPIGBOSSCHARGE
					else
					ai AIPIGBOSSSHOOT
				}
			}
		}
	}

ends

state pigbossroarstate

	// damage resistance during roar to prevent cheap kills
	ifvarg sprite[THISACTOR].htextra 0
	{
		getactor[THISACTOR].htextra PIGBVAR
		divvar PIGBVAR 2
		setactor[THISACTOR].htextra PIGBVAR
	}
	ifactioncount 3 
	{
		ifn bottarget -1 geta[bottarget].z z2 else geta[player[].i].z z2
		add z2 8192
		
		ifg z2 sprite[].z
		ifactornotstayput
			ai AIPIGBOSSCHARGE
		else
		{
		
			set target -1
			ifn bottarget -1 set target bottarget else
			ifn team 1 set target player[].i
			
			ifn target -1
			{
				ife sprite[target].sectnum sprite[].sectnum ai AIPIGBOSSCHARGE
				else
				ai AIPIGBOSSSEEK
			}
		}
	}

ends

state pigbosstauntstate

	ifvarg sprite[THISACTOR].htextra 0
	{
		getactor[THISACTOR].htextra PIGBVAR
		divvar PIGBVAR 2
		setactor[THISACTOR].htextra PIGBVAR
	}
	
	ifaction APIGBOSSSTOMP1
	{
		ifactioncount 2 
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			sound PGBSTOMP
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
				
			action APIGBOSSSTOMP2
			
			add PIGBRESET 1
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT break
			
			set PIGBVAR NO
			ifspritepal 0 ifpdistl 8192 set PIGBVAR YES
			else
			ifspritepal 42 ifpdistl 8192 set PIGBVAR YES
			else ifpdistl 6144 set PIGBVAR YES
			ife PIGBVAR YES
			{
				wackplayer
				setp[].poszv -2048
				getp[].posz PIGBVAR
				sub PIGBVAR 1024
				setp[].posz PIGBVAR
			}
			ifpdistl 10240 quake 15
			
		}
		break
	}
	ifaction APIGBOSSSTOMP2
	{
		ifactioncount 1
		{
			ifvarg PIGBRESET 1
			{
				setvar PIGBRESET 0
				ifn bottarget -1 geta[bottarget].z z2 else geta[player[].i].z z2
				
				ifactornotstayput
				ifg z2 sprite[].z
					ai AIPIGBOSSCHARGE
				else 
				{
					set target -1
					ifn bottarget -1 set target bottarget else
					ifn team 1 set target player[].i
					
					ifn target -1
					{
						ife sprite[target].sectnum sprite[].sectnum ai AIPIGBOSSCHARGE
						else
						ai AIPIGBOSSSHOOT
					}
					else ai AIPIGBOSSSEEK
					
				}
				break
			}
			else action APIGBOSSSTOMP1
		}
	}
	
	ifaction APIGBOSSTAUNTFORW ifactioncount 3 
	{
		action APIGBOSSTAUNTBACK
		ifspritepal 0 
		{
			getp[].sound_pitch PIGBVAR
			setp[].sound_pitch -512
		}
		ifspritepal 42
		{
			getp[].sound_pitch PIGBVAR
			setp[].sound_pitch -512
		}
		ife shrunken 0 sound INTRO4_5
		ifspritepal 0 setp[].sound_pitch PIGBVAR
		ifspritepal 42 setp[].sound_pitch PIGBVAR
		addvar PIGBRESET 1
	}
	ifaction APIGBOSSTAUNTBACK ifactioncount 3 
	{
		ifvarg PIGBRESET 1
		{
			setvar PIGBRESET 0
			ifn bottarget -1 geta[bottarget].z z2 else geta[player[].i].z z2
			add z2 8192
			ifactornotstayput 
			ifg z2 sprite[].z
			ai AIPIGBOSSCHARGE 
			else
			{
				set target -1
				ifn bottarget -1 set target bottarget else
				ifn team 1 set target player[].i
				
				ifn target -1
				{
					ife sprite[target].sectnum sprite[].sectnum ai AIPIGBOSSCHARGE
					else
					ai AIPIGBOSSSHOOT
				}
				else ai AIPIGBOSSSEEK
				
			}
			break
		}
		action APIGBOSSTAUNTFORW
	}
ends

state pigbossgrowstate

	state genericgrowcode
	sizeto 255 192

ends

state pigbossshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIPIGBOSSSEEK
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 0 { sizeto 66 60 sizeto 66 60 }
	  else
	  ifspritepal 42 { sizeto 66 60 sizeto 66 60 }
	  else
      sizeto 33 30
	}
  else
    state genericshrunkcode

ends

state pigbossfrozenstate

    ifcount THAWTIME
    {
      ai AIPIGBOSSSEEK
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
        strength 0
        break
      }
	  state spawngunstate
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
      state standard_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state pigbossseekstate

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	setp[].sound_pitch SHRUNKPITCH
	ifrnd 1 sound PIGBROAM1
	else
	ifrnd 1
	{
		ai AIPIGBOSSROAR
		ifrnd 128 sound PIGBROAR else sound PIGBROAR2
		break
	}
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0

	set temp NO
	ifge SKILL 4 { ifcount 20 set temp YES }
	else ifcount 40 set temp YES
	
	ife temp YES 
		ife seemytarget YES
		  ife targetalive YES
	{
	
		ifn bottarget -1 geta[bottarget].z z2 else geta[player[].i].z z2
		add z2 8192
		
		ifactornotstayput
		ifg z2 sprite[].z
		{
			ifrnd 32
				ifl targetdist 8192
					ai AIPIGBOSSCHARGE
		}
		else
			ifrnd 6 ai AIPIGBOSSSHOOT
					
		ifrnd 3
		{
			ifrnd 128
			ifactornotstayput
			ifg z2 sprite[].z
			ai AIPIGBOSSCHARGE
			else ai AIPIGBOSSSHOOT
		}
	}
ends

state pigbossfire

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
	ifrnd 128 sound M4FIRE
	else
	sound PIG_ATTACK
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
	
	spawn SHOTGUNSHELL
	spawn SHELL
	ifn bottarget -1
	{
		set spriteid bottarget
		state facesprite
		state hitscan_targetprep
		zshoot zdist SHOTSPARK1
		zshoot zdist SHOTSPARK1
		zshoot zdist SHOTSPARK1
		ifspritepal 0
		{
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
		}
		ifspritepal 42
		{
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
		}
	}
	else
	{
		shoot SHOTSPARK1
		shoot SHOTSPARK1
		shoot SHOTSPARK1
		ifspritepal 0
		{
			shoot SHOTSPARK1
			shoot SHOTSPARK1
		}
		ifspritepal 42
		{
			shoot SHOTSPARK1
			shoot SHOTSPARK1
		}
	}
ends

state pigbossfire2

	geta[].x bossx
	geta[].y bossy
	geta[].ang bossang
	sub bossang 448
	set PIGBVAR bossx
	set tempb sprite[].xrepeat
	shiftl tempb 4
	add PIGBVAR tempb
	rotatepoint sprite[].x sprite[].y PIGBVAR sprite[].y bossang newx newy
	seta[].x newx
	seta[].y newy
	
	sound FLAKSHOT
	set xvel 844
	state rpg_targetprep
	zshoot zdist FLAKPROJ
	set xvel 844			
	state rpg_targetprep
	zshoot zdist FLAKPROJ
	set xvel 844
	state rpg_targetprep
	zshoot zdist FLAKPROJ 
	set xvel 844
	state rpg_targetprep
	zshoot zdist FLAKPROJ
	set xvel 844
	state rpg_targetprep
	zshoot zdist FLAKPROJ
	set xvel 844
	state rpg_targetprep			
	zshoot zdist FLAKPROJ

	seta[].x bossx
	seta[].y bossy

ends

state pigbossshootstate

	ifaction APIGBOSSSHOOT1
	{
		ifactioncount 1 
		{
			action APIGBOSSSHOOT2
			state pigbossfire
			ifn bottarget -1 ifn bottarget player[].i
			move STOPPED geth
			else
			move STOPPEDFORPLAYER faceplayerslow
		}
	}
	
	ifaction APIGBOSSSHOOT2
	{
		ifactioncount 1 
		{
			action APIGBOSSSHOOT3
			state pigbossfire2
		}
	}
	
	ifaction APIGBOSSSHOOT3
	{
		ifactioncount 1 
		{
			action APIGBOSSSHOOT4
			ifrnd 128
				state pigbossfire
			else
			{
				geta[].x bossx
				geta[].y bossy
				geta[].ang bossang
				sub bossang 448
				set PIGBVAR bossx
				set tempb sprite[].xrepeat
				shiftl tempb 4
				add PIGBVAR tempb
				rotatepoint sprite[].x sprite[].y PIGBVAR sprite[].y bossang newx newy
				seta[].x newx
				seta[].y newy
				
				sound RPG_SHOOT
				
				ifn shrunken 0 setactorsoundpitch THISACTOR RPG_SHOOT SHRUNKPITCH
				
				eshoot MORTER2
				getactor[RETURN].zvel PIGBVAR
				subvar PIGBVAR 1280
				setactor[RETURN].zvel PIGBVAR
		
				seta[].x bossx
				seta[].y bossy
			}
			
			
		}
	}
	
	ifaction APIGBOSSSHOOT4
	{
		ifactioncount 1
		{
			ife bottarget -1 
			{
				setvar PIGBRESET 0
				ai AIPIGBOSSWAIT
			}
			else
			{
				addvar PIGBRESET 1
				ifvarg PIGBRESET 4
				ifrnd 192
				{
					setvar PIGBRESET 0
					ai AIPIGBOSSWAIT
				}
				else 
				{
					action APIGBOSSSHOOT2
					state pigbossfire
				}
			}
		}
	}

ends

state pigbosswaitstate

	ifactioncount 2 ai AIPIGBOSSSEEK

ends

state pigbossmeleestate

	ifactioncount 1
	{
		ifl targetdist 1816
		ife seemytarget YES ife shootmytarget YES
		ifvare PIGBRESET NO
		{
			set PIGBRESET YES
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				break
			ifactorsound THISACTOR PIGBSMACK nullop else
			sound PIGBSMACK
			
			ife bottarget -1 getp[].i PIGBVAR else
			ife bottarget player[].i getp[].i PIGBVAR else
			set PIGBVAR bottarget
			
			ife PIGBVAR player[].i // player only stuff
			{
				ife pdown NO
				{
				palfrom 48 63
				wackplayer
				setp[].weapon_pos -9
				setp[].kickback_pic 0
				getp[].ang angvar
				add angvar 1024
				cos PIGBVAR2 angvar
				sin PIGBVAR3 angvar
				shiftvarl PIGBVAR2 9
				shiftvarl PIGBVAR3 9
				ifspritepal 0 { shiftvarl PIGBVAR2 1 shiftvarl PIGBVAR3 1 }
				ifspritepal 42 { shiftvarl PIGBVAR2 1 shiftvarl PIGBVAR3 1 }
				setplayer[THISACTOR].posxv PIGBVAR2
				setplayer[THISACTOR].posyv PIGBVAR3
				}
				// push player backwards
			}
			else
			{
				seta[PIGBVAR].xvel -1024
				getav[PIGBVAR].stun temp
				add temp 30
				setav[PIGBVAR].stun temp
				geta[PIGBVAR].z z
				sub z 2048
				seta[PIGBVAR].z z
				geta[PIGBVAR].zvel z
				sub z 2048
				seta[PIGBVAR].zvel z
			}

			ifspritepal 0
			seta[PIGBVAR].htextra PIGBMELEEDAMAGE
			else ifspritepal 42
			seta[PIGBVAR].htextra PIGBMELEEDAMAGE
			else seta[PIGBVAR].htextra PALPIGBMELEEDAMAGE
			seta[PIGBVAR].htpicnum KNEE
			seta[PIGBVAR].htowner THISACTOR
			seta[PIGBVAR].htang sprite[THISACTOR].ang
			
			
		}
		
	}
	ifactioncount 3 { ai AIPIGBOSSWAIT set PIGBRESET NO }

ends

state pigbosschargestate

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
	ifrnd 1 sound PIGBROAM2
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0

	ife seemytarget YES
		ife shootmytarget YES
			ife shrunken 0
			  ife targetalive YES
	{
		ifmove PIGBOSSRUNVELS 
		{
			ifn bottarget -1 ifn bottarget player[].i
			move PIGBOSSRUNFACEVELS geth
			else
			move PIGBOSSRUNFACEVELS faceplayersmart
		}
		ifmove PIGBOSSRUNFACEVELS ifl targetdist 1560 ife seemytarget YES
		{
			ai AIPIGBOSSMELEE
			sound PIGBMELEE
			sound PIGBSWING
			break
		}
		
		ifcount 40 ifrnd 12 ai AIPIGBOSSSHOOT
	}
	
	ifcount 30 ifrnd 6 
	{
		ai AIPIGBOSSTAUNT
		ifrnd 96 action APIGBOSSSTOMP1
	}
	else
	ifcount 90 ifrnd 6 ai AIPIGBOSSSEEK

ends

useractor enemystayput PIGBOSSSTAYPUT PIGBOSSSTRENGTH cactor PIGBOSS enda

useractor enemy PIGBOSS PIGBOSSSTRENGTH

fall
ifai 0 
{
	ifl sprite[].xrepeat 4 spawn GLARESTAR
	ai AIPIGBOSSROAR
	ifrnd 128 sound PIGBROAR else sound PIGBROAR2
	ifspritepal 0 clipdist 128 else
	ifspritepal 42 clipdist 128 else clipdist 80
	ifspritepal 0 
		sizeat 66 60
	else
	ifspritepal 42
	
		sizeat 66 60
	else
	{
		sizeat 33 30
		strength PIGBOSSPALSTRENGTH
	}
	set init_xrepeat sprite[].xrepeat
	set init_yrepeat sprite[].yrepeat
	cstat 257
}

ifai AIPIGBOSSDYING
{
	strength 0
	
	ifaction APIGBOSSDYING
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			globalsound THUD
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			cstat 0
			resetcount
			action APIGBOSSDEAD
			ife gametype 0
			state spawngunstate
			ifrnd 128 setactor[RETURN].cstat 4
			break
		}
	}
	ifaction APIGBOSSDEAD
	{
		ifspritepal 42
		{
			set temp NO
			ife VOLUME 6 set temp YES
			ife attmode YES set temp YES
			
			ife temp YES
			{
				ifcount 10 nullop
				else ifcount 9
				{
					ife VOLUME 6
					{
						stopallsounds
						starttrack 10
						globalsound VACA_ENDMUSIC
						set midscreen CARIB_ENDCUT
						set cutend totalclock
						add cutend 9999
						set cutmash 4
						startscreen
						
						set midscreen 10842
						set cutmash 5
						startscreen
						
						set midscreen 10843
						set cutmash 5
						startscreen
						
						set midscreen 10844
						set cutmash 5
						startscreen
						
						set midscreen 10845
						set cutmash 5
						startscreen
						
						endofgame 10
					}
					else setp[].timebeforeexit 150
				}
				break
			}
		}
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				state standard_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength PIGCOPSTRENGTH
		  // ai AIPIGBOSSROAR
		  // ifrnd 128 globalsound PIGBROAR else globalsound PIGBROAR2
		// }
	}
	
	break
}

ifai AIPIGBOSSFROZEN { state pigbossfrozenstate break }
ifai AIPIGBOSSGROW { state pigbossgrowstate break }

state monsterai 

ifai AIPIGBOSSHIT state pigbosshitstate else
ifai AIPIGBOSSSHRUNK state pigbossshrunkstate else
ifai AIPIGBOSSROAR state pigbossroarstate else
ifai AIPIGBOSSTAUNT state pigbosstauntstate else
ifai AIPIGBOSSSEEK state pigbossseekstate else
ifai AIPIGBOSSCHARGE state pigbosschargestate else
ifai AIPIGBOSSMELEE state pigbossmeleestate else
ifai AIPIGBOSSSHOOT state pigbossshootstate else
ifai AIPIGBOSSWAIT state pigbosswaitstate

ifmove STOPPEDFORPLAYER
ifn bottarget -1 ifn bottarget player[].i
{
	move STOPPED geth
	set spriteid bottarget
	state facesprite
}

ifmove PIGBOSSRUNVELS
ifn bottarget -1 ifn bottarget player[].i
{
	move PIGBOSSRUNBOT geth
	set spriteid bottarget
	state facesprite
}

ife monstatus -1 break
ifhitweapon
{
    ifwasweapon SHRINKSPARK
	{
		ifn sprite[].pal 42
		{
		  sound ACTOR_SHRINKING
		  ai AIPIGBOSSSHRUNK
		  set shrunken 1
		  break
		}
	}
	else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		ifn sprite[].pal 42
		{
		  sound NEWFREEZE
		  spritepal 1
		  ai AIPIGBOSSFROZEN
		  strength 0
		  state freezeme
		  break
		}
		ifwasweapon GROWSPARK
		ifn sprite[].pal 42
		{
		  cstat 0
		  sound ACTOR_GROWING
		  ai AIPIGBOSSGROW
		  break
		}
		addkills 1
		state enemy_death
		ai AIPIGBOSSDYING
		guts JIBS6 8
		ifspritepal 0 { globalsound PIGBDIE break }
		ifspritepal 42 { globalsound PIGBDIE break }
		ifwasweapon RPG
		{
			ife gametype 0 state spawngunstate
			sound SQUISHED
			spawn BLOODPOOL
			state standard_jibs
			state standard_jibs
			killit
        }
		ifwasweapon RADIUSEXPLOSION
		{
			ife gametype 0 state spawngunstate
			sound SQUISHED
			spawn BLOODPOOL
			state standard_jibs
			state standard_jibs
			killit
        }
		sound PIGBDIE
		break
	}
	ifrnd 2
		spawn BLOODPOOL
	
	ifg stun 0 ai AIPIGBOSSHIT
	
	ifai AIPIGBOSSHIT nullop else
	ifai AIPIGBOSSMELEE nullop else
	ifai AIPIGBOSSSHRUNK nullop else
	{
		ifrnd 16 
		ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
		{ sound PIGBPAIN ai AIPIGBOSSHIT setvar PIGBRESET 0 break }
		ifrnd 64 
		{
			sound PIGBPAIN
			ifai AIPIGBOSSSHOOT nullop else ai AIPIGBOSSSHOOT
		}
	}
}
else
ifai AIPIGBOSSSHRUNK nullop else
ifg stun 0 ai AIPIGBOSSHIT


enda


appendevent EVENT_KILLIT

ifactor MORTER2 { debris SCRAP1 3 debris SCRAP2 3 }

endevent
