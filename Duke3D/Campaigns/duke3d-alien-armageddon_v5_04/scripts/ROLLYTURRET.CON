// ROLLYTURRET

spriteshadow ROLLYTURRET
spriteshadow DOLLBOMB

action AROLLGO  21  4  7  -1  5
action AROLLHATCH  28  1  7  1  10
action AROLLREADY  35  1  7  1  10
action AROLLSHOOT  42  1  7  1  10
move ROLLYMOV 256


state rollyexplode

	espawn MINEEXP
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	
	// hitradius PIPEBOMBRADIUS 15 30 45 60
	sound PIPEBOMB_EXPLODE
	debris SCRAP1 4
	debris SCRAP2 4
	debris SCRAP5 4
	state tech_debris
	set monstatus 0
	killit

ends

state rollyfirerpg

	sound RPG_SHOOT
	ife team 1 set xvel 1024 else
	set xvel 644
	state rpg_targetprep	
	ezshoot zdist RPG
	ife team 1
	seta[RETURN].extra 70
	else
	seta[RETURN].extra 60
	ife team 1 seta[RETURN].xvel 1024
	seta[RETURN].ang sprite[].ang
	addstrength -12
	set botclip -1

ends

state rollyshoot

	switch sprite[bottarget].picnum
	case CRACK1 case CRACK2 case CRACK3 case CRACK4
		
		geta[bottarget].z zdist
		add zdist 3072
		
		sub zdist sprite[].z
		ldist xydist THISACTOR bottarget
		ife xydist 0 setvar xydist 1
		ifl xydist 768 hitradius 844 2 2 2 2
		sound RPG_SHOOT
		mulvar zdist 644
		div zdist xydist
		geta[].z savz
		sub savz 2048
		seta[].z savz
		ezshoot zdist RPG
		add savz 2048
		seta[].z savz
		seta[RETURN].ang sprite[].ang
	
		addstrength -15
		set botclip -1
		
		dist xydist THISACTOR bottarget
		ifl xydist 2048 hitradius 2560 2 2 2 2
	break
	
	case APLAYER
	case SHELLY
	case SHELLYCROUCH
	case DUKEBOT
	case DUKEBOTCROUCH
		
		geta[].z savz
		sub savz 2048
		seta[].z savz
			state rollyfirerpg
		add savz 2048
		seta[].z savz
		seta[RETURN].ang sprite[].ang
		
	break
	
	default
	ife sprite[bottarget].statnum 12
	{
		geta[bottarget].z zdist
		add zdist 3072
		ifvarand sprite[bottarget].cstat 32
		{
			ifl sprite[bottarget].z sprite[].z sub zdist 6144 else
			add zdist 2048
		}
		sub zdist sprite[].z
		ldist xydist THISACTOR bottarget
		ife xydist 0 setvar xydist 1
		ifl xydist 768 hitradius 844 2 2 2 2
		sound RPG_SHOOT
		mulvar zdist 644
		div zdist xydist
		geta[].z savz
		sub savz 2048
		seta[].z savz
		ezshoot zdist RPG
		add savz 2048
		seta[].z savz
		seta[RETURN].ang sprite[].ang

		addstrength -15
		set botclip -1
	}
	else
	{
		ife initsprite 1
			state rollyfirerpg
		else
		{
			spawn AIRFLASH1
			sound M4FIRE
			state hitscan_targetprep
			zshoot zdist CHAINGUN
		}
	}
	break
	endswitch
	
	resetcount
	ifn sprite[].pal 19
	addstrength -3
	sub botclip 1
ends

defstate reclaimcheck
	
	ife team 0 break // badguy turret
	
	ife myspawner myshelly
	{
		ifactor ROLLYTURRET ife pchar 1 killit
		ifactor DOLLBOMB ife pchar 4 killit
		
	}
	
	
	ifge player[].ammo_amount HANDBOMB_WEAPON MAXHANDBOMBAMMO break
	
	// ifn pchar 1 break
	ifp pfacing
	ifp palive
	ifcansee
	ifpdistl 1280
	{
		ifl player[].fta 30
		ife myspawner player[].i
		{
			setp[].fta 60
			setp[].ftq 158
		}
		ifhitspace
		{
			// ifn myspawner player[].i
			ife myspawner myshelly
			{
				set subtitle_time 30
				set subtitle_numlines 1
				set subtitle_start 1360
				break
			}
			getp[].ammo_amount HANDBOMB_WEAPON temp
			geta[].extra tempb
			ifactor ROLLYTURRET
			{
				ifvarand shellyupgrades[HANDBOMB_WEAPON] 2
				{
					ifg tempb 100
					{
						sub tempb 100
						add temp 1
						setp[].ammo_amount HANDBOMB_WEAPON temp
					}
				}
				else
				{
					ifg tempb 75
					{
						sub tempb 75
						add temp 1
						setp[].ammo_amount HANDBOMB_WEAPON temp
					}
				}
			
				add temp 1
				setp[].ammo_amount HANDBOMB_WEAPON temp
				shiftl tempb 1
				add turret_leftover tempb
				ifvarand shellyupgrades[HANDBOMB_WEAPON] 2
				{
					ifge turret_leftover 200 set turret_leftover 0
				}
				else
				ifge turret_leftover 150 set turret_leftover 0
			}
			else
			{
				add temp 1
				setp[].ammo_amount HANDBOMB_WEAPON temp
			}
			ife player[].ftq 158 setp[].fta 0
			globalsound DUKE_GET
			palfrom 16 0 32
			set team 1
			set monstatus 0
			killit
		}
	}
	else ife player[].ftq 158 setp[].fta 0
	
ends

useractor notenemy ROLLYTURRET 150

ifaction 0
{
	action AROLLGO
	sizeat 30 26
	strength 150
	set spawnprotect 0
	
	ife team 1
	// ifspawnedby APLAYER
	ifvarand shellyupgrades[HANDBOMB_WEAPON] 2
	{
		orvar monstflags 8 // energy barrier
		espawn EBARRIER
		setav[RETURN].myspawner THISACTOR
		addstrength 50
		sizeat 34 32
	}
	ife myspawner player[].i
	ifg turret_leftover 0 { seta[].extra turret_leftover set turret_leftover 0 }
	cstat 256
	move ROLLYMOV geth
	seta[].zvel mtype
	shiftr dodgetime 3
	add dodgetime 20
	spriteflags 2097152
	getp[].player_par ikicked
	ifg gametype 0 // max 3 active turrets per player
	{
		set TMP_A -1
		set countvar 0
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum ROLLYTURRET
			ife actorvar[spriteid].team 1
			ifn spriteid THISACTOR
			ife myspawner actorvar[spriteid].myspawner
			{
				ifl actorvar[spriteid].ikicked ikicked set TMP_A spriteid
				add countvar 1
			}
			nextspritestat spriteid spriteid
		}
		ifg countvar 2 { seta[TMP_A].htextra 150, seta[TMP_A].htpicnum RPG seta[TMP_A].htowner 0 }
	}
	set countvar 0
	set teamspawned 99999
	
}

ifg sprite[].zvel 0 { add countvar 1 geta[].zvel FEMFALLDMG }
else set countvar 0

ifmove ROLLYMOV
ifg countvar 0
iffloordistl 2
{
	sound GRENADE_BOUNCE
	ifl countvarb 3
	{
		add countvarb 1
		set countvar 0
		mul FEMFALLDMG -1
		ifl FEMFALLDMG -2560 set FEMFALLDMG -2560
		seta[].zvel FEMFALLDMG
		geta[].z z
		sub z 512
		seta[].z z
	}
}

ifaction AROLLGO
{
	state projectilehitscan
	ifcount 60 action AROLLHATCH
	ifnotmoving action AROLLHATCH
	sub dodgetime 1
	ifl dodgetime 0 action AROLLHATCH
	
	ifaction AROLLHATCH
	{
		sound ROLLHATCH
		cstat 257
		// ife team 1
		set monstatus 1
		move STOPPED
		ifl sprite[].zvel 0 seta[].zvel 0
	}
}

ifaction AROLLHATCH
ifactioncount 3
	action AROLLREADY
	
// ifn monstatus 2 ife mysignpost -1 state spawnmysignpost
	
ifaction AROLLREADY
{
	state reclaimcheck
	ife team 0 // badguy turret
	{
		state monsterai
		ife bottarget -1 ifcansee getplayer[].i bottarget
	}
	else
		state targetsearch


	ife team 1
	ife bottarget -1
	{
		set target -1
		
		set xydist2 99999
		headspritestat spriteid 12
		whilevarn spriteid -1
		{
			dist xydist THISACTOR spriteid
			ifl xydist 8192
			ifl xydist xydist2
			{
				set target spriteid
				set xydist2 xydist
			}
			nextspritestat spriteid spriteid
		}
		
				
		ife target -1
		{
			set xydist2 8192
			headspritestat spriteid 6
			whilevarn spriteid -1
			{
				switch sprite[spriteid].picnum
				case CRACK1
				case CRACK2
				case CRACK3
				case CRACK4
					ifn sprite[spriteid].hitag 0
					{
						ldist xydist THISACTOR spriteid
						ifl xydist xydist2
						{
							set xydist2 xydist
							set target spriteid
						}
					}
				break
				endswitch

				nextspritestat spriteid spriteid
			}
		}
		
		
		ifn target -1
		{
		
			dist xydist THISACTOR target
			ifl xydist 2560
			{
				set bottarget target
				setav[bottarget].monstatus 1
			}
			else
			{
				geta[].z z
				sub z 6144
				seta[].z z
				canseespr THISACTOR target temp
				add z 6144
				seta[].z z
				ifn temp 0 
				{
					set bottarget target
					setav[bottarget].monstatus 1
				}
			}
		}
	}

	ifn bottarget -1
	{
		state turntotarget
		
		ifcount 30
		ifl temp 48
		{
			// start shooting
			action AROLLSHOOT
			set botclip 6
			state rollyshoot
		}
	}
}

ifaction AROLLSHOOT
{
	state reclaimcheck
	ife team 0 // badguy turret
	{
		state monsterai
		ife bottarget -1 ifcansee getplayer[].i bottarget
	} else
	ifn actorvar[bottarget].monstatus 1 set bottarget -1 
	
	
	ifn bottarget -1
	state turntotarget
	else 
	{
		set botclip 6
		action AROLLREADY
		resetcount
		break
	}
	
	ifcount 3
	state rollyshoot
	
	ifl botclip 0
	{
		set botclip 6
		action AROLLREADY
		resetcount
	}
}
ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }
ifstrength 30 ifrnd 8
{
	espawn BIGSMOKE
	seta[RETURN].htpicnum TANKWRECK
}

ife team 1
ifg sprite[].htextra 1 // damage reduction based on weapon prof
{
	geta[].htextra temp
	mul temp 100
	
	set tempb bweaplevel[HANDBOMB_WEAPON]
	add tempb 1
	mul tempb 10
	ifg tempb 85 set tempb 85
	add tempb 100
	
	div temp tempb
	seta[].htextra temp
}

ife team 1 state predamage
ifg sprite[].htextra 0
ife sprite[].htowner THISACTOR seta[].htextra -1

set temp NO
ife actorvar[myspawner].monstatus 2 set temp YES
ife sprite[myspawner].statnum 1024 set temp YES

ife temp YES
{
	seta[].htextra 1000
	seta[].htowner -1
	seta[].htpicnum RPG
}
ifhitweapon
{
	ifdead state rollyexplode
}
ifstrength 0 state rollyexplode

fall

enda

state sidegundeath

	ife team 0 addkills 1
	set monxp 0
	state enemy_death
	ife leftsidegun THISACTOR 
	{
		ife gametype DM set leftsidegun -3 else
		set leftsidegun -30
	}
	ife rightsidegun THISACTOR 
	{
		ife gametype DM set rightsidegun -3 else
		set rightsidegun -30
	}
	state rollyexplode
	
ends

action ASIDEGUNIDLE  0  1  5  1  16
action ASIDEGUNSHOOT 5  1  5  1  12

move SIDEGUNSHOOT

useractor enemy SIDEGUN 80 ASIDEGUNIDLE

ifmove 0
{
	move STOPPED geth
	ife myspawner -1 
	{
		set myspawner player[].i
		findnearactor SIDEGUN 3072 spriteid
		ifn spriteid -1 ife actorvar[spriteid].myspawner player[].i
		set botclip 384 else set botclip -384
		rand mtype 5
	}
	// ife team 1
	// {
		// getp[].max_actors_killed temp
		// sub temp 1
		// setp[].max_actors_killed temp
	// }
	else spritepal 21
	set monstflags actorvar[myspawner].monstflags
	ifvarand monstflags 2 xorvar monstflags 2
	ifvarand monstflags 4 xorvar monstflags 4
	ifvarand monstflags 16 xorvar monstflags 16
	ifvarand monstflags 32 xorvar monstflags 32
	ifvarand monstflags 512 xorvar monstflags 512 // lets not have recursive sideguns
	ifvarand monstflags 1024 xorvar monstflags 1024
	ifvarand monstflags 4096 xorvar monstflags 4096
	ifvarand monstflags 131072 xorvar monstflags 131072
	set temp actorvar[myspawner].inithp
	div temp 4
	add temp 30
	ife team 1 add temp 20
	seta[].extra temp
	clipdist 8
	set lastang sprite[myspawner].ang
	set countvar 100
	set teamspawned 99999
	geta[].mdflags temp
	orvar temp 16
	seta[].mdflags temp
	
	set countvarb 2000
}

set team actorvar[myspawner].team
seta[].ang lastang
state monsterai
seta[].alpha sprite[myspawner].alpha
ife bottarget -1 seta[].ang sprite[myspawner].ang

ife sprite[myspawner].picnum NURGLE
	ifg actorvar[myspawner].countvar 30 { cstat 32768 break }


ife sprite[myspawner].picnum MONSHADOW
	{ cstat 32768 break }
 
cstat 256


ife team 1 set burning 0
ife sprite[].htowner myspawner seta[].htextra -1
ifhitweapon
{
	ifdead state sidegundeath
}
ifn myspawner player[].i
ifn myspawner myshelly
{
	ifn actorvar[myspawner].monstatus 1 state sidegundeath else
	ife sprite[myspawner].statnum 1024 state sidegundeath
}
else
{
	ifl countvarb 0 state sidegundeath
}

ife myspawner player[].i
ife pdown YES state sidegundeath

set x2 tiledata[sprite[myspawner].picnum].xsize
mul x2 sprite[myspawner].xrepeat
// shiftr x2 2 
div x2 3 // distance from center
ifg x2 2048 set x2 2048

set x x2
shiftr x 6, add x 8, ifl x 4 set x 4
seta[].xrepeat x
mul x 3 div x 4
seta[].yrepeat x

ifg x 80 set x 80
// seta[].clipdist x

set y2 tiledata[sprite[myspawner].picnum].ysize
mul y2 sprite[myspawner].yrepeat
shiftl y2 1 // distance from base of owner

// put it in z position now
geta[myspawner].z z
sub z y2
ife myspawner player[].i sub z 1024
seta[].z z

// calculate position gun is supposed to be in

geta[myspawner].ang angvar
add angvar botclip

geta[myspawner].x newx
add newx x2

rotatepoint sprite[myspawner].x sprite[myspawner].y newx sprite[myspawner].y angvar x y

// setsprite THISACTOR x y z
set savx x
set savy y

// now calculate angle to make gun move towards that position

sub x sprite[].x
sub y sprite[].y
getangle angvel x y

// now calculate how far gun is from that position currently

set xydist sprite[].x
sub xydist savx
mul xydist xydist
set starty sprite[].y
sub starty savy
mul starty starty
add xydist starty
sqrt xydist xydist

// now move towards the correct position with no clipping based on distance

ifg xydist 3072 setsprite THISACTOR savx savy z
else
ifg xydist 256
{
	cos xvel angvel
	sin tempb angvel
	
	// div by 128 for 1024 distance
	set TMP_A 3072 // maximum division
	sub TMP_A xydist
	shiftr TMP_A 5
	ifl TMP_A 4 set TMP_A 4
	
	div xvel TMP_A
	div tempb TMP_A

	movesprite THISACTOR xvel tempb 0 0 RETURN
	
	ife myspawner player[].i
	ifn RETURN 0
	ifpdistl 1024
	{
		setsprite THISACTOR savx savy sprite[].z
	}
}
geta[].ang lastang

ifn bottarget -1
ifl targetdist 16384
ife targetalive YES
ife shootmytarget YES
ifmove STOPPED
ifg countvar 100
	move SIDEGUNSHOOT geth

ifmove STOPPED
{
	ifaction ASIDEGUNSHOOT action ASIDEGUNIDLE
	ifle countvar 120 add countvar 1
}

ifmove SIDEGUNSHOOT
{
	ifaction ASIDEGUNIDLE
	{
		ifn bottarget -1
		ifl targetdist 16384
		ife targetalive YES
		ife shootmytarget YES
		ifg countvar 0
		nullop else
		{
			move STOPPED geth
			break
		}
	
		ifactioncount 1
		{
			geta[].x savx
			geta[].y savy
			set x savx
			add x 384
			
			rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
			setsprite THISACTOR x2 y2 sprite[].z
	
			ife mtype 0
			{
				action ASIDEGUNSHOOT
				sound TERMLASER
				shoot GREENLASER
				sub countvar 20
				sub countvarb 20
			} else
			ife mtype 1
			{
				ifactioncount 2
				{
					action ASIDEGUNSHOOT
					sound RIFLEFIRE
					shoot RANGERPROJ
					sub countvar 40
					sub countvarb 40
				}
			} else
			ife mtype 2
			{
				action ASIDEGUNSHOOT
				sound LASERSHOOT
				shoot BIGPLASMA
				sub countvar 25
				sub countvarb 25
			} else
			ife mtype 3
			{
				ifactioncount 3
				{
					action ASIDEGUNSHOOT
					sound FLAKSHOT
					set xvel 844
					state rpg_targetprep
					zshoot zdist FLAKPROJ
					set xvel 844			
					state rpg_targetprep
					zshoot zdist FLAKPROJ
					set xvel 844
					state rpg_targetprep
					zshoot zdist FLAKPROJ 
					set xvel 844
					state rpg_targetprep
					zshoot zdist FLAKPROJ
					set xvel 844
					state rpg_targetprep
					zshoot zdist FLAKPROJ
					sub countvar 80
					sub countvarb 80
				}
			} else
			ife mtype 4
			{
				ifactioncount 2
				{
					action ASIDEGUNSHOOT
					sound RPG_SHOOT
					set xvel 644
					state rpg_targetprep
					ezshoot zdist RPG
					seta[RETURN].extra 35
					seta[RETURN].xrepeat 14
					seta[RETURN].yrepeat 14
					sub countvar 60
					sub countvarb 60
				}
			} else
			ife mtype 5
			{
				ifvarand monstflags 16384 ifcount 1 count 2
				ifcount 2
				{
					resetcount
					sound M_GUN_SHOOT
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					sub countvar 8
					sub countvarb 8
					action ASIDEGUNSHOOT
				}
			}
			seta[].x savx
			seta[].y savy
		}
	}
	ifaction ASIDEGUNSHOOT
	{
		ife mtype 5 
		{ 
			ifvarand monstflags 16384 ifcount 1 count 2
			ifcount 2 { action ASIDEGUNIDLE resetcount }
		}
		ifactioncount 1 action ASIDEGUNIDLE
	}
}

enda

defstate icebombdamage

	sub temp xydist
	shiftr temp 5
	set tempb sprite[spriteid].htextra
	add tempb temp
	ifl tempb 1 set tempb 1
	seta[spriteid].htextra tempb
	seta[spriteid].htowner player[].i
	ife actorvar[spriteid].monstatus 1
	seta[spriteid].htpicnum FREEZEBLAST

ends

defstate wesbombblow
	
	ife countvarc 1
	{
		ifrnd 128 sound SMALLEXP1 else sound SMALLEXP2
		spawn MINEEXP
		hitradius 1560 10 20 30 40
		
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ifn spriteid THISACTOR
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1560 
				{
					set temp 1560
					state icebombdamage
					
					ife sprite[spriteid].picnum WESBOMB
					ife actorvar[spriteid].countvar 0
						setav[spriteid].countvar 2
				}
			}
			nextspritestat spriteid spriteid
		}

		ifpdistl 1560 
		{
			seta[player[].i].htpicnum FREEZEBLAST
			seta[player[].i].htowner player[].i
		}
	}
	else
	{
		sound PIPEBOMB_EXPLODE
		debris SCRAP1 4
		debris SCRAP2 4
		debris SCRAP3 4
		espawn EXPLOSION2
		seta[RETURN].cstat 32768
		hitradius 2048 20 36 52 64
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ifn spriteid THISACTOR
			{
				dist xydist THISACTOR spriteid
				ifle xydist 2048 
				{
					set temp 2048
					state icebombdamage
					
					ife sprite[spriteid].picnum WESBOMB
					ife actorvar[spriteid].countvar 0
						setav[spriteid].countvar 2
				}
			}
			nextspritestat spriteid spriteid
		}
		ifpdistl 2049 
		{
			seta[player[].i].htpicnum FREEZEBLAST
			seta[player[].i].htowner player[].i
		}
	}
	ife countvarc 0 ifvarand wesupgrades[HANDBOMB_WEAPON] 1
	{
		sub mtype -1024
		
		rand angvar 2048
		seta[].ang angvar
		espawn WESBOMB
		setav[RETURN].countvarc 1
		setav[RETURN].dodgetime dodgetime
		setav[RETURN].mtype mtype
		
		rand angvar 2048
		seta[].ang angvar
		espawn WESBOMB
		setav[RETURN].countvarc 1
		setav[RETURN].dodgetime dodgetime
		setav[RETURN].mtype mtype
		
		rand angvar 2048
		seta[].ang angvar
		espawn WESBOMB
		setav[RETURN].countvarc 1
		setav[RETURN].dodgetime dodgetime
		setav[RETURN].mtype mtype
		
		rand angvar 2048
		seta[].ang angvar
		espawn WESBOMB
		setav[RETURN].countvarc 1
		setav[RETURN].dodgetime dodgetime
		setav[RETURN].mtype mtype
		
		rand angvar 2048
		seta[].ang angvar
		espawn WESBOMB
		setav[RETURN].countvarc 1
		setav[RETURN].dodgetime dodgetime
		setav[RETURN].mtype mtype
	}
	set monstatus 0
	killit

ends

action WESBOMBFRAMES  0  4  1  1  5
useractor notenemy WESBOMB 0

ifaction 0
{
	set team 1
	action WESBOMBFRAMES
	ife countvarc 1 
	{
		sizeat 4 4 
		geta[].z z, sub z 1024, seta[].z z
		rand mtype 4096, mul mtype -1
	}
	else
	sizeat 8 8
	clipdist 8
	cstat 256
	move ROLLYMOV geth
	seta[].zvel mtype
	shiftr dodgetime 3
	add dodgetime 20
	spriteflags 2105344 // smooth, nowaterdip
	set FEMKILLCOUNT 0
	set teamspawned 99999
	
}
fall
ifrnd 8 ife sprite[].zvel 0
ifge countvarb 3
{
	rand z 1280, sub z 4096
	seta[].zvel z
	geta[].z z, sub z 512, seta[].z z
}

ifg sprite[].zvel 0 { add FEMKILLCOUNT 1 geta[].zvel FEMFALLDMG }
else set FEMKILLCOUNT 0

ife countvarc 1
ifvarand wesupgrades[HANDBOMB_WEAPON] 2
{
	state targetsearch
	state turntotarget
}
ifmove ROLLYMOV
ifg FEMKILLCOUNT 0
iffloordistl 2
{
	sound GRENADE_BOUNCE
	ifl countvarb 3
	{
		add countvarb 1
		set FEMKILLCOUNT 0
		mul FEMFALLDMG -1
		ifl FEMFALLDMG -2560 set FEMFALLDMG -2560
		seta[].zvel FEMFALLDMG
		geta[].z z
		sub z 768
		seta[].z z
	}
}

ifaction WESBOMBFRAMES
{
	ifg countvar 0 sub countvar 1
	state projectilehitscan
	state blowupcrack
	ife TMP_B 1 state wesbombblow else
	ifcount 90 ifrnd 96 state wesbombblow else
	{
		set TMP_B NO
		headspritestat spriteid 12
		whilevarn spriteid -1
		{
			dist xydist THISACTOR spriteid
			ifl xydist 512
				set TMP_B YES
			nextspritestat spriteid spriteid
			ife TMP_B YES set spriteid -1
		}
		ife TMP_B NO
		ife countvar 0
		{
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				ifn team actorvar[spriteid].team
				ifg actorvar[spriteid].monxp 0
				ifvarand sprite[spriteid].cstat 256
				ife actorvar[spriteid].monstatus 1
				{
					dist xydist THISACTOR spriteid
					ifl xydist 384
						set TMP_B YES
				}
				nextspritestat spriteid spriteid
				ife TMP_B YES set spriteid -1
			}
		}
		ife TMP_B YES state wesbombblow
	}
	ifnotmoving ifcount 6 
	{
		getactor[].htmovflag temp
		andvar temp 49152
		ifvare temp 32768
		{
			// hit a wall
			set TMP_A sprite[].htmovflag
			andvar TMP_A 16383
			getwall[TMP_A].point2 B
			getwall[TMP_A].x x2
			getwall[TMP_A].y y2
			getwall[B].x x
			getwall[B].y y
			sub x2 x
			sub y2 y
			getangle angvar x2 y2
			getactor[].ang tempb
			getincangle tempc angvar tempb
			sub angvar tempc
			ifvarg angvar 2047 subvar angvar 2048
			ifvarl angvar 0 addvar angvar 2048
			setactor[].ang angvar
		}
		else
		{
			getactor[].htmovflag temp
			add temp 16384
			ifl temp 16384 ifg temp -1 
			{
				ifn actorvar[temp].team 1
				state wesbombblow
				else
				{
					geta[].ang angvar
					add angvar 768
					rand tempb 512
					add angvar tempb
					seta[].ang angvar
				}
			}
		}
	}
	
	sub dodgetime 1
	ifg dodgetime 0
	{
		geta[].xvel xvel
		set B dodgetime
		mul B 24
		add xvel B
		seta[].xvel xvel
	}
}

enda

eventloadactor BIGTURRET
cstator 257
ifspritepal 0 spritepal 21
ifl sprite[].xrepeat 20 sizeat 32 32
enda

move BIGTURRAIM
move BIGTURRFIRE
action ABIGTURRAIMUP 1 1 1 1 1

useractor enemy BIGTURRET 100

ifmove 0
{
	move BIGTURRAIM
	ifl sprite[].xrepeat 20 sizeat 32 32
	seta[].yrepeat sprite[].xrepeat
	set temp sprite[].xrepeat
	shiftl temp 3
	add temp 100
	seta[].extra temp
	cstator 257
	set botclip 3
	ifspritepal 0 spritepal 21
	
	geta[].htflags temp, 
	orvar temp 1048576, orvar temp 131072
	seta[].htflags temp
	
	ifvarand monstflags 131072 // mimic
	{
		set team 0 
		ifrnd 128 spritepal 10 else spritepal 21
	}
	else
	ifspritepal 3 set team 1
	else
	ifspritepal 16 set team 1
}
cstator 1
set dodgetime 3 // prevents instant turning
seta[].xvel 0
state monsterai

state hitscan_targetprep
abs zdist
ifg zdist 3072 action ABIGTURRAIMUP else action ANULLACTION

ifn bottarget -1
{
	geta[bottarget].x x2
	geta[bottarget].y y2
	sub x2 sprite[].x
	sub y2 sprite[].y
	getangle tempb x2 y2
	
	getincangle temp sprite[].ang tempb
	
	ifn sprite[].ang tempb
	{
		ifg temp -32 ifl temp 32
		{
			seta[].ang tempb
			ifcount 10
			ifactorsound THISACTOR TURRET_TURN stopactorsound THISACTOR TURRET_TURN
		}
		else
		{
			geta[].ang angvar
			ifl temp 0 sub angvar 32
			else add angvar 32
			seta[].ang angvar
			ifactorsound THISACTOR TURRET_TURN nullop else sound TURRET_TURN
			resetcount
		}
		
	}
	else
	ifcount 10
	ifactorsound THISACTOR TURRET_TURN stopactorsound THISACTOR TURRET_TURN
	
	
	ifg botclip 0
	ifcount 6 
	{
		set temp sprite[].cstat
		ifvarand temp 1 xorvar temp 1
		seta[].cstat temp
		sub botclip 1
		ife botclip 0 set botclip -30
		geta[].z savz
		set z savz
		set TMP_A sprite[].yrepeat
		mul TMP_A 96
		// shiftl TMP_A 6
		ifvarand sprite[].cstat 8 add z TMP_A else
		sub z TMP_A
		seta[].z z
		
		// sound TERMLASER
		// shoot GREENLASER 
		
		set temp NO
		ifspritepal 16 set temp YES
		ifspritepal 10 set temp YES
		ife temp YES
		{
			count 2
			soundonce THREEROX
			ezshoot -3072 RPG
			setav[RETURN].mtype TARGETLOCK
			setav[RETURN].bottarget bottarget
			seta[RETURN].extra 100
			
			rand angvar 512, sub angvar 256
			add angvar sprite[RETURN].ang
			seta[RETURN].ang angvar
			ifl botclip 1 set botclip -60
		}
		else
		{
			sound SHOCKBALLFIRE
			set xvel 844
			state rpg_targetprep
			setprojectile[SWIRLPROJ].pal 11
			setprojectile[SWIRLPROJ].xrepeat 24
			setprojectile[SWIRLPROJ].yrepeat 24
			zshoot zdist SWIRLPROJ
			setprojectile[SWIRLPROJ].pal 0
			setprojectile[SWIRLPROJ].xrepeat 42
			setprojectile[SWIRLPROJ].yrepeat 42
		}
		move BIGTURRFIRE
		seta[].z savz
		resetcount 
	}
}
set lastang sprite[].ang
ifl botclip 0 { add botclip 1 ife botclip 0 set botclip 3 }

ifrnd 2
{
	set temp inithp
	div temp 2
	ifl sprite[].extra temp
	{
		espawn BIGSMOKE
		seta[RETURN].htpicnum RECONWRECK
	}
}

ifhitweapon
{
	ifdead
	{
		state enemy_death
		addkills 1
		spawn EXPLOSION2
		sound PIPEBOMB_EXPLODE
		sound ROBOT_EXPLODE
		debris SCRAP1 6
		debris SCRAP2 4
		debris SCRAP3 5
		state tech_debris
		killit
	}
}

enda

spritenoshade NANOBUG
move NANOVEL 48

useractor notenemy NANOBUG 0

	ifmove 0
	{
		rand bluetimer 40
		move NANOVEL geth
		sizeat 8 8
		seta[].shade -48
		rand angvar 2047
		rand initsprite 999
		seta[].ang angvar
		set team 1
		
		ifn myvictim -1
		{
			cstat 16
			ifrnd 128 cstator 8
			set startx tiledata[sprite[myvictim].picnum].xsize
			mul startx sprite[myvictim].xrepeat
			// shiftr startx 3 distance from center
			div startx 12
			
			set starty tiledata[sprite[myvictim].picnum].ysize
			mul starty sprite[myvictim].yrepeat
			shiftl starty 2 // height of owner
			
			rand float starty // starting height offset
			rand countvar 1 // 0 is up 1 is down
		}
	}
	else 
	ifnotmoving
	ife myvictim -1
	{
		geta[].htmovflag spriteid
		addvar spriteid 16384
		
		ifvarl spriteid 16384 ifvarg spriteid -1 
		ife sprite[spriteid].statnum 1
		ife actorvar[spriteid].monstatus 1
		ifn actorvar[spriteid].team 1
		ifn actorvar[spriteid].team 3
		
		{
			set bottarget -1
			set myvictim spriteid
			cstat 16
			ifrnd 128 cstator 8
			spriteflags 2048
			set startx tiledata[sprite[myvictim].picnum].xsize
			mul startx sprite[myvictim].xrepeat
			// shiftr startx 3 distance from center
			div startx 12
			
			set starty tiledata[sprite[myvictim].picnum].ysize
			mul starty sprite[myvictim].yrepeat
			shiftl starty 2 // height of owner
			
			rand float starty // starting height offset
			rand countvar 1 // 0 is up 1 is down
		}
		
		ifn bottarget -1 { ifcount 6 ifrnd 8 move NANOVEL randomangle } else
		ife myvictim -1 move NANOVEL randomangle
	}
	add countvarb 1
	ifge countvarb 5400 { set monstatus 0 killit }
		
	ifn myvictim -1
	{
		set temp player[].player_par
		add temp initsprite
		mul temp 16
		sin tempb temp
		shiftr tempb 4
		set botclip tempb
		
		geta[myvictim].ang angvar
		add angvar botclip

		geta[myvictim].x newx
		add newx startx

		rotatepoint sprite[myvictim].x sprite[myvictim].y newx sprite[myvictim].y angvar x y
		
		ife countvar 0 
		{
			add float 128
			ifg float starty
			{
				set countvar 1
				add float 128
			}
		}
		else 
		{
			sub float 128
			ifl float 0
			{
				set countvar 0
				sub float 128
			}
		}
		
		set z sprite[myvictim].z
		sub z float
		
		setsprite THISACTOR x y z
		ife sprite[myvictim].statnum 1024 { set monstatus 0 killit }
		ife actorvar[myvictim].monstatus 2 { set monstatus 0 killit }
		
		ife actorvar[myvictim].team 0
		{
			ifrnd 8
			{
				add countvarb 2
				set temp actorvar[myvictim].bleeding
				ifle temp 0 sub temp 1
				setav[myvictim].bleeding temp
				ifl temp -30
				{
					set temp sprite[myvictim].mdflags
					orvar temp 16
					seta[myvictim].htflags temp
				}
			}
		}
		else
		{
			ife bottarget -1
			state targetsearch
			ifn bottarget -1 ife sprite[bottarget].picnum GREENSLIME set bottarget -1
			ifn bottarget -1
			ifrnd 64
			{
				seta[].z sprite[myvictim].z
				cstat 32
				set temp sprite[].htflags
				ifvarand temp 2048 xorvar temp 2048
				seta[].htflags temp
				set myvictim -1
			}
		}
	}
	else
	{
		fall
		ifn bottarget -1
		{
			ife actorvar[bottarget].monstatus 2 set bottarget -1 else
			ifg sprite[bottarget].statnum 2 set bottarget -1 else
			ife actorvar[bottarget].team 1 set bottarget -1
			ifn bottarget -1
			{
				set spriteid bottarget
				state facesprite
			}
		}
	}
	ifn myspawner -1
	{
		ifn sprite[myspawner].picnum DOLLBOMB { set monstatus 0 killit } else
		ife sprite[myspawner].statnum 1024 { set monstatus 0 killit }
	}

enda

defstate dollexplode

	ifn myvictim -1
	ifl actorvar[myvictim].gooify 0
	ife actorvar[myvictim].team 1
	ife sprite[myvictim].statnum 1
	ife actorvar[myvictim].monstatus 1
	{
		setav[myvictim].team 0
		set temp actorvar[myvictim].mlevel
		sub temp 1
		sub temp nweaplevel[HANDBOMB_WEAPON]
		ifl temp 1 set temp 1
		setav[myvictim].mlevel temp
		
		set TMP_A nweaplevel[HANDBOMB_WEAPON]

		shiftl TMP_A 5
		add TMP_A 150

		seta[myvictim].htextra TMP_A
		seta[myvictim].htowner player[].i
		seta[myvictim].htpicnum RADIUSEXPLOSION
	}
	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		ife sprite[spriteid].htpicnum RADIUSEXPLOSION
		ifg sprite[spriteid].htextra 0
		{
			dist xydist THISACTOR spriteid
			ifle xydist PIPEBOMBRADIUS
			{
				seta[spriteid].htowner player[].i
			}
		}
		nextspritestat spriteid spriteid
	}
	ifpdistl PIPEBOMBRADIUS
	{
		seta[player[].i].htowner player[].i
		seta[player[].i].htextra 20
	}
		
		spawn EXPLOSION2
		sound PIPEBOMB_EXPLODE

	spritepal 8
	debris SCRAP3 12 
	getlastpal 
	
	set monstatus 0
	killit

ends

action ADOLLROLL  -8  8  1  1  6

defstate dollbombcode

// pal 24 is nano-worm doll

ifaction 0
{
	action ADOLLROLL
	sizeat 32 32
	cstator 256
	strength 20
	set team 7
	set botclip 0
	set monstatus 1
	set spawnprotect 0
	move ROLLYMOV geth
	seta[].zvel mtype
	shiftr dodgetime 2
	add dodgetime 20
	spriteflags 2097152
	getp[].player_par ikicked
	set countvar 0
	set teamspawned 99999
	set droptile 32 // number of bugs that can spawn
}

ifg sprite[].zvel 0 { add countvar 1 geta[].zvel FEMFALLDMG }
else set countvar 0

ifmove ROLLYMOV
ifg countvar 0
iffloordistl 2
{
	ifl countvarb 3
	{
		add countvarb 1
		set countvar 0
		mul FEMFALLDMG -1
		ifl FEMFALLDMG -1480 set FEMFALLDMG -1480
		seta[].zvel FEMFALLDMG
		geta[].z z
		sub z 512
		seta[].z z
	}
}

ifaction ADOLLROLL
{
	ifcount 60 action ANULLACTION
	ifnotmoving action ANULLACTION
	sub dodgetime 1
	ifl dodgetime 0 action ANULLACTION
	
	ifaction ANULLACTION
	{
		set monstatus 1
		move STOPPED
		ifl sprite[].zvel 0 seta[].zvel 0
	}
}

ifsquished	
{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }


state predamage

ifhitweapon
{
	ifdead 
	{
		hitradius PIPEBOMBRADIUS 50 80 110 140
		state dollexplode
	}
}
ifstrength 0 
{
	hitradius PIPEBOMBRADIUS 50 80 110 140
	state dollexplode
}

ifn myvictim -1
{
	add countvarc 1
	ife sprite[myvictim].statnum 1024 
	{
		hitradius PIPEBOMBRADIUS 50 80 110 140
		state dollexplode 
	}
	else
	ife actorvar[myvictim].monstatus 2 
	{
		hitradius PIPEBOMBRADIUS 50 80 110 140
		state dollexplode 
	}
	else
	{
		ifvarand novaupgrades[HANDBOMB_WEAPON] 1
		ifcount 6
		ifg droptile 0
		{
			resetcount
			sub droptile 1
			espawn NANOBUG
			setav[RETURN].myvictim myvictim
			setav[RETURN].team 1
			seta[RETURN].cstat 16
			geta[RETURN].htflags temp
			orvar temp 2048
			seta[RETURN].htflags temp
		}
		ifg actorvar[myvictim].gooify -150 cstat 256
		
		ifvarand extbits 64 ifl actorvar[myvictim].gooify -6 setav[myvictim].gooify -6
		else
		ifn pchar 4 ifrnd 2 ifl actorvar[myvictim].gooify -6 setav[myvictim].gooify -6
		
		ifge actorvar[myvictim].gooify 0
		{
			hitradius PIPEBOMBRADIUS 50 80 110 140
			
			setav[myvictim].team 0
			set temp actorvar[myvictim].mlevel
			sub temp 1
			sub temp nweaplevel[HANDBOMB_WEAPON]
			ifl temp 1 set temp 1
			setav[myvictim].mlevel temp
			
			set TMP_A nweaplevel[HANDBOMB_WEAPON]

			shiftl TMP_A 5
			add TMP_A 150
			seta[myvictim].htextra TMP_A
			seta[myvictim].htowner player[].i
			seta[myvictim].htpicnum RADIUSEXPLOSION
			
			state dollexplode
		}
	}
	
	set newx sprite[myvictim].x
	set x2 tiledata[sprite[myvictim].picnum].xsize
	mul x2 sprite[myvictim].xrepeat
	shiftr x2 3
	add newx x2
	rotatepoint sprite[myvictim].x sprite[myvictim].y newx sprite[myvictim].y sprite[myvictim].ang x y
	set z sprite[myvictim].z
	set z2 tiledata[sprite[myvictim].picnum].ysize
	mul z2 sprite[myvictim].yrepeat
	shiftl z2 1
	sub z z2
	setsprite THISACTOR x y z
	
	set temp countvarc
	modvar temp 15
	ife temp 0
	{
		espawn HEARTFLOAT
		sub z 4096
		setsprite RETURN x y z
	}
	break
}

ife botclip 0 ifvarand extbits 64 set botclip 1
ifg botclip 0 add botclip 1
ifge botclip 7 
{
	hitradius PIPEBOMBRADIUS 50 80 110 140
	state dollexplode	
}
	
ifaction ANULLACTION
ife myvictim -1
ife botclip 0
{
	state reclaimcheck
	
	ifvarand novaupgrades[HANDBOMB_WEAPON] 1
	ifcount 6
	ifg droptile 0
	{
		resetcount
		sub droptile 1
		espawn NANOBUG
		setav[RETURN].team 1
		setav[RETURN].myspawner THISACTOR
		seta[RETURN].cstat 32
	}
	
	set team 1
	state targetsearch
	set team 7
	
	ifn bottarget -1
	{
		ifn sprite[bottarget].picnum ROTATEGUN
		ifn sprite[bottarget].picnum PODHEAD
		ifn sprite[bottarget].picnum PODTAIL
			dist xydist THISACTOR bottarget
		else set xydist 10000
		
		ifl xydist 1280
		{
			findnearactorz DOLLBOMBHELD 1280 10240 spriteid
			ife spriteid -1
			{
				set myvictim bottarget
				cactor DOLLBOMBHELD
				sound MENUPAGE
				ifrnd 64 screensound DEA_ENJOYGIFT
				// ifspritepal 24
				// {
					set team 1
					cstat 0
					ifn actorvar[myvictim].team 1 sub maxkills 1
					setav[myvictim].team 1
					setav[myvictim].bottarget -1
					setav[myvictim].bluetimer 0
					set TMP_A nweaplevel[HANDBOMB_WEAPON]
					shiftl TMP_A 8
					add TMP_A 900
					
					sub TMP_A actorvar[myvictim].inithp
					ifl TMP_A 30 set TMP_A 30
					mul TMP_A -1
					setav[myvictim].gooify TMP_A
					
					geta[myvictim].extra temp
					add temp 100
					seta[myvictim].extra temp
					set temp actorvar[myvictim].mlevel
					add temp 1
					add temp nweaplevel[HANDBOMB_WEAPON]
					setav[myvictim].mlevel temp
				// }
				// else
				// set team 0
				set monstatus 1
			}
			else
			{
				set bottarget -1
				set bluetimer 0
			}
		}
		else 
		{
			set bottarget -1
			set bluetimer 0
		}
		break
	}
	/*
	else
	{
		set target -1
		
		set xydist2 PIPEBOMBRADIUS
		headspritestat spriteid 12
		whilevarn spriteid -1
		{
			dist xydist THISACTOR spriteid
			ifl xydist xydist2
			{
				set target spriteid
				set xydist2 xydist
			}
			nextspritestat spriteid spriteid
		}
				
		ife target -1
		{
			set xydist2 PIPEBOMBRADIUS
			headspritestat spriteid 6
			whilevarn spriteid -1
			{
				switch sprite[spriteid].picnum
				case CRACK1
				case CRACK2
				case CRACK3
				case CRACK4
					ifn sprite[spriteid].hitag 0
					{
						ldist xydist THISACTOR spriteid
						ifl xydist xydist2
						{
							set xydist2 xydist
							set target spriteid
						}
					}
				break
				endswitch

				nextspritestat spriteid spriteid
			}
		}
		
		ifn target -1
		{
			hitradius PIPEBOMBRADIUS 50 80 110 140
			state dollexplode	
		}
	}
	*/
}


fall

ends

useractor notenemy DOLLBOMB 20 state dollbombcode enda
useractor notenemy DOLLBOMBHELD 20 state dollbombcode enda
