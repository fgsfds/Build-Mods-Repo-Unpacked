/*
--------------------------------------------------------------------------------
Duke Nukem 3D - Blast Edition Version 2.0
By Marcos
2022 - Space In a Bottle
*/

//Sector FX

var in_space 0 1
var se_owner -1 1
var se61a 0 2

defstate SpaceLT60

	ifg sprite[].hitag 0
	{
		set atemp 0
		for atemp allsprites
		{
			ife sprite[atemp].picnum SECTOREFFECTOR
			ife sprite[atemp].lotag 61
			ife sprite[atemp].hitag sprite[].hitag
			{
				ifand sector[sprite[].sectnum].lotag 32768 //Door opened
					setactorvar[atemp].se61a 1
				else
					setactorvar[atemp].se61a 0
			}
		}
	}

	ife sprite[].sectnum player[].cursectnum
	{		
		set in_space 1
		set se_owner THISACTOR
	}
	else
	{
		ife se_owner THISACTOR
			set in_space 0
	}

ends

defstate SpaceLT61
	
	ife sprite[].sectnum player[].cursectnum
	{
		ifg sprite[].hitag 0
		ife se61a 1
		{
			set in_space 1
			set se_owner THISACTOR
		}
	}
	else
	{
		ife se_owner THISACTOR
			set in_space 0
	}

ends
