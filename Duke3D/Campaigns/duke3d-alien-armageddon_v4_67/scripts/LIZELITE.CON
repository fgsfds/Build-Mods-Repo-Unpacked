// ELITE LIZARD TROOPER

action AELITESTAND  0  1  5  1  10
action AELITERUN	5  4  5  1  8
action AELITEMELEE1	25  1  5  1  22
action AELITEMELEE2 30  1  5  1  20
action AELITEJUMP	35  1  5  1  20
action AELITEFALL	40  1  5  1  20
action AELITEDIE	90  5  1  1  16
action AELITEDEAD   95  1  1  1  10
action AELITEJET	35  1  5  1  10

action AELITEHIT	9390  1  5  1  20

action AELITECROUCH 0  1  5  1  10
action AELITECRAWL	5  4  5 1  12

action AELITESWIM	70  4  5  1  12

move ELITESHOOTBOT
move ELITESHOOTPLAYER

move ELITESHRUNKVEL 40
move ELITEJUMPVEL 32 -128 // 288 -128
move ELITEFALLVEL 0 // 256

ai AIELITEWAIT AELITESTAND STOPPED geth
ai AIELITEFROZEN AELITESTAND STOPPED geth
ai AIELITESHRUNK AELITERUN ELITESHRUNKVEL furthestdir
ai AIELITEGROW AELITEMELEE1 STOPPED geth
ai AIELITERUN AELITERUN STOPPED geth
ai AIELITEJUMP AELITEJUMP ELITEJUMPVEL geth getv
ai AIELITEDYING AELITEDIE STOPPED geth
ai AIELITEWANDER AELITERUN STOPPED geth
ai AIELITEJET AELITEJET STOPPED geth
ai AIELITEMELEE AELITEMELEE1 STOPPED geth
ai AIELITESTUN AELITEHIT STOPPED geth

state eliteroamsounds

	ifactorsound THISACTOR ELITEROAM1 break
	ifactorsound THISACTOR ELITEROAM2 break
	ifrnd 1
	{
		ifrnd 128 sound ELITEROAM1 else
		sound ELITEROAM2
	}

ends

state eliteshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIELITEWAIT
  else
    ifcount SHRUNKCOUNT
      sizeto 23 23
  else
    state genericshrunkcode

ends

state elitejumpcheck

	ifceilingdistl 96 break
	geta[].z z
	sub z 4096
	
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 1024
		{
			sub z 8192
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			set xydist startx
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 1560 
			{ 
				cactor LIZELITE ai AIELITEJUMP 
				set startx angvel 
				set starty 0
				seta[].ang angvel 
			}
			else ifl xydist 384
			{
				set RETURN hitwall
				ifg navpoint -1 ifrnd 32 set navpoint -1
			}
		}
	}

ends

state randomjumpcheck
	ifai AIELITEJUMP break
	ifceilingdistl 96 break
	ifg navpoint -1
	{
		findnearactor JUMPAD 1280 spriteid
		ife spriteid -1
		ifn RETURN 0
		{
			set starty 0
			cactor LIZELITE ai AIELITEJUMP 
		}
		break
	}
	geta[].z savz
	sub savz 9216
	
	ifn bottarget -1
	ifl targetdist 8192
	ifrnd 2
	ifl navpoint 0
	ifn redcarrier THISACTOR
	{
		set starty 1
		cactor LIZELITE ai AIELITEJUMP 
		set myspawner -1
	}

	
	// ifactor LIZELITE
	// ifn myspawner -1
	// {
		// ifg sprite[myspawner].zvel 0
		// {
			// set starty 1
			// ifg navpoint -1 set starty 0
			// cactor LIZELITE
			// ai AIELITEJUMP
		// } else
		// ifl sprite[myspawner].z savz
		// {
			// set myspawner -1
			// cactor LIZELITECROUCH
			// action AELITECRAWL
			// set dodgetime 30
		// }
		// set myspawner -1
	// }

ends

state elitejumpstate

	ife starty 0
	seta[].ang angvel
	
	cos xvel angvel
	sin yvel angvel
	shiftr xvel 6
	shiftr yvel 6
	set zdist 0
	ifg stun 0 { shiftr xvel 2 shiftr yvel 2 }
	ifvarand monstflags 16 { mul xvel 3 div xvel 2 mul yvel 3 div yvel 2 state makespeedblur }
	movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
	
	
	ifmove ELITEJUMPVEL
	{
		ifcount 6 
		{
			move ELITEFALLVEL geth
			action AELITEFALL
		}
	}
	ifaction AELITEFALL
	{
		iffloordistl 8
		{
			ai AIELITERUN
			sound ELITELAND
			// ifnotmoving { set navpoint -1 set savedvalue -1 }
		}
		ifn RETURN 0 { set navpoint -1 set savedvalue -1 }
	}

ends

state elitefrozenstate

    ifcount THAWTIME
    {
      ai AIELITEWAIT
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  ife gametype 0
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
	  ifn team actorvar[player[].i].team
        state pkick_check
  
ends

state elitegrowstate state genericgrowcode ends

state eliteslice

	ife targetalive NO
	{
		sound NEWBEAST_ATTACKMISS
		break
	}
	
	ifn bottarget -1
	ifn bottarget player[].i
	{
		ife seemytarget YES
		ifl targetdist 1340
		{
		  seta[bottarget].htextra 22
		  seta[bottarget].htpicnum SHOTSPARK1
		  seta[bottarget].htowner THISACTOR
		  seta[bottarget].htang sprite[].ang
		  sound RENDFLESH
		}
		else sound NEWBEAST_ATTACKMISS
	}
	else
	{
		ifpdistl 1340
		ife seeplayer YES
		{
		  getp[].i spriteid
		  seta[spriteid].htpicnum SHOTSPARK1
		  seta[spriteid].htowner THISACTOR
		  seta[spriteid].htextra 22
		  seta[spriteid].htang sprite[].ang
		  
		  state checksaberblock
		  ifn sprite[spriteid].htextra -1
		  {
			   palfrom 16 16
			  ife pchar 1 sound B_GRUNT else
				sound DUKE_GRUNT
			  sound RENDFLESH
		  }
		}
		else sound NEWBEAST_ATTACKMISS
	}

ends

state elitemeleestate

	ife targetalive NO
	{
		ai AIELITERUN
		break
	}

	ifaction AELITEMELEE1
	{
		ifactioncount 1
		{
			action AELITEMELEE2
			state eliteslice
		}
	}

	ifaction AELITEMELEE2
	{
		ifactioncount 1
		{
			ifn bottarget -1
			ifl targetdist 1024
			ifrnd 192
			{
				action AELITEMELEE1
			}
			else 
			{
				ai AIELITERUN
				set botclip -5
			}
		}
	}
ends

state elitedyingstate

	strength 0
	state deadsquished
	ifaction AELITEDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action AELITEDEAD
			iffloordistl 8 sound THUD
			ifrnd 128 spawn BLOODPOOL
			break
		}
	}
	ifaction AELITEDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength LIZELITESTRENGTH
		  // ai AIELITEWAIT
		// }
	}

ends

state elitetargetmove

	ifai AIELITEJUMP break
	
	// these move commands are being used as labels only
	// they indicate what the bot is supposed to be doing
	ifn bottarget -1
	{
		ifai AIELITEMELEE
		{
			ifmove ELITESHOOTBOT nullop else
			move ELITESHOOTBOT geth
		}
		else
		ife bottarget player[].i
		{
			ifmove ELITESHOOTPLAYER nullop else
			move ELITESHOOTPLAYER faceplayersmart
		}
		else
		{
			ifmove ELITESHOOTBOT nullop else
			move ELITESHOOTBOT geth
		}
	}
	else
	{
		ifmove ELITESHOOTPLAYER move STOPPED geth
		else
		ifmove ELITESHOOTBOT move STOPPED geth
	}
	
	// ifaction AELITERUN
	// ife bottarget -1 ifl navpoint 0
	// {
		// ifn lastcrumb -1
		// {
			// getav[lastcrumb].mtype temp
			// add temp 300
			// ifg player[].player_par temp ai AIELITEWAIT
		// }
	// }

ends

state elitestartwander
	
	cactor LIZELITE
	ai AIELITEWANDER
	set dodgetime 10
	set navpoint -1
	set savedvalue -1
	set B 0
	set xydist 2048
	whilevarn B 32
	{
		add B 1
		geta[].z z
		ifactor LIZELITECROUCH sub z 2048 else
		sub z 4096
		rand angvel 2047
		cos mycos angvel
		sin mysin angvel
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz clipmask
		
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		
		ifg startx xydist set B 32
	}
	seta[].ang angvel

ends

state elitewaitstate

	ifn bottarget -1 set countvarc 1 else
	ife seeplayer YES set countvarc 1 else
	ife countvarc 0 ife gametype 0 break
	
	ifgapzl 64
	{
		ifaction AELITESTAND
		{
			cactor LIZELITECROUCH
			action AELITECROUCH
		}
	}
	ifcount 6
	{
		ifn bottarget -1 ai AIELITERUN else
		ifg navpoint 0 ai AIELITERUN else
		{
			set temp lastcrumb
			add temp 300
			ifl temp player[].player_par ai AIELITERUN
		}
		ifai AIELITERUN cactor LIZELITE
	}

ends

state eliteshootstate

	
	ifl botclip 0 // reloading
	{
		add botclip 1
		ife botclip 0
		{
			// 0=plasma, 1=RPG, 2=flak, 3=chaingun, 4=shotgun
			ife mtype 0 set botclip 10 else
			ife mtype 1 set botclip 8 else
			ife mtype 2 set botclip 6 else
			ife mtype 3 set botclip 30 else
			ife mtype 4 set botclip 6 else
			set botclip 8
		}
		break
	}
	ifai AIELITEMELEE break
	
	add countvar 1
	
	ife targetalive YES
	ife seemytarget YES
	ife shootmytarget YES
	{
		ifl countvar 10
		ifl targetdist 1280
		iffloordistl 4
		ifrnd 32
		{
			cactor LIZELITE
			ai AIELITEMELEE
			break
		}
		ife mtype 1
		ifl targetdist 2560
		{
			set botclip -20
			ifspritepal 12 set mtype 4 else
			set mtype 2
			set countvar 0
		}
		switch mtype // weapon type
		case 0 // FIRELASER2
		ifg countvar 5
		{
			geta[].z savz
			set z2 savz
			ifactor LIZELITE sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
			set countvar 0
			setprojectile[FIRELASER2].workslike 36874
			shoot FIRELASER2
			setprojectile[FIRELASER2].workslike 32778
			sound PULSEFIRE
			seta[].z savz
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 160 
				{
					ifrnd 128 ifl targetdist 8192 set mtype 2
					else ifg targetdist 2560 set mtype 5
					else set botclip -40 // no change
				}
			}
		}
		break
		case 1 // RPG
		ifg countvar 19
		{
			geta[].z savz
			set z2 savz
			ifactor LIZELITE sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
			set countvar 0

			ifg targetdist 1024
			{
				geta[].x savx
				geta[].y savy
				set x savx
				add x 256
				rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
				seta[].x x2
				seta[].y y2
			}
			set xvel 644
			state rpg_targetprep
			ezshoot zdist RPG
			ife gametype 0 seta[RETURN].extra 60 else
			seta[RETURN].extra 80 // RPG_WEAPON_STRENGTH
			ifg targetdist 1024
			{
				seta[].x savx
				seta[].y savy
			}

			sound RPG_SHOOT
			seta[].z savz
			sub botclip 2
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 128 
				{
					ifspritepal 12
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 4
						else
						set mtype 3
					}
					else
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 2
						else
						set mtype 0
					}
				}
			}
		}
		else
		ifl targetdist 1560
		{
			set botclip -10
			ifrnd 128 
			{
				ifspritepal 12
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 4
					else
					set mtype 3
				}
				else
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 2
					else
					set mtype 0
				}
			}
		}
		break
		case 2 // FLAK
		ifg targetdist 8704
		{
			set botclip -20
			ifrnd 128 set mtype 0 else set mtype 5
			set countvar 0
		}
		
		ifg countvar 19
		{
			sound FLAKSHOT
			set countvar 0
			geta[].z savz
			set z2 savz
			ifactor LIZELITE sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
	
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844			
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ 
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep			
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			// set xvel 844
			// state rpg_targetprep			
			// zshoot zdist FLAKPROJ
			
			seta[].z savz
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 128
				{
					ifrnd 128 ifg targetdist 2560 set mtype 5
					else set mtype 0
				}
			}
		}
		break
		case 3 // CHAINGUN
		ifg countvar 2
		{
			
			set countvar 0
			ifn bottarget -1
			{
				state hitscan_targetprep
				zshoot zdist CHAINGUN
			}
			else shoot CHAINGUN
			
			espawn AIRFLASH1
			setav[RETURN].mtype 1
			
			sound M_GUN_SHOOT
			
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -40
				ifrnd 160 
				{
					set botclip -60
					ifrnd 128 ifl targetdist 8192 set mtype 4
					else ifg targetdist 2560 set mtype 1
					else set botclip -40 // no weapon change
				}
			}
		}
		break
		case 4 // SHOTGUN
		ifg targetdist 8704
		{
			set botclip -20
			set countvar 0
			ifrnd 128 set mtype 3 else set mtype 1
		}
		ife countvar 14 sound NEWSHOTCOCK
		ifg countvar 29
		{
			
			sound NEWSHOTFIRE
			set countvar 0
			
			ifn bottarget -1
			{
				state hitscan_targetprep
				state randzshoot
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				// zshoot tempb SHOTSPARK1
				
			}
			else
			{
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				// shoot SHOTSPARK1
			}
			espawn AIRFLASH1
			setav[RETURN].mtype 2
			
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -30
				ifrnd 128
				{
					set botclip -60
					ifrnd 128 ifg targetdist 2560 set mtype 1
					else set mtype 3
				}
			}
		}
		break
		case 5 // shock rifle
		ifg countvar 22
		{
			geta[].z savz
			set z2 savz
			ifactor LIZELITE sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
			set countvar 0

			ifg targetdist 1024
			{
				geta[].x savx
				geta[].y savy
				set x savx
				add x 256
				rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
				seta[].x x2
				seta[].y y2
			}
			
			ifrnd 128 // shock ball
			{
				set xvel 644
				state rpg_targetprep
				setprojectile[SHOCKBALL].extra 40
				ezshoot zdist SHOCKBALL
				setav[RETURN].myspawner THISACTOR
				setprojectile[SHOCKBALL].extra 55
				sound SHOCKBALLFIRE
			}
			else
			{
				state hitscan_targetprep
				ife gametype 0 setprojectile[SHOCKBEAM].extra 24
				else
				setprojectile[SHOCKBEAM].extra 16
				ezshoot zdist SHOCKBEAM
				setprojectile[SHOCKBEAM].extra 50
				sound LIGHTNINGFIRE
			}
			
			ifg targetdist 1024
			{
				seta[].x savx
				seta[].y savy
			}
			
			seta[].z savz
			sub botclip 2
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 128 
				{
					ifspritepal 12
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 4
						else
						set mtype 3
					}
					else
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 2
						else
							set mtype 0
					}
				}
			}
		}
		else ifl targetdist 1560
		{
			set botclip -10
			ifrnd 128 
			{
				ifspritepal 12
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 4
					else
					set mtype 3
				}
				else
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 2
					else
					set mtype 0
				}
			}
		}
		break
		
		default break
		endswitch
	}

ends
/*
state spritestaircheck
	
	// getzrange <x> <y> <z> <sector> <ceilingz> <ceilinghit> <floorz> <floorhit> <walldist> <clipmask>
	geta[].z z
	sub z 4096
	
	ifg sprite[].xvel 0 set angvar sprite[].ang else
	set angvar angvel
	
	set x sprite[].x
	add x 512
	rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
	updatesectorz x2 y2 z tempc
	ifn tempc -1
	{
		getzrange x2 y2 z tempc z2 temp tempd spriteid tempb CLIPMASK0
		ifg spriteid 49152 
		{
			sub spriteid 49152
			ifvarand sprite[spriteid].cstat 32
			ifvarand sprite[spriteid].cstat 1
			{
				set z sprite[].z
				sub z sprite[spriteid].z
				abs z
				ifle z 1280
				{
					set z sprite[spriteid].z
					sub z 512
					seta[].z z
					al spriteid
				}
			}
		}
	}

ends
*/

state elitemovement

	ifinwater
	{
		ifaction AELITESWIM nullop else
		{
			action AELITESWIM
			cactor LIZELITE
		}
		ifrnd 24 
		{
			geta[].z savz
			sub savz 6144
			seta[].z savz
			spawn WATERBUBBLE
			add savz 6144
			seta[].z savz
		}
	}
	else ifaction AELITESWIM { cactor LIZELITE action AELITERUN }
	
	ifn padmove 0 { set angvel padang break }
	
	ife targetalive YES
	ife seemytarget YES
	ifn bottarget -1
	ifl targetdist 10240
	{
		ifl navpoint 0
		{
			geta[bottarget].x x2
			geta[bottarget].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2
		}
		set temp player[].player_par
		ifrnd 1 rand initsprite 4096
		add temp initsprite
		shiftl temp 5
		sin tempb temp
		div tempb 37
		ifinwater shiftr tempb 1
		
		ifawayfromwall 
		{
			ifg navpoint -1
			{
				ldist xydist THISACTOR navpoint
				ifl xydist 844 set tempb 0
				else ifn gametype DM shiftr tempb 1	
			}
		}
		else set tempb 0
		
		
		ifg navpoint -1 ifg actorvar[navpoint].SPRITELOTAG 999 set tempb 0
		add angvel tempb
	}
	
	ifgapzl 64 
	{ 
		set temp sprite[].sectnum
		updatesectorz sprite[].x sprite[].y sprite[].z temp
		ifn temp -1
		{
			getflorzofslope temp sprite[].x sprite[].y z // z is the return var
			getceilzofslope temp sprite[].x sprite[].y z2
			sub z z2
			abs z
			ifl z 10240
			ifaction AELITERUN 
			{
				cactor LIZELITECROUCH 
				action AELITECRAWL 
			}
		}
	}
	else
	ifaction AELITECRAWL
	ife dodgetime 0
	{
		cactor LIZELITE
		action AELITERUN
	}
	set temp NO
	ifaction AELITERUN set temp YES
	ifaction AELITECRAWL set temp YES
	ifaction AELITESWIM set temp YES
	// use movesprite to move at angvel
	ife temp YES
	{
		cos xvel angvel
		sin yvel angvel
		shiftr xvel 6
		shiftr yvel 6
		set zdist 0
		ifg stun 0 { shiftr xvel 2 shiftr yvel 2 }
		ifaction AELITESWIM 
		{
			shiftr xvel 1 shiftr yvel 1
			
			ifg navpoint -1 { geta[navpoint].z zdist add zdist 4096 } else
			ifn bottarget -1 geta[bottarget].z zdist else
			geta[].z zdist
			
			sub zdist sprite[].z // if below target value will be negative
			set z2 zdist
			abs z2
			ifl z2 2048 set zdist 0 else
			ifl zdist 0 set zdist -1280 else
			ifg zdist 0 set zdist 1280
			
		}
		ifaction AELITECRAWL { shiftr xvel 1 shiftr yvel 1 }
		ifvarand monstflags 16 { mul xvel 3 div xvel 2 mul yvel 3 div yvel 2 state makespeedblur }
		movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
		
		
		ifn RETURN 0
		{
			// bumping something
			sub RETURN 49152
			ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
			{
				set B NO
				ife actorvar[RETURN].monstatus 1 
					ife actorvar[RETURN].team team set B YES
				ifg gametype 0 ifg navpoint -1
				ifvarand sprite[RETURN].cstat 16 set B YES
				ife B YES
				{
					set x2 sprite[].x
					add x2 256
					rotatepoint sprite[].x sprite[].y x2 sprite[].y angvel x y
					setsprite THISACTOR x y sprite[].z
					seta[].htmovflag 0
					set RETURN 0
				}
			}
			else ifg navpoint -1 ifrnd 32 set navpoint -1
		}
		ifinwater break
		
		state elitejumpcheck
		state randomjumpcheck
		ifai AIELITERUN ifn RETURN 0 ifrnd 32 state elitestartwander
		
		ifai AIELITEWANDER ife dodgetime 0 
		{ 
			cactor LIZELITE ai AIELITEWAIT 
			ifgapzl 64
			{	
				cactor LIZELITECROUCH
				action AELITECROUCH
			}
		}
	}

ends

state lizelitecode

	ifai 0
	{
		ai AIELITEWAIT
		sizeat 23 23
		clipdist 40
		cstator 257
		orvar monstflags YES
		set botclip 10
		ifn attmode 0 addstrength 100
		rand initsprite 4096
		set navmode 4
		ifspritepal 12 { set mtype 3 set botclip 30 }
		ife gametype DM
		{
			set temp bluescore
			mul temp 5
			ifg temp 100 set temp 100
			set tempb redscore
			mul tempb 4
			sub temp tempb
			add temp 110
			seta[].extra temp
		}
	}
	/*
	ifai AIELITEJUMP fall
	else
	ifg navpoint -1 ifg actorvar[navpoint].SPRITELOTAG 999
	{
		// updatesectorz sprite[].x sprite[].y sprite[].z temp
		// ifn temp -1
			// getflorzofslope temp sprite[].x sprite[].y z // z is the return var
		// else gets[].floorz z
		
		
		geta[navpoint].z z2
		add z2 8192
		ifl sprite[].z z2 fall else
		{
			ldist xydist THISACTOR navpoint
			geta[].z z
			sub z z2
			shiftl z 2
			div z xydist
			movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
		}
	}
	else
	*/
	ife gametype DM state dmtauntcode
	ifinwater nullop else
	fall
	state checksquished
	ifai AIELITEDYING { state elitedyingstate break }
	ifai AIELITEGROW { state elitegrowstate break }
	ifai AIELITEFROZEN { state elitefrozenstate break }
	state monsterai
	// ifstrength LIZELITESTRENGTH ifrnd 8 addstrength 1
	state eliteroamsounds
	ifai AIELITESHRUNK state eliteshrunkstate else
	{
		ife bottarget -1 seta[].ang angvel
		state elitetargetmove
		state eliteshootstate
		// state spritestaircheck
		ifai AIELITEWAIT state elitewaitstate else
		ifai AIELITEJUMP state elitejumpstate else
		ifai AIELITEMELEE state elitemeleestate else
		state elitemovement
		
		// ifg sprite[].zvel 0 add FEMFALLDMG 1
		// else
		// {
			// set FEMFALLDMG 0
			// ifaction AELITEFALL
			// {
				// ifai AIELITEWAIT action AELITESTAND else
				// ifai AIELITERUN action AELITERUN
			// }
		// }
		// ifg FEMFALLDMG 3
		// {
			// ifaction AELITESTAND action AELITEFALL else
			// ifaction AELITERUN action AELITEFALL
		// }
	}
	
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{	
		  cactor LIZELITE
		  sound ACTOR_SHRINKING
		  ai AIELITESHRUNK
		  set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		ifdead
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor LIZELITE
			  ai AIELITEFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  cactor LIZELITE
			  ai AIELITEGROW
			  break
			}
			addkills 1
			state enemy_death
			cactor LIZELITE
			ai AIELITEDYING
			
			ifrnd 128 sound ELITEDEATH1 else sound ELITEDEATH2
			
			guts JIBS6 4
			
			ifwasweapon RPG
			{
				cactor LIZELITE
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				cactor LIZELITE
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		ifai AIELITESHRUNK nullop else
		{
			ifrnd 84 sound ELITEPAIN1 else ifrnd 128 sound ELITEPAIN2 else sound ELITEPAIN3
		}
	}
	else state checksquished

ends

useractor enemy LIZELITE LIZELITESTRENGTH state lizelitecode enda

useractor enemy LIZELITECROUCH LIZELITESTRENGTH state lizelitecode enda
