// ARMY ANT

// animation test
	// ifhitspace ifcount 10
	// {
		// resetcount
		// ifaction ANTSTAND action ANTSHOOT else
		// ifaction ANTSHOOT action ANTWALK else
		// ifaction ANTWALK action ANTMELEE else
		// ifaction ANTMELEE action ANTJUMP else
		// ifaction ANTJUMP action ANTDIE else
		// ifaction ANTDIE action ANTDEAD else
		// ifaction ANTDEAD action ANTSTAND
		
		// ifaction ANTCRAWLWAIT action ANTCRAWLWALK else
		// ifaction ANTCRAWLWALK action ANTCRAWLSHOOT else
		// ifaction ANTCRAWLSHOOT action ANTCRAWLWAIT
	// }


action ANTSTAND		0  1  5  1  10
action ANTSHOOT		0  2  5  1  6
action ANTSHOOT2    5  1  5  1  6
action ANTWALK		10 4  5  1  12
action ANTRUN		10 4  5  1  8
action ANTMELEE		30 2  5  1  20
action ANTTHROW1	30 1  5  1  50
action ANTTHROW2	35 1  5  1  50
action ANTJUMP		40 1  5  1  24
action ANTCRAWLSHOOT  -5  1  5  1  6
action ANTCRAWLSHOOT2  0  1  5  1  6
action ANTCRAWLWAIT   0  1  5  1  10
action ANTCRAWLGO   0  4  5  1  8
action ANTDIE		70  6  1  1  18
action ANTDEAD		75  1  1  1  1
action ANTGROW		70  1  1  1  10
action ANTFROZEN    0  1  5  1  10
action ANTHURT      -7558 1  5  1  24
action AANTDECAP	196  6  1  1  18
action AANTDECAPDEAD 201  1  1  1  1
action ANTDIE2		222  6  1  1  18
action ANTDEAD2		227  1  1  1  10
action ANTANT		7416 2  5  1  8

move ANTMELEE_DONE
move ANTWALKVEL  140
move ANTDODGEVEL 224
move ANTJUMPVEL 224 -96
move ANTSHRUNKVEL 32
move ANTCRAWLVEL 128
move ANTCRAWLFAST 224

ai AIANTGRENADE ANTTHROW1 STOPPED faceplayer
ai AIANTWAIT ANTSTAND STOPPED geth
ai AIANTJUMP ANTJUMP ANTJUMPVEL geth getv
ai AIANTSHOOT ANTSHOOT STOPPED faceplayer
ai AIANTCRAWLSHOOT ANTCRAWLSHOOT STOPPED faceplayer
ai AIANTCRAWLWAIT ANTCRAWLWAIT STOPPED geth
ai AIANTCRAWLGO ANTCRAWLGO ANTCRAWLVEL seekplayer
ai AIANTSEEK ANTWALK ANTWALKVEL seekplayer
ai AIANTDODGE ANTRUN ANTDODGEVEL randomangle
ai AIANTCRAWLDODGE ANTCRAWLGO ANTCRAWLFAST randomangle
ai AIANTDYING ANTDIE STOPPED geth
ai AIANTGROW ANTGROW STOPPED geth
ai AIANTSHRUNK ANTRUN ANTSHRUNKVEL randomangle
ai AIANTFROZEN ANTFROZEN STOPPED geth
ai AIANTHURT ANTHURT STOPPED geth
ai AIANTMELEE ANTMELEE STOPPED geth

state antmeleestate

	ifactioncount 3 ai AIANTWAIT
	ifmove STOPPED
	ifactioncount 1
	{
		move ANTMELEE_DONE
		ife shrunken 0
		ife seemytarget YES ifl targetdist 1560
		ife targetalive YES
		{
			sound KICK_HIT
			ifn bottarget -1 set target bottarget
			else set target player[].i
			ife target player[].i
			{
				palfrom 40 63
				wackplayer
				getp[].ang bossang
				add bossang 1024
				cos newx bossang
				sin newy bossang
				shiftvarl newx 8
				shiftvarl newy 8
				setplayer[THISACTOR].posxv newx
				setplayer[THISACTOR].posyv newy
				// push player backwards
			}
			else
			{
				seta[target].xvel -512
				getav[target].stun temp
				add temp 15
				setav[target].stun temp
				geta[target].z z
				sub z 512
				seta[target].z z
				geta[target].zvel z
				sub z 512
				seta[target].zvel z
			}
			
			seta[target].htextra 20
			seta[target].htpicnum SHOTSPARK1
			seta[target].htowner THISACTOR
			seta[target].htang sprite[THISACTOR].ang
		}
	}

ends

state antjumpcheck

	ifceilingdistl 96 break
	geta[].z z
	sub z 4096
	
	cos mycos sprite[].ang
	sin mysin sprite[].ang
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 512
		{
			sub z 8192
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 1560 { cactor ARMYANT ai AIANTJUMP geta[].ang mtype }
		}
	}


ends

state antshootbullet
	
	sound M4FIRE
	state hitscan_targetprep
	zshoot zdist SHOTSPARK1

ends

state antshootpurple

	ife initsprite  YES
	{
		geta[].z savz
		add savz 4096
		seta[].z savz
	}
	set savx sprite[].x
	set savy sprite[].y
	
	set x sprite[].x
	add x 384
	rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
	seta[].x x2
	seta[].y y2
				
	sound PURPFIRE
	set xvel 1024
	state rpg_targetprep	
	ezshoot zdist PURPPROJ
	
	seta[].x savx
	seta[].y savy
	
	ife initsprite  YES
	{
		sub savz 4096
		seta[].z savz
	}

ends

state antshootstate

	ifaction ANTSHOOT
	{
		ifactioncount 1
		{
			ife bottarget -1 set botclip 10
			ifg botclip 8
			{
				ai AIANTWAIT
				set botclip 0
				break
			}
			action ANTSHOOT2
			add botclip 1
			ifvarand initflags 32 state antshootpurple else
			state antshootbullet
		}
		else
		{
			ifn myspawner -1
			{
				state incoming_eval
				seta[].ang angvel
				set dodgetime 8
				set crumbwait 9
				ifrnd 128 ife initsprite NO
				{
					cactor ARMYANT
					ai AIANTDODGE
				}
				else
				{
					cactor ARMYANTCRAWL
					ai AIANTCRAWLDODGE
				}
			}
		}
	}
	ifaction ANTSHOOT2
	{
		ifactioncount 1
		action ANTSHOOT
	}
	
	
		
	
ends

state antgrenadestate

	ifaction ANTTHROW1
	{
		ifactioncount 1
		{
			action ANTTHROW2
			set xvel 644
			state rpg_targetprep
			sub zdist 1024
			ezshoot zdist GRENADEPROJ
		}
	}
	
	ifaction ANTTHROW2
	{
		ifactioncount 1 ai AIANTWAIT
	}

ends

state antcrawlshootstate

	ifaction ANTCRAWLSHOOT2
	{
		ifactioncount 1
		{
			ife bottarget -1 set botclip 10
			ifg botclip 8
			{
				ai AIANTCRAWLWAIT
				set botclip 0
				break
			}
			action ANTCRAWLSHOOT
			add botclip 1
			ifvarand initflags 32 state antshootpurple else
			state antshootbullet
		}
		else
		{
			ifn myspawner -1
			{
				state incoming_eval
				seta[].ang angvel
				set dodgetime 8
				set crumbwait 9		
				ai AIANTCRAWLDODGE
			}
		}
	}
	ifaction ANTCRAWLSHOOT
	{
		ifactioncount 1
		action ANTCRAWLSHOOT2
	}

ends

state antdodgestate 

	ifcount 12 
	{
		ifactor ARMYANT
		ai AIANTWAIT 
		else
		ai AIANTCRAWLWAIT
	}

ends


state antcheckdodge

	ifcount 6 nullop else break
	
	ifp pfacing
	ife seemytarget YES
	ifl targetdist 8192
	ifn player[].kickback_pic 0
	ifn player[].curr_weapon 5
	{
		set dodgetime 10
		set crumbwait 12
		ifrnd 128 ife initsprite NO
		{
			cactor ARMYANT
			ai AIANTDODGE
		}
		else
		{
			cactor ARMYANTCRAWL
			ai AIANTCRAWLDODGE
		}
		break
	}
	ifg targetdist 3072
	ifbulletnear
	{
		set dodgetime 10
		set crumbwait 12
		ifrnd 128 ife initsprite NO
		{
			cactor ARMYANT
			ai AIANTDODGE
		}
		else
		{
			cactor ARMYANTCRAWL
			ai AIANTCRAWLDODGE
		}
	}
ends

state antgrowstate state genericgrowcode ends

state antroamsounds

	ifactorsound THISACTOR ANTROAM1 break
	ifactorsound THISACTOR ANTROAM2 break
	ifactorsound THISACTOR ANTROAM3 break
	ifactorsound THISACTOR ANTROAM4 break
	rand temp 3
	switch temp
	case 0 sound ANTROAM1 break
	case 1 sound ANTROAM2 break
	case 2 sound ANTROAM3 break
	case 3 sound ANTROAM4 break
	endswitch

ends

state anthurtstate
	ifg stun 0 nullop else
	ifcount 10 { cactor ARMYANT ai AIANTSEEK }
ends

state antshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIANTSEEK
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 40 sizeto 28 26 else
      sizeto 24 24
	}
  else
  {
    state genericshrunkcode
	ifcount 20 { ifaction ANTANT nullop else action ANTANT ifl sprite[].xrepeat 16 sizeat 16 16 }
  }

ends

state antjumpstate

	
	ifmove ANTJUMPVEL
	{
	    seta[].ang mtype
		ifcount 8 move ANTWALKVEL geth
	}
	ifmove ANTWALKVEL
	{
		iffloordistl 8
			ai AIANTSEEK
		
	}

ends

state antfrozenstate

    ifcount THAWTIME
    {
      ai AIANTSEEK
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }

      addkills 1 
	  state enemy_death
	  ifrnd 128 state drop_chaingun // half chance of normal drop

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
	  ifn team actorvar[player[].i].team
        state pkick_check
  
ends

state antdyingstate

	strength 0
	state deadsquished
	ifaction AANTDECAP
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 4
		{
			cstat 0
			action AANTDECAPDEAD
			iffloordistl 8 sound THUD
			ifrnd 128 spawn BLOODPOOL
			break
		}
	}
	ifaction AANTDECAPDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength ARMYANTSTRENGTH
		  // ai AIANTSEEK
		// }
		break
	}
	
	ifaction ANTDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action ANTDEAD
			iffloordistl 8 sound THUD

			ifrnd 128 spawn BLOODPOOL
			break
		}
	}
	ifaction ANTDIE2
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			geta[].cstat temp
			ifvarand temp 256 xorvar temp 256
			ifvarand temp 1 xorvar temp 1
			seta[].cstat temp
			action ANTDEAD2
			iffloordistl 8 sound THUD

			ifrnd 128 spawn BLOODPOOL
			break
		}
	}
	set temp NO
	ifaction ANTDEAD set temp YES
	ifaction ANTDEAD2 set temp YES
	ife temp YES
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				// state ant_body_jibs
				// state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				// state ant_body_jibs
				// state lite_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength ARMYANTSTRENGTH
		  // ai AIANTSEEK
		// }
	}
	
ends

state antmeleecheck
    ife initsprite YES break
	ifrnd 192
	{
		cactor ARMYANT
		ai AIANTMELEE
		sound ANTMELEESND
		ifn bottarget -1
		{
			set spriteid bottarget
			state facesprite
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
		}
	}
	else
	{
		ifrnd 128
		{
			set dodgetime 12
			set crumbwait 12
			ifrnd 128
			{
				cactor ARMYANT
				ai AIANTDODGE
			}
			else
			{
				cactor ARMYANTCRAWL
				ai AIANTCRAWLDODGE
			}
		}
	}
ends

state antseekstate

	ifrnd 1 ifrnd 64 state antroamsounds

	ifl targetdist 1280 
		ife seemytarget YES
		  ife targetalive YES
	{
		state antmeleecheck
		break
	}
		
	state antcheckdodge
	ifactor ARMYANTCRAWL { ai AIANTCRAWLGO break }
	ifai AIANTSEEK
	{
		ifnotmoving 
		ifcount 6
		{ 
			ifrnd 64 operate
			state antjumpcheck
			ifai AIANTJUMP nullop else
			{
				ai AIANTWAIT
				set countvar YES
			}
			break
		}
		
		set temp NO
		ifge SKILL 4 { ifcount 30 set temp YES }
		else ifcount 60 set temp YES
		
		ife seemytarget YES
		ife temp YES ifrnd 8 ai AIANTWAIT
		
		ifl navpoint 0
		ifcount 50 ifrnd 16 ai AIANTWAIT
		
		ifactornotstayput nullop else
		ifawayfromwall nullop else
		ifl navpoint 0
		ifrnd 12 ai AIANTWAIT
	}

ends

state antcrawlstate

	ifrnd 1 ifrnd 64 state antroamsounds

	ifl targetdist 1280 
		ife seemytarget YES
		  ife targetalive YES
		    ife initsprite NO
	{
		state antmeleecheck
		break
	}
		
	state antcheckdodge
	ifai AIANTCRAWLGO
	{
		ife initsprite NO ifmove ANTCRAWLVEL move ANTCRAWLFAST seekplayer
		ifnotmoving 
		{
			ifrnd 64 operate
			state antjumpcheck
			ifai AIANTJUMP nullop else
			{
				ai AIANTCRAWLWAIT
				set countvar YES
			}
			break
		}
		ife seemytarget YES
		ifcount 40 ai AIANTCRAWLWAIT
		
		set temp NO
		ifge SKILL 4 { ifcount 30 set temp YES }
		else ifcount 60 set temp YES
		
		ifl navpoint 0
		ife temp YES ifrnd 8 ai AIANTCRAWLWAIT
		
		ifactornotstayput nullop else
		ifawayfromwall nullop else
		ifl navpoint 0
		ifrnd 12 ai AIANTCRAWLWAIT
	}

ends

state antwaitstate

	ifrnd 1 state antroamsounds

	ife targetalive YES
	{
		ifcount 8
		{
			ifl targetdist 1280 state antmeleecheck
			else
			ifl targetdist 2560 
			ifrnd 64
			{
				ai AIANTDODGE
				set dodgetime 12
				set crumbwait 12
			}
			else
			ife seemytarget YES
			ife shootmytarget YES
			ifrnd 64
			{
				ifrnd 96 sound ANTATTACK1
				ifn bottarget -1
				{
					set spriteid bottarget
					state facesprite
				}
				ifrnd 96 
				{
					cactor ARMYANTCRAWL
					ai AIANTCRAWLSHOOT 
					ifvarand initflags 32 state antshootpurple else
					state antshootbullet
				}
				else 
				{
					ifg targetdist 5120 ifrnd 64 ai AIANTGRENADE
					else
					ai AIANTSHOOT
				}
			}
			else
			ifbulletnear
			{
				ai AIANTDODGE
				set dodgetime 12
				set crumbwait 12
			}
			else
			ifrnd 64 ai AIANTSEEK
		}
	}
	else 
	{
		ai AIANTSEEK
		ife countvar YES
		{
			move ANTWALKVEL furthestdir
			set countvar NO
		}
	}

ends

state antcrawlwaitstate

	ifrnd 1 state antroamsounds

	ife targetalive YES
	{
		ifcount 8
		{
			ifl targetdist 1280 state antmeleecheck
			else
			ifl targetdist 2560
			ifrnd 64
			{
				ai AIANTCRAWLDODGE
				set dodgetime 12
				set crumbwait 12
			}
			else
			ife seemytarget YES
			ife shootmytarget YES
			ifrnd 64
			{
				ifrnd 96 sound ANTATTACK1
				ifn bottarget -1
				{
					set spriteid bottarget
					state facesprite
				}
				ifrnd 128 ife initsprite NO
				{
					cactor ARMYANT
					ai AIANTSHOOT
				}
				else 
				{
				    ai AIANTCRAWLSHOOT
					ifvarand initflags 32 state antshootpurple else
					state antshootbullet
					
				}
					
				
			}
			else
			ifbulletnear
			{
				ai AIANTCRAWLDODGE
				set dodgetime 12
				set crumbwait 12
			}
			else
			ifrnd 64 ai AIANTCRAWLGO
		}
	}
	else 
	{
		ai AIANTCRAWLGO
		ife countvar YES
		{
			move ANTCRAWLVEL furthestdir
			set countvar NO
		}
	}

ends

state armyantcode

	ifai 0
	{
		cstator 257
		sizeat 24 24
		clipdist 40
		ai AIANTWAIT
		ifspritepal 40
		{
			orvar initflags 32
			addstrength 50
			sizeat 28 26
		}
	}
	
	ife initsprite NO { fall geta[].cstat temp ifvarand temp 8 xorvar temp 8 seta[].cstat temp }
	else 
	{
		cstator 8
		getceilzofslope sprite[].sectnum sprite[].x sprite[].y z
		geta[].z zdist
		sub zdist z
		ifg zdist 9728 // 10240
		movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
		else ifl zdist 6144 { geta[].z temp add temp 1024 seta[].z temp  }
		ifg zdist 20480 set initsprite NO
		ifvarand sector[].ceilingstat 1 set initsprite NO
	}
	
	ifai AIANTGROW { state antgrowstate break }
	ifai AIANTFROZEN { state antfrozenstate break }
	ifai AIANTDYING { state antdyingstate break }
	
	state monsterai
	
	ifai AIANTWAIT state antwaitstate else
	ifai AIANTSHRUNK state antshrunkstate else
	ifai AIANTSEEK state antseekstate else
	ifai AIANTDODGE state antdodgestate else
	ifai AIANTCRAWLDODGE state antdodgestate else
	ifai AIANTJUMP state antjumpstate else
	ifai AIANTSHOOT state antshootstate else
	ifai AIANTCRAWLSHOOT state antcrawlshootstate else
	ifai AIANTGRENADE state antgrenadestate else
	ifai AIANTMELEE state antmeleestate else
	ifai AIANTCRAWLWAIT state antcrawlwaitstate else
	ifai AIANTCRAWLGO state antcrawlstate else
	ifai AIANTHURT state anthurtstate
	
	set myspawner -1
	
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{	
		  cactor ARMYANT
		  set initsprite NO
		  sound ACTOR_SHRINKING
		  ai AIANTSHRUNK
		  set botclip 0
		  set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		ifwasweapon HEADJIB1
		{
			addkills 1
			state enemy_death
			cactor ARMYANT
			set initsprite NO
			ai AIANTDYING
			action AANTDECAP
			strength 0
			cstat 0
			guts JIBS6 4
			rand zdist 1024
			sub zdist 3500
			ezshoot zdist ANTHEADPROJ
			rand angvar 512
			sub angvar 256
			add angvar sprite[].htang
			seta[RETURN].ang angvar
			seta[RETURN].pal sprite[].pal
			sound SQUISHED
			break
		}
		ifdead
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor ARMYANT
			  set initsprite NO
			  ai AIANTFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  cactor ARMYANT
			  set initsprite NO
			  ai AIANTGROW
			  break
			}
			addkills 1
			state enemy_death
			 ifrnd 128 state drop_chaingun // half chance of normal drop
			cactor ARMYANT
			set initsprite NO
			ai AIANTDYING
			ifrnd 128
			action ANTDIE2
			ifrnd 128 cstator 4
			sound ANTDEATH
			spritepal 6
			guts JIBS6 4
			getlastpal
			cactor ARMYANT
			ifwasweapon RPG
			{
				cactor ARMYANT
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				// state ant_body_jibs
				// state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				cactor ARMYANT
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state standard_jibs
				// state ant_body_jibs
				// state lite_jibs
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		ifai AIANTHURT nullop else
		ifai AIANTSHRUNK nullop else
		{
			ifg stun 0 { cactor ARMYANT set initsprite NO ai AIANTHURT break }
			
			ifactorsound THISACTOR ANTPAIN1 nullop else
			ifactorsound THISACTOR ANTPAIN2 nullop else
			{
				ifrnd 128 sound ANTPAIN1 else sound ANTPAIN2
			}
			ifrnd 32
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			// ife initsprite NO
			{ 
				cactor ARMYANT
				ai AIANTHURT 
				set botclip 0
				set initsprite NO
				break 
			}
		}
	}
	else state checksquished
	
ends

useractor enemy ARMYANT ARMYANTSTRENGTH state armyantcode enda

useractor enemy ARMYANTCRAWL ARMYANTSTRENGTH 

ifai 0
{
	cstator 257
	sizeat 24 24
	ifspritepal 40
	{
		orvar initflags 32
		addstrength 50
		sizeat 28 26
	}
	ai AIANTCRAWLWAIT
	ife initsprite YES
	seta[].mdflags 16
}
state armyantcode 

enda

useractor enemystayput ARMYANTCRAWLPUT ARMYANTSTRENGTH 

	cstator 257
	sizeat 24 24
	ifspritepal 40
	{
		orvar initflags 32
		addstrength 50
		sizeat 28 26
	}
	ai AIANTCRAWLWAIT
	set initsprite YES
	seta[].mdflags 16
	cactor ARMYANTCRAWL

enda

useractor enemy ARMYANTJUMP ARMYANTSTRENGTH 

	cstator 257
	sizeat 24 24
	ifspritepal 40
	{
		orvar initflags 32
		addstrength 50
		sizeat 28 26
	}
	ai AIANTJUMP
	clipdist 40
	geta[].ang mtype
	cactor ARMYANT
enda


action AANTWALK  0  2  5  1  8
eventloadactor ANT sizeat 10 10 enda
useractor notenemy ANT 1 


fall
ifaction 0
{
	action AANTWALK
	move ANTSHRUNKVEL randomangle
	ifspawnedby ARMYANT sizeat 16 16 else
	ifspawnedby ARMYANTCRAWL sizeat 16 16 else
	sizeat 10 10
	cstat 256
	clipdist 2
}
ifnotmoving move ANTSHRUNKVEL randomangle

ifhitweapon 
{ 
	spritepal 6 guts JIBS6 1 getlastpal
	killit
}
enda
