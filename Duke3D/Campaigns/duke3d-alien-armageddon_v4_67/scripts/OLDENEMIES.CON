// ACTOR CODE FOR CLASSIC ENEMIES
// CODE HAS BEEN HEAVILY MODIFIED FROM VANILLA GAME

actor ORGANTIC TURRETSTRENGTH
  ifcount 48
    resetcount
  else
  {
    ifcount 32
    sizeto 32 32
  }
  
    ifcount 16
    {
      sizeto 48 18
      ifl targetdist 2048
      {
        sound TURR_ATTACK
        addphealth -2
        palfrom 32 16
      }
    }
  ifhitweapon
  {
    ifdead
    {
      addkills 1 state enemy_death
      sound TURR_DYING
      guts JIBS5 10
      killit
    }
    sound TURR_PAIN
    break
  }
  ifrnd 1
    soundonce TURR_ROAM
enda

move ZOMBIESEEK 128
move ZOMBIERUN 196
action AZOMBIESTAND -2 1 1 1 1
action AZOMBIEUP 1 1 1 30
action AZOMBIE1 0 1 1 1 3
action AZOMBIE2 0 1 1 1 3

eventloadactor ZOMBIETROOP 
// ife upscaled YES { sizeat 21 20 set init_xrepeat 21 set init_yrepeat 20 } else
sizeat 42 40 
enda

useractor enemy ZOMBIETROOP 20

ifmove 0
{
	seta[].mdflags 1
	ifl sprite[].xrepeat 20 { sizeat 21 20 set init_xrepeat 21 set init_yrepeat 20 }
	move STOPPED faceplayer
	cstat 257
	action AZOMBIEUP
	set monstatus 1
	ifspritepal 21 strength 40
}
fall
state monsterai ife monstatus -1 break

ifhitweapon
{
	sound PRED_DYING
	guts JIBS6 1
	ifdead
	{
		hitradius 1792 20 25 30 35
		ifpdistl 1800 seta[player[].i].htowner THISACTOR
		addkills 1 state enemy_death
		spawn EXPLOSION2
		globalsound PIPEBOMB_EXPLODE
		sound SQUISHED
		state troop_body_jibs
	    state lite_jibs
	    killit
    }
}

ifaction AZOMBIEUP
{
	ifactioncount 4 { action AZOMBIE1 move ZOMBIERUN faceplayer }
	break
}

ifactioncount 5
{
	sound STEPSND
	ifaction AZOMBIE1 { action AZOMBIE2 cstat 261 }
	else { action AZOMBIE1 cstat 257 }
}

ife seemytarget YES
{
	ifl targetdist 928
	{
		hitradius 1792 20 25 30 35
		ifpdistl 1800 seta[player[].i].htowner THISACTOR
		addkills 1 state enemy_death
		spawn EXPLOSION2
		globalsound PIPEBOMB_EXPLODE
		sound SQUISHED
		state troop_body_jibs
		state lite_jibs
		killit
	}
	else ifmove ZOMBIESEEK move ZOMBIERUN faceplayer
		
}
else ifmove ZOMBIERUN
ifrnd 2 move ZOMBIESEEK seekplayer

ifnotmoving
{
	add countvarb 1
	ifg countvarb 60
	{
		hitradius 1792 20 25 30 35
		ifpdistl 1800 seta[player[].i].htowner THISACTOR
		addkills 1 state enemy_death
		spawn EXPLOSION2
		globalsound PIPEBOMB_EXPLODE
		sound SQUISHED
		state troop_body_jibs
	    state lite_jibs
	    killit
    }
}
else set countvarb 0



ifrnd 1 sound PRED_PAIN
ifrnd 4 
{
	espawn SMALLSMOKE
	seta[RETURN].htpicnum EXPLOSION2
	seta[RETURN].xrepeat 128
	seta[RETURN].yrepeat 128
}

enda

state shootBIGFIRELASER
  	sound FIRE_LASER
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR FIRE_LASER SHRUNKPITCH
	ifn bottarget -1 setprojectile[FIRELASER2].workslike 36874
	shoot FIRELASER2
	ifn bottarget -1 setprojectile[FIRELASER2].workslike 32778

ends

defstate shootbigplasma
    sound LASERSHOOT
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR LASERSHOOT SHRUNKPITCH
	set xvel 844
	state rpg_targetprep
	setprojectile[BIGPLASMA].extra 14
	ezshoot zdist BIGPLASMA
	setprojectile[BIGPLASMA].extra 20
ends

//
//
//    TROOP/LIZTROOP CODE
//
//

action AENEMYGUARD -2  1  5  1  10

action ATROOPSTAND     0  1  5  1  1
action ATROOPGROW    0  1  5  1  1
action ATROOPSTAYSTAND  -2  1  5  1  1
action ATROOPWALKING   0  4  5  1   12
action ATROOPWALKINGBACK 15   4  5   -1   12
action ATROOPRUNNING   0  4  5  1  8
action ATROOPSHOOT    35  1  5  1   30
action ATROOPJETPACK  40  1  5  1  1
action ATROOPJETPACKILL 40  2  5  1   50
action ATROOPFLINTCH  50  1  1  1  6
action ATROOPDYING    50  5  1  1   16
action ATROOPDEAD     54
action ATROOPPLAYDEAD   54
action ATROOPSUFFERDEAD 58  2  1   -4   24
action ATROOPSUFFERING  59  2  1  1   21
action ATROOPDUCK     64  1  5  1  3
action ATROOPDUCKSHOOT  64  2  5  1   25
action ATROOPABOUTHIDE  74  1  1  1   25
action ATROOPHIDE     79  1  1  1   25
action ATROOPREAPPEAR   74  1  1  1   25
action ATROOPFROZEN    0  1  5
action ATROOPDECAP    4880  5  1  1  16
action ATROOPDECAPDEAD  4884
action ATROOPDYING2		9095  6  1  1  16
action ATROOPDEAD2		9100
action ATROOPDYING2FALL 9095  2  1  1  8
action ATROOPDYING3		5955  6  1  1  16
action ATROOPDEAD3		5960

move TROOPWALKVELS 72
move TROOPWALKVELSBACK -72
move TROOPJETPACKVELS 64 -84
move TROOPJETUPVELS 64 -96
move TROOPJETDOWN 72 8
move TROOPJETPACKILLVELS 192 -38
move TROOPRUNVELS 108
move DONTGETUP
move SHRUNKVELS 32
move TROOPBOOSTVELS 384 -16

ai AITROOPSEEKENEMY  ATROOPWALKING   TROOPWALKVELS seekplayer
ai AITROOPSEEKPLAYER ATROOPWALKING   TROOPWALKVELS seekplayer
ai AITROOPFLEEING  ATROOPWALKING   TROOPWALKVELS fleeenemy
ai AITROOPFLEEINGBACK ATROOPWALKINGBACK   TROOPWALKVELSBACK faceplayer
ai AITROOPDODGE    ATROOPWALKING   TROOPRUNVELS  dodgebullet
ai AITROOPSHOOTING   ATROOPSHOOT   STOPPED  faceplayer
ai AITROOPDUCKING  ATROOPDUCK    STOPPED  faceplayer
ai AITROOPJETPACK  ATROOPJETPACK   TROOPJETPACKVELS seekplayer
// ai AITROOPSHRUNK   ATROOPWALKING   SHRUNKVELS fleeenemy
ai AITROOPHIDE     ATROOPABOUTHIDE STOPPED faceplayer
ai AITROOPGROW     ATROOPGROW    DONTGETUP faceplayerslow
ai AITROOPBOOST ATROOPJETPACK TROOPBOOSTVELS geth getv
ai AITROOPDECAP ATROOPDECAP STOPPED geth

state checktroophit

  ifwasweapon HEADJIB1 // HEADSHOT!
  {
    // sound PRED_DYING
	sound SQUISHED
	cstat 0
    strength -1
	ifaction ATROOPSUFFERING stopsound LIZARD_BEG
	state upscaleme
	ai AITROOPDECAP
	// guts HEADJIB1 1
	rand zdist 1024
	  sub zdist 3500
	  ezshoot zdist TROOPHEADPROJ
	  ife upscaled YES { seta[RETURN].xrepeat init_xrepeat seta[RETURN].yrepeat init_yrepeat }
	  rand angvar 512
	  sub angvar 256
	  add angvar sprite[].htang
	  seta[RETURN].ang angvar
	  seta[RETURN].pal sprite[].pal
	state random_wall_jibs
	ife mtype 3 break
	ife gametype 0
	{
		ifvarand initflags 16 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		state drop_ammo
	}
    
	addkills 1 state enemy_death
	break
  }
  ifaction ATROOPSUFFERING
  {
    stopsound LIZARD_BEG
	
    sound PRED_DYING
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_DYING 1560
    cstat 0
    strength 0
    action ATROOPSUFFERDEAD
	ife mtype 3 set mtype 0
	ifwasweapon RPG nullop else ifwasweapon RADIUSEXPLOSION nullop else
    break
  }
  ifdead
  {
    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action ATROOPFROZEN
      strength 0
	  state freezeme
      break
    }
	ife gametype 0
	{
		ifvarand initflags 16 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		state drop_ammo
	}
    state random_wall_jibs

    ifwasweapon GROWSPARK
    {
      cstat 0
      sound ACTOR_GROWING
      ai AITROOPGROW
      break
    }

    addkills 1 state enemy_death

    ifwasweapon RPG
    {
      sound SQUISHED
      state troop_body_jibs
      state lite_jibs
      killit
    }
    else
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      state troop_body_jibs
      state lite_jibs
      killit
    }
    else
    {
      sound PRED_DYING
	  ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_DYING 1560
      ifrnd 64
        iffloordistl 32
      {
        sound LIZARD_BEG
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR LIZARD_BEG 1560
        spawn BLOODPOOL2
        strength 0
        move 0
        action ATROOPSUFFERING
		set mtype 3 // suffering flag for headshots
        break
      }
	  ifawayfromwall 
	  ifrnd 96
	  { ifrnd 128 cstator 4 action ATROOPDYING2 guts ARMJIB1 1 state upscaleme } 
	  else
	  iffloordistl 32 ifrnd 128 { ifrnd 128 cstator 4 action ATROOPDYING3 state upscaleme } else
      action ATROOPDYING
	  move STOPPED
      break
    }
  }
  else
  {
    state random_wall_jibs
	
    sound PRED_PAIN
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_PAIN 1560
    ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      // ai AITROOPSHRUNK
	  set shrunken 1
    }
    else
      ifai AITROOPBOOST nullop else
      iffloordistl 32
      {
        ifrnd 96
		ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
          action ATROOPFLINTCH
        else ifvarg stun 0 action ATROOPFLINTCH
      }
	  else
	  {
		ifvarg stun 0
		{
			ai AITROOPSEEKPLAYER
			action ATROOPFLINTCH
			move STOPPED geth
		}
	  }
			
	  
     ifwasweapon GROWSPARK
        sound SPARKLESOUND
  
  }
ends

state troophidestate
  ifaction ATROOPREAPPEAR
  {
    ifactioncount 2
    {
      sound TELEPORTER
      ai AITROOPSHOOTING
      cstat 257
    }
    else
    {
	  set burning 0
	  seta[].htextra -1
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		sizeto MINXSTRETCH MINYSTRETCH
		sizeto MINXSTRETCH MINYSTRETCH
	  }
	  else
	  {
		  sizeto 41 40
		  sizeto 41 40
		  sizeto 41 40
		  sizeto 41 40
	  }
      spawn FRAMEEFFECT1
    }
  }
  else
    ifaction ATROOPWALKING
  {
    set burning 0
	seta[].htextra -1
    ifl targetdist 2448
      ifg targetdist 1024
    {
      ifceilingdistl 48
        break
      ifp pfacing
        break

      ifgapzl 64 nullop
      else
        ifawayfromwall
      {
        spawn TRANSPORTERSTAR
        action ATROOPREAPPEAR
        move 0
        break
      }
    }
	ife bluecarrier THISACTOR
	{
		spawn TRANSPORTERSTAR
        action ATROOPREAPPEAR
        move 0
        break
	}
	ife redcarrier THISACTOR
	{
		spawn TRANSPORTERSTAR
        action ATROOPREAPPEAR
        move 0
        break
	}
  }
  else
    ifaction ATROOPHIDE
  {
    ifactioncount 2
    {
      spawn TRANSPORTERSTAR
      sound TELEPORTER
      action ATROOPWALKING
      move TROOPWALKVELS faceplayer
      cstat 32768
    }
    else
    {
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		sizeto 2 MINYSTRETCH
		sizeto 2 MINYSTRETCH
	  }
	  else
	  {
		  ife upscaled YES
		  {
		    sizeto 2 40
		    sizeto 2 40
		  }
		  else
		  {
		  sizeto 4 40
		  sizeto 4 40
		  sizeto 4 40
		  sizeto 4 40
		  }
	  }
      spawn FRAMEEFFECT1
	  ifhitweapon
      state checktroophit
    }
  }
  else
    ifaction ATROOPABOUTHIDE
	{
		ifactioncount 2
		{
		  action ATROOPHIDE
		  cstat 0
		}
		ifhitweapon
        state checktroophit
	}
ends

state troopgunnashoot

  ife targetalive YES
{
	set temp NO
	ifvarand initflags 16 ifrnd 64 ife seemytarget YES set temp YES
	
    ifl targetdist 1024
      ai AITROOPSHOOTING
    else
      ifactornotstayput
      ifvare temp NO
    {
      ifactioncount 12
        ifrnd 16
          ife shootmytarget YES
      {
        ifspritepal 21
          ifrnd 4
            ifg targetdist 4096
				ifn redcarrier THISACTOR ifn bluecarrier THISACTOR
              ai AITROOPHIDE
				
        else
        {
          ifl targetdist 1100
		  {
            ai AITROOPFLEEING
			set dodgetime 8
		  }
          else
          {
            ifl targetdist 4096
              ife seemytarget YES
                ife shootmytarget YES
                  ai AITROOPSHOOTING
            else
            {
              move TROOPRUNVELS seekplayer
              action ATROOPRUNNING
            }
          }
        }
      }
    }
    else
      ifcount 26
        ifrnd 32
        {
	      ifrnd 160
          ai AITROOPSHOOTING
          else
          ai AITROOPDUCKING
        }
		
	  ifvarand initflags 16
	  ifactornotstayput
	  {
		  ifai AITROOPSHOOTING nullop else
		  ifai AITROOPDUCKING nullop else
		  {
			  getactor[].ang angvar
			  ifrnd 32
			  ife scannedsprite THISACTOR ai AITROOPBOOST else
			  ifbulletnear { ai AITROOPBOOST set dodgetime 8 }
			  
			  ifai AITROOPBOOST
			  {
				  ifrnd 128 addvar angvar 256 else subvar angvar 256
				  seta[].ang angvar
			  }  
		  }
	  }
}
ends

state troopbooststate

	ifl dodgetime 2 set dodgetime 2
	cstat 514
	spawn FRAMEEFFECT1
	cstat 257
	seta[].ang lastang
	
	soundonce DUKE_JETPACK_IDLE
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DUKE_JETPACK_IDLE 1560
	
	ifcount 30
	{
		set dodgetime 0
		iffloordistl 32 ai AITROOPDUCKING else { ai AITROOPJETPACK count 36 }
	}
	else ifnotmoving
	{
		set dodgetime 0
		iffloordistl 32 ai AITROOPDUCKING else { ai AITROOPJETPACK move TROOPJETDOWN faceplayer getv count 40 }
	}

ends

state troopseekstate

  ifg sprite[].zvel 0
	{
	  ifceilingdistl 128 nullop
	  else
	  iffloordistl 128 nullop else
		ifactornotstayput
		{
		  ai AITROOPJETPACK
		  ifvarand initflags 16 move TROOPJETUPVELS seekplayer
		  break
		}
	  
	}
  state troopgunnashoot
  ifinwater
  {
    ai AITROOPJETPACK
	ifvarand initflags 16 move TROOPJETUPVELS seekplayer
    break
  }
  ife seemytarget YES
  {
    ifmove TROOPRUNVELS
      ifl targetdist 1596
        ai AITROOPDUCKING
    ife targethigher YES
    {
      ifceilingdistl 128 nullop
      else
        ifactornotstayput
		{
          ai AITROOPJETPACK
		  ifvarand initflags 16 move TROOPJETUPVELS seekplayer
		}
      break
    }
    else
      ifrnd 2
    {
      ifspritepal 21
        ifg targetdist 1596
		ifn redcarrier THISACTOR ifn bluecarrier THISACTOR
      {
        ai AITROOPHIDE
        break
      }
      ifbulletnear
      {
        ifrnd 128
		{
          ai AITROOPDODGE
		  set dodgetime 8
		}
        else
          ai AITROOPDUCKING
        break
      }
	  ifceilingdistl 128 nullop
      else
      ifrnd 2
      ifvarand initflags 16
      ifactornotstayput
      {
      	ai AITROOPJETPACK
      	ifvarand initflags 16 move TROOPJETUPVELS seekplayer
  	  }
    }
  }
  ifnotmoving
  {
    ifrnd 32
      operate
    else
      ifcount 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
              ai AITROOPSHOOTING
	ifai AITROOPSHOOTING nullop else
	ifg crumbwait 20
	{
		ai AITROOPJETPACK
		  move TROOPJETUPVELS geth
		set crumbwait 0
	}
  }
  
  ife shrunken 0
  ifrnd 1
  {
    ifrnd 128
      soundonce PRED_ROAM
    else
      soundonce PRED_ROAM2
  }

ends

state troopduckstate
  ifaction ATROOPDUCK
  {
    ifactioncount 8
    {
      ife targetalive YES
      {
        ifrnd 128
          action ATROOPDUCKSHOOT
      }
      else
        ifmove DONTGETUP
          break
        else
          ai AITROOPSEEKPLAYER
    }
  }
  else
    ifaction ATROOPDUCKSHOOT
  {
    ifcount 64
    {
      ifmove DONTGETUP
        resetcount
      else
      {
        ifl targetdist 1100
		{
          ai AITROOPFLEEING
		  set dodgetime 8
		}
        else
          ai AITROOPSEEKPLAYER
      }
    }
    else
      ifactioncount 2
      {
        ife shootmytarget YES
        {
			
		  ifvarand initflags 16
			state shootBIGFIRELASER
		  else ifspritepal 21
			state shootBIGFIRELASER
		  else
		  {  
			sound PRED_ATTACK
			ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK 1560
			shoot FIRELASER 
		  }
		  resetactioncount
			
        }
        else ai AITROOPSEEKPLAYER
      }
  }
ends

state troopshootstate
  ifactioncount 2
  {
    ife shootmytarget YES
    {
      ifvarand initflags 16
		state shootBIGFIRELASER
	  else ifspritepal 21
		state shootBIGFIRELASER
	  else
	  {
	    shoot FIRELASER
	    sound PRED_ATTACK
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK 1560
      }
      resetactioncount
      ifrnd 128
        ai AITROOPSEEKPLAYER
      ifcount 24
      {
        ifrnd 96
          ifg targetdist 2048
            ai AITROOPSEEKPLAYER
          else
          {
            ifg targetdist 1596
			{
              ai AITROOPFLEEING
			  set dodgetime 8
			}
            else
              ai AITROOPFLEEINGBACK
          }
      }
    }
    else
      ai AITROOPSEEKPLAYER
  }
ends

state troopfleestate
  ifl dodgetime 2 set dodgetime 2
  ifactioncount 7
  {
    ifg targetdist 3084
    {
      ai AITROOPSEEKPLAYER
      break
    }
    else
      ifrnd 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
      {
	    set dodgetime 0
        ifrnd 128
          ai AITROOPDUCKING
        else
          ai AITROOPSHOOTING
          break
      }
  }
  ifnotmoving
  {
    ifrnd 32
      operate
    else
      ifcount 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
      {
	    set dodgetime 0
        ifrnd 128
          ai AITROOPSHOOTING
        else
          ai AITROOPDUCKING
      }
  }
ends

state troopdying
  set damflash 0
  iffloordistl 32
  {
    ifaction ATROOPDYING2FALL action ATROOPDYING2
    ifactioncount 5
    {
	  ifvarand sprite[].cstat 4 cstat 4 else
      cstat 0
      iffloordistl 8
      sound THUD
      ifrnd 64
        spawn BLOODPOOL2
      ifaction ATROOPDYING2 nullop else state rf
      strength 0
      move STOPPED geth
	  ifaction ATROOPDECAP action ATROOPDECAPDEAD else
	  ifaction ATROOPDYING2
	  {
		action ATROOPDEAD2 
		ife shrunken 0
		{
		geta[].yrepeat y
		ife upscaled YES sub y 2 else
		sub y 4
		seta[].yrepeat y
		}
	  }
	  else
	  ifaction ATROOPDYING3
		action ATROOPDEAD3
	  else
      action ATROOPDEAD
    }
    break
  }
  else
  {
    move 0
    ifaction ATROOPDYING2 action ATROOPDYING2FALL
	else ifaction ATROOPDYING2FALL nullop
	else
	{
		state rf
		ifai AITROOPDECAP action ATROOPDECAP else
		ifaction ATROOPDYING3 action ATROOPDYING3 else
		action ATROOPDYING
	}
  }
ends

state troopjetpackstate

  
  ifaction ATROOPJETPACKILL
  {
    ife seemytarget YES
      ifactioncount 2
    {
      resetactioncount  
      ifvarand initflags 16
	  	state shootBIGFIRELASER
	  else ifspritepal 21
	    state shootBIGFIRELASER
	  else
	  {
	      sound PRED_ATTACK
		  ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK 1560
	      shoot FIRELASER
      }
    }

    ife targethigher YES
	{
      ai AITROOPJETPACK
	  ifvarand initflags 16 move TROOPJETUPVELS seekplayer
	}
    else
      ifinwater
        ai AITROOPJETPACK
    else
	{
      ifcount 26
        iffloordistl 32
          ai AITROOPSEEKPLAYER
	  ifcount 90
	  {
	    ife seemytarget YES nullop else
		{
			ai AITROOPJETPACK
			move TROOPJETDOWN seekplayer getv
		}
	  }
	}
  }
  else
    ifcount 48
  {
      ife seemytarget YES
	  {
		action ATROOPJETPACKILL
		move TROOPJETPACKILLVELS seekplayer
	  }
	  else
	  	  ifcount 150
	  {
		  ifmove TROOPJETDOWN nullop
		  else move TROOPJETDOWN seekplayer getv
	  }
  }
  getactor[].ang angvar
  ifvarand initflags 16
  {
	  ifbulletnear 
	  ifactornotstayput
	  {
		  set dodgetime 8
		  ai AITROOPBOOST
		  ifrnd 128 addvar angvar 256 else subvar angvar 256
		  seta[].ang angvar
		  count 10
	  }
  }
ends

state troopsufferingstate
  ifactioncount 2
  {
    ifrnd 16
      spawn WATERDRIP
    ifactioncount 14
    {
      stopsound LIZARD_BEG
      cstat 0
      strength 0
      action ATROOPSUFFERDEAD
      break
    }
  }
ends

state troopshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AITROOPSEEKENEMY
  else
    ifcount SHRUNKCOUNT
      sizeto 40 40
  else
    state genericshrunkcode
ends

state troopdeadstate
  
    strength 0
    // ifrespawn ife gametype 0
      // ifcount RESPAWNACTORTIME
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength TROOPSTRENGTH
      // ai AITROOPSEEKENEMY
    // }
    ifhitweapon
    {
      ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
		ifaction ATROOPDECAPDEAD
		{
			guts LEGJIB1 2
			guts ARMJIB1 1
		  ifrnd 48
		  ife shrunken 0
			spawn BLOODPOOL2
		}
		else
        state troop_body_jibs
        state lite_jibs
        killit
      }
      break
    }
    else
      state checksquished
 ends


state troopcode fall

	

 state monsterai 
  ifinwater
    ifrnd 1
      spawn WATERBUBBLE
  ifaction ATROOPSTAND
  {
    ifrnd 192
      ai AITROOPSHOOTING
    else
      ai AITROOPSEEKPLAYER
  }
  else
    ifaction ATROOPFROZEN
  {
    ifcount THAWTIME
    {
      ai AITROOPSEEKENEMY
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  ife gametype 0
	  {
		  ifvarand initflags 16 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		  ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
			state drop_ammo
	  }

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 10
      state troop_body_jibs
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else ifaction ATROOPPLAYDEAD
  {
    ifhitweapon
    {
      ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state troop_body_jibs
        state lite_jibs
        killit
      }
      break
    }
    else
      state checksquished

    ifcount PLAYDEADTIME
    ifg targetdist 2560
    ife seemytarget YES
    {
      addkills -1 set monstatus 1
      soundonce PRED_PAIN
	  ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_PAIN 1560
      cstat 257
      // strength 1
      // ai AITROOPSHOOTING
      getactor[].xrepeat x
      subvar x 4
      seta[].xrepeat x
      cactor ZOMBIETROOP
      strength 20
      move 0
      action AZOMBIEUP
	  set monxp 100
    }
    // else
      // ifp pfacing
        // resetcount

    break
  }
  else
    ifaction ATROOPDEAD
	{
     state troopdeadstate
	 break
	}
  else
    ifaction ATROOPDECAPDEAD
	{
	 state troopdeadstate
	 break
	}
  else
    ifaction ATROOPDEAD2
	{
	 state troopdeadstate
	 break
	}
  else
    ifaction ATROOPDEAD3
	{
	 state troopdeadstate
	 break
	}
  else
    ifaction ATROOPSUFFERDEAD
  {
    ifactioncount 2
    {
      ifrnd 64
	  ife gametype 0
      {
        resetcount
        action ATROOPPLAYDEAD
      }
      else
      {
        soundonce PRED_DYING
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_DYING 1560
        action ATROOPDEAD
      }
    }
  }
  else
    ifaction ATROOPDYING
  {
    state troopdying
    break
  }
  else
    ifaction ATROOPDYING2
  {
    state troopdying
	break
  }
  else
    ifaction ATROOPDYING3
  {
    state troopdying
	break
  }
  else
    ifaction ATROOPDYING2FALL
  {
    state troopdying
	break
  }
  else
    ifai AITROOPDECAP
  {
    state troopdying
	break
  }
  else
    ifaction ATROOPSUFFERING
  {
    state troopsufferingstate
    ifhitweapon
      state checktroophit
    break
  }
  else
    ifaction ATROOPFLINTCH
  {
    move STOPPED geth
    ifvarg stun 0 resetactioncount
    ifactioncount 4
      ai AITROOPSEEKENEMY
  }
  else
  {
    ifai AITROOPSEEKPLAYER
      state troopseekstate
    else
      ifai AITROOPJETPACK
    {
      state troopjetpackstate
      ifinwater nullop
      else
	  {  
        soundonce DUKE_JETPACK_IDLE
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DUKE_JETPACK_IDLE 1560
	  }
    }
    else
      ifai AITROOPSEEKENEMY
        state troopseekstate
    else
      ifai AITROOPSHOOTING
        state troopshootstate
    else
      ifai AITROOPFLEEING
        state troopfleestate
    else
      ifai AITROOPFLEEINGBACK
        state troopfleestate
    else
      ifai AITROOPDODGE
        state troopseekstate
    else
      ifai AITROOPDUCKING
        state troopduckstate
    else
	  ifai AITROOPBOOST
        state troopbooststate
	else
      // ifai AITROOPSHRUNK
        // state troopshrunkstate
      ifai AITROOPGROW
	  {
        state genericgrowcode
		break
	  }
    else
      ifai AITROOPHIDE
    {
      state troophidestate
      break
    }
  }

  ife monstatus -1 break
  ifhitweapon
    state checktroophit
  else
    state checksquished
ends

state checktrooppalette
  ifai 0
  {
    ifspritepal 0
        nullop
    else
        ifspritepal 21
          addstrength TROOPSTRENGTH // Double the hitpoint vals
  }
ends


actor LIZTROOPJETPACK TROOPSTRENGTH
  state checktrooppalette
  ai AITROOPJETPACK
  ifvarand initflags 16 move TROOPJETUPVELS seekplayer
  cactor LIZTROOP
enda

actor LIZTROOPDUCKING TROOPSTRENGTH
  state checktrooppalette
  ai AITROOPDUCKING
  cactor LIZTROOP
  ifgapzl 48
    move DONTGETUP
enda

actor LIZTROOPSHOOT TROOPSTRENGTH ATROOPSTAND
  state checktrooppalette
  ai AITROOPSHOOTING
  cactor LIZTROOP
enda
actor LIZTROOPSTAYPUT TROOPSTRENGTH ATROOPSTAYSTAND
  state checktrooppalette
  ai AITROOPSEEKPLAYER
  cactor LIZTROOP
enda
actor LIZTROOPRUNNING TROOPSTRENGTH ATROOPSTAND
  state checktrooppalette
  ai AITROOPSEEKPLAYER
  cactor LIZTROOP
enda
actor LIZTROOPONTOILET TROOPSTRENGTH
  ifcount 24
  {
    sound FLUSH_TOILET
	ifg sprite[].htextra sprite[].extra spawn FECES
    operate
    ai AITROOPSEEKPLAYER
    cactor LIZTROOP
  }
  else
    ifcount 2
      nullop
  else
    state checktrooppalette
	
	ifg sprite[].htextra 0 count 40
enda

actor LIZTROOPJUSTSIT TROOPSTRENGTH
  ifcount 30
  {
    operate
    ai AITROOPSEEKPLAYER
    cactor LIZTROOP
  }
  else
    ifcount 2
      nullop
  else
    state checktrooppalette
	
	ifg sprite[].htextra 0 count 40
enda

actor LIZTROOP TROOPSTRENGTH ATROOPSTAND
  state checktrooppalette
  state troopcode
enda

action ALIZROLL		-25  5  5  1  12
action ALIZSTAND     0  1  5  1  10
action ALIZWALKING     0   4  5  1  15
action ALIZRUNNING     0   3  5  1  11 // done by LIZMANDUCKING
action ALIZTHINK     20  2  5  1  40
action ALIZSCREAM    30  1  5  1  2
action ALIZEAT		 35  2  5  1  14
action ALIZJUMP      45  3  5  1  20
action ALIZFALL      55  1  5

action ALIZSHOOTING    70  2  5  1  7
action ALIZDYING     60  6  1  1  15
action ALIZLYINGDEAD   65  1
action ALIZFROZEN     0  1  5
action ALIZFLINTCH 16732 1 5 1 10  // 60
action ALIZDECAP  4451  6  1  1  15
action ALIZDECAPDEAD 4456  1
action ALIZDYING2	8661  6  1  1  22
action ALIZLYINGDEAD2 8666 1

move LIZWALKVEL 96
move LIZRUNVEL 208
move LIZJUMPVEL 184
move KNOCKBACKVEL -320

ai AILIZGETENEMY ALIZWALKING LIZWALKVEL seekplayer
ai AILIZDODGE ALIZRUNNING LIZRUNVEL dodgebullet
ai AILIZCHARGEENEMY ALIZRUNNING LIZRUNVEL seekplayer
ai AILIZFLEENEMY ALIZWALKING LIZWALKVEL fleeenemy
ai AILIZSHOOTENEMY ALIZSHOOTING STOPPED faceplayer
ai AILIZJUMPENEMY ALIZJUMP LIZJUMPVEL jumptoplayer
ai AILIZTHINK ALIZTHINK STOPPED faceplayerslow
// ai AILIZSHRUNK ALIZWALKING SHRUNKVELS fleeenemy
ai AILIZGROW ALIZSTAND  STOPPED faceplayerslow
ai AILIZSPIT ALIZSCREAM STOPPED faceplayerslow
ai AILIZHIT ALIZFLINTCH STOPPED geth
ai AILIZDYING ALIZDYING STOPPED geth
ai AILIZROLL ALIZROLL LIZRUNVEL geth
ai AILIZEAT ALIZEAT STOPPED geth


state lizstartroll

	state incoming_eval
		
	set dodgetime 12
	set crumbwait 10
	cactor LIZMANDUCKING
	ai AILIZROLL
	seta[].ang angvel
	
	set myspawner -1
	
ends

defstate lizseekstate

	ifge SKILL 4
	{
		ifcount 42 nullop else count 42
	}

  ifactornotstayput
  {
    ife seemytarget YES
      ife targetalive YES
        ifl targetdist 2048
          ifcount 16
            ife shootmytarget YES
    {
	  cactor LIZMAN
      ai AILIZSHOOTENEMY
      break
    }
	ifn myspawner -1
	{
		state lizstartroll
		break
	}
    ifai AILIZCHARGEENEMY
    {
	  ifg myvictim -1
	  {
		ife sprite[myvictim].statnum 1024 set myvictim -1
		else
		ifn actorvar[myvictim].monstatus 2 set myvictim -1
		
		ifg myvictim -1
		{
		  ifn dodgetime 0
		  ife actorvar[myvictim].monstatus 2
		  {
			set dodgetime 3
			set spriteid myvictim
			state facesprite
			
			dist temp THISACTOR myvictim
			ifl temp 644 { cactor LIZMAN ai AILIZEAT break }
			
			canseespr THISACTOR spriteid temp
			ife temp NO 
			{
				set myvictim -1 
				cactor LIZMAN
				ai AILIZJUMPENEMY
				move LIZJUMPVEL jumptoplayer randomangle
			}
			else
			ifnotmoving 
			{
				set myvictim -1 
				cactor LIZMAN
				ai AILIZJUMPENEMY
				move LIZJUMPVEL jumptoplayer randomangle
        
			}
			else
			break
		  }
		  else set myvictim -1
		}
	  }
      ifcount 72
	  {
		  ife shootmytarget YES
		  ifrnd 32
		  {
			cactor LIZMAN
			ai AILIZSHOOTENEMY
			break
		  }
	  }
	  
      ife targethigher YES
        ifg targetdist 2048
          ifrnd 6
      {
	    cactor LIZMAN
        ai AILIZJUMPENEMY
        break
      }
    }
    else
      ifg targetdist 4096
    {
      ifrnd 92
      {
        ifcount 48
          ife shootmytarget YES
		  {
            ai AILIZSHOOTENEMY
			break
		  }
      }
      else
        ifcount 24
      {
	    cactor LIZMANDUCKING
        ai AILIZCHARGEENEMY
        break
      }
    }
	
	set temp inithp
	mul temp 3 div temp 2
	ifg targetdist 3328
	ife gametype 0
	ifl sprite[].extra temp // inithp
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 2
			ifn spriteid myshelly
			ifge sprite[spriteid].z sprite[].z
			{
				ldist temp THISACTOR spriteid
				ifl temp 8192
				{
					canseespr THISACTOR spriteid temp
					ife temp YES
					{
						ife dodgetime 0 count 10
						set myvictim spriteid
						set mtype sprite[myvictim].picnum
						set dodgetime 5
						set bottarget -1
						ifai AILIZCHARGEENEMY nullop else
						{
							ai AILIZCHARGEENEMY
							cactor LIZMANDUCKING
							break
						}
						
					}
				}
			}
			nextspritestat spriteid spriteid
		}
	  
	}

    iffloordistl 16
    {
      ifcount 48
        ifnotmoving
          ife seemytarget YES
      {
	    cactor LIZMAN
        ai AILIZJUMPENEMY
        break
      }
    }
    else
    {
      ifg targetdist 1280
	  {
        ai AILIZJUMPENEMY
		cactor LIZMAN
	  }
      break
    }

    ifrnd 4
      ifnotmoving
        operate
    else
      ifrnd 1
        ifbulletnear
    {
      ifgapzl 128
	  {
	    cactor LIZMANDUCKING
        ai AILIZDODGE
		set dodgetime 8
	  }
      else
        ifactornotstayput
      {
        ifrnd 32
		{
		  cactor LIZMAN
          ai AILIZJUMPENEMY
		}
        else
		{
		  cactor LIZMANDUCKING
          ai AILIZDODGE
		  set dodgetime 8
		}
      }
    }
  }
  else
  {
    ifactioncount 16
    {
      ife targetalive YES
        ifrnd 32
          ife seemytarget YES
            ife shootmytarget YES
              ai AILIZSHOOTENEMY
    }
    ifcount 16
      ifrnd 32
        move LIZWALKVEL randomangle geth
  }
  
  ifai AILIZSHOOTENEMY nullop else
  ifg crumbwait 20 { set crumbwait 0 cactor LIZMAN ai AILIZJUMPENEMY }

ends

state lizshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AILIZGETENEMY
  else
    ifcount SHRUNKCOUNT
	  sizeto 28 26
  else
    state genericshrunkcode
ends

state lizfleestate
 
  ifl dodgetime 2 set dodgetime 2
  ifcount 16
  {
    ifrnd 48
      ife targetalive YES
        ife seemytarget YES
		{
          ai AILIZSPIT
		  set dodgetime 0
		  ifn bottarget -1 { set spriteid bottarget state facesprite }
		}
  }
  else
  {
    iffloordistl 16 nullop
    else
      ai AILIZGETENEMY
    break
  }
ends

state lizthinkstate
	ifn myspawner -1
	{
		state lizstartroll
		break
	}
  ifrnd 8
  {
    soundonce CAPT_ROAM
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR CAPT_ROAM 1560
  }
  ifactioncount 3
  {
    ifrnd 32
      ife targetalive YES
        ife seemytarget YES
		{
          ai AILIZSPIT
		  ifn bottarget -1 { set spriteid bottarget state facesprite }
		}
        else
          ifrnd 96
            ai AILIZGETENEMY
  }
  else
    ifactioncount 2
      ifrnd 1
        spawn FECES
  ifrnd 2
    ifbulletnear
  {
    ifgapzl 96
	{
	  cactor LIZMANDUCKING
      ai AILIZDODGE
	  set dodgetime 8
	}
    else
    {
      ifrnd 128
	  {
        ai AILIZJUMPENEMY
		cactor LIZMAN
	  }
      else
	  {
	    cactor LIZMANDUCKING
        ai AILIZDODGE
		set dodgetime 8
	  }
    }
  }
ends

state lizshootstate

ifcount 2 nullop else
ifvarand initflags 8 ife countvar 0
ife shrunken 0
ifl targetdist 8192 
{ 
	ai AILIZSPIT 
	ifn bottarget -1 { set spriteid bottarget state facesprite } 
	break
}

  ifcount 20
    ifrnd 8
  {
    ife seemytarget YES
      ifl targetdist 2048
    {
      ifrnd 128
	  {
        ai AILIZFLEENEMY
		set dodgetime 8
	  }
      break
    }
    ifrnd 80
      ai AILIZTHINK
    else
      ai AILIZGETENEMY
  }
  ifactioncount 2
  {
    ife seemytarget YES
    {
      ife shootmytarget YES
      {
	    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			ifvarand initflags 8 sound FLAMER_SHOOT else sound CAPT_ATTACK
			ifactorsound THISACTOR FLAMER_SHOOT setactorsoundpitch THISACTOR FLAMER_SHOOT SHRUNKPITCH
			ifactorsound THISACTOR CAPT_ATTACK setactorsoundpitch THISACTOR CAPT_ATTACK SHRUNKPITCH
			shoot SHRUNKBULLET
		}
		else
		{
			ifvarand initflags 32 
			{ 
				geta[].z savz sub savz 1560 seta[].z savz
				sound TERMLASER 
				setprojectile[GREENLASER].extra 12
				shoot GREENLASER 
				setprojectile[GREENLASER].extra 16
				add savz 1560 seta[].z savz
			} 
			else
			ifvarand initflags 8 { sound FLAMER_SHOOT shoot FIREBOLT }
			else 
			{
				sound CAPT_ATTACK 
				ifn bottarget -1
				{
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
				}
				else
				shoot SHOTSPARK1 
			}
		}
        resetactioncount
      }
      else
        ai AILIZTHINK
    }
    else
      ai AILIZGETENEMY
  }
ends

state checklizhit

// ifn sprite[].htpicnum FREEZEBLAST
// ifn sprite[].htpicnum SHRINKSPARK
// ife shrunken 0
  // spawn BLOOD
  
  // ifai AILIZSHRUNK
  // {
    // addkills 1 state enemy_death
    // sound SQUISHED
    // state standard_jibs
    // killit
  // }
  ifdead
  {
    cactor LIZMAN
    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action ALIZFROZEN
      strength 0
	  state freezeme
      break
    }

    state drop_chaingun

    ifwasweapon GROWSPARK
    {
      cstat 0
      sound ACTOR_GROWING
      ai AILIZGROW
      break
    }

    addkills 1 state enemy_death

    ifwasweapon RPG
    {
      sound SQUISHED
      state liz_body_jibs
      state lite_jibs
      killit
    }
    else
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      state liz_body_jibs
      state lite_jibs
      killit
    }
    else
    {
      state rf
      ai AILIZDYING
	  
      ifrnd 64
        spawn BLOODPOOL2
    }
    sound CAPT_DYING
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR CAPT_DYING 1560
	ifwasweapon SABERPROJ
	{
	    espawn LIZMANSABERDEATH
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].pal sprite[].pal
		killit
	}
	  ifrnd 96
	  {
		  action ALIZDYING2
		  sound LIZARD_BEG
	  }
  }
  else
  {
    ifactorsound THISACTOR CAPT_PAIN nullop else
	{
		sound CAPT_PAIN
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR CAPT_PAIN 1560
	}
    ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      // ai AILIZSHRUNK
	  set shrunken 1
      break
    }
    ifwasweapon GROWSPARK
      sound SPARKLESOUND

    state random_wall_jibs
	ifai AILIZHIT nullop else
    ifrnd 32 { ai AILIZHIT cactor LIZMAN }
    else
    ifvarg stun 0
		{ ai AILIZHIT cactor LIZMAN }
    else
    ife targetalive YES
      ife seemytarget YES
        ife shootmytarget YES
    {
      ai AILIZSHOOTENEMY
	  cactor LIZMAN
      break
    }
  }
ends

state lizjumpstate
  ifaction ALIZFALL
  {
    iffloordistl 16
      ai AILIZGETENEMY
  }
  else
    ifactioncount 3
      action ALIZFALL
ends

state lizdyingstate
  set temp NO
  ifaction ALIZLYINGDEAD set temp YES
  ifaction ALIZLYINGDEAD2 set temp YES
  ife temp YES
  {
    strength 0
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
	  state liz_body_jibs
	  state lite_jibs
      killit
    }
    // ifcount RESPAWNACTORTIME
      // ifrespawn ife gametype 0
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength LIZSTRENGTH
      // ai AILIZGETENEMY
    // }
  }
  else
  ifaction ALIZDECAPDEAD
  {
    strength 0
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      guts LIZMANLEG1 2
	  guts LIZMANARM1 1
	  ifrnd 48
	  ife shrunken 0
		spawn BLOODPOOL2
	  state lite_jibs
      killit
    }
    // ifcount RESPAWNACTORTIME
      // ifrespawn ife gametype 0
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength LIZSTRENGTH
      // ai AILIZGETENEMY
    // }
  }
  else
    ifaction ALIZDYING
      ifactioncount 6
  {
    iffloordistl 8
      sound THUD
    move STOPPED
    action ALIZLYINGDEAD
  }
  else
  ifaction ALIZDYING2
  {
	ifactioncount 6
	{
		iffloordistl 8
		  sound THUD
		move STOPPED
		action ALIZLYINGDEAD2
		stopactorsound THISACTOR LIZARD_BEG
	}
  }
    ifaction ALIZDECAP
	  ifactioncount 6
  {
    iffloordistl 8
      sound THUD
    move STOPPED
    action ALIZDECAPDEAD
  }
ends

state lizeatstate

	ife myvictim -1 { ai AILIZGETENEMY break }
	ifg myvictim -1
	{
		ifn sprite[myvictim].statnum 1 set myvictim -1 else
		ifn actorvar[myvictim].monstatus 2 set myvictim -1 else
		ifn sprite[myvictim].picnum mtype set myvictim -1 else
		{
			dist temp THISACTOR myvictim
			ifg temp 1024 set myvictim -1
		}
		ifg myvictim -1
		{
			ifactioncount 2
			{
				resetactioncount

				set temp inithp
				div temp 10
				geta[].extra tempb
				add tempb temp
				seta[].extra tempb
				soundonce LIZEATSND
			}
			set dodgetime 3
			set spriteid myvictim
			state facesprite
		}
	
	}
	ifg sprite[].htextra 0
	ifn bottarget -1
	{
		ai AILIZSHOOTENEMY
		set myvictim -1
		ifactorsound THISACTOR LIZEATSND stopactorsound THISACTOR LIZEATSND
		break
	}
	set tempb inithp
	mul tempb 3 div tempb 2
	
	ifge sprite[].extra tempb // inithp 
	{ 
		ai AILIZGETENEMY 
		ifactorsound THISACTOR LIZEATSND stopactorsound THISACTOR LIZEATSND
		ifg myvictim -1
		{
			seta[myvictim].htextra 10
			seta[myvictim].htpicnum RADIUSEXPLOSION
			seta[myvictim].htowner player[].i
			set myvictim -1
		}
		break 
	}
	
ends

state lizdodgestate
  ifl dodgetime 2 set dodgetime 2
  ifcount 13
  {
    cactor LIZMAN
    ai AILIZGETENEMY
  }
ends

state lizrollstate
	ifl dodgetime 2 set dodgetime 2
	ifactioncount 5
	{
		cactor LIZMAN
		ifn target -1 ifrnd 160
		ai AILIZSHOOTENEMY else
		ai AILIZGETENEMY
	}
	ifactioncount 4 ifnotmoving
	{
		cactor LIZMAN
		ifn target -1 ifrnd 160
		ai AILIZSHOOTENEMY else
		ai AILIZGETENEMY
	}
ends

useractor enemystayput LIZMANSTAYPUT LIZSTRENGTH sizeat 26 24 ai AILIZSHOOTENEMY cactor LIZMAN enda
actor LIZMANSPITTING LIZSTRENGTH sizeat 26 24 ai AILIZSPIT cactor LIZMAN enda
actor LIZMANJUMP LIZSTRENGTH sizeat 26 24 ai AILIZJUMPENEMY cactor LIZMAN enda

defstate wakeupguards

headspritestat spriteid 1
whilevarn spriteid -1
{
	ife actorvar[spriteid].monstatus -2
	{
		ldist xydist THISACTOR spriteid
		ifl xydist 6144
		ife sprite[spriteid].htextra -1 seta[spriteid].htextra 0
	}
	nextspritestat spriteid spriteid
}

ends

useractor enemy LIZMANGUARD LIZSTRENGTH AENEMYGUARD

	cstator 257
	sizeat 28 26
	fall
	ifpdistl 20480 ifcansee ifg ptargeted 0 seta[].htextra 0
	set monstatus -2
	ifg sprite[].htextra -1
	{
		cactor LIZMAN
		sound CAPT_RECOG
		state wakeupguards
	}

enda

defstate lizcode

  state monsterai 
  state checksquished
  
  ifai 0
  {

	sizeat 28 26
    ifspritepal 42 ife seemytarget YES ifl targetdist 8192 
	{
		ai AILIZSPIT
		ifn bottarget -1 { set spriteid bottarget state facesprite }
	}
	else
    ai AILIZGETENEMY
  }
  else
    ifaction ALIZLYINGDEAD
  {
    fall
    state lizdyingstate
    break
  }
  else
  ifaction ALIZLYINGDEAD2
  {
    fall
    state lizdyingstate
    break
  }
  else
    ifaction ALIZDECAPDEAD
  {
    fall
	state lizdyingstate
	break
  }
  else
    ifaction ALIZFROZEN
  {
    ifcount THAWTIME
    {
      ai AILIZGETENEMY
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 10
      state liz_body_jibs
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else
    ifai AILIZJUMPENEMY
      state lizjumpstate
  else
  {
    fall
    ifai AILIZGETENEMY
      state lizseekstate
    else
      ifai AILIZCHARGEENEMY
        state lizseekstate
    else
	  ifai AILIZEAT
	    state lizeatstate
    else
      ifai AILIZDODGE
        state lizdodgestate
	else
	  ifai AILIZROLL
	    state lizrollstate
    else
      ifai AILIZSHOOTENEMY
        state lizshootstate
    else
      ifai AILIZFLEENEMY
        state lizfleestate
    else
      ifai AILIZTHINK
        state lizthinkstate
    else
      // ifai AILIZSHRUNK
        // state lizshrunkstate
    // else
      ifai AILIZGROW
        state genericgrowcode
    else
      ifai AILIZDYING
        state lizdyingstate
	else
        ifai AILIZHIT
    {
	  ifvarg stun 0 
	  {
		resetactioncount
		ifmove STOPPED ifwasweapon KNEE move KNOCKBACKVEL geth
		ifmove KNOCKBACKVEL ifcount 10 { move STOPPED seta[].htpicnum SHOTSPARK1 }
	  }
	  ifactioncount 3 
	  {
		  ifrnd 128
		  {
		  	ai AILIZSHOOTENEMY 
		  	ifvarand initflags 8 ifrnd 128 move STOPPED faceplayersmart
	  	  }
	  	else
		  ai AILIZTHINK  
	  }
    }
    else
      ifai AILIZSPIT
    {
      ifcount 26
        ai AILIZGETENEMY
      else
        ifcount 18
          ifrnd 96
      {
        shoot SPIT
        sound LIZARD_SPIT
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR LIZARD_SPIT 1560
		add countvar 1
      }
    }
  }

  // ifai AILIZSHRUNK
    // break

	ife monstatus -1 break
  ifhitweapon
  {
    ifwasweapon HEADJIB1
	{
		// sound CAPT_DYING
		cactor LIZMAN
		sound SQUISHED
		state enemy_death
		state drop_chaingun
		state random_wall_jibs
		addkills 1
		strength 0
		ai AILIZDYING
		action ALIZDECAP
		// guts LIZMANHEAD1 1
		rand zdist 1024
	    sub zdist 3500
	    ezshoot zdist LIZHEADPROJ
		ife upscaled YES { seta[RETURN].xrepeat init_xrepeat seta[RETURN].yrepeat init_yrepeat }
	    rand angvar 512
	    sub angvar 256
	    add angvar sprite[].htang
	    seta[RETURN].ang angvar
		seta[RETURN].pal sprite[].pal
		cstat 0
	}
	else
    state checklizhit
  }
  else ife monstatus 1
  ifvarg stun 0
		{ ai AILIZHIT cactor LIZMAN }
ends

actor LIZMAN LIZSTRENGTH
  fall
  state lizcode
enda

useractor enemy LIZMANDUCKING LIZSTRENGTH
  fall
  state lizcode

enda

action DRONEFRAMES   0   1   7   1   1
action DRONESCREAM   0   1   7   1   1
move DRONERUNVELS 128 64
move DRONERUNUPVELS 128 -64
move DRONEBULLVELS 252 -64
move DRONEFASTVELS 512 -12
move DRONEBACKWARDS -64 -64
move DRONERISE 32 -32
move DRONESTOPPED -16

ai AIDRONEGETE DRONESCREAM DRONERUNVELS faceplayerslow getv
ai AIDRONEWAIT DRONEFRAMES DRONESTOPPED faceplayerslow
ai AIDRONEGETUP DRONESCREAM DRONERUNUPVELS faceplayer getv
ai AIDRONEPULLBACK DRONEFRAMES DRONEBACKWARDS faceplayerslow
ai AIDRONEHIT DRONESCREAM DRONEBACKWARDS faceplayer
// ai AIDRONESHRUNK DRONEFRAMES SHRUNKVELS fleeenemy
ai AIDRONEDODGE DRONEFRAMES DRONEBULLVELS dodgebullet geth
ai AIDRONEDODGEUP DRONEFRAMES DRONERISE getv geth

state dronedead
  addkills 1 state enemy_death
  debris SCRAP1 6
  debris SCRAP2 3
  debris SCRAP3 5
  spawn EXPLOSION2
  state tech_debris
  
  sound RPG_EXPLODE
  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR RPG_EXPLODE SHRUNKPITCH
  
  
  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  hitradius 844 2 2 2 2
  else
  {
    ife inithp 0 hitradius 2048 15 20 25 30
	else
	{
		set temp inithp
		ife mtype 1 shiftl temp 3
		ifg temp 420 set temp 420
		set B temp
		sub temp 20
		div temp 4
		
		set tempb B, div tempb 5
		set tempc B, div tempc 6
		set tempd B, div tempd 7
		hitradiusvar 2048 tempd tempc tempb temp
	}
	ifpdistl 2300 seta[player[].i].htowner THISACTOR
	
  }
  killit
ends

state checkdronehitstate

  ifdead state dronedead
  else ifsquished state dronedead
  else
  {
    ifwasweapon SHRINKSPARK { set shrunken 1 sound ACTOR_SHRINKING }
	else
	{
    sound DRON_PAIN
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DRON_PAIN SHRUNKPITCH
	}
    ifbulletnear
    {
      ifceilingdistl 64
        ifrnd 48
		{
          ai AIDRONEDODGE
		  set dodgetime 8
		}
      ai AIDRONEDODGEUP
    }
    else
      ai AIDRONEGETE
  }
ends

state droneshrunkstate
  ifcount 24
    killit
  else
    sizeto 1 1
ends

state checkdronenearplayer

ifn myspawner -1 break
ifg shrunken 10 ifl shrunken SHRUNKCOUNT break

ifn bottarget -1
{
	ifl targetdist 1596
	{
		ifcount 8
		{
		  state dronedead
		}
		else
		  ifcount 3 nullop
		  else
			sound LASERTRIP_ARMING
	}
}

  ife targetalive YES
    ifl targetdist 1596
	  ife seemytarget YES
  {
    ifcount 8
    {
	  state dronedead
    }
    else
      ifcount 3 nullop
      else
        sound LASERTRIP_ARMING
  }

ends

state dronefaststate

      ifmove DRONEFASTVELS
      {
        ifcount 90
          ai AIDRONEPULLBACK
        else
          ifnotmoving
            ifcount 24
              ai AIDRONEPULLBACK
      }
      else
        ifcount 16
      {
        ife targethigher YES
          move DRONEFASTVELS geth getv
        else
          move DRONEFASTVELS geth
      }

ends

state dronegetstate

ifvare mtype 1 { state dronefaststate break }
  ifrnd 192
  {
    ife seemytarget YES
    {
      ifbulletnear
      {
        ai AIDRONEDODGE
		set dodgetime 8
        break
      }
      ifmove DRONEBULLVELS
      {
        ifcount 64
          ai AIDRONEPULLBACK
        else
          ifnotmoving
            ifcount 16
              ai AIDRONEPULLBACK
      }
      else
        ifcount 32
      {
        ife targethigher YES
          move DRONEBULLVELS geth getv
        else
          move DRONEBULLVELS geth
      }
    }
    else
      ifrnd 1
        operate
  }
ends

state dronedodgestate
  ifl dodgetime 2 set dodgetime 2
  ifai AIDRONEDODGEUP
  {
     ifcount 8
       ai AIDRONEGETE
     else
       ifnotmoving
         ai AIDRONEGETE
  }
  else
  {
    ifcount 8
      ai AIDRONEGETE
    else
      ifnotmoving
        ai AIDRONEGETE
  }
ends

actor DRONE DRONESTRENGTH

  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  { state dronedead break }
  
  ifrnd 2
    fall
  else
  {
    soundonce DRON_JETSND
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DRON_JETSND SHRUNKPITCH
  }

  ifvare mtype 1
  {
	  addvar countvarb 1
	  ifvarg countvarb 1800
	  {
		  addkills 1 state enemy_death
	      sound DRON_ATTACK2
		  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DRON_ATTACK2 SHRUNKPITCH
	      debris SCRAP1 8
	      debris SCRAP2 4
	      debris SCRAP3 7
	      spawn EXPLOSION2
	      sound RPG_EXPLODE
		  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR RPG_EXPLODE SHRUNKPITCH
		  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		  hitradius 844 2 2 2 2
		  else
	      hitradius 2048 15 20 25 30
	      killit
	  }
  }
  ifaction 0
  {
     ai AIDRONEGETE
	 
     ifvare mtype 1 
     { 
		 set init_xrepeat 10 set init_yrepeat 10
	     sizeat 10 10 
		 strength 20 spritepal 2 
	     cstator 128
		 seta[].xvel 0
		 seta[].zvel 0
	     move DRONEFASTVELS geth faceplayer
	 }
	 else
     ifvarand initflags 8
     {
	     espawn ROTATEGUN
	     set myspawner RETURN
		 setav[RETURN].myspawner THISACTOR
		 seta[RETURN].pal sprite[].pal
	 }
   }
   state monsterai 
 
  
  ifai AIDRONEWAIT
  {
    ifactioncount 4
      ifrnd 16
        ife seemytarget YES
    {
      sound DRON_ATTACK1
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DRON_ATTACK1 SHRUNKPITCH
      ife targethigher YES
        ai AIDRONEGETUP
      else ai AIDRONEGETE
    }
	else ifn gametype 0 ifactioncount 4 ai AIDRONEGETE
  }
  else
    ifai AIDRONEGETE
      state dronegetstate
  else
    ifai AIDRONEGETUP
      state dronegetstate
  else
    ifai AIDRONEPULLBACK
  {
    ifcount 32
    ai AIDRONEWAIT
  }
  else
    ifai AIDRONEHIT
  {
    ifcount 8
      ai AIDRONEWAIT
  }
  // else
    // ifai AIDRONESHRUNK
      // state droneshrunkstate
  else
    ifai AIDRONEDODGE
      state dronedodgestate
  else
    ifai AIDRONEDODGEUP
      state dronedodgestate
      
  
  ifvare monstatus -1 break
  state checkdronenearplayer

  
  
  ifhitweapon
    state checkdronehitstate

  ifrnd 1
  {
    soundonce DRON_ROAM
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DRON_ROAM SHRUNKPITCH
  }

enda

action AOCTAWALK    0   4   5   1   15
action AOCTASTAND     0   1   5   1   15
action AOCTASCRATCH   5   4   5   1   15
action AOCTAHIT    17057   1   5   1   10  // 45
action AOCTASHOOT1    25   1   5   1   30
action AOCTASHOOT2    30   1   5   1   30
action AOCTANEWSHOOT1  35  1  5  1  50
action AOCTANEWSHOOT2  40  1  5  1  20
action AOCTADYING    51   7   1   1   17
action AOCTADEAD     57   1   1   1  1
action AOCTAFROZEN    0   1   5
action AOCTADYING2   45  6  1  1  18
action AOCTADEAD2    50  1  1  1  1

move OCTAFASTBITE 160
move OCTAFASTVELS 192 -25
move OCTAUPFASTVELS 192 -70
move OCTAWALKVELS 96 -25
move OCTAUPVELS 96 -65
move OCTASTOPPED 0 -30
move OCTAINWATER 96 24
move OCTAFASTWATER 192 24

ai AIOCTAGETENEMY AOCTAWALK OCTAWALKVELS seekplayer
ai AIOCTASHOOTENEMY AOCTASHOOT1 OCTASTOPPED faceplayer
ai AIOCTANEWSHOOT AOCTANEWSHOOT1 OCTASTOPPED faceplayer
ai AIOCTASCRATCHENEMY AOCTASCRATCH OCTASTOPPED faceplayerslow
ai AIOCTAHIT AOCTAHIT OCTASTOPPED faceplayer
// ai AIOCTASHRUNK AOCTAWALK SHRUNKVELS faceplayer
ai AIOCTAGROW AOCTASTAND OCTASTOPPED faceplayerslow
ai AIOCTADYING AOCTADYING OCTASTOPPED geth

state octagetenemystate

  ifinwater nullop else
  ifmove OCTAWALKVELS
  ifnotmoving ifcount 30
  {
	  move OCTAUPVELS seekplayer getv
  }
  
  ife targethigher YES nullop else
  ifmove OCTAUPVELS ifcount 90 ifrnd 8
  move OCTAWALKVELS seekplayer
  

 ifvarand initflags 16
 {
    ifmove OCTAFASTBITE ifcount 20 move OCTAFASTVELS seekplayer
	
	ife seemytarget YES
      ifl targetdist 1560
        ai AIOCTASCRATCHENEMY
		
	ifactioncount 32
    {
      ifrnd 48
        ife shootmytarget YES
      {
	    
        sound OCTA_ATTACK1
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_ATTACK1 SHRUNKPITCH
		ifge botclip 2 ifrnd 128
		{
			ai AIOCTANEWSHOOT 
			setactorsoundpitch THISACTOR OCTA_ATTACK1 512
		}
		else ai AIOCTASHOOTENEMY
		add botclip 1
        break
      }
    }
	
	ifcount 10
	{
	ifmove OCTAWALKVELS move OCTAFASTVELS seekplayer
	ifmove OCTAINWATER move OCTAFASTWATER seekplayer
	ifmove OCTAUPVELS move OCTAUPFASTVELS seekplayer
	}
	
	ifl targetdist 3072 { ifmove OCTAFASTBITE nullop else move OCTAFASTBITE faceplayer }
	
	break
 }

  ife seemytarget YES
  {
    ifactioncount 32
    {
      ifrnd 48
        ife shootmytarget YES
      {
        sound OCTA_ATTACK1
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_ATTACK1 SHRUNKPITCH
		ifge botclip 2 ifrnd 128
		{
			ai AIOCTANEWSHOOT 
			setactorsoundpitch THISACTOR OCTA_ATTACK1 512
		}
		else ai AIOCTASHOOTENEMY
		add botclip 1
        break
      }
    }
    else
      ifl targetdist 1280
        ai AIOCTASCRATCHENEMY
  }

ends

state octascratchenemystate

  ifcount 32
  {
    resetcount
    sound OCTA_ATTACK2
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_ATTACK2 SHRUNKPITCH
	ifn bottarget -1 set spriteid bottarget
	else
	{
		set spriteid player[].i
		palfrom 16 32
	}
    
    // addphealth OCTASCRATCHINGPLAYER
	
	  seta[spriteid].htpicnum FIRELASER
	  seta[spriteid].htowner THISACTOR
	  set temp OCTASCRATCHINGPLAYER
	  mul temp -1
	  ifvarand initflags 16 mul temp 2
	  seta[spriteid].htextra temp
	  seta[spriteid].htang sprite[].ang
	
  }
  
  ifvarand initflags 16 { ifg targetdist 1560 ai AIOCTAGETENEMY break }
  else
  ifg targetdist 1280
   ai AIOCTAGETENEMY
ends


state octashootenemystate

	ifaction AOCTASHOOT1
	{
		ifactioncount 1
		{
			ifvarand initflags 16
			{
				set xvel 768
				state rpg_targetprep
				ezshoot zdist COMETPROJ
				seta[RETURN].pal 6
			}
			else
			shoot COOLEXPLOSION1
			action AOCTASHOOT2
			// eshoot COOLEXPLOSION1
			// ifvarand initflags 16 setav[RETURN].mtype 1
		}
	}
	ifaction AOCTASHOOT2
	{
		ifactioncount 1
		ai AIOCTAGETENEMY
	}

ends

state octanewshootstate
	set botclip 0
	ifaction AOCTANEWSHOOT1
	{
		ifactioncount 1
		action AOCTANEWSHOOT2
	}
	else
	ifaction AOCTANEWSHOOT2
	{
		ifactorsound THISACTOR SHOOTSPIKE nullop else sound SHOOTSPIKE
		
		// setprojectile[SPIKEPROJ].workslike 32770
		ifcount 6
		{
			resetcount
			set B 0
			whilevarn B 12
			{
				set xvel 768
				state rpg_targetprep
				ezshoot zdist SPIKEPROJ
				seta[RETURN].blend 1
				rand angvar 192
				sub angvar 96
				add angvar sprite[RETURN].ang
				seta[RETURN].ang angvar
				rand zdist 6144
				sub zdist 3072
				add zdist sprite[RETURN].zvel
				seta[RETURN].zvel zdist
				add B 1
			}
		}
		// setprojectile[SPIKEPROJ].workslike 36866
	}
	ifactioncount 4 ai AIOCTAGETENEMY

ends

// state octashootenemystate
  // ifcount 25
  // {
    // ifcount 27
      // ai AIOCTAGETENEMY
  // }
  // else
    // ifcount 24
	// {
      // eshoot COOLEXPLOSION1
	  // ifvarand initflags 16 setav[RETURN].mtype 1
	// }
  // else
    // ifactioncount 6
      // resetactioncount
// ends

state checkoctahitstate
  ifwasweapon SHRINKSPARK
  {
    sound ACTOR_SHRINKING
    // ai AIOCTASHRUNK
	set shrunken 1
  }
  else
  {
    ifdead
    {
      ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action AOCTAFROZEN
        strength 0
		state freezeme
        break
      }

      addkills 1 state enemy_death
	  
        sound OCTA_DYING
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setactorsoundpitch THISACTOR OCTA_DYING 1560
      ifwasweapon RPG
      {
        sound SQUISHED
        state standard_jibs
		ifvarand initflags 16 
		{ 
			espawn OCTABRAIN 
			seta[RETURN].pal 3
			espawn OCTABRAIN 
			seta[RETURN].pal 3
		}
        killit
      }
      else
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
		ifvarand initflags 16 
		{ 
			espawn OCTABRAIN 
			seta[RETURN].pal 3
			espawn OCTABRAIN 
			seta[RETURN].pal 3
		}
        killit
      }
      else
        ifwasweapon GROWSPARK
      {
        cstat 0
        sound ACTOR_GROWING
        ai AIOCTAGROW
        break
      }
      else
      {
        state rf
        ai AIOCTADYING
		ifrnd 96
		action AOCTADYING2
      }
      sound OCTA_DYING
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_DYING SHRUNKPITCH
    }
    else
    {
      
        ifwasweapon GROWSPARK
          sound SPARKLESOUND

      sound OCTA_PAIN
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_PAIN SHRUNKPITCH

      ifai AIOCTAHIT nullop else
      ifrnd 64
        ai AIOCTAHIT
      else
      ifvarg stun 0
        ai AIOCTAHIT
    }
  }
  state random_wall_jibs
ends

state octashrunkstate
  ifcount SHRUNKDONECOUNT
    ai AIOCTAGETENEMY
  else
    ifcount SHRUNKCOUNT
	{
	  ifvarand initflags 16 sizeto 64 64
	  else
      sizeto 24 22
	}
  else
    state genericshrunkcode
ends


state octadyingstate

  ifmove OCTASTOPPED
  ife shrunken 0
  {
	ifcount 6
	{
		sizeto 24 14
		resetcount
	}
  }
  ifaction AOCTADYING2
  {
	  ifactioncount 5
	  {
		ifrnd 64
		  spawn BLOODPOOL2
		move OCTASTOPPED
		action AOCTADEAD2
		break
	  }
	  else
		ifactioncount 5 nullop
	  else
		ifactioncount 4
		  iffloordistl 8
			sound THUD
	break
  }
  ifactioncount 6
  {
    ifrnd 64
      spawn BLOODPOOL2
    move OCTASTOPPED
    action AOCTADEAD
    break
  }
  else
    ifactioncount 5 nullop
  else
    ifactioncount 4
      iffloordistl 8
        sound THUD
ends

actor OCTABRAINSTAYPUT OCTASTRENGTH
  
  ifvarand initflags 16 sizeat 36 32 else
  sizeat 24 22
  ai AIOCTAGETENEMY
  cactor OCTABRAIN
enda

actor OCTABRAIN OCTASTRENGTH
  fall
  state monsterai 
  
  
  
  state checksquished
  ifai 0
  {
    ai AIOCTAGETENEMY
	ifvarand initflags 16 sizeat 36 32 else
	sizeat 24 22
  }
  else
    ifaction AOCTADEAD
  {
    strength 0
    // ifcount RESPAWNACTORTIME
      // ifrespawn ife gametype 0
    // {
      // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength OCTASTRENGTH
      // ai AIOCTAGETENEMY
    // }
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      state standard_jibs
      killit
    }
    break
  }
  else ifaction AOCTADEAD2
  {
	strength 0
    // ifcount RESPAWNACTORTIME
      // ifrespawn ife gametype 0
    // {
      // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength OCTASTRENGTH
      // ai AIOCTAGETENEMY
    // }
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      state standard_jibs
      killit
    }
    break
  }
  else ifaction AOCTAFROZEN
  {
    ifcount THAWTIME
    {
      ai AIOCTAGETENEMY
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
      {
        ifactioncount 26
        {
          spawn WATERDRIP
          resetactioncount
        }
      }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  addkills 1 state enemy_death
	  lotsofglass 10
      state standard_jibs
      ifrnd 84
        spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else
  {
    ifg gametype 0
	{
		ifg navpoint -1
		ife monstatus 1
		{
			orvar monstflags YES
		    ifmove STOPPED nullop else move STOPPED geth
			ifn bottarget -1 { set spriteid bottarget state facesprite }
			ifai AIOCTAHIT nullop else
			ife stun 0
			{
				cos xvel angvel
				sin yvel angvel
				shiftr xvel 7
				shiftr yvel 7
				geta[navpoint].z zdist
				ife sprite[navpoint].picnum BLUEFLAG sub zdist 4096 else
				ife sprite[navpoint].picnum REDFLAG sub zdist 4096
				sub zdist sprite[].z
				
				ldist xydist THISACTOR navpoint
				ife xydist 0 set xydist 1
				shiftl zdist 8
				div zdist xydist
				ifvarand monstflags 16 { mul xvel 3 div xvel 2 mul yvel 3 div yvel 2 state makespeedblur }
				movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
				ifn RETURN 0
				ifg navpoint -1
				{
					// bumping something
					sub RETURN 49152
					ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
					{
						set B NO
						ife actorvar[RETURN].monstatus 1
						ife actorvar[RETURN].team team set B YES
						ifvarand sprite[RETURN].cstat 16 set B YES
						ife B YES
						{
							set x2 sprite[].x
							add x2 256
							rotatepoint sprite[].x sprite[].y x2 sprite[].y sprite[].ang x y
							setsprite THISACTOR x y sprite[].z
							seta[].htmovflag 0
						}
					}
				}
			}
		}
		else
		{
			ifvarand monstflags YES xorvar monstflags YES
			ifmove STOPPED
			{
				ifai AIOCTAGETENEMY move OCTAWALKVELS seekplayer
				ifai AIOCTASHOOTENEMY move OCTASTOPPED faceplayer
				ifai AIOCTASCRATCHENEMY move OCTASTOPPED faceplayerslow
				ifai AIOCTAHIT move OCTASTOPPED faceplayer
			}
		}
	}
	
    ifrnd 1
	{
        soundonce OCTA_ROAM
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR OCTA_ROAM SHRUNKPITCH
	}

    ifai AIOCTAGETENEMY
      state octagetenemystate
    else
      ifai AIOCTAHIT
      {
	    ifvarg stun 0 resetcount
        ifcount 8
          ai AIOCTASHOOTENEMY
      }
    else
      ifai AIOCTADYING
      {
        state octadyingstate
        break
      }
    else
      ifai AIOCTASCRATCHENEMY
        state octascratchenemystate
    else
      ifai AIOCTASHOOTENEMY
        state octashootenemystate
	else
	  ifai AIOCTANEWSHOOT
		state octanewshootstate
    // else
      // ifai AIOCTASHRUNK
      // {
        // state octashrunkstate
        // break
      // }
    else
      ifai AIOCTAGROW
        state genericgrowcode

    ifmove OCTAUPVELS nullop
    else
	ifmove OCTAUPFASTVELS nullop
	else
      ife targethigher YES
	  {
	    ifg gametype 0
		ifg navpoint -1 nullop else
        move OCTAUPVELS seekplayer
	  }
    else
      ifmove OCTAINWATER nullop
      else
	  ifmove OCTAFASTBITE nullop
	  else
        ifinwater
          move OCTAINWATER seekplayer
	ife monstatus -1 break
	ifg sprite[].htextra 0 ife sprite[].htpicnum RPG
	{
		geta[].htextra temp shiftl temp 1 seta[].htextra temp
	}
    ifhitweapon
      state checkoctahitstate
  }
enda

eventloadactor OCTACAVEGIRL
	sizeat 24 22
	cstator 257
enda

eventloadactor OCTADEADSPRITE
cactor 1870
enda

useractor notenemy OCTACAVEGIRL 40

ifmove 0
{
	move STOPPED faceplayer
	sizeat 24 22
	cstator 257
}

ifhitweapon
{
	soundonce OCTA_PAIN
	ifdead
	{
		state lite_jibs
		ifwasweapon RPG state standard_jibs else
		{
			spawn OCTABRAIN
			spawn CAVEGIRL
		}
		killit
	}
}
else
ifrnd 2 soundonce OCTA_ROAM

enda

eventloadactor PIGGROPE
	cstator 256
	sizeat 28 26
enda

eventloadactor TROOPGROPE1
	cstator 256
	sizeat 24 22
enda

eventloadactor TROOPGROPE2
	cstator 256
	sizeat 24 22
enda

eventloadactor LIZGROPE
	cstator 256
	sizeat 24 22
enda

useractor notenemy PIGGROPE 40

ifmove 0
{
	move STOPPED faceplayer
	sizeat 28 26
	cstator 257
}
fall

ifhitweapon
{
	ifdead
	{
		state lite_jibs
		sound PIG_DYING
		ifwasweapon RPG state standard_jibs else
		{
			espawn PIGCOP
			seta[RETURN].pal sprite[].pal
			
			espawn HOOKER2
			seta[RETURN].pal sprite[].pal
			seta[RETURN].htextra 1
			seta[RETURN].htpicnum SHOTSPARK1
			seta[RETURN].htowner THISACTOR
		}
		killit
	}
}
else
{
	ifrnd 2 soundonce PIG_PAIN
	// ifsound SG_PAIN1 break
	// ifsound SG_PAIN2 break
	// ifsound SG_PAIN3 break
	// ifrnd 1 soundonce SG_PAIN1 else
	// ifrnd 1 soundonce SG_PAIN2 else
	// ifrnd 1 soundonce SG_PAIN3
}

enda

useractor notenemy TROOPGROPE1 20

ifmove 0
{
	move STOPPED faceplayer
	cstator 257
	sizeat 24 22
}
fall
ifhitweapon
{
	spawn BLOOD
	ifdead
	{
		guts JIBS6 4
		sound PRED_DYING
		ifwasweapon RPG state standard_jibs else
		{
			espawn NEWTROOP
			seta[RETURN].pal sprite[].pal
			espawn NAKEDBABE
			seta[RETURN].pal sprite[].pal
		}
		killit
	}
}
else
{
	ifrnd 2 soundonce PRED_ROAM
	// ifsound SG_PAIN1 break
	// ifsound SG_PAIN2 break
	// ifsound SG_PAIN3 break
	// ifrnd 1 soundonce SG_PAIN1 else
	// ifrnd 1 soundonce SG_PAIN2 else
	// ifrnd 1 soundonce SG_PAIN3
}

enda

useractor notenemy TROOPGROPE2 20

ifmove 0
{
	move STOPPED faceplayer
	cstator 257
	sizeat 24 22
}
fall
ifhitweapon
{
	spawn BLOOD
	ifdead
	{
		guts JIBS6 4
		sound PRED_DYING
		ifwasweapon RPG state standard_jibs else
		{
			state lite_jibs
			espawn LIZTROOP
			seta[RETURN].pal sprite[].pal
			espawn SCHOOLGIRL
			seta[RETURN].pal sprite[].pal
			
		}
		killit
	}
}
else
	ifrnd 2 soundonce PRED_ROAM
	


enda

useractor notenemy LIZGROPE 30

ifmove 0
{
	move STOPPED faceplayer
	cstator 257
	sizeat 26 26
}
fall
ifhitweapon
{
	spawn BLOOD
	ifdead
	{
		guts JIBS6 4
		sound CAPT_DYING
		ifwasweapon RPG state standard_jibs else
		{
			espawn LIZMAN
			seta[RETURN].pal sprite[].pal
			espawn CHEERLEADER
			seta[RETURN].pal sprite[].pal
		}
		killit
	}
}
else
{
	ifrnd 2 soundonce CAPT_ROAM
	// ifsound SG_PAIN1 break
	// ifsound SG_PAIN2 break
	// ifsound SG_PAIN3 break
	// ifrnd 1 soundonce SG_PAIN1 else
	// ifrnd 1 soundonce SG_PAIN2 else
	// ifrnd 1 soundonce SG_PAIN3
}

enda


eventloadactor OLDPIGSTRIPPER 
sizeat 28 26 cstator 257
enda

spriteflags OLDPIGSTRIPPER 4194305
useractor enemy OLDPIGSTRIPPER 60

	ifmove 0
	{
		move STOPPED
		sizeat 28 26
		cstator 257
		getp[].max_actors_killed temp
		sub temp 1
		setp[].max_actors_killed temp
	}
	ifhitweapon
	{
		ifdead
		{
			sound SQUISHED
			sound PIG_DYING
			state standard_jibs
			killit
		}
		soundonce PIG_PAIN
	}
	ifhitspace
	ifp pfacing
	ifp palive
	ifpdistl 1560
	{
		soundonce YOURUGLY
	}
	fall

enda


action APIGWALK     0  4 5 1 20
action APIGRUN      0  4 5 1 11
action APIGSHOOT    30 2 5 1 58
action APIGCOCK     25 1 5 1 16
action APIGSTAND    30 1 5 1 1
action APIGDIVE     -5 2 5 1 40
action APIGDIVESHOOT  0 2 5 1 58

action APIGDYING    55 5 1 1 18
action APIGHIT      55 1 1 1 10
action APIGDEAD     60 1 1 1 1
action APIGFROZEN    0 1 5
action APIGGROW      0
action APIGDECAP    4565 6 1 1 18
action APIGDECAPDEAD 4570 1 1 1 1
action APIGDYING2   8787 6 1 1 20
action APIGDEAD2    8792 1 1 1 1 


move PIGWALKVELS 72
move PIGRUNVELS 108

move PIGDEADVEL

ai AIPIGSEEKENEMY APIGWALK PIGWALKVELS seekplayer
ai AIPIGSHOOTENEMY APIGSHOOT STOPPED faceplayer
ai AIPIGFLEEENEMY APIGWALK PIGWALKVELS fleeenemy
ai AIPIGSHOOT APIGSHOOT STOPPED faceplayer
ai AIPIGDODGE APIGRUN PIGRUNVELS dodgebullet
ai AIPIGCHARGE APIGRUN PIGRUNVELS seekplayer
ai AIPIGDIVING APIGDIVE STOPPED faceplayer
ai AIPIGDYING APIGDYING STOPPED geth
// ai AIPIGSHRINK APIGWALK SHRUNKVELS fleeenemy
ai AIPIGGROW APIGGROW STOPPED faceplayerslow
ai AIPIGHIT APIGHIT STOPPED faceplayer

state pigseekenemystate

  ifai AIPIGCHARGE
  {
    ife seemytarget YES
      ifl targetdist 3084
    {
      ifnotmoving
	        ai AIPIGSEEKENEMY
	      else
		  ifn sector[].lotag 1
	      {
		    cactor PIGCOPDIVE
	        ai AIPIGDIVING
	      }
    }
    break
  }
  else iffloordistl 32
  {
    ifg targetdist 4096
    {
      ifactornotstayput
      ai AIPIGCHARGE
    }
    ifrnd 8
    {
      ifbulletnear
	  {
        ai AIPIGDODGE
		set dodgetime 6
	  }
    }
	else
	ifactor PIGCOP
	ifrnd 16
	{
	  ifbulletnear
	  {
		cactor PIGCOPDIVE
        ai AIPIGDIVING
		break
	  }
	}
  }

  ifrnd 128
    ife seemytarget YES
  {
    ifai AIPIGDODGE
    {
	  ifl dodgetime 2 set dodgetime 2
      ifcount 32
        ai AIPIGCHARGE
          break
    }
    iffloordistl 32
    {
      ifl targetdist 1024
        ife targetalive YES
          ife shootmytarget YES
      {
        ai AIPIGSHOOTENEMY
        break
      }
      ifcount 48
      {
        ifrnd 8
          ife targetalive YES
            ife shootmytarget YES
        {
		  ifonwater
		    ai AIPIGSHOOTENEMY
		  else
          ifrnd 192
            ai AIPIGSHOOTENEMY
          else
          {
	        cactor PIGCOPDIVE
            ai AIPIGDIVING
          }
          break
        }
      }
    }
  }
ends

state pigfiring
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound PISTOL_FIRE
		setactorsoundpitch THISACTOR PISTOL_FIRE SHRUNKPITCH
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
	}
	else
	{
		ifvarand initflags 32
		ife shrunken 0
		{
			sound FLAKSHOT
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844			
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ 
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep
			zshoot zdist FLAKPROJ
			set xvel 844
			state rpg_targetprep			
			zshoot zdist FLAKPROJ
		}
		else
		{
			sound PIG_ATTACK
			ifn bottarget -1
			{
				state hitscan_targetprep
				zshoot zdist SHOTSPARK1
				zshoot zdist SHOTSPARK1
				zshoot zdist SHOTSPARK1
				zshoot zdist SHOTSPARK1
			}
			else
			{
				shoot SHOTGUN
				shoot SHOTGUN
				shoot SHOTGUN
				shoot SHOTGUN
			}
		}
	}
ends


state pigshootenemystate

    ifcount 12 nullop
    else
      ifcount 11
    {
      ife shootmytarget YES
		state pigfiring
      else
        ai AIPIGSEEKENEMY
    }
    ifcount 25 nullop
    else
      ifcount 24
      {
        action APIGCOCK
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound SHOTGUN_COCK
			setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
			
		}
		else
        sound SHOTGUN_COCK
      }
    ifcount 48 nullop
    else
      ifcount 47
    {
      ife shootmytarget YES
		state pigfiring
      else
        ai AIPIGSEEKENEMY
    }
    ifcount 60 nullop
    else
      ifcount 59
      {
        action APIGCOCK
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound SHOTGUN_COCK
			setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
		}
		else
        sound SHOTGUN_COCK
      }
    ifcount 72
    {
      ifrnd 64
        resetcount
      else
      {
        ifl targetdist 768
          ai AIPIGFLEEENEMY
        else
          ai AIPIGSEEKENEMY
      }
    }
    ifaction APIGCOCK
      ifactioncount 2
        action APIGSHOOT

ends

state pigfleeenemystate

  ifl dodgetime 2 set dodgetime 2
  ifactioncount 8
    ai AIPIGSEEKENEMY
  else
    ifnotmoving
      ai AIPIGSEEKENEMY
ends

state pigdivestate
  ifaction APIGDIVESHOOT
  {
    ife seemytarget YES
    {
      ifcount 12 nullop
      else
        ifcount 11
      {
        ife shootmytarget YES
        state pigfiring
        else
		{
		  cactor PIGCOP
          ai AIPIGSEEKENEMY
		}
      }
      ifcount 25 nullop
      else
        ifcount 24
		{
		  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			{
				sound SHOTGUN_COCK
				setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
			}
		  else
          sound SHOTGUN_COCK
		}
      ifcount 48 nullop
      else
        ifcount 47
      {
        ife shootmytarget YES
        state pigfiring
        else
		{
		  cactor PIGCOP
          ai AIPIGSEEKENEMY
		}
      }
      ifcount 60 nullop
      else
        ifcount 59
      {
	    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound SHOTGUN_COCK
			setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
		}
		else
        sound SHOTGUN_COCK
        ifgapzl 32
          ai AIPIGDIVING
        else
        {
          ifl targetdist 4096
		  {
		    cactor PIGCOP
            ai AIPIGFLEEENEMY
			set dodgetime 6
		  }
          else
		  {
		    cactor PIGCOP
            ai AIPIGSEEKENEMY
		  }
        }
      }
    }
    else
      ifgapzl 32
        ai AIPIGDIVING
      else
	  {
	    cactor PIGCOP
        ai AIPIGSEEKENEMY
	  }
  }
  else
  {
    ifactioncount 2
	{
		ife targetalive YES
		{
		  resetcount
		  action APIGDIVESHOOT
		}
		else
		{
		  cactor PIGCOP
		  ai AIPIGSEEKENEMY
		}
	}
  }
ends

state checkpighitstate
	// ifn sprite[].picnum PIGCOPDIVE
	// ifn sprite[].htpicnum FREEZEBLAST
	// ifn sprite[].htpicnum SHRINKSPARK
	// ife shrunken 0
  // spawn BLOOD
  ifdead
  {
    cactor PIGCOP

    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action APIGFROZEN
      strength 0
	  state freezeme
      break
    }
	state random_wall_jibs
	ife gametype 0
	{
    ifrnd 16
      spawn SHIELD
    else
      state drop_shotgun
	}
	addkills 1 state enemy_death
	
    ifwasweapon GROWSPARK
    {
      sound ACTOR_GROWING
      ai AIPIGGROW
      break
    }
	
    ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      state lite_jibs
	  state pig_body_jibs
      killit
    }
    else
      ifwasweapon RPG
    {
      sound SQUISHED
      state lite_jibs
	  state pig_body_jibs
      killit
    }
    else
      ai AIPIGDYING
	  ifrnd 128 cstator 4
	  ifrnd 128
	  action APIGDYING2
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound PIG_DYING
			setactorsoundpitch THISACTOR PIG_DYING SHRUNKPITCH
		}
		else
        sound PIG_DYING
  }
  else
  {
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound PIG_PAIN
		setactorsoundpitch THISACTOR PIG_PAIN SHRUNKPITCH
	}
	else
    sound PIG_PAIN
    state random_wall_jibs

    ifwasweapon SHRINKSPARK
    {
	  // cactor PIGCOP
      sound ACTOR_SHRINKING
      // ai AIPIGSHRINK
	  set shrunken 1
    }
    else
      ifwasweapon GROWSPARK
        sound SPARKLESOUND
    
	ifvarg stun 0
      {
	      ai AIPIGHIT
	      cactor PIGCOP
      }
    else
    ifrnd 32
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
	{
	    ifmove STOPPED ifcount 13 nullop else
		{
	      cactor PIGCOP
          ai AIPIGHIT
		}
	}
	else
	ifrnd 112
	iffloordistl 8
	{
	  ifai AIPIGSHOOTENEMY nullop else
	  ifai AIPIGDIVING nullop else
	  {
		ifonwater
		{
		  cactor PIGCOP
		  ai AIPIGSHOOTENEMY
		}
		else
		  ifrnd 128
		  {
			cactor PIGCOP
			ai AIPIGSHOOTENEMY
		  }
		else
		  {
			cactor PIGCOPDIVE
			ai AIPIGDIVING
			action APIGDIVESHOOT
		  }
	  }
	}
  }
ends

state pigshrinkstate
  ifcount SHRUNKDONECOUNT
    ai AIPIGSEEKENEMY
  else
    ifcount SHRUNKCOUNT
      sizeto 40 40
  else
    state genericshrunkcode
ends

state pigdyingstate

	ifmove PIGDEADVEL
	{
		// ifrespawn ife gametype 0
		  // ifcount RESPAWNACTORTIME
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength PIGCOPSTRENGTH
		  // ai AIPIGSEEKENEMY
		// }
		// else
		// {
		  strength 0
		  ifhitweapon
			ifwasweapon RADIUSEXPLOSION
		  {
			sound SQUISHED
			state lite_jibs
			ifaction APIGDEAD
				state pig_body_jibs
			else
			ifaction APIGDEAD2
			    state pig_body_jibs
			killit
		  }
		  
		// }
		break
	}

	  ifactioncount 5
	  {
	    ifvarand sprite[].cstat 4 cstat 4 else cstat 0
		ifrnd 64
		  spawn BLOODPOOL2
		iffloordistl 8
		  sound THUD
		ifaction APIGDECAP action APIGDECAPDEAD
		else
		ifaction APIGDYING action APIGDEAD
		else
		ifaction APIGDYING2 action APIGDEAD2
		move PIGDEADVEL
	  }
  
ends

state pigcopcode
  fall
  state monsterai
  
  ife monstatus -1 break
  state checksquished
  ifaction APIGSTAND
    ai AIPIGSEEKENEMY
  else
    ifaction APIGDEAD { state pigdyingstate break }
  else
    ifaction APIGDECAPDEAD { state pigdyingstate break }
  else
    ifaction APIGDEAD2 { state pigdyingstate break }
  else
    ifaction APIGFROZEN
  {
    ifcount THAWTIME
    {
      ai AIPIGSEEKENEMY
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
        ifrnd 8
          spawn WATERDRIP

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  lotsofglass 10
      state standard_jibs
      ifrnd 84 spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else
    ifai AIPIGDYING
      state pigdyingstate
  // else
    // ifai AIPIGSHRINK
      // state pigshrinkstate
  else
  { 
    ifai AIPIGHIT
    {
	  ifvarg stun 0 resetactioncount
      ifactioncount 3
      ai AIPIGSEEKENEMY
    }
	else
    ifai AIPIGSEEKENEMY
      state pigseekenemystate
    else
      ifai AIPIGDODGE
        state pigseekenemystate
    else
      ifai AIPIGSHOOTENEMY
        state pigshootenemystate
    else
      ifai AIPIGGROW
        state genericgrowcode
    else
      ifai AIPIGFLEEENEMY
        state pigfleeenemystate
    else
      ifai AIPIGDIVING
        state pigdivestate
    else
      ifai AIPIGCHARGE
        state pigseekenemystate
    ifhitweapon
	{
	    ifwasweapon HEADJIB1
		{
			// sound PIG_DYING
			cactor PIGCOP
			sound SQUISHED
			guts JIBS2 2
			state enemy_death
			state drop_shotgun
			state random_wall_jibs
			addkills 1
			strength 0
			state upscaleme
			ai AIPIGDYING
			action APIGDECAP
			rand zdist 1024
			sub zdist 3500
			ezshoot zdist PIGHEADPROJ
			rand angvar 512
			sub angvar 256
			add angvar sprite[].htang
			seta[RETURN].ang angvar
			seta[RETURN].pal sprite[].pal
			cstat 0
		}
		else
        state checkpighitstate
	}
	else
    ifrnd 1
    {
      ifrnd 32
        soundonce PIG_ROAM
      else
        ifrnd 64
          soundonce PIG_ROAM2
      else
        soundonce PIG_ROAM3
    }
  }
ends

actor PIGCOPDIVE PIGCOPSTRENGTH
  ifai 0
  ai AIPIGDIVING
  state pigcopcode
enda

actor PIGCOPSTAYPUT PIGCOPSTRENGTH
  ai AIPIGSEEKENEMY
  cactor PIGCOP
enda

actor PIGCOP PIGCOPSTRENGTH APIGSTAND

  state pigcopcode
  
enda

move PISSING
useractor enemy PIGPISSING PIGCOPSTRENGTH

cactor NEWPIGPISS
sizeat 28 26
break

fall
ifmove 0
{
	ifcansee move PISSING
	break
}
ifmove PISSING
{


	soundonce DUKE_URINATE
	ifcount 40 
	{ 
		stopsound DUKE_URINATE sound FLUSH_TOILET 
		move 0
		cactor PIGCOP
		action APIGSTAND
		sound PIG_RECOG
	}
	
}

enda

action ABOSS1WALK        0  4  5  1  12
action ABOSS1FROZEN        30 1  5
action ABOSS1RUN         0  6  5  1  5
action ABOSS1SHOOT         30 2  5  1  4
action ABOSS1LOB         40 2  5  1  35
action ABOSS1DYING        50 5  1  1  35
action BOSS1FLINTCH       50 1  1  1  1
action ABOSS1DEAD         55

move PALBOSS1SHRUNKRUNVELS 32
move PALBOSS1RUNVELS 128
move BOSS1WALKVELS 208
move BOSS1RUNVELS 296


ai AIBOSS1SEEKENEMY ABOSS1WALK BOSS1WALKVELS seekplayer
ai AIBOSS1RUNENEMY ABOSS1RUN BOSS1RUNVELS faceplayer
ai AIBOSS1SHOOTENEMY ABOSS1SHOOT STOPPED faceplayer
ai AIBOSS1LOBBED ABOSS1LOB STOPPED faceplayer // faceplayersmart
ai AIBOSS1DYING ABOSS1DYING STOPPED geth
// ai AIBOSS1PALSHRINK ABOSS1WALK PALBOSS1SHRUNKRUNVELS furthestdir

state boss1palshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AIBOSS1SEEKENEMY
  else
    ifcount SHRUNKCOUNT
      sizeto 40 40
  else
    state genericshrunkcode
ends

state checkboss1seekstate
  ai AIBOSS1SEEKENEMY
  ifspritepal 0 nullop
    else   // a fake way of doing a ifspritepal NOT.
      move PALBOSS1RUNVELS seekplayer
ends

state boss1runenemystate
  ifl targetdist 2048
  {
    ife targetalive YES
      ai AIBOSS1SHOOTENEMY
    break
  }
  else
    ife seemytarget YES
  {
    ifactioncount 6
    {
      ife shootmytarget YES
      {
        resetactioncount
		ife shrunken 0
        sound BOS1_WALK
      }
      else
        ai AIBOSS1SEEKENEMY
    }
  }
  else
    ai AIBOSS1SEEKENEMY
ends

state boss1seekenemystate
  ifrnd 2
  {
    soundonce BOS1_ROAM
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		setactorsoundpitch THISACTOR BOS1_ROAM 1560
  }
  else
    ifactioncount 6
  {
    resetactioncount
	ife shrunken 0
    sound BOS1_WALK
  }

  ifl targetdist 2548
    ife targetalive YES
  {
    ai AIBOSS1SHOOTENEMY
    break
  }

  ife seemytarget YES
    ifcount 32
  {
    ifrnd 32
    {
      ife targetalive YES
        ife shootmytarget YES
          ai AIBOSS1SHOOTENEMY
    }
    else
      ifg targetdist 2548
        ifrnd 192
          ife shootmytarget YES
    {
      ifrnd 64
      {
        ai AIBOSS1RUNENEMY
        ifspritepal 0 nullop
        else
          move PALBOSS1RUNVELS seekplayer
      }
      else
        ai AIBOSS1LOBBED
    }
  }

ends

state boss1dyingstate
  ifaction ABOSS1DEAD
  {
    ifspritepal 0
      break
    // ifrespawn ife gametype 0
      // ifcount RESPAWNACTORTIME
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength PIGCOPSTRENGTH
      // state checkboss1seekstate
    // }
    // else
    // {
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
        killit
      }
      break
    // }
  }
  ifactioncount 5
  {
    iffloordistl 8
      sound THUD
    action ABOSS1DEAD
    cstat 0
    ifspritepal 0
	{
		setuserdef[].display_bonus_screen 1
		ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
		ife marathon YES 
		{
			setp[].timebeforeexit 150
			set cutscene_ready 1252
		}
		else
		{
			endofgame 60
			set cutscene_ready 1252
			setuserdef[].volume_number 0
			ife attmode YES set attmid 4
		}
	}
  }
ends

state boss1lobbedstate
  ife seemytarget YES
  {
    ifactioncount 2
    {
      resetactioncount
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
			sound BOS1_ATTACK2
			setactorsoundpitch THISACTOR BOS1_ATTACK2 SHRUNKPITCH
	  }
	else
      sound BOS1_ATTACK2
      shoot MORTER
    }
    else
      ifcount 64
        ifrnd 16
          state checkboss1seekstate
  }
  else
    state checkboss1seekstate
ends

state boss1shootenemy

  ife targetalive NO count 80
  ifcount 72
    state checkboss1seekstate
  else
    ifaction ABOSS1SHOOT
      ifactioncount 2
  {
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS1_ATTACK1
		setactorsoundpitch THISACTOR BOS1_ATTACK1 SHRUNKPITCH
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
	}
	else
	{
		cactor LIZMAN
		sound BOS1_ATTACK1
		ifn bottarget -1
		{
			state hitscan_targetprep
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			ifspritepal 0
			{
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			}
		}
		else
		{
			shoot SHOTSPARK1
			shoot SHOTSPARK1
			shoot SHOTSPARK1
			shoot SHOTSPARK1
			ifspritepal 0
			{
			shoot SHOTSPARK1
			shoot SHOTSPARK1
			}
		}
		cactor BOSS1
	}
    resetactioncount
  }
ends

state checkboss1hitstate
  ifrnd 2
    spawn BLOODPOOL2
  ifdead
  {
    ifspritepal 0
      globalsound DUKE_TALKTOBOSSFALL
    else
    {
      ife sprite[].htowner player[].i
        globalsound DUKE_TALKTOBOSSFALL
      ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action ABOSS1FROZEN
        strength 0
		state freezeme
        break
      }
    }
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS1_DYING
		setactorsoundpitch THISACTOR BOS1_DYING SHRUNKPITCH
	}
	else
    sound BOS1_DYING

    addkills 1 state enemy_death
    ai AIBOSS1DYING
  }
  else
  {
    ifvarg stun 0 action BOSS1FLINTCH else
    ifrnd 32
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
    {
      action BOSS1FLINTCH
      move 0
    }

    ifspritepal 0 nullop
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      // ai AIBOSS1PALSHRINK
	  set shrunken 1
      break
    }

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS1_PAIN
		setactorsoundpitch THISACTOR BOS1_PAIN SHRUNKPITCH
	}
	else
    soundonce BOS1_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends

state boss1code


  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  { state standard_jibs state standard_jibs }
  
  ifaction ABOSS1FROZEN
  {
    ifcount THAWTIME
    {
      ai AIBOSS1SEEKENEMY
      spritepal 21
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
      state standard_jibs
	  state standard_jibs
	  lotsofglass 10
      ifrnd 84
        spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  
  
  ifai 0
  {
    ifspritepal 0
      ai AIBOSS1RUNENEMY
    else
    {
      strength BOSS1PALSTRENGTH
	  // ife upscaled YES { sizeat 20 20 set init_xrepeat 20 set init_yrepeat 20 } else
	  sizeat 40 40
      ai AIBOSS1SHOOTENEMY
	  clipdist 96
    }
  }
  
  state monsterai 
  
  ifaction BOSS1FLINTCH
  {
    ifspritepal 0 ifg stun 15 set stun 15
    ifvarg stun 0 resetactioncount
    ifactioncount 3
	{
		ife targetalive YES
		ai AIBOSS1SHOOTENEMY
		else
		ai AIBOSS1SEEKENEMY
	}
  }
  else
    ifai AIBOSS1SEEKENEMY
      state boss1seekenemystate
  else
    ifai AIBOSS1RUNENEMY
      state boss1runenemystate
  else
    ifai AIBOSS1SHOOTENEMY
      state boss1shootenemy
  else
    ifai AIBOSS1LOBBED
      state boss1lobbedstate
  // else
    // ifai AIBOSS1PALSHRINK
      // state boss1palshrunkstate
  
  ifai AIBOSS1DYING
    state boss1dyingstate
  else
  {
	ife monstatus -1 break
    ifhitweapon
      state checkboss1hitstate
    else
      ife targetalive YES
        // ifspritepal 0
          ifpdistl 1280
			ife pdown NO
				ife spawnprotect 0
    {
	    ifspritepal 0
		{
			addphealth -1000
			palfrom 63 63
		}
		else ifvarand monstflags 256
		{
			addphealth -20
			palfrom 50 63
		}
    }
	  ifspritepal 0
	  state squish_bot
	  else
	  ifvarand monstflags 256 state squish_bot
  }
ends

actor BOSS1STAYPUT BOSS1STRENGTH cactor BOSS1 enda
actor BOSS1 BOSS1STRENGTH fall state boss1code enda


action ABOSS2WALK        0  4  5  1  30
action ABOSS2FROZEN        0  1  5
action ABOSS2RUN         0  4  5  1  15
action ABOSS2SHOOT         20 2  5  1  15
action ABOSS2LOB         30 2  5  1  105
action ABOSS2DYING        40 9  1  1  35
action BOSS2FLINTCH       40 1  1  1  1
action ABOSS2DEAD         49

move PALBOSS2SHRUNKRUNVELS 32
move PALBOSS2RUNVELS 84
move BOSS2WALKVELS 192
move BOSS2RUNVELS 256

ai AIBOSS2SEEKENEMY ABOSS2WALK BOSS2WALKVELS seekplayer
ai AIBOSS2RUNENEMY ABOSS2RUN BOSS2RUNVELS faceplayer
ai AIBOSS2SHOOTENEMY ABOSS2SHOOT STOPPED faceplayer
ai AIBOSS2LOBBED ABOSS2LOB STOPPED faceplayer
ai AIBOSS2DYING ABOSS2DYING STOPPED geth
// ai AIBOSS2PALSHRINK ABOSS2WALK PALBOSS2SHRUNKRUNVELS furthestdir
ai AIBOSS2GROW ABOSS2FROZEN STOPPED geth

state boss2palshrunkstate
  ifcount SHRUNKDONECOUNT
  {
    cstat 257
    ai AIBOSS2SEEKENEMY
  }
  else
    ifcount SHRUNKCOUNT
      sizeto 40 32
  else
    state genericshrunkcode
ends

state checkboss2seekstate
  ai AIBOSS2SEEKENEMY
  // ifspritepal 0 nullop
  // else   // a fake way of doing a ifspritepal NOT.
    // move PALBOSS2RUNVELS seekplayer
ends

state boss2runenemystate
  ife seemytarget YES
  {
    ifactioncount 3
    {
      ife shootmytarget YES
      {
        resetactioncount
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
		}
		else
        sound BOS1_WALK
      }
      else
        ai AIBOSS2SEEKENEMY
    }
    ifcount 48
      ifrnd 2
    {
      ife targetalive YES
      {
	    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound BOS2_ATTACK
			setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
		}
		else
        sound BOS2_ATTACK
		ifl targetdist 10240 ifrnd 64 ai AIBOSS2LOBBED else
        ai AIBOSS2SHOOTENEMY
      }
      break
    }
  }
  else
    ai AIBOSS2SEEKENEMY
ends

state boss2seekenemystate
  ifrnd 2
  {
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		soundonce BOS2_ROAM
		setactorsoundpitch THISACTOR BOS2_ROAM SHRUNKPITCH
	}
	else
    soundonce BOS2_ROAM
  }
  else
    ifactioncount 3
  {
    resetactioncount
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
	}
	else
    sound BOS1_WALK
  }

  ife seemytarget YES
    ifcount 32
      ife targetalive YES
        ifrnd 48
          ife shootmytarget YES
  {
    ifrnd 64
      ifg targetdist 4096
    {
      ai AIBOSS2RUNENEMY
      // ifspritepal 0 nullop
      // else
        // move PALBOSS2RUNVELS seekplayer
      break
    }

    ifl targetdist 10240
    {
      ifrnd 128
      {
	    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound BOS2_ATTACK
			setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
		}
		else
        sound BOS2_ATTACK
        ai AIBOSS2LOBBED
      }
    }
    else
    {
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound BOS2_ATTACK
			setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
		}
		else
      sound BOS2_ATTACK
      ai AIBOSS2SHOOTENEMY
    }
  }
ends

state boss2dyingstate
  ifaction ABOSS2DEAD
  {
    ifspritepal 0
      break
    // ifrespawn ife gametype 0
      // ifcount RESPAWNACTORTIME
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength PIGCOPSTRENGTH
      // state checkboss2seekstate
    // }
    // else
    // {
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
        killit
      }
      break
    // }
  }
  ifactioncount 9
  {
    iffloordistl 8
      sound THUD
    action ABOSS2DEAD
    cstat 0
	ife attmode YES ife initsprite 1996 { set initsprite 0 spritepal 0 }
	ife initsprite 1996
	{
		ife rainpal SCRAPFALL set raining 0
		starttrackslot 6 29
		add ancients 1
	}
	else
    ifspritepal 0
	{
		setuserdef[].display_bonus_screen 1
		ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
		ife marathon YES 
		{
			setp[].timebeforeexit 150
			set cutscene_ready 1254
		}
		else
		{
			endofgame 60
			set cutscene_ready 1254
			setuserdef[].volume_number 2
			ife attmode YES set attmid 4
		}
	}
  }
ends

state boss2lobbedstate
  ife seemytarget YES
  {
    ifactioncount 2
      resetactioncount
    else
      ifactioncount 1
    {
	  geta[].z savz
	  set z savz
	  set z2 sprite[].yrepeat
	  mul z2 160
	  sub z z2
	  seta[].z z
	  
	  ife initsprite 1996
	  {
		rand zdist 6144
		sub zdist 3072
		
		setprojectile[FTPROJ].workslike 38914
		setprojectile[FTPROJ].xrepeat 64
		setprojectile[FTPROJ].yrepeat 64
		
		ezshoot zdist FTPROJ
		rand angvar 256
		sub angvar 128
		add angvar sprite[].ang
		seta[RETURN].ang angvar
		
		ezshoot zdist FTPROJ
		rand angvar 256
		sub angvar 128
		add angvar sprite[].ang
		seta[RETURN].ang angvar
		
		setprojectile[FTPROJ].workslike 34818
		setprojectile[FTPROJ].xrepeat 32
		setprojectile[FTPROJ].yrepeat 32
	  }
	  else
      ifrnd 128
	  {
		ife bottarget -1
        shoot COOLEXPLOSION1
		else
		ife bottarget player[].i
		shoot COOLEXPLOSION1 else
		{
		  eshoot MEGAPROJ
		  rand angvar 256
		  sub angvar 128
		  add angvar sprite[].ang
		  seta[RETURN].ang angvar
		}
	  }
		
	  seta[].z savz
    }
    else
      ifcount 64
	  {
	    ife initsprite 1996
		{
			ifcount 90
			ifrnd 16
				state checkboss2seekstate
		}
		else
        ifrnd 16
          state checkboss2seekstate
	  }
  }
  else
    state checkboss2seekstate
ends

state boss2shootenemy

  ife targetalive NO count 80
  ifcount 72
    state checkboss2seekstate
  else
    ifaction ABOSS2SHOOT
      ifactioncount 2
  {
    geta[].z savz
	sub savz 2048
	seta[].z savz
    eshoot RPG
	add savz 2048
	seta[].z savz
	ifn sprite[].pal 0 seta[RETURN].extra 50
    resetactioncount
  }
ends

state checkboss2hitstate

  ifwasweapon GROWSPARK sound SPARKLESOUND
  ifrnd 2
    spawn BLOODPOOL2
  ifdead
  {
    ifspritepal 0
      globalsound DUKE_TALKTOBOSSFALL
	else
	ife initsprite 1996
	  globalsound DUKE_TALKTOBOSSFALL
    else
    {
      ife sprite[].htowner player[].i
        globalsound DUKE_TALKTOBOSSFALL
      ifwasweapon FREEZEBLAST
	  ife shrunken 0
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action ABOSS2FROZEN
        strength 0
		state freezeme
        break
      }
	  ifwasweapon GROWSPARK
	  {
		  cstat 0
		  sound ACTOR_GROWING
		  ai AIBOSS2GROW
		  break
	  }
    }

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS2_DYING
		setactorsoundpitch THISACTOR BOS2_DYING SHRUNKPITCH
	}
	else
    sound BOS2_DYING

    addkills 1 state enemy_death
	ife attmode 1 ifrnd 64 spawn NUKESPRITE

    ai AIBOSS2DYING
  }
  else
  {
    ifrnd 144
    {
      ifrnd 32
	  ifg TMP_A 6
	  ifn initsprite 1996
	  ifn sprite[].htpicnum BURNING
	  ifn sprite[].htpicnum RADWOUND
      { 
		action BOSS2FLINTCH
		move 0
      }
      else
      {
	    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound BOS2_ATTACK
			setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
		}
		else
        sound BOS2_ATTACK
		ifl targetdist 10240 ifrnd 64 ai AIBOSS2LOBBED else
        ai AIBOSS2SHOOTENEMY
      }
    }

    ifspritepal 0 nullop
	else
	ife initsprite 1996 nullop
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
	  set shrunken 1
      // ai AIBOSS2PALSHRINK
      break
    }

	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS2_PAIN
		setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
	}
	else
    soundonce BOS2_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends


state boss2code

  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  { state standard_jibs state standard_jibs }
  ifspritepal 29 ife initsprite 1996 spritepal 19
  
  ifai AIBOSS2GROW { state genericgrowcode break }
  
  ifaction ABOSS2FROZEN
  {
    ifcount THAWTIME
    {
	  getlastpal
      ai AIBOSS2SEEKENEMY
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  lotsofglass 10
      state standard_jibs
	  state standard_jibs
      sound GLASS_BREAKING
      ifrnd 84 spawn BLOODPOOL
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  ifspritepal 19 set burning 0
  ife attmode NO
  ife initsprite 1996 ife spawnprotect 0 ife monstatus 1 ifpdistg 16384 ifrnd 1 set spawnprotect -45
  
  ifai 0
  {
    ifspritepal 0
	{
      ai AIBOSS2RUNENEMY
	  sizeat 80 64
	}
	else
	ife initsprite 1996
	{
	  ai AIBOSS2RUNENEMY
	  sizeat 105 80
	}
    else
    {
      strength BOSS1PALSTRENGTH
	  set inithp BOSS1PALSTRENGTH
	  sizeat 44 38
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		{
			sound BOS2_ATTACK
			setactorsoundpitch THISACTOR BOS2_ATTACK SHRUNKPITCH
		}
		else
      sound BOS2_ATTACK 
	  ai AIBOSS2SHOOTENEMY
    }
  }
  
  state monsterai
  
  ifaction BOSS2FLINTCH
  {
    ifactioncount 3
      ai AIBOSS2SEEKENEMY
  }
  else
    ifai AIBOSS2SEEKENEMY
      state boss2seekenemystate
  else
    ifai AIBOSS2RUNENEMY
      state boss2runenemystate
  else
    ifai AIBOSS2SHOOTENEMY
      state boss2shootenemy
  else
    ifai AIBOSS2LOBBED
      state boss2lobbedstate
  // else
    // ifai AIBOSS2PALSHRINK
      // state boss2palshrunkstate
  
  ifai AIBOSS2DYING
    state boss2dyingstate
  else
  { 
	ife monstatus -1 break
	geta[].htextra TMP_A
    ifhitweapon state checkboss2hitstate
    else
      ife targetalive YES
			ifpdistl 1280
				ife pdown NO
					ife spawnprotect 0
    {
	  ifspritepal 0
	  {
		  addphealth -1000
		  palfrom 63 63
	  }
	  else
	  ife initsprite 1996
	  {
		  addphealth -1000
		  palfrom 63 63
	  }
	  else ifvarand monstflags 256
	  {
		addphealth -20
		palfrom 50 63
	  }
    }
	ifspritepal 0
	state squish_bot
	else
	ife initsprite 1996
	state squish_bot
	else
	ifvarand monstflags 256
	state squish_bot
  }


ends

actor BOSS2 BOSS2STRENGTH fall state boss2code enda
useractor enemystayput BOSS2STAYPUT cstator 257 ifspritepal 0 sizeat 80 64 else sizeat 40 32 cactor BOSS2 state boss2code enda

action ABOSS3WALK        0  4  5  1  30
action ABOSS3FROZEN        0  1  5
action ABOSS3RUN         0  4  5  1  15
action ABOSS3LOB        20 2  5  1  50
action ABOSS3LOBBING      30 2  5  1  15
action ABOSS3DYING        40 8  1  1  20
action BOSS3FLINTCH       30 1  5  1  1
action ABOSS3DEAD         48

action ABOSS3RAMPREP	 -60  1  5  1  70
action ABOSS3RAM         -55  1  5  1  12

action ABOSS3BELLY1		 286  1  5  1  30
action ABOSS3BELLY2		 291  2  5  1  4

action ABOSS3JUMP		 -65  1  5  1  20
action ABOSS3LAND		 -60  1  5  1  20

action ABOSS3RAIN1		 271  1  5  1  100
action ABOSS3RAIN2	     276  2  5  1  6

move PALBOSS3SHRUNKRUNVELS 32
move PALBOSS3RUNVELS 256
move PALBOSS3WALKVELS 192
move BOSS3WALKVELS 208
move BOSS3RUNVELS 300
move BOSS3RAMVELS 700 -8
move BOSS3JUMPVEL 256 -140

ai AIBOSS3SEEKENEMY ABOSS3WALK BOSS3WALKVELS seekplayer
ai AIBOSS3RUNENEMY ABOSS3RUN BOSS3RUNVELS faceplayerslow
ai AIBOSS3LOBENEMY ABOSS3LOB STOPPED faceplayer
ai AIBOSS3DYING ABOSS3DYING STOPPED geth
// ai AIBOSS3PALSHRINK ABOSS3WALK PALBOSS3SHRUNKRUNVELS faceplayer
ai AIBOSS3RAMPREP ABOSS3RAMPREP STOPPED faceplayerslow
ai AIBOSS3RAM ABOSS3RAM BOSS3RAMVELS geth
ai AIBOSS3JUMP ABOSS3JUMP BOSS3JUMPVEL geth
ai AIBOSS3GROW ABOSS3LAND STOPPED geth
ai AIBOSS3RAIN ABOSS3RAIN1 STOPPED geth

state boss3jumpcheck

	ifceilingdistl 128 break
	geta[].z z
	sub z 4096
	
	cos mycos sprite[].ang
	sin mysin sprite[].ang
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 512
		{
			sub z 10240
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 2048 ai AIBOSS3JUMP
		}
	}
	ifai AIBOSS3JUMP nullop else ifnotmoving ifrnd 8 ai AIBOSS3JUMP

ends

state boss3jumpstate

	ifn myspawner -1
	{
		set spriteid myspawner
		state facesprite
	}
	ifaction ABOSS3JUMP
	{
		ifmove BOSS3JUMPVEL
		{
			ifcount 12 move BOSS3WALKVELS geth
		}
		else
		ifmove BOSS3WALKVELS
		{
			iffloordistl 4
			{
				action ABOSS3LAND
				sound THUD
			}
			
		}
	}
	ifaction ABOSS3LAND ifcount 8 ai AIBOSS3SEEKENEMY

ends

state boss3ramstate

ifai AIBOSS3RAMPREP
{
	ifspritepal 121 getlastpal else spritepal 121
	ifcount 20
	{
		ifspritepal 121 getlastpal 
		globalsound BOS2_RECOG
		globalsound DUKEDEV
		sound ROCKETLOOP
		ai AIBOSS3RAM
	}
	break
}

seta[].ang lastang
ifnotmoving
{
	stopactorsound THISACTOR ROCKETLOOP
	ife seeplayer YES ifpdistl 8192 quake 30
	debris SCRAP1 2
	debris SCRAP2 2
	debris SCRAP3 2
	debris SCRAP4 2
	debris SCRAP5 2
	debris SCRAP6 2
	ifl targetdist 6144
	ife seemytarget YES
	{
		sound RPG_EXPLODE
		setactorsoundpitch THISACTOR RPG_EXPLODE -768
		ifspritepal 0 
		{
			hitradius 5120 20 40 60 80
			// seta[].htextra -1
			ifpdistl 5120 seta[player[].i].htowner THISACTOR
		}
		else 
		{
			hitradius 3072 15 30 45 60
			// seta[].htextra -1
			ifpdistl 3072 seta[player[].i].htowner THISACTOR
		}
		sound BOS2_PAIN
		setactorsoundpitch THISACTOR BOS2_PAIN -768
		ifpdistl 6144
		ifp ponground
		{
			wackplayer 
			wackplayer
			setp[].posxv 0
			setp[].posyv 0
			getp[].posz z
			sub z 1024
			setp[].posz z
			getp[].poszv z
			sub z 3072
			setp[].poszv z
		}
	}
	ai AIBOSS3SEEKENEMY
	action BOSS3FLINTCH
	move STOPPED geth
	
}
	espawn SPEEDBLUR
	spawn EXPLOSION2
	setav[RETURN].mtype sprite[].htdispicnum
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	ifg sprite[].alpha 96 seta[RETURN].alpha sprite[].alpha else
	seta[RETURN].alpha 80
	seta[RETURN].pal sprite[].pal
	seta[RETURN].mdflags 16
	
	ifn bottarget -1
	{
		set z sprite[bottarget].z
		sub z sprite[].z
		shiftr z 3
		movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
	}

ifcount 60 
{
	stopactorsound THISACTOR ROCKETLOOP
	ai AIBOSS3SEEKENEMY
	ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
}

ends

state boss3palshrunkstate
  ifcount SHRUNKDONECOUNT
  {
    ai AIBOSS3SEEKENEMY
	ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
  }
  else
    ifcount SHRUNKCOUNT
      sizeto 44 38
  else
    state genericshrunkcode
ends

state checkboss3seekstate
  ai AIBOSS3SEEKENEMY
  ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
  
ends

state boss3runenemystate

  ifn myspawner -1
  ife seeplayer YES
  {
    set bottarget -1
    set spriteid myspawner
	state facesprite
	ifnotmoving ai AIBOSS3JUMP
	dist temp THISACTOR myspawner
	ifl temp 1560 { ai AIBOSS3RAIN set myspawner -1 }
	break
  }
  ife seemytarget YES
  {
    ifactioncount 3
    {
      ife shootmytarget YES
      {
        resetactioncount
		ife shrunken 0
        sound BOS1_WALK
      }
      else
	  {
        ai AIBOSS3SEEKENEMY
		ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
	  }
    }
  }
  else
  {
    ai AIBOSS3SEEKENEMY
	ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
  }
  state boss3jumpcheck
ends

state boss3seekenemystate
  ifn myspawner -1
  {
    ai AIBOSS3RUNENEMY
	break
  }
  ifrnd 2
  {
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS3_ROAM
		setactorsoundpitch THISACTOR BOS3_ROAM 1560
	}
	else
    soundonce BOS3_ROAM
	
	ifspritepal 0
	{
		set temp inithp
		div temp 3
		ifle sprite[].extra temp
		findnearsprite ROCKETRAIN 65536 myspawner
	}
  }
  else
    ifactioncount 3
    {
      resetactioncount
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
		nullop
		else
      sound BOS1_WALK
    }

  ife seemytarget YES
    ifcount 60
      ifrnd 36
        ife shootmytarget YES
  {
    ifrnd 64
      ifg targetdist 4096
      {
        ai AIBOSS3RUNENEMY
        ifn sprite[].pal 0 move PALBOSS3RUNVELS seekplayer
        break
      }

    ife targetalive YES
      ai AIBOSS3LOBENEMY
  }
  state boss3jumpcheck
ends

state boss3dyingstate
  ifaction ABOSS3DEAD
  {
    ifspritepal 0
      break
    // ifrespawn ife gametype 0
      // ifcount RESPAWNACTORTIME
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength PIGCOPSTRENGTH
      // state checkboss3seekstate
    // }
    // else
    // {
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
        killit
      }
      break
   //  }
  }
  ifactioncount 8
  {
    iffloordistl 8
      sound THUD
    action ABOSS3DEAD
    cstat 0
	ife attmode YES ife initsprite 1996 { set initsprite 0 spritepal 0 }
    ifspritepal 0
	{
		ife initsprite 1996
		{
			ife rainpal SCRAPFALL set raining 0
			starttrackslot 6 29
			add ancients 1
		}
		else
		{
			setuserdef[].display_bonus_screen 1
			ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
			ife marathon YES 
			{
				setp[].timebeforeexit 150
				set cutscene_ready 1253
			}
			else
			{
				endofgame 60
				set cutscene_ready 1253
				setuserdef[].volume_number 1
				ife attmode YES set attmid 4
			}
		}
	}
  }
ends

state boss3rainstate

	set stun 0
	ifaction ABOSS3RAIN1
	{	
		ifactioncount 1
		{
			ifcount 300 nullop else
			{
				ife seeplayer YES nullop else resetactioncount
			}
			action ABOSS3RAIN2
		}
		set countvar 0
		break
	}
	ifaction ABOSS3RAIN2
	{
		add countvar 1
		ifge countvar 5
		{
			set countvar 0
			geta[].z savz
			sub savz 24000
			seta[].z savz
			espawn VERTROCKET
			rand x 1560, sub x 780
			add x sprite[].x
			seta[RETURN].x x
			rand y 1560, sub y 780
			add y sprite[].y
			seta[RETURN].y y
			add savz 24000
			seta[].z savz
		}
		ifactioncount 80
			action ABOSS3LAND
	}
	
	ifaction ABOSS3LAND
	{
		ifactioncount 20
		ai AIBOSS3SEEKENEMY
	}

ends

state boss3lobbedstate

	ife seemytarget NO
	ife bottarget -1
	ifp palive
	ife seeplayer YES
	{
		set bottarget player[].i
		set seemytarget YES
		findplayer targetdist
		set targetalive YES
	}
 
    ifaction ABOSS3LOBBING
      ifactioncount 2
    {
	  set z sprite[].yrepeat
	  mul z 96
	  geta[].z savz
	  
	  set z2 sprite[].z
	  sub z2 z
	  seta[].z z2

	  geta[].x savx
	  geta[].y savy
	  set x sprite[].x
	  set temp sprite[].xrepeat
	  mul temp 12
	  add x temp
	  geta[].ang angvar sub angvar 480
	  rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
	  seta[].x x2
	  seta[].y y2
		
	  eshoot RPG
	  ife initsprite 1996 
	  { 
		seta[RETURN].xvel 966 geta[RETURN].zvel z mul z 3 div z 2 seta[RETURN].zvel z 
		setav[RETURN].mtype CANHEAD 
		seta[RETURN].extra 80
	  }
	  else
	  ifn sprite[].pal 0 seta[RETURN].extra 50
	  
  
	  seta[].x savx
	  seta[].y savy
		
	  eshoot RPG
	  ife initsprite 1996 
	  { 
		 seta[RETURN].xvel 966 geta[RETURN].zvel z mul z 3 div z 2 seta[RETURN].zvel z 
		 setav[RETURN].mtype CANHEAD 
		 seta[RETURN].extra 80
	  }
	  else
	  ifn sprite[].pal 0 seta[RETURN].extra 50
	  seta[].z savz
	  
      resetactioncount
      ifrnd 10
	  ifcount 50
	  {
        ai AIBOSS3SEEKENEMY
		ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
	  }
    }
	
	ifaction ABOSS3BELLY2
	 ifactioncount 1
	{
	  
	  ifspritepal 0
	  {
		state hitscan_targetprep
		state randzshoot
		zshoot zdist CHAINGUN
	  }
	  state hitscan_targetprep
	  state randzshoot
	  zshoot zdist CHAINGUN
	  sound M4FIRE
	  
      resetactioncount
      ifrnd 10
	  ifcount 40
	  {
        ai AIBOSS3SEEKENEMY
		ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
	  }
	}
	
	ifaction ABOSS3BELLY1
	{
		ifactioncount 1
		action ABOSS3BELLY2
	}

	ifaction ABOSS3LOB
	{
		ifcount 10 nullop else ifcount 9
		{
			ifrnd 96 
			action ABOSS3BELLY1
		}
		ifactioncount 2
		{
		  action ABOSS3LOBBING
		  resetcount
		}
	}
  
  ife seemytarget NO
    state checkboss3seekstate
	
ends

state checkboss3hitstate

  ifspritepal 121 getlastpal 
  ifwasweapon GROWSPARK sound SPARKLESOUND
  ifrnd 2
    spawn BLOODPOOL2
  ifdead
  {
    stopactorsound THISACTOR ROCKETLOOP
    ifspritepal 0
      globalsound DUKE_TALKTOBOSSFALL
    else
    {
      ife sprite[].htowner player[].i
        globalsound DUKE_TALKTOBOSSFALL
      ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move STOPPED geth
        action ABOSS3FROZEN
        strength 0
		state freezeme
        break
      }
	  ifwasweapon GROWSPARK
	  {
		  cstat 0
		  sound ACTOR_GROWING
		  ai AIBOSS3GROW
		  break
	  }
    }

    addkills 1 state enemy_death
    ai AIBOSS3DYING
    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS3_DYING
		setactorsoundpitch THISACTOR BOS3_DYING 1560
	}
	else
    sound BOS3_DYING
    sound JIBBED_ACTOR9

  }
  else
  {
    ifaction BOSS3FLINTCH nullop else
    ifai AIBOSS3RAMPREP nullop else
	ifai AIBOSS3RAM nullop else
	ifai AIBOSS3RAIN nullop else
    {
	  // TMP_A is amount of damage taken
	  ifg TMP_A 150 set TMP_A 150
	  rand temp 250
	  
	  ifle temp TMP_A // some kind of reaction
	  {
	    set temp YES
		ifn shrunken 0 set temp NO
		ifcount 30 nullop else set temp NO
		ife temp YES
		{
			ifactornotstayput
			ifrnd 160 ai AIBOSS3RAMPREP
			else
			{
				action BOSS3FLINTCH
				move 0
			}
		}
	  }
	  else ifai AIBOSS3SEEKENEMY
	  ifrnd 48
	  {
		ai AIBOSS3LOBENEMY
	  }
	  else ifai AIBOSS3RUNENEMY
	  ifrnd 24
	  {
	    ai AIBOSS3LOBENEMY
	  }
    }

    ifspritepal 0 nullop
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      // ai AIBOSS3PALSHRINK
	  set shrunken 1
      break
    }
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound BOS3_PAIN
		setactorsoundpitch THISACTOR BOS3_PAIN 1560
	}
	else
    soundonce BOS3_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends

state boss3code

  ifai AIBOSS3GROW { state genericgrowcode break }
  ife attmode YES nullop else
  ifai AIBOSS3RAM nullop else
  ife initsprite 1996 ife spawnprotect 0 ife monstatus 1 ifpdistg 16384 ifrnd 1 set spawnprotect -45
  
  ifspritepal 29 ife initsprite 1996 spritepal 0
   
  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  { state standard_jibs state standard_jibs }

  ifaction ABOSS3FROZEN
  {
    ifcount THAWTIME
    {
      ai AIBOSS3SEEKENEMY
	  ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
      getlastpal
	  strength 1
	  break
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }

      addkills 1 state enemy_death
	  state standard_jibs
	  state standard_jibs
	  lotsofglass 10
      ifrnd 84
        spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  ifai 0
  {
    ifspritepal 0
	{
      ai AIBOSS3RUNENEMY
	  sizeat 64 60
	  ife marathon YES orvar monstflags 512
	  findnearsprite ROCKETRAIN 65536 myspawner
	}
    else
    {
      strength LIZBOSSPALSTRENGTH
	  set inithp LIZBOSSPALSTRENGTH
      ai AIBOSS3LOBENEMY
	  sizeat 44 38
    }
  }
  state monsterai
    ifaction BOSS3FLINTCH
    {
      ifactioncount 3
	  {
		ai AIBOSS3SEEKENEMY
		ifn sprite[].pal 0 move PALBOSS3WALKVELS seekplayer
	  }
    }
  else
    ifai AIBOSS3RAIN
	  state boss3rainstate
  else
    ifai AIBOSS3RAMPREP
	  state boss3ramstate
  else
    ifai AIBOSS3RAM
	  state boss3ramstate
  else
    ifai AIBOSS3SEEKENEMY
      state boss3seekenemystate
  else
    ifai AIBOSS3RUNENEMY
      state boss3runenemystate
  else
    ifai AIBOSS3JUMP
	  state boss3jumpstate
  else
    ifai AIBOSS3LOBENEMY
      state boss3lobbedstate
  // else
    // ifai AIBOSS3PALSHRINK
      // state boss3palshrunkstate

  

  ifai AIBOSS3DYING
    state boss3dyingstate
  else
  {
    ife monstatus -1 break
	geta[].htextra TMP_A
	ifai AIBOSS3RAIN { ifg TMP_A 1 { div TMP_A 2 seta[].htextra TMP_A } }
    ifhitweapon
      state checkboss3hitstate
    else
      ife targetalive YES
        // ifspritepal 0
          ifpdistl 1280
			ife pdown NO
				ife spawnprotect 0
      {
	    ifspritepal 0
		{
			addphealth -1000
			palfrom 63 63
		}
		else ifvarand monstflags 256
		{
			addphealth -20
			palfrom 50 63
		}
      }
	  ifspritepal 0
	  state squish_bot
	  else
	  ifvarand monstflags 256 state squish_bot
  }
ends

actor BOSS3 BOSS3STRENGTH fall state boss3code enda

useractor enemystayput BOSS3STAYPUT BOSS3STRENGTH 

ifspritepal 0 sizeat 64 60
else sizeat 44 38
cstat 257

cactor BOSS3

enda


action ACOMMBREETH  0  3  5  1  30
action ACOMMBREETHBACK  10  3  5  -1  30
action ACOMMFROZEN  0  1  5
action ACOMMSPIN   -5  1  5  1  12
action ACOMMGET   0  3  5  1  30
action ACOMMSHOOT  20  1  5   1 35
action ACOMMABOUTTOSHOOT 15 1 5 1 30
action ACOMMDYING  30  8  1  1  12
action ACOMMFLINTCH 25 1 5 1 30
action ACOMMDEAD   38  1  1  1  1

action ACOMMHEADSHOT  15680  5  1  1  16
action ACOMMHEADSHOTDEAD  15685

move COMMGETUPVELS 128 -64
move COMMGETVELS 128 64
move COMMSLOW 64 24

ai AICOMMWAIT ACOMMBREETH STOPPED faceplayerslow
ai AICOMMGET ACOMMBREETH COMMGETVELS seekplayer
ai AICOMMSHOOT ACOMMSHOOT STOPPED faceplayerslow
ai AICOMMABOUTTOSHOOT ACOMMABOUTTOSHOOT STOPPED faceplayerslow
ai AICOMMSPIN ACOMMSPIN STOPPED spin
ai AICOMMDYING ACOMMDYING STOPPED geth
// ai AICOMMSHRUNK ACOMMGET COMMSLOW furthestdir
ai AICOMMGROW ACOMMBREETH STOPPED furthestdir
ai AICOMMFLINTCH ACOMMFLINTCH STOPPED faceplayer

state spawn_commjibs

	sound SQUISHED
	state lite_jibs
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT break
	geta[].z z
	sub z 8192
	seta[].z z
	espawn COMMJIB
	seta[RETURN].pal sprite[].pal
	seta[RETURN].ang sprite[].htang
	ifrnd 128 seta[RETURN].cstat 4
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	
	state comm_body_jibs
ends

state checkcommhitstate

  geta[].htextra tempb
  ifhitweapon
  {
    ifn sprite[].htpicnum FREEZEBLAST
	ifn sprite[].htpicnum SHRINKSPARK
	ifg tempb 3
    guts JIBS6 2

	  ifwasweapon HEADJIB1 // HEADSHOT!
	  {
		// sound PRED_DYING
		sound SQUISHED
		cstat 0
		strength -1
		
		ai AICOMMDYING
		action ACOMMHEADSHOT
		// guts HEADJIB1 1
		rand zdist 1024
		  sub zdist 3500
		  ezshoot zdist COMMHEADPROJ
		  rand angvar 512
		  sub angvar 256
		  add angvar sprite[].htang
		  seta[RETURN].ang angvar
		  seta[RETURN].pal sprite[].pal
		state random_wall_jibs
		addkills 1 state enemy_death
		ifrnd SPAWNAMMOODDS 
		ife gametype 0
		{
			ife attmode 1
			ifrnd 32 spawn NUKESPRITE else
			spawn RPGSPRITE
		}
		break
	  }
    ifdead
    {
      ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action ACOMMFROZEN
        strength 0
		state freezeme
        break
      }
      else
        ifwasweapon GROWSPARK
      {
        sound ACTOR_GROWING
        ai AICOMMGROW
        break
      }

      addkills 1 state enemy_death
	  ife gametype 0
	  ifrnd SPAWNAMMOODDS spawn RPGSPRITE

      ifwasweapon RADIUSEXPLOSION
      {
		state spawn_commjibs
        killit
      }
      else
        ifwasweapon RPG
      {
        state spawn_commjibs
        killit
      }
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		sound COMM_DYING
		setactorsoundpitch THISACTOR COMM_DYING 1560
	  }
	  else
      sound COMM_DYING
      ai AICOMMDYING
    }
    else
    {
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		soundonce COMM_PAIN
		setactorsoundpitch THISACTOR COMM_PAIN SHRUNKPITCH
	  }
	  else
      soundonce COMM_PAIN
      ifwasweapon SHRINKSPARK
      {
        sound ACTOR_SHRINKING
        // ai AICOMMSHRUNK
		set shrunken 1
      }
      else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	  
      ifai AICOMMFLINTCH nullop else
      ifai AICOMMABOUTTOSHOOT nullop else
      ifai AICOMMSHOOT nullop else
      ifvarg stun 0 ai AICOMMFLINTCH
      else
      ifrnd 24 
	  ifn sprite[].htpicnum BURNING
	  ifn sprite[].htpicnum RADWOUND
	  ai AICOMMFLINTCH
        ifrnd 32
		ife targetalive YES
          ai AICOMMABOUTTOSHOOT
    }
  }
ends

actor COMMANDERSTAYPUT COMMANDERSTRENGTH
  cactor COMMANDER
  ai AICOMMABOUTTOSHOOT
enda

actor COMMANDER COMMANDERSTRENGTH

  state checksquished

  ifaction ACOMMFROZEN
  {
    fall

    ifcount THAWTIME
    {
      getlastpal
      ai AICOMMWAIT
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  ife gametype 0
	  ifrnd SPAWNAMMOODDS spawn RPGSPRITE
	  lotsofglass 10
      ifrnd 84
        spawn BLOODPOOL
      state standard_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  
  ifaction ACOMMBREETH ifactioncount 3 action ACOMMBREETHBACK else
  ifaction ACOMMBREETHBACK ifactioncount 3 action ACOMMBREETH
  
  ifai 0
  {
    ifspritepal 24 orvar initflags 32
    ife gametype 0 ai AICOMMSHOOT
	else
	{
		ifge SKILL 4 ai AICOMMWAIT
		else
		ife targetalive YES
		ai AICOMMSHOOT
		else
		ai AICOMMWAIT
	}
  }
  
  state monsterai 
  
  ifai AICOMMWAIT
  {
    ifcount 20
    {
      ife seemytarget YES
      {
        ife shootmytarget YES
        {
          ifrnd 96
            ai AICOMMGET
          else
            ai AICOMMABOUTTOSHOOT
        }
      }
      else
        ai AICOMMGET
    }
  }
  else
    ifai AICOMMABOUTTOSHOOT
  {
    ifactioncount 2
    {
      ife seemytarget YES
        ai AICOMMSHOOT
      else
      {
        ai AICOMMGET
        break
      }
    }
    ifrnd 32
	{
	   ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		soundonce COMM_ATTACK
		setactorsoundpitch THISACTOR COMM_ATTACK SHRUNKPITCH
	  }
	  else
		soundonce COMM_ATTACK
	}
  }
  else
    ifai AICOMMSHOOT
  {
    ife shootmytarget YES
    {
      ifcount 24
        ifrnd 16
          ai AICOMMWAIT
      ifactioncount 2
      {
        ifvarand initflags 32
	    {
			eshoot RPG
			setav[RETURN].mtype CANHEAD
	    }
	    else
        shoot RPG
        resetactioncount
      }
    }
    else
      ai AICOMMGET
  }
  else
    // ifai AICOMMSHRUNK
  // {
    // ifcount SHRUNKDONECOUNT
      // ai AICOMMGET
    // else
      // ifcount SHRUNKCOUNT
        // sizeto 48 40
    // else
      // state genericshrunkcode
  // }
  // else
    ifai AICOMMGROW
      state genericgrowcode
  else
    ifai AICOMMGET
  {
    ifnotmoving
      ifrnd 4
        operate
		
	set temp NO
	ifl targetdist 1024 set temp YES
	else ifrnd 2 set temp YES
	
	ifge SKILL 4 ifn myspawner -1 set temp YES
	
	ifnotmoving nullop else
	ife temp YES
	ife shrunken 0
    ifl targetdist 8192 // 1024
	ife seemytarget YES
      ife targetalive YES
    {
	  ifn sprite[].htactorstayput -1 ifn sprite[].sectnum player[].cursectnum nullop else
	  {
      sound COMM_SPIN
      ai AICOMMSPIN
	  set dodgetime 3
	  ifn myspawner -1 { set spriteid myspawner state facesprite state incoming_eval }
	  else
	  ifn bottarget -1 { set spriteid bottarget state facesprite set angvel sprite[].ang }
	  else
	  set angvel sprite[].ang
	  ifvarand monstflags YES xorvar monstflags YES
	  set myspawner -1
      break
	  }
    }
	set myspawner -1
    ife seemytarget YES
    {
      ife targethigher YES
	  {
	    ifmove COMMGETUPVELS nullop else
        move COMMGETUPVELS getv geth faceplayer
	  }
      else
	  {
	    ifmove COMMGETVELS nullop else
        move COMMGETVELS getv geth faceplayer
	  }
    }
	
	set temp NO
	ifge SKILL 4
	{
		ifcount 30
		ifrnd 3
			set temp YES
	}
	else
    ifcount 60
      ifrnd 2
	  set temp YES
	  
	
	ife temp YES
        ai AICOMMABOUTTOSHOOT
  }
  else
    ifai AICOMMSPIN
  {
    set dodgetime 3
    soundonce COMM_SPIN
	geta[].ang angvar
	add angvar 48
	seta[].ang angvar
	cos xvel angvel
	sin yvel angvel
	shiftr xvel 5
	shiftr yvel 5

	set zdist 0
	ifn bottarget -1
	{
		geta[bottarget].z zdist
		sub zdist sprite[].z
		
		ldist xydist THISACTOR bottarget
		ife xydist 0 set xydist 1
		shiftl zdist 10
		div zdist xydist
	}
	ifvarand monstflags 16 { mul xvel 3 div xvel 2 mul yvel 3 div yvel 2 state makespeedblur }
	movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
	ifn RETURN 0
	{
		// bumping something
		sub RETURN 49152
		ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
		{
			sound SQUISHED
			set tempb CAPTSPINNINGPLAYER
			mul tempb -5
			
			seta[RETURN].htextra tempb
			seta[RETURN].htpicnum RPG
			seta[RETURN].htowner THISACTOR
			seta[RETURN].htang angvel
		}
		ai AICOMMWAIT
		ifge SKILL 4 count 20
	}
	else ifcount 30 ai AICOMMWAIT
  }
  else ifai AICOMMFLINTCH { ifvarg stun 0 resetactioncount ifactioncount 2 ai AICOMMGET }

  ifai AICOMMDYING
  {
    fall
    strength 0

    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      spawn BLOODPOOL2
      state standard_jibs
	  ifaction ACOMMDEAD
	  state comm_body_jibs
	  else ifaction ACOMMHEADSHOTDEAD
	  {
		rand zdist 1024
		sub zdist 3500
		ezshoot zdist COMMARMPROJ
		rand angvar 2047
		seta[RETURN].ang angvar
		seta[RETURN].pal sprite[].pal
		
		rand zdist 1024
		sub zdist 3500
		ezshoot zdist COMMARMPROJ
		rand angvar 2047
		seta[RETURN].ang angvar
		seta[RETURN].pal sprite[].pal
		seta[RETURN].cstat 132
	  }
      killit
    }

    ifaction ACOMMDYING
      ifactioncount 8
    {
      iffloordistl 8
        sound THUD
      cstat 0
      action ACOMMDEAD
    }
	
	ifaction ACOMMHEADSHOT
	  ifactioncount 5
	{
	  iffloordistl 8
        sound THUD
      cstat 0
      action ACOMMHEADSHOTDEAD
    }
  }
  else
  {
    ifrnd 2
	{
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		soundonce COMM_ROAM
		setactorsoundpitch THISACTOR COMM_ROAM SHRUNKPITCH
	  }
	  else
      soundonce COMM_ROAM
	}
	ifai AICOMMSPIN { ifvarand monstflags YES xorvar monstflags YES } else
	ifg gametype 0
	{
		ifg navpoint -1
		ife monstatus 1
		{
			orvar monstflags YES
		    ifmove STOPPED nullop else move STOPPED geth
			ifn bottarget -1 { set spriteid bottarget state facesprite }
			ifaction ACOMMFLINTCH nullop else
			ife stun 0
			{
				cos xvel angvel
				sin yvel angvel
				// shiftr xvel 7
				// shiftr yvel 7
				div xvel 96, div yvel 96
				geta[navpoint].z zdist
				ife sprite[navpoint].picnum BLUEFLAG sub zdist 4096 else
				ife sprite[navpoint].picnum REDFLAG sub zdist 4096
				sub zdist sprite[].z
				
				ldist xydist THISACTOR navpoint
				ife xydist 0 set xydist 1
				shiftl zdist 8
				div zdist xydist
				ifn shrunken 0 { shiftr xvel 2 shiftr yvel 2 }
				ifvarand monstflags 16 { mul xvel 3 div xvel 2 mul yvel 3 div yvel 2 state makespeedblur }
				movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
				ifn RETURN 0
				{
					// bumping something
					sub RETURN 49152
					ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
					{
						set B NO
						ife actorvar[RETURN].monstatus 1 
							ife actorvar[RETURN].team team set B YES
						ifvarand sprite[RETURN].cstat 16 set B YES
						ife B YES
						{
							set x2 sprite[].x
							add x2 256
							rotatepoint sprite[].x sprite[].y x2 sprite[].y angvel x y
							setsprite THISACTOR x y sprite[].z
							seta[].htmovflag 0
						}
					}
					else ifg navpoint -1 set navpoint -1
				}
			}
		}
		else
		{
			ifvarand monstflags YES xorvar monstflags YES
			ifmove STOPPED
			{
				ifai AICOMMGET move COMMGETVELS seekplayer
			}
		}
	}
	
    state checkcommhitstate
  }
enda


// RIOT TANK with Pigcop inside

action ATANKSPIN   0 2 5 1 4
action ATANKSHOOTING 10 2 5 1 10
action ATANKWAIT 0 1 5 1 1
action ATANKDESTRUCT 0 1 5 1 1
action ATANKDEAD 0 1 5 1 1
// move TANKVEL horiz vert
move TANKFORWARD 112
move TANKSTOP

eventloadactor TANK sizeat 60 60 enda

useractor enemy TANK TANKSTRENGTH 

  fall

  state monsterai
  ifn myspawner -1
  {
    ifg sprite[myspawner].statnum 2 set myspawner -1 else
	ifl sprite[myspawner].extra 1 set myspawner -1 else
	ife actorvar[myspawner].monstatus 2 set myspawner -1 else
	ifn sprite[myspawner].picnum PIGCOP ifn sprite[myspawner].picnum PIGCOPDIVE set myspawner -1
	
	ifn myspawner -1
	ifg sprite[myspawner].xvel 0
	// ifge sprite[].z sprite[myspawner].z
	{
		geta[].ang angvar
		add angvar 1024
		set x2 sprite[].x
		add x2 1024
		rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
		geta[myspawner].x temp
		sub x temp
		div x 20
		geta[myspawner].x x2
		add x2 x
		// seta[myspawner].x x2
		geta[myspawner].y temp
		sub y temp
		div y 20
		geta[myspawner].y y2
		add y2 y
		// seta[myspawner].y y2
		setsprite myspawner x2 y2 sprite[].z	
		// updatesectorz x y sprite[].z temp
		// ifn temp -1 setsprite myspawner x y sprite[].z
	}
  }
  // else findnearspritez PIGCOP 6144 16384 myspawner
  
  
  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
  ife player[].actorsqu THISACTOR
  ife player[].knee_incs 14
  action ATANKDEAD
  
  ifg shrunken 0
  {
	clipdist 16
	cstat 256
	set temp shrunken
	add temp 1
	ife temp SHRUNKCOUNT { clipdist 100 cstator 257 }
  }
  ifaction 0
  {
    sizeat 60 60
    action ATANKWAIT
    cstat 257
    clipdist 100
	ifspritepal 24 orvar initflags 32
  }
  else
    ifaction ATANKSPIN
  {
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		ifactorsound THISACTOR TANK_ROAM nullop else sound TANK_ROAM
		setactorsoundpitch THISACTOR TANK_ROAM SHRUNKPITCH
	}
	else
    ifactorsound THISACTOR TANK_ROAM nullop else sound TANK_ROAM
	
	set temp NO
	ifge SKILL 4 { ifactioncount 10 set temp YES }
	else ifactioncount 20 set temp YES
	
	ife temp YES
    {
      ifrnd 16
        ife seemytarget YES
          ife shootmytarget YES
      {
        move TANKSTOP geth
        action ATANKSHOOTING
        stopactorsound THISACTOR TANK_ROAM
      }
    }

    ifrnd 16
      move TANKFORWARD seekplayer
  }
  else
    ifaction ATANKSHOOTING
  {
    ifactioncount 22
    {
      ifg targetdist 4096 ife shrunken 0
      {
        sound BOS1_ATTACK2
		ifvarand initflags 32
		{
			eshoot RPG
			setav[RETURN].mtype CANHEAD
		}
		else
        shoot MORTER
      }
      resetcount
      move 0 action ATANKWAIT
    }
    else
      ifactioncount 2
    {
      ife seemytarget YES
      {
        ifl targetdist 16384
        {
		  add countvar 1
          ifge countvar 2
          {
		    set countvar 0
		    ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			{
				sound PISTOL_FIRE
				shoot SHRUNKBULLET
				setactorsoundpitch THISACTOR PISTOL_FIRE SHRUNKPITCH
			}
			else
			{
				espawn SOUNDPLATE
				setav[RETURN].SPRITELOTAG HEAVY_RIFLE
				setav[RETURN].mtype 1
				ifn bottarget -1
				{
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					geta[].z savz
					set z savz
					sub z 10240
					seta[].z z
					state hitscan_targetprep
					zshoot zdist SHOTSPARK1
					seta[].z savz
				}
				else
				{
					shoot SHOTSPARK1
					shoot SHOTSPARK1
				}
			}
            
          }
        }
        else
        {
		  add countvar 1
          ifge countvar 4
		  {
		    ife countvarb 1 set countvarb 0 else set countvarb 1
			
		    set countvar 0
			  // ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				// {
					// sound PRED_ATTACK
					// setactorsoundpitch THISACTOR PRED_ATTACK SHRUNKPITCH
				// }
				// else
			  // {
			    // espawn SOUNDPLATE
				// setav[RETURN].SPRITELOTAG LASERSHOOT
				// setav[RETURN].mtype 1
			  // }
			  sound TERMLASER
			  eshoot GREENLASER 
			  set zdist sprite[RETURN].z
			  rand z 5120
			  sub zdist z
			  seta[RETURN].z zdist
			  
			  set angvar sprite[].ang
			  ife countvarb 0 add angvar 384 else sub angvar 384
			  set x2 sprite[].x
			  add x2 320
			  rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
				
			  setsprite RETURN x y zdist
		
			  // shoot FIRELASER
		  }
        }
      }
      else
      {
        move TANKFORWARD seekplayer
        action ATANKSPIN
      }
    }

    ifrnd 16
    {
      stopactorsound THISACTOR TANK_ROAM
      move TANKSTOP faceplayerslow
    }
  }
  else
    ifaction ATANKWAIT
  {
    ifactioncount 32
    {
      move TANKFORWARD seekplayer
      action ATANKSPIN
    }
  }
  else
    ifaction ATANKDESTRUCT
  {
    	
    ifactioncount 64
      action ATANKDEAD
    else
      ifactioncount 56
       sound LASERTRIP_ARMING
     else
       ifactioncount 48
         sound LASERTRIP_ARMING
     else
       ifactioncount 32
         sound LASERTRIP_ARMING
     else
       ifactioncount 16
         sound LASERTRIP_ARMING
		 
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	setactorsoundpitch THISACTOR LASERTRIP_ARMING SHRUNKPITCH
	
     break
  }
  else
    ifaction ATANKDEAD
  {
    addkills 1 state enemy_death
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	hitradius 1024 3 3 3 3
	else
	{
		hitradius 6144 TOUGH TOUGH TOUGH TOUGH
		ifpdistl 6144 seta[player[].i].htowner THISACTOR
	}
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		soundonce LASERTRIP_EXPLODE
		setactorsoundpitch THISACTOR LASERTRIP_EXPLODE SHRUNKPITCH
	}
	else
    sound LASERTRIP_EXPLODE
    debris SCRAP1 8
    spawn EXPLOSION2
	ife mtype 1
	{
		espawn TANKWRECK3
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	else
    ifrnd 160 
	{
		espawn PIGCOP 
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT 
		{
			setav[RETURN].shrunken 50 
			seta[RETURN].xrepeat 12
			seta[RETURN].yrepeat 10
		}
		else setav[RETURN].burning 120
		
		espawn TANKWRECK
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	else 
	{
		espawn TANKWRECK2
		seta[RETURN].pal sprite[].pal
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
	}
	
	set countvarb 0
	whilevarn countvarb 2
	{
	   rand z 3072
	   add z 2048
	   mul z -1
	   ezshoot z CARXPLODE
	   randvar temp  2047
	   setactor[RETURN].ang temp 
	   randvar temp  192 add temp 128
	   setactor[RETURN].xvel temp 
	   add countvarb 1
	}
	state tech_debris
	state tech_debris
    killit
  }

 
  ife monstatus -1 break
  ifhitweapon
  {
    ifwasweapon SHRINKSPARK
	{
		sound ACTOR_SHRINKING
		set shrunken 1
	}
	else
    ifdead
      action ATANKDEAD
    else
    {
      debris SCRAP1 1
      ifaction ATANKSHOOTING break

      ifrnd 192
      {
        move TANKSTOP geth
        action ATANKSHOOTING
        stopactorsound THISACTOR TANK_ROAM
      }
    }
  }

  ifpdistl 1280
    ifhitspace
      ifp pfacing
        ifangdiffl 512
		{
		  ifstrength 400
		  {
			  action ATANKDESTRUCT
			  set mtype 1
		  }
		  else
		  {
			  addphealth -2
			  soundonce SHORT_CIRCUIT
			  palfrom 32 63 63 63
		  }
		}
enda


action ABOSS4WALK          0 4  5  1  30
action ABOSS4DYING        40 9  1  1  20
action ABOSS4ABOUTTOSHOOT 20 1  5  1  40
action ABOSS4SHOOT        25 2  5  1  10
action ABOSS4LAYIT        50 3  5  1  120
action BOSS4FLINTCH       40 1  1  1  1
action ABOSS4DEAD         49

move BOSS4WALKVELS 128

ai AIBOSS4LAYEGGS ABOSS4WALK BOSS4WALKVELS randomangle geth
ai AIBOSS4SHOOT ABOSS4ABOUTTOSHOOT STOPPED faceplayer
ai AIBOSS4DYING ABOSS4DYING STOPPED geth

state boss4shootstate
  ifaction ABOSS4ABOUTTOSHOOT
    ifactioncount 3
      action ABOSS4SHOOT

  ifaction ABOSS4SHOOT
  {
    ifcount 48
    {
      ifrnd 4
        ai AIBOSS4LAYEGGS
    }
    ifcount 26
      ifrnd 32
    {
      ifrnd 128
      {
        sound SHORT_CIRCUIT
        addphealth -2
      }
      else
      {
        ife pchar 1 sound B_GRUNT else
        sound DUKE_GRUNT
        addphealth -1
      }
      palfrom 20 63 63 63
	  add pshocked 12
	  ifg pshocked 90 set pshocked 90
    }
  }

ends

state boss4layeggs
  ifrnd 2
    sound BOS4_ROAM

  ifaction ABOSS4LAYIT
  {
    ifactioncount 3
      ifcount 32
    {
      ai AIBOSS4LAYEGGS
      ifrnd 32
        move BOSS4WALKVELS furthestdir geth
	  
      espawn NEWBEASTHANG
	  seta[RETURN].pal 15
    }
  }
  else
    ifcount 64
      ifrnd 4
  {
    move 0

    ifrnd 88
    {
      action ABOSS4LAYIT
      sound BOS4_LAY
    }
    else
      ife targetalive YES
        ife seemytarget YES
    {
      ai AIBOSS4SHOOT
      sound BOS4_ATTACK
    }
  }

ends

state boss4dyingstate
  ifaction ABOSS4DEAD
    break
  else
    ifactioncount 9
  {
    iffloordistl 8
      sound THUD

	setuserdef[].display_bonus_screen 1
	ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
	{
		endofgame 60
		set cutscene_ready 1255
		setuserdef[].volume_number 3
		ife attmode YES set attmid 4
	}
	
    action ABOSS4DEAD
    cstat 0
  }
ends

state checkboss4hitstate
  ifrnd 2
    spawn BLOODPOOL

  ifdead
  {
    globalsound DUKE_TALKTOBOSSFALL

    addkills 1 state enemy_death
    ai AIBOSS4DYING

    sound BOS4_DYING
    sound BOSS4_DEADSPEECH
  }
  else
  {
    soundonce BOS4_PAIN
    debris SCRAP1 1
    guts JIBS6 1

    ifaction ABOSS4LAYIT
      break

    ifrnd 16
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
    {
      action BOSS4FLINTCH
      move 0
    }
  }
ends

state boss4code

  state monsterai 
  ifai 0
  {
    ai AIBOSS4LAYEGGS
	ife pchar 1 globalsound B_GETAWAYBITCH
  }
  else
    ifaction BOSS4FLINTCH
    {
      ifactioncount 3
        ai AIBOSS4LAYEGGS
    }
  else
    ifai AIBOSS4LAYEGGS
      state boss4layeggs
  else
    ifai AIBOSS4SHOOT
      state boss4shootstate

  ifai AIBOSS4DYING
    state boss4dyingstate
  else
  {
	ife monstatus -1 break
    ifhitweapon
      state checkboss4hitstate
    else
      ife targetalive YES
        ifpdistl 1280
			ife pdown NO
				ife spawnprotect 0
      {
        addphealth -1000
        palfrom 63 63
      }
	  ifspritepal 0
	  state squish_bot
	  else ifvarand monstflags 256
	  state squish_bot
  }
ends

actor BOSS4STAYPUT BOSS4STRENGTH

  fall
  cactor BOSS4
  spritepal 6
  state boss4code
  getlastpal

enda

actor BOSS4 BOSS4STRENGTH

  fall
  cactor BOSS4
  spritepal 6
  state boss4code
  getlastpal

enda

action ANEWBEASTSTAND     -10
action ANEWBEASTWALKING   0  4  5  1  12
action ANEWBEASTRUNNING   0  4  5  1  8
action ANEWBEASTTHINK     -10   2  5  1  40
action ANEWBEASTSCRATCHING  20  3  5  1  20
action ANEWBEASTDYING     62  8  1  1  15
action ANEWBEASTFLINTCH   14227  1  5  1  10  // 61
action ANEWBEASTLYINGDEAD 69  1  1
action ANEWBEASTSCREAM    40  2  5  1  40
action ANEWBEASTGRAB	  45  1  5  1  50
action ANEWBEASTJUMP      70  2  5  1  50
action ANEWBEASTFALL      80  1  5
action ANEWBEASTDYING2	  87  6  1  1  15  
action ANEWBEASTLYINGDEAD2   92  1  1
action ANEWBEASTFROZEN    0  1  5
action ANEWBEASTDECAP	  	2246  7  1  1  16
action ANEWBEASTDECAPDEAD	2252  1  1  1  10

action ANEWBEASTHANG      0  1  5
useractor enemy NEWBEASTHANG NEWBEASTSTRENGTH
  ifaction 0
  {
    action ANEWBEASTHANG
    cstator 257
    sizeat 32 30
  }
  else
    ifhitweapon
  {
    cactor NEWBEAST
    action ANEWBEASTSTAND
    sound NEWBEAST_PAIN
  }
  else
    // ifspawnedby BOSS4
	ifn sprite[].htpicnum NEWBEASTHANG
      ifcount 200
        ifrnd 1
  {
    cactor NEWBEAST
    action ANEWBEASTSTAND
    sound NEWBEAST_PAIN
  }
enda

action ANEWBEASTHANGDEAD      -1  1  5
useractor enemy NEWBEASTHANGDEAD TOUGH

  ifaction 0
  {
    action ANEWBEASTHANGDEAD
    sizeat 32 30
    cstator 257
  }
  else
    ifhitweapon
  {
    ifdead
    {
      state standard_jibs
      spawn BLOODPOOL
      sound SQUISHED
      killit
    }
    else
    {
      guts JIBS6 1
      sound SQUISHED
    }
  }
enda

move NEWBEASTWALKVEL 182
move NEWBEASTRUNVEL 256
move NEWBEASTJUMPVEL 264
move NEWBEASTSCRATCHVEL 192

ai AINEWBEASTGETENEMY ANEWBEASTWALKING NEWBEASTWALKVEL seekplayer
ai AINEWBEASTDODGE ANEWBEASTRUNNING NEWBEASTRUNVEL dodgebullet
ai AINEWBEASTCHARGEENEMY ANEWBEASTRUNNING NEWBEASTRUNVEL seekplayer
ai AINEWBEASTFLEENEMY ANEWBEASTWALKING NEWBEASTWALKVEL fleeenemy
ai AINEWBEASTSCRATCHENEMY ANEWBEASTSCRATCHING NEWBEASTSCRATCHVEL faceplayerslow
ai AINEWBEASTJUMPENEMY ANEWBEASTJUMP NEWBEASTJUMPVEL jumptoplayer
ai AINEWBEASTTHINK ANEWBEASTTHINK STOPPED
// ai AINEWBEASTSHRUNK ANEWBEASTWALKING SHRUNKVELS fleeenemy
ai AINEWBEASTGROW ANEWBEASTSTAND  STOPPED faceplayerslow
ai AINEWBEASTDYING ANEWBEASTDYING STOPPED geth
ai AINEWBEASTSHOOT ANEWBEASTSCREAM STOPPED faceplayer
ai AINEWBEASTFLINTCH ANEWBEASTFLINTCH STOPPED faceplayerslow
ai AIBEASTBITE ANEWBEASTGRAB STOPPED faceplayer

state beastbitestate

	ife pdown NO ifp palive nullop else 
	ife zombieonplayer THISACTOR { set zombieonplayer -1 set zombiecount -30 }
	
	ifn zombieonplayer THISACTOR
	{
		ai AINEWBEASTGETENEMY
		cstat 257
		break
	}
	else
	{
		cstator 256
		
		getp[].posz z
		add z 9216
		
		getp[].oang angvar
		
		set x2 player[].posx
		add x2 512
		rotatepoint player[].posx player[].posy x2 player[].posy angvar x y
		
		setsprite THISACTOR x y z
		setp[].posxv 0
		setp[].posyv 0
		setp[].jumping_counter 0
		setp[].ang player[].oang
		setp[].horiz 100
	}

ends

state jibshoot

	geta[].z savz
	set temp savz
	sub temp 8192
	seta[].z temp
	espawn JIBS1
	setav[RETURN].mtype YES
	seta[RETURN].xrepeat 16 // 32
	seta[RETURN].yrepeat 16 // 32
	seta[RETURN].xvel 256
	rand temp 1024 mul temp -1 seta[RETURN].zvel temp
	rand temp 128 sub temp 64 add temp sprite[].ang
	seta[RETURN].ang temp
	espawn JIBS2
	setav[RETURN].mtype YES
	seta[RETURN].xrepeat 16 // 32
	seta[RETURN].yrepeat 16 // 32
	seta[RETURN].xvel 256
	rand temp 1024 mul temp -1 seta[RETURN].zvel temp
	rand temp 128 sub temp 64 add temp sprite[].ang
	seta[RETURN].ang temp
	espawn JIBS3
	setav[RETURN].mtype YES
	seta[RETURN].xrepeat 16 // 32
	seta[RETURN].yrepeat 16 // 32
	seta[RETURN].xvel 256
	rand temp 1024 mul temp -1 seta[RETURN].zvel temp
	rand temp 128 sub temp 64 add temp sprite[].ang
	seta[RETURN].ang temp
	espawn JIBS4
	setav[RETURN].mtype YES
	seta[RETURN].xrepeat 16 // 32
	seta[RETURN].yrepeat 16 // 32
	seta[RETURN].xvel 256
	rand temp 1024 mul temp -1 seta[RETURN].zvel temp
	rand temp 128 sub temp 64 add temp sprite[].ang
	seta[RETURN].ang temp
	espawn JIBS5
	setav[RETURN].mtype YES
	seta[RETURN].xrepeat 16 // 32
	seta[RETURN].yrepeat 16 // 32
	seta[RETURN].xvel 256
	rand temp 1024 mul temp -1 seta[RETURN].zvel temp
	rand temp 128 sub temp 64 add temp sprite[].ang
	seta[RETURN].ang temp
	seta[].z savz
ends

state newbeastseekstate

  ifvarand initflags 4 { set tempb 1280 set tempc 6144 } else { set tempb 4096 set tempc 16384 }
  
  ifactornotstayput
  {
    ife targetalive YES
      ife seemytarget YES
        ifl targetdist 1596
    {
      ai AINEWBEASTSCRATCHENEMY
	  ife bottarget myshelly setav[myshelly].myspawner THISACTOR
      break
    }

    ifai AINEWBEASTCHARGEENEMY
    {
      ife targetalive YES
        ifl targetdist 1596
          ife seemytarget YES
      {
        ai AINEWBEASTSCRATCHENEMY
		ife bottarget myshelly setav[myshelly].myspawner THISACTOR
        break
      }

	  set temp NO
	  ifge SKILL 4 { ifrnd 2 set temp YES }
	  else ifrnd 1 set temp YES
	  
      ife temp YES
        // ifpdistg 4096
		ifg targetdist tempb ifl targetdist tempc
          ife targetalive YES
            ife seemytarget YES
      {
        ai AINEWBEASTSHOOT
        break
      }
    }
    else
      ifg targetdist 4096
    {
      ai AINEWBEASTCHARGEENEMY
      break
    }

    iffloordistl 16
    {
      ifcount 32
        ifrnd 16
      {
        ifceilingdistl 96 nullop else
        { ai AINEWBEASTJUMPENEMY break }
      }
      // break
    }

    ifrnd 4
    {
      ifnotmoving
        operate
    }
    else
	{
		geta[].ang angvar
		  ifrnd 16
			ifbulletnear
		{
		  ifgapzl 128
			ai AINEWBEASTDODGE
		  else
			// ifactornotstayput
		  {
			ifrnd 128
			{
			  ifceilingdistl 96 { seta[].ang angvar break }
			  iffloordistl 16
			  {
			    seta[].ang angvar
				ai AINEWBEASTJUMPENEMY
			  }
			}
			else
			  ai AINEWBEASTDODGE
		  }
		  ifai AINEWBEASTDODGE { set dodgetime 8 }
		}
	}
  }
  else
  {
    ifactioncount 16
    {
      ife targetalive YES
        ifl targetdist 1596
          ife seemytarget YES
      {
        ai AINEWBEASTSCRATCHENEMY
		ife bottarget myshelly setav[myshelly].myspawner THISACTOR
        break
      }
    }
    ifcount 16
      ifrnd 32
        move NEWBEASTWALKVEL randomangle geth
  }
  ifai AINEWBEASTJUMPENEMY nullop else
  ifg crumbwait 20 { set crumbwait 0 ai AINEWBEASTJUMPENEMY }

ends

/*
state newbeastshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AINEWBEASTGETENEMY
  else
    ifcount SHRUNKCOUNT
      sizeto 48 40
  else
    state genericshrunkcode
ends
*/

state newbeastfleestate

ifvarand initflags 4 { set tempb 1280 set tempc 6144 } else { set tempb 3500 set tempc 16384 }
  
  ifcount 8
  {
    ifrnd 64
      // ifpdistg 3500
	  ifg targetdist tempb ifl targetdist tempc
        ife targetalive YES
          ife seemytarget YES
            ai AINEWBEASTSHOOT
  }
  else
  {
    iffloordistl 16
    {
      ifnotmoving
        ai AINEWBEASTGETENEMY
    }
    else
      ai AINEWBEASTGETENEMY
    break
  }
ends

state newbeastthinkstate

ifvarand initflags 4 { set tempb 1280 set tempc 6144 } else { set tempb 3500 set tempc 16384 }

  ifrnd 8
    soundonce NEWBEAST_ROAM

  ifactioncount 3
  {
    ifrnd 128
    {
      // ifpdistg 3500
	  ifg targetdist tempb ifl targetdist tempc
        ife targetalive YES
          ife seemytarget YES
            ai AINEWBEASTSHOOT
    }
    else
      ai AINEWBEASTGETENEMY
  }

  ifrnd 16
    ifbulletnear
  {
    ifgapzl 96
      ai AINEWBEASTDODGE
    else
    {
      ifrnd 128
      {
        ifceilingdistl 144 break
        ai AINEWBEASTJUMPENEMY
      }
      else
        ai AINEWBEASTDODGE
    }
	ifai AINEWBEASTDODGE set dodgetime 8
  }
ends

state newbeastscratchstate

  ifl targetdist 1024
  ifmove NEWBEASTSCRATCHVEL move STOPPED faceplayer
  
  ifcount 20
    ifrnd 8
  {
    ife seemytarget YES
      ifl targetdist 2048
    {
      ifrnd 128
        ai AINEWBEASTFLEENEMY
      break
    }
    ifrnd 80
      ai AINEWBEASTTHINK
    else
      ai AINEWBEASTGETENEMY
  }

  ifactioncount 3
  {
    ifg targetdist 1596
      ai AINEWBEASTTHINK
    else ife targetalive YES ife seemytarget YES
    {
      set target bottarget
	  ife target -1
	  {
		move STOPPED geth
        resetactioncount
        resetcount
		break
	  }
	  
      
	  
	  seta[target].htpicnum FIRELASER
	  seta[target].htowner THISACTOR
	  set temp NEWBEASTSCRATCHAMOUNT
	  mul temp -1
	  seta[target].htextra temp
	 
	  ife target player[].i
	  {
	    state checksaberblock
		ifn sprite[target].htextra -1
		{
			ife pchar 1 sound B_GRUNT else
			sound DUKE_GRUNT
			palfrom 16 16
			
			ifrnd 128
			ifpdistl 1440
			ife slidekick 0
			ife pdown NO
			ife sprite[].sectnum player[].cursectnum
			{
				ife zombieonplayer -1
				ife zombiecount 0
				{
					ai AIBEASTBITE
					globalsound NEWBEAST_RECOG
					set zombieonplayer THISACTOR
					set zombietile NEWBEASTBITE
					ifrnd 128 set zombieflip YES
					geta[].x x2
					geta[].y y2
					sub x2 player[].posx
					sub y2 player[].posy
					getangle angvar x2 y2
					setp[].ang angvar
					setp[].oang angvar
					setp[].weapon_pos -9
					cstat 33024
					break
				}
			}
		}
	  }
	  ifvarand initflags 4
	  {
		  getav[target].burning temp
		  add temp 60
		  setav[target].burning temp
	  }
	  seta[target].htang sprite[].ang
      ife target player[].i move STOPPED faceplayerslow
	  else
	  move STOPPED geth
      resetactioncount
      resetcount
    }
  }
  else
    ifcount 15
      nullop
  else
    ifcount 14
  {
    ifl targetdist 1596
      soundonce NEWBEAST_ATTACK
    else
      soundonce NEWBEAST_ATTACKMISS
  }
ends

defstate checknewbeasthit
// ifn sprite[].htpicnum FREEZEBLAST
// ifn sprite[].htpicnum SHRINKSPARK
// ife shrunken 0
  // spawn BLOOD
  
/*
  ifai AINEWBEASTSHRUNK
  {
    addkills 1 state enemy_death
    sound SQUISHED
    state standard_jibs
    killit
  }
*/
  ifwasweapon HEADJIB1
  {
    ife zombieonplayer THISACTOR 
	{
		set zombieonplayer -1
		set zombiecount -30
	}
	addkills 1 state enemy_death
	sound SQUISHED
	cstat 0
    strength -1
	state rf
      ai AINEWBEASTDYING
      ifrnd 64
	  {
	    spritepal 6
        spawn BLOODPOOL
		getlastpal
	  }
	action ANEWBEASTDECAP
	
	rand zdist 1024
	  sub zdist 3500
	  ezshoot zdist BEASTHEADPROJ
	  rand angvar 512
	  sub angvar 256
	  add angvar sprite[].htang
	  seta[RETURN].ang angvar
	  ifspritepal 0 seta[RETURN].pal 15 else
	  seta[RETURN].pal sprite[].pal
	
	break
  }
  ifdead
  {
	ife zombieonplayer THISACTOR 
	{
		set zombieonplayer -1
		set zombiecount -30
		cstat 257
	}
    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action ANEWBEASTFROZEN
      strength 0
	  state freezeme
      break
    }

    ifwasweapon GROWSPARK
    {
      cstat 0
      sound ACTOR_GROWING
      ai AINEWBEASTGROW
      break
    }

    addkills 1 state enemy_death

    ifwasweapon RPG
    {
      sound SQUISHED
	  spritepal 6
      state lite_jibs
	  getlastpal
	  state beast_body_jibs
      killit
    }
    else
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
	  spritepal 6
      state lite_jibs
	  getlastpal
	  state beast_body_jibs
      killit
    }
    else
    {
      state rf
      ai AINEWBEASTDYING
      ifrnd 64
	  {
	    spritepal 6
        spawn BLOODPOOL
		getlastpal
	  }
	  ifrnd 96
		action ANEWBEASTDYING2
    }
    sound NEWBEAST_DYING
  }
  else
  {
    sound NEWBEAST_PAIN
    ifwasweapon GROWSPARK
      sound SPARKLESOUND
/*
//  The alien is immune to the shrinker
    ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      ai AINEWBEASTSHRUNK
      break
    }
*/

    ifvarand initflags 4 { set tempb 1280 set tempc 6144 } else { set tempb 3500 set tempc 16384 }
	spritepal 6
    state random_wall_jibs
	getlastpal
    ifrnd 32
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
      ai AINEWBEASTFLINTCH
	else
    ifvarg stun 0
      ai AINEWBEASTFLINTCH
    else
      ifrnd 32
        // ifpdistg 3500
		ifg targetdist tempb ifl targetdist tempc
          ife targetalive YES
            ife seemytarget YES
              ai AINEWBEASTSHOOT
			  
	ife zombieonplayer THISACTOR 
	{
		ai AINEWBEASTFLINTCH
		set zombieonplayer -1
		set zombiecount -30
		cstat 257
	}
  }
ends

state newbeastjumpstate
  ifaction ANEWBEASTFALL
  {
    iffloordistl 16
      ai AINEWBEASTGETENEMY
  }
  else
    ifcount 32
      action ANEWBEASTFALL
ends

state newbeastdyingstate

  ifaction ANEWBEASTDECAP
  {
	ifactioncount 6
	{
      iffloordistl 8
      sound THUD
      move STOPPED
      action ANEWBEASTDECAPDEAD
    }
	break
  }
  ifaction ANEWBEASTDECAPDEAD
  {
    strength 0
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
	  spritepal 6
      state lite_jibs
	  getlastpal
	  state beast_armslegs
      killit
    }
	break
  }
  set temp NO
  ifaction ANEWBEASTLYINGDEAD set temp YES
  ifaction ANEWBEASTLYINGDEAD2 set temp YES
  ife temp YES
  {
    strength 0
    ifhitweapon
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
	  spritepal 6
      state lite_jibs
	  getlastpal
	  state beast_body_jibs
      killit
    }
    // ifcount RESPAWNACTORTIME
      // ifrespawn ife gametype 0
    // {
	  // addkills -1 set monstatus 1
      // spawn TRANSPORTERSTAR
      // cstat 257
      // strength NEWBEASTSTRENGTH
      // ai AINEWBEASTGETENEMY
    // }
	break
  }
  
  ifaction ANEWBEASTDYING2
  {
	ifactioncount 5
	{
		iffloordistl 8
		sound THUD
		move STOPPED
		action ANEWBEASTLYINGDEAD2
	}
  }
  else
  ifaction ANEWBEASTDYING
  ifactioncount 7
  {
	iffloordistl 8
	  sound THUD
	move STOPPED
	action ANEWBEASTLYINGDEAD
  }
  
ends

state newbeastdodgestate
  ifl dodgetime 2 set dodgetime 2
  ifcount 13
    ai AINEWBEASTGETENEMY
ends

useractor enemystayput NEWBEASTSTAYPUT NEWBEASTSTRENGTH ai AINEWBEASTGETENEMY clipdist 32 cstator 257 cactor NEWBEAST enda
useractor enemy NEWBEASTJUMP NEWBEASTSTRENGTH ai AINEWBEASTJUMPENEMY clipdist 32 cstator 257 cactor NEWBEAST enda

useractor enemy NEWBEASTCROUCH NEWBEASTSTRENGTH 

clipdist 32 cstator 257 
sizeat 32 30 
cactor NEWBEAST ai AINEWBEASTGETENEMY 
enda

state newbeastcode

	geta[].mdflags temp
	orvar temp 16
	seta[].mdflags temp

  ifvarand initflags 4 
  {
    ifspritepal 1 set burning 0
	else
	ife monstatus 1
	set burning 200
  }
  fall
  state checksquished
  ifai 0
  {
    clipdist 36
    cstator 257
    sizeat 32 30
	clipdist 36
	ifspritepal 21 orvar initflags 4
    ai AINEWBEASTDODGE
	set dodgetime 8
	sound NEWBEAST_RECOG
  
  }
  state monsterai 

	ifai AINEWBEASTDYING
	{
		state newbeastdyingstate
		break
	}
    
  
  ifaction ANEWBEASTFROZEN
  {
    ifcount THAWTIME
    {
      ai AINEWBEASTGETENEMY
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  lotsofglass 10
      ifrnd 84
        spawn BLOODPOOL
      state beast_body_jibs
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  
  ifai AINEWBEASTJUMPENEMY
      state newbeastjumpstate
  else
  {
    ifai AINEWBEASTGETENEMY
      state newbeastseekstate
    else
      ifai AINEWBEASTCHARGEENEMY
        state newbeastseekstate
    else
      ifai AINEWBEASTFLINTCH
    {
	  ifvarg stun 0 resetactioncount
      ifactioncount 3
        ai AINEWBEASTGETENEMY
    }
    else
      ifai AINEWBEASTDODGE
        state newbeastdodgestate
    else
      ifai AINEWBEASTSCRATCHENEMY
        state newbeastscratchstate
	else
	  ifai AIBEASTBITE
	    state beastbitestate
    else
      ifai AINEWBEASTFLEENEMY
        state newbeastfleestate
    else
      ifai AINEWBEASTTHINK
        state newbeastthinkstate
/*
    else
      ifai AINEWBEASTSHRUNK
        state newbeastshrunkstate
*/
    else
      ifai AINEWBEASTGROW
        state genericgrowcode
    else
      ifai AINEWBEASTSHOOT
    {
      ifp pshrunk
        ai AINEWBEASTGETENEMY
      else
        ifcount 26
          ai AINEWBEASTGETENEMY
      else
	  {
	    ifvarand initflags 4
		{
		  ifcount 9
          { 
			set temp player[].player_par
			modvar temp 3
			ife temp 0
			state jibshoot
			ifcount 10 nullop else
			sound NEWBEAST_SPIT
          }
		}
		else
		{
			ifcount 25
			  shoot SHRINKER
		  else
		  {
			ifcount 5
			  nullop
			else
			  ifcount 4
				sound NEWBEAST_SPIT
		  }
		}
	  }
    }
  }

  ifhitweapon
    state checknewbeasthit
  else
  ifl sprite[].extra 0
  {
	strength 0
	seta[].htextra 10
	seta[].htpicnum SHOTSPARK1
	seta[].htowner player[].i
  }
ends


useractor enemy NEWBEAST NEWBEASTSTRENGTH
  fall

  state newbeastcode
  
  // ifaction ANEWBEASTFROZEN
  //  state newbeastcode
  // else
  // {
    // ifg gametype 0 ifn spawnprotect 0 nullop else
	// {
		// ifvarand initflags 4 spritepal 21 else spritepal 6
	// }
    // state newbeastcode
    // ifaction ANEWBEASTFROZEN
      // break
	// ifg gametype 0 ifn spawnprotect 0 nullop else
    // getlastpal
  // }
enda

state statuecode

	action ASTATUEFRAME 
	seta[].ang lastang

ends
useractor enemy BOSS1STATUE 6000 
ife initsprite 5
{
	state statuecode
}
enda
useractor enemy BOSS2STATUE 6000 
ife initsprite 5
{
	state statuecode
}
enda
useractor enemy BOSS3STATUE 6000 
ife initsprite 5
{
	state statuecode
}
enda
