// DEVOURER BOSS CODE
// SPRITES BY SEBABDUKEBOSS20
// CODE BY DAN GASKILL

action ADEVOURFLOAT 0  4  5  1  24
action ADEVOURSHOOT1 23  1  5  1  50 // prepping
action ADEVOURSHOOT2 28  1  5  1  6 // shooting
action ADEVOURSHOOT3 33  1  5  1  8 // quick prep
action ADEVOURSPAWN 39  3  1  1  50
action ADEVOURDIE 42  5  1  1  60
action ADEVOURDEAD 46  1  1  1  10

action ADEVOURFROZEN  0  1  5  1  10

move DEVOURCHASEVELS 204 32
move PALDEVOURVELS  144 32
// move DEVOURCHASEUP 256 -76

ai AIDEVOURFLOAT ADEVOURFLOAT STOPPED faceplayerslow
ai AIDEVOURSHOOT ADEVOURSHOOT1 STOPPED faceplayerslow
ai AIDEVOURSHOOT2 ADEVOURSHOOT1 STOPPED faceplayerslow
ai AIDEVOURSPAWN ADEVOURSPAWN STOPPED faceplayerslow
ai AIDEVOURCHASE ADEVOURFLOAT DEVOURCHASEVELS faceplayerslow
ai AIDEVOURDYING ADEVOURDIE STOPPED geth
ai AIDEVOURSHRINK ADEVOURFLOAT SHRUNKVELS fleeenemy

state devourshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AIDEVOURFLOAT
  else
    ifcount SHRUNKCOUNT
      sizeto 18 16
  else
    state genericshrunkcode
ends

state devourchasestate

	getp[].posz zdist
	add zdist 16384
	
	sub zdist sprite[].z
	ifn mtype 0 
	{ 
		ifp phigher sub zdist 1560 
	}
	
	ifl zdist -3072 set zdist -3072
	ifg zdist 3072 set zdist 3072
	ifceilingdistl 96 ifl zdist 0 set zdist 0
	
	ifn sprite[].pal 0 
	ifn sprite[].pal 19
	{ 
		mul zdist 3 div zdist 4 
		ifmove DEVOURCHASEVELS move PALDEVOURVELS faceplayerslow
	}
	
	movesprite THISACTOR 0 0 zdist CLIPMASK0 RETURN
	
	ifspritepal 19
	ifn bottarget -1
	ifrnd 2
	{
		set dodgetime 0
		ifrnd 128
		{
			ai AIDEVOURSHOOT
			set countvarb 1
		}
		else
		{
			ai AIDEVOURSHOOT2
			set countvarb 2
		}
		soundonce DEVOUR_ROAM
		setactorsoundpitch THISACTOR DEVOUR_ROAM 1280
		break
	}
	
	
	add dodgetime 1
	ifn sprite[].pal 0 add dodgetime 1
	ifg dodgetime 300
	{
		set dodgetime 0
		ai AIDEVOURFLOAT
		ifspritepal 19
		{
			ifn bottarget -1
			{
				add countvarb 1
				ifg countvarb 3 set countvarb 1
				ife countvarb 1 ai AIDEVOURSHOOT
				ife countvarb 2 
				{ 
					ai AIDEVOURSHOOT2
					soundonce DEVOUR_ROAM 
				}
				ife countvarb 3 
				{
					ai AIDEVOURCHASE 
					soundonce DEVOUR_ROAM
				}
			}
			else 
			{
				ai AIDEVOURCHASE 
				move DEVOURCHASEVELS seekplayer
			}
			ifactorsound THISACTOR DEVOUR_ROAM setactorsoundpitch THISACTOR DEVOUR_ROAM 1280
		}
	}
	
	ifn sprite[].pal 0
	{
		ifcount 15
		{
			ife targetalive YES
			ife seemytarget YES
			ifl targetdist 1024
			ifn bottarget -1
			{
				ife sector[].lotag sector[sprite[bottarget].sectnum].lotag
				{
				  sound BEASTBITESND
				  resetcount
				  ife bottarget player[].i
				  {
					  ife pchar 1 sound B_GRUNT else
					  ife pchar 2 sound WESPAIN2 else
					   sound DUKE_GRUNT
					  palfrom 32 32
				  }
				  // getp[].i spriteid
				  seta[bottarget].htpicnum FIRELASER
				  seta[bottarget].htowner THISACTOR
				  set temp SHARKBITESTRENGTH
				  mul temp -1
				  ifspritepal 19 mul temp 3
				  seta[bottarget].htextra temp
				  seta[bottarget].htang sprite[].ang
				}
			}
		}
		break
	}
	
	ifp palive
       ifpdistl 1280
		ife pdown NO
    {
      addphealth -1000
      palfrom 63 63
    }
	state squish_bot
	
ends

state devourspawnstate

ifactioncount 2
{
	ife mtype 0
	{
		ifn player[].cursectnum -1
		ife sector[player[].cursectnum].lotag 2
		{
			ifspritepal 0
			{
				espawn EGG
				setav[RETURN].mtype initsprite
				ife initsprite 1 set initsprite 2 else set initsprite 1
			}
			else
			{ spawn WATERSLIME spawn WATERSLIME  }
		}
		sound SLIM_HATCH
		set mtype 1
	}
}
ifactioncount 3
{
	set mtype 0
	ai AIDEVOURFLOAT
}

ends

state devourshoot2state

	ifaction ADEVOURSHOOT1
	{
		ifcount 8
		{
			action ADEVOURSHOOT2
			move STOPPED faceplayer
		}
	}
	ifaction ADEVOURSHOOT2
	{
		ifcount 8
		{
			action ADEVOURSHOOT3
			move STOPPED faceplayer
			
			set xvel 768
			state rpg_targetprep
			ezshoot zdist COMETPROJ
			seta[RETURN].pal 6
		}	
	}
	ifaction ADEVOURSHOOT3
	{
		ifcount 8
		{
			ifn bottarget -1 ai AIDEVOURCHASE
			else
			ai AIDEVOURFLOAT
		}
	}

ends

state devourshootstate


	ifaction ADEVOURSHOOT1
	{
		ifactioncount 1
			action ADEVOURSHOOT2
	}
	ifaction ADEVOURSHOOT2
	{
		ifactioncount 1
		action ADEVOURSHOOT3
	}
	ifaction ADEVOURSHOOT3
	{
		ifactioncount 1
		{
			ifg countvarc 9
			{
				set countvarc 0
				ifspritepal 19 ifn bottarget -1 ai AIDEVOURCHASE else
				ai AIDEVOURFLOAT
				break
			}
			else
			{
				sound SHOOTSPIKE
				add countvarc 1
				action ADEVOURSHOOT2
				
				ifn sprite[].pal 0
				{
					set B 0
					whilevarn B 12
					{
						set xvel 768
						state rpg_targetprep
						ezshoot zdist SPIKEPROJ
						seta[RETURN].blend 1
						rand angvar 192
						sub angvar 96
						add angvar sprite[RETURN].ang
						seta[RETURN].ang angvar
						rand zdist 6144
						sub zdist 3072
						add zdist sprite[RETURN].zvel
						seta[RETURN].zvel zdist
						add B 1
					}
					break
				}
				
				geta[].x startx
				geta[].y starty
				geta[].z savz
				add savz 3584
				seta[].z savz
				geta[].ang angvar
				sub angvar 512
				set x sprite[].x
				add x 1280
				rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
				seta[].x x2
				seta[].y y2
				set B 0
				whilevarn B 12
				{
					eshoot SPIKEPROJ
					rand angvar 192
					sub angvar 96
					add angvar sprite[RETURN].ang
					seta[RETURN].ang angvar
					rand zdist 6144
					sub zdist 3072
					add zdist sprite[RETURN].zvel
					seta[RETURN].zvel zdist
					add B 1
				}
				seta[].x startx
				seta[].y starty
				geta[].ang angvar
				add angvar 512
				set x sprite[].x
				add x 1280
				rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
				seta[].x x2
				seta[].y y2
				set B 0
				whilevarn B 12
				{
					eshoot SPIKEPROJ
					rand angvar 192
					sub angvar 96
					add angvar sprite[RETURN].ang
					seta[RETURN].ang angvar
					rand zdist 6144
					sub zdist 3072
					add zdist sprite[RETURN].zvel
					seta[RETURN].zvel zdist
					add B 1
				}
				seta[].x startx
				seta[].y starty
				sub savz 3584
				seta[].z savz
			}
		}
	}

ends

useractor enemy DEVOURER DEVOURERSTRENGTH

	ifn myspawner -1
	{
		getav[myspawner].countvarb countvarb
		ifn countvarb 3 killit
		ifl sprite[myspawner].extra 1 killit
		setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
		seta[].ang sprite[myspawner].ang
		cstat 33040
		// cstat 274
		sizeat 72 72
		ifg sprite[].htextra 0
		{
			geta[].htextra temp
			geta[myspawner].htextra tempb
			ife sprite[myspawner].htextra -1
			{
				seta[myspawner].htpicnum sprite[].htpicnum
				seta[myspawner].htowner sprite[].htowner
				seta[myspawner].htang sprite[].htang
			}
			add tempb temp
			seta[myspawner].htextra tempb
			seta[].htextra -1
		}
		break
	}
	ifai 0
	{
		ai AIDEVOURFLOAT
		
		
		ifspritepal 0
		{
		sizeat 84 72
		clipdist 192
		}
		else
		ifspritepal 19
		{
			sizeat 30 28
			strength 400
			rand countvarb 2
		}
		else
		{ sizeat 18 16 clipdist 48 strength 200 rand countvarb 2 }
		cstator 257
		
		set initsprite 1
		soundonce DEVOUR_ROAM
		ifn sprite[].pal 0 setactorsoundpitch THISACTOR DEVOUR_ROAM 1280 
	}

	ifai AIDEVOURDYING
	{
		fall
		set countvarb 0
		ifaction ADEVOURDIE
		ifactioncount 4
			action ADEVOURDEAD
			
		ifaction ADEVOURDEAD
		ifn sprite[].pal 0
		{
			strength 0
			ifhitweapon
			{
				sound SQUISHED
				spritepal 6
				state standard_jibs
				killit
			}
		}
		break
	}

	state monsterai
	
  ifaction ADEVOURFROZEN
  {
    fall
    ifcount THAWTIME
    {
      ai AIDEVOURFLOAT
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
        ifrnd 8
          spawn WATERDRIP

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  lotsofglass 10
      guts JIBS6 50
	  geta[].z savz
	  sub savz 10240
	  seta[].z savz
	  state lite_jibs
	  state lite_jibs
      
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
	
	ifai AIDEVOURFLOAT
	{
		ifcount 50
		{
			add countvarb 1
			ifg countvarb 3 set countvarb 1
			ife countvarb 1 ai AIDEVOURSHOOT
			ife countvarb 2 
			{ 
				ifspritepal 19 ai AIDEVOURSHOOT2
				else
				ai AIDEVOURSPAWN 
				soundonce DEVOUR_SPAWN 
			}
			ife countvarb 3 
			{
				ai AIDEVOURCHASE soundonce DEVOUR_ROAM
				ifspritepal 0
				{
					espawn DEVOURER
					setav[RETURN].myspawner THISACTOR
					seta[RETURN].cstat 33040
					getp[].max_actors_killed temp
					sub temp 1
					setp[].max_actors_killed temp
				}
			}
			
			ifn sprite[].pal 0
			{
				ifactorsound THISACTOR DEVOUR_ROAM setactorsoundpitch THISACTOR DEVOUR_ROAM 1280
				ifactorsound THISACTOR DEVOUR_SPAWN setactorsoundpitch THISACTOR DEVOUR_SPAWN 1280
			}
		}
	}
	
	ifai AIDEVOURSHOOT state devourshootstate else
	ifai AIDEVOURSHOOT2 state devourshoot2state else
	ifai AIDEVOURSPAWN state devourspawnstate else
	ifai AIDEVOURCHASE state devourchasestate else
	ifai AIDEVOURSHRINK state devourshrunkstate
	
	ifai AIDEVOURCHASE
	{
		ifn sprite[].pal 0
		{
			set temp player[].player_par
			shiftl temp 5
			sin tempb temp
			shiftr tempb 7
			geta[].z z
			sub z tempb
			seta[].z z
		}
	}
	else
	{
		set temp player[].player_par
		
		shiftl temp 5
		sin tempb temp
		ifn sprite[].pal 0 shiftr tempb 8 else
		shiftr tempb 7
		geta[].z z
		sub z tempb
		seta[].z z
	}
	
	ifhitweapon
	{
		ifn sprite[].pal 0
		ifwasweapon SHRINKSPARK
		{
			sound ACTOR_SHRINKING
			ai AIDEVOURSHRINK
			set shrunken 1
			break
		}
		ifdead
		{
			ifn sprite[].pal 0
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  move 0
			  action ADEVOURFROZEN
			  strength 0
			  state freezeme
			  break
			}
			addkills 1
			cstat 0
			state enemy_death
			
			sound DEVOUR_DIE
			ifn sprite[].pal 0 ifactorsound THISACTOR DEVOUR_DIE setactorsoundpitch THISACTOR DEVOUR_DIE 1280
			ai AIDEVOURDYING
			spritepal 6
			ifspritepal 0
			{
				guts JIBS6 50
				geta[].z savz
				sub savz 10240
				seta[].z savz
				state lite_jibs
				state lite_jibs
				state lite_jibs
				add savz 10240
				seta[].z savz
				getlastpal
			}
			else
			{
				guts JIBS6 12
				geta[].z savz
				sub savz 4096
				seta[].z savz
				state lite_jibs
				add savz 4096
				seta[].z savz
				getlastpal
			}
			break
		}
		else soundonce DEVOUR_PAIN
		ifn sprite[].pal 0
		{
		ifactorsound THISACTOR DEVOUR_PAIN setactorsoundpitch THISACTOR DEVOUR_PAIN 1280
		}
		
	}
	
enda

move WSLIMEVELS 128 24
move WSLIMEFACE 128 24
move WSLIMEVELSUP 96 -68
move WSLIMEUPFACE 96 -68

action WSLIMEFLOAT 0  0  0  0  1
action WSLIMESWIM  1  1  1  1  20

ai AIWSLIMESEEK WSLIMESWIM WSLIMEVELSUP seekplayer faceplayerslow

state slimeseekstate

	ifmove WSLIMEFACE ifpdistg 6144 move WSLIMEVELS seekplayer faceplayerslow
	ifmove WSLIMEUPFACE ifpdistg 6144 move WSLIMEVELSUP seekplayer faceplayerslow
	
	ifmove WSLIMEVELSUP
	{
		ifpdistl 5120 move WSLIMEUPFACE faceplayer getv
		
		ifceilingdistl 32 move WSLIMEVELS seekplayer faceplayerslow
		else
		ifcount 40
		{
			ifaction WSLIMEFLOAT action WSLIMESWIM
			// ifp phigher nullop else
			ifg sprite[player[].i].z sprite[].z
			move WSLIMEVELS seekplayer faceplayerslow
		}
	}
	else ifcount 30
	{
		ifmove WSLIMEVELS ifpdistl 5120 move WSLIMEFACE faceplayer
		iffloordistl 16 move WSLIMEVELSUP seekplayer faceplayerslow
		ifceilingdistl 32 nullop else
		ifp phigher move WSLIMEVELSUP seekplayer faceplayerslow
	}

ends

useractor enemy WATERSLIME 1

ifai 0
{
	sizeat 32 32
	cstator 256
	cstator 128
	ai AIWSLIMESEEK
	move WSLIMEVELSUP randomangle
	action WSLIMEFLOAT
}
fall

ifai AIWSLIMESEEK state slimeseekstate

ifg sprite[].htextra 0 cactor GREENSLIME

ifpdistl 1280
{ 
	addkills -1
	espawn GREENSLIME
	setsprite RETURN player[].posx player[].posy player[].posz
	killit
}

enda
