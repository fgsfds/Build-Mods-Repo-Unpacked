// zombie

// pal 17 is a bluish variant
// pal 19 is a reddish variant
// pal 27 is a gray/whitish variant
// pal 28 is brown
// define ZOMBIE 8795

action GIDUPWAIT  0  1  5  1  10
action GIDUP  0  3  5  1  24
action AZOMBIEWALK  0  4  5  1  20
action AZOMBIERUN   0  4  5  1  12
action AZOMBIEHIT   20 2  5  1  18
action AZOMBIEBITE  10510 2  5  1  20
action AZOMBIEHIT2  30 1  5  1  24
action AZOMBIEFROZEN 20  1  5  1  10
action AZOMBIEDIE   35 6  1  1  16
action AZOMBIESTUN	35 1  1  1  8
action AZOMBIEDEAD  40 1  1  1  10
action AZOMBIEHEAD  0  8  1  1  14
action AZOMBIEHEADDEAD 7  1  1  1  1
move ZOMBIEWALKVEL 84
move ZOMBIESHRUNKVEL 32
move ZOMBIERUNVEL 160

ai AIZOMBIEWANDER AZOMBIEWALK ZOMBIEWALKVEL randomangle
ai AIZOMBIESEEK AZOMBIEWALK ZOMBIEWALKVEL seekplayer
ai AIZOMBIECHARGE AZOMBIERUN ZOMBIERUNVEL faceplayer
ai AIZOMBIESHRUNK AZOMBIERUN ZOMBIESHRUNKVEL fleeenemy
ai AIZOMBIEFROZEN AZOMBIEFROZEN STOPPED geth
ai AIZOMBIEGROW AZOMBIEFROZEN STOPPED geth
ai AIZOMBIEDYING AZOMBIEDIE STOPPED geth
ai AIZOMBIEATTK AZOMBIEHIT STOPPED geth
ai AIZOMBIEBITE AZOMBIEBITE STOPPED faceplayer
ai AIZOMBIEBITENPC AZOMBIEBITE STOPPED geth
ai AIZOMBIESTUN AZOMBIESTUN STOPPED geth

action ACRONENVOMIT	1890  2  5  1  50
ai AICRONENVOMIT ACRONENVOMIT STOPPED geth

state zombiestunstate

	ife stun 0 ai AIZOMBIESEEK

ends

defstate zombiedrop
  ife gametype 0 
  ifrnd SPAWNAMMOODDS
	ifrnd SPAWNAMMOODDS
	{
	  ife pchar 2
	  {
		ifrnd 64 spawn QUIVERSPRITE else spawn STAKESPRITE
	  }
	  else
	  {
		  ife gotsaw NO
		  ife gotsaber NO
			spawn SAWSPRITE
		  else 
		   spawn SAWAMMO
	  }
	  
	}
ends

state vomitstate

	set dodgetime 2
	state turntotarget
	ifactioncount 1
	{
		soundonce VOMIT
		setprojectile[EGGJIBPROJ].extra 4
		setprojectile[EGGJIBPROJ].vel 420
		setprojectile[EGGJIBPROJ].workslike 36866
		setprojectile[EGGJIBPROJ].xrepeat 32
		setprojectile[EGGJIBPROJ].yrepeat 32
		
		rand z 2048
		sub z 2048
		ezshoot z EGGJIBPROJ
		rand angvar 64
		sub angvar 32
		add angvar sprite[].ang
		seta[RETURN].ang angvar

		setprojectile[EGGJIBPROJ].extra 0
		setprojectile[EGGJIBPROJ].vel 160
		setprojectile[EGGJIBPROJ].workslike 6286
		setprojectile[EGGJIBPROJ].xrepeat 40
		setprojectile[EGGJIBPROJ].yrepeat 40
	}
	ifactioncount 2
	{
		ai AIZOMBIESEEK
	}

ends

state zombiegrowstate state genericgrowcode ends

state zombieshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIZOMBIESEEK
  else
    ifcount SHRUNKCOUNT
      sizeto 26 24
  else
    state genericshrunkcode

ends

state zombiefrozenstate

    ifcount THAWTIME
    {
      ai AIZOMBIESEEK
      getlastpal
	  strength 1
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state zombiedyingstate

	strength 0
	state deadsquished
	ifaction AZOMBIEDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action AZOMBIEDEAD
			iffloordistl 8 sound THUD
			ife myspawner -1
			ifn team 1
			ifrnd 128 add mtype 1
			else set mtype 0
			resetcount
			break
		}
	}
	ifaction AZOMBIEDEAD
	{
		ifcount 60
		ifg mtype 0
		ifl mtype 3
		{
			cactor ZOMBIEGIDUP
			action GIDUPWAIT
			ai 0
			move 0
			strength ZOMBIESTRENGTH
			set monstatus -1
			set monstflags 0
			break
		}
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 8
				state standard_jibs
				getlastpal
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 8
				state standard_jibs
				getlastpal
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength ZOMBIESTRENGTH
		  // ai AIZOMBIESEEK
		// }
	}
	
ends

state zombiebitestate

	ife player[].over_shoulder_on NO cstat 33024 else cstat 257
	
	ifp palive nullop else ife zombieonplayer THISACTOR { set zombieonplayer -1 set zombiecount -30 }
	
	ifn zombieonplayer THISACTOR
	{
		ai AIZOMBIESEEK
		cstat 257
		break
	}
	else
	{
		cstator 256
		
		getp[].posz z
		add z 9216
		
		getp[].oang angvar
		
		set x2 player[].posx
		add x2 512
		rotatepoint player[].posx player[].posy x2 player[].posy angvar x y
		
		setsprite THISACTOR x y z
		setp[].posxv 0
		setp[].posyv 0
		setp[].jumping_counter 0
		setp[].ang player[].oang
		setp[].horiz 100
	}

ends

state zombiescratch

	ifn bottarget -1
	{
		ifl targetdist 1596
		ife targetalive YES
		{
		  set temp sprite[bottarget].htextra
		  ife temp -1 set temp 6 else add temp 6
		  seta[bottarget].htextra temp
		  seta[bottarget].htpicnum SHOTSPARK1
		  seta[bottarget].htowner THISACTOR
		  seta[bottarget].htang sprite[].ang
		  ife team 1 ifspritepal 2 ifvarand wesupgrades[SHRINKER_WEAPON] 1 add leach 50
		  ife bottarget player[].i
		  {
		      palfrom 24 16
			  setp[].posxv 0
			  setp[].posyv 0
			  ife pchar 2 sound WESPAIN3 else
			  ife pchar 1 sound B_GRUNT else
				sound DUKE_GRUNT
		  }
		  else
		  seta[bottarget].xvel 0
		  ifrnd 128 sound Z_HITP1 else sound Z_HITP2
		}
		else sound Z_SWING
	}
	else sound Z_SWING
	// else
	// ifp palive
	// {
		// ifpdistl 1596
		// ife seeplayer YES
		// {
		  // palfrom 24 16
		  // getp[].i spriteid
		  // seta[spriteid].htpicnum SHOTSPARK1
		  // seta[spriteid].htowner THISACTOR
		  // seta[spriteid].htextra 6
		  // seta[spriteid].htang sprite[].ang
		  // setp[].posxv 0
		  // setp[].posyv 0
		  // ife pchar 1 sound B_GRUNT else
			// sound DUKE_GRUNT
		  // ifrnd 128 sound Z_HITP1 else sound Z_HITP2
		// }
		// else sound Z_SWING
	// }

ends

state zombieattkstate
	
	ifaction AZOMBIEHIT
	{
		ifactioncount 2
		{
			action AZOMBIEHIT2
			state zombiescratch
		}
	}
	else
	ifaction AZOMBIEHIT2
	{
		ifactioncount 1
		{
			ife bottarget -1 { ai AIZOMBIESEEK break }
			set target bottarget
			// ife target -1 set target player[].i
			
			ifn target player[].i
			{
				set spriteid target
				state facesprite
				
				ifg targetdist 1560 ai AIZOMBIESEEK
				else 
				{
					ai AIZOMBIEATTK
					ife bottarget myshelly setav[myshelly].myspawner THISACTOR
				}
				ife targetalive NO ai AIZOMBIESEEK
				
			}
			else
			{
				set spriteid player[].i
				state facesprite
				ifaction AZOMBIEHIT2
				ifactioncount 1
				{
					ifpdistg 1560 ai AIZOMBIESEEK
					else ife seeplayer YES ifp palive
					{
						ifpdistl 1280
						ife slidekick 0
						ife pdown NO
						ife sprite[].sectnum player[].cursectnum
						{
							ife zombieonplayer -1
							ife zombiecount 0
							{
								ai AIZOMBIEBITE
								globalsound Z_BITESTART
								set zombieonplayer THISACTOR
								set zombietile ZOMBIEBITE
								ifrnd 128 set zombieflip YES
								geta[].x x2
								geta[].y y2
								sub x2 player[].posx
								sub y2 player[].posy
								getangle angvar x2 y2
								setp[].ang angvar
								setp[].oang angvar
								setp[].weapon_pos -9
								cstat 33024
								break
							}
						}
						else
						{
							ai AIZOMBIEATTK
							ife bottarget myshelly setav[myshelly].myspawner THISACTOR
						}
					}
				}
			}
		}
	}
	else ai AIZOMBIESEEK
	
	

ends

state zombieroamsounds

	ifrnd 1
	ifrnd 128
	{
		ifactorsound THISACTOR Z_ROAM1 break
		ifactorsound THISACTOR Z_ROAM2 break
		ifactorsound THISACTOR Z_ROAM3 break
		rand temp 2
		ife temp 0 sound Z_ROAM1 else
		ife temp 1 sound Z_ROAM2 else
		sound Z_ROAM3
	}
	
ends

state zombiecheckmelee
	ifn bottarget -1
	{
		ife seemytarget YES
		ife targetalive YES
		ifl targetdist 1408
		{
			ife bottarget player[].i
			ife zombieonplayer -1
			ife zombiecount 0
			ifpdistl 1280
			ifcount 15
			ife slidekick 0
			ife pdown NO
			ife sprite[].sectnum player[].cursectnum
			{
				ai AIZOMBIEBITE
				globalsound Z_BITESTART
				set zombieonplayer THISACTOR
				set zombietile ZOMBIEBITE
				ifrnd 128 set zombieflip YES
				geta[].x x2
				geta[].y y2
				sub x2 player[].posx
				sub y2 player[].posy
				getangle angvar x2 y2
				setp[].ang angvar
				setp[].oang angvar
				setp[].weapon_pos -9
				cstat 33024
				break
			}
			
			ai AIZOMBIEATTK 
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
		}
		else ifg targetdist 2048
		{
			ife bluecarrier THISACTOR
			{
				set bottarget -1 break
			}
			ife redcarrier THISACTOR
			{
				set bottarget -1 break
			}
		}
	}
	// else
	// ife seeplayer YES ifpdistl 1408 
	// ifp palive
	// {
		// ife zombieonplayer -1
		// ife zombiecount 0
		// ifpdistl 1280
		// ifcount 15
		// ife slidekick 0
		// ife pdown NO
		// ife sprite[].sectnum player[].cursectnum
		// {
			// ai AIZOMBIEBITE
			// globalsound Z_BITESTART
			// set zombieonplayer THISACTOR
			// set zombietile ZOMBIEBITE
			// ifrnd 128 set zombieflip YES
			// geta[].x x2
			// geta[].y y2
			// sub x2 player[].posx
			// sub y2 player[].posy
			// getangle angvar x2 y2
			// setp[].ang angvar
			// setp[].oang angvar
			// setp[].weapon_pos -9
			// cstat 33024
		// }
		// else
		// {
			// ai AIZOMBIEATTK
			// ife bottarget myshelly setav[myshelly].myspawner THISACTOR
		// }
	// }
		
	
ends

state checkzombiehit

	set initsprite YES
	ife zombieonplayer THISACTOR 
	{
		set zombieonplayer -1
		set zombiecount -30
		cstat 257
		ai AIZOMBIESEEK
	}
	ifai AIZOMBIEWANDER ai AIZOMBIESEEK
	
  ifwasweapon HEADJIB1 // HEADSHOT!
  {
	sound SQUISHED
	ifrnd 128 cstat 0 else cstat 4
    move STOPPED
	cactor ZOMBIEHEADSHOT
	action AZOMBIEHEAD

	  rand zdist 1024
	  sub zdist 3500
	  ezshoot zdist ZOMBIEHEADPROJ
	  rand angvar 512
	  sub angvar 256
	  add angvar sprite[].htang
	  seta[RETURN].ang angvar
	  seta[RETURN].pal sprite[].pal
	  
	  state zombiedrop
	  
	addkills 1 state enemy_death
	break
  }
	
	ifwasweapon SHRINKSPARK
	{	
	  cactor ZOMBIE
	  sound ACTOR_SHRINKING
	  ai AIZOMBIESHRUNK
	  set shrunken 1
	  break
	}
	else
		ifwasweapon GROWSPARK
		  sound SPARKLESOUND
	state random_wall_jibs
	
	ifg sprite[].extra 0
	{
		ifactorsound THISACTOR Z_PAIN1 nullop else
		ifactorsound THISACTOR Z_PAIN2 nullop else
		ifrnd 128 sound Z_PAIN1 else sound Z_PAIN2
	}

	ifdead
	{
		ifai AIZOMBIESHRUNK nullop else
		ifwasweapon FREEZEBLAST
		{
		  sound NEWFREEZE
		  spritepal 1
		  cactor ZOMBIE
		  ai AIZOMBIEFROZEN
		  strength 0
		  state freezeme
		  break
		}
		ifwasweapon GROWSPARK
		{
		  cstat 0
		  sound ACTOR_GROWING
		  cactor ZOMBIE
		  ai AIZOMBIEGROW
		  break
		}
		addkills 1
		state enemy_death
		state zombiedrop
		cactor ZOMBIE
		ifrnd 128 cstator 4
		ai AIZOMBIEDYING
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch SHRUNKPITCH
		
		ifrnd 128 sound Z_DEATH1 else sound Z_DEATH2
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT
			setp[].sound_pitch 0
		
		ifwasweapon RPG
		{
			cactor ZOMBIE
			sound SQUISHED
			spritepal 8
			state standard_jibs
			getlastpal
			killit
		}
		ifwasweapon RADIUSEXPLOSION
		{
			cactor ZOMBIE
			sound SQUISHED
			spritepal 8
			state standard_jibs
			getlastpal
			killit
		}
		break
	}
	ifai AIZOMBIESTUN nullop else
	ifai AIZOMBIESHRUNK nullop else
	ifg stun 0 ai AIZOMBIESTUN

ends

state zombiechargestate

	ifactor CRONEN
	ife seemytarget YES
	ifl targetdist 6144
	{
		ifrnd 2 ai AICRONENVOMIT
		else
		ifl targetdist 3072
		ifrnd 8
		ai AICRONENVOMIT
	}

	ifactor ZOMBIE
	state zombiecheckmelee
	ifai AIZOMBIEATTK break
	ifai AIZOMBIEBITE break

ifcount 90
	ifn gametype SURVIVAL
	ifn bluecarrier THISACTOR
	ifn redcarrier THISACTOR
		ai AIZOMBIESEEK
	
	ifn bottarget -1 set target bottarget 
	// else ife seeplayer YES ifn sprite[].picnum CRONEN getp[].i target
	else set target -1
	ifn target -1
	{
		set spriteid target
		state facesprite
	}
	else ifnotmoving ifcount 10 ifrnd 32 { operate ai AIZOMBIESEEK }

ends

state zombiewalkstate

	ifcount 10
	ifactor ZOMBIE
	state zombiecheckmelee
	
	ifai AIZOMBIEATTK break
	ifai AIZOMBIEBITE break
	
	ifn bottarget -1 
	{
		set target bottarget 
		set initsprite YES
	}
	// else ife seeplayer YES 
	// ifn sprite[].picnum CRONEN
	// {
		// getp[].i target
		// set initsprite YES
	// }
	else set target -1
	
	ifactor CRONEN
	ife seemytarget YES
	ifl targetdist 6144
	{
		ifrnd 2 ai AICRONENVOMIT
		else
		ifl targetdist 3072
		ifrnd 8
		ai AICRONENVOMIT
	}

	ifai AIZOMBIEWANDER
	{
		ifrnd 12
		ifcount 20 ifn target -1
			ai AIZOMBIESEEK
	}
	else
	ifai AIZOMBIESEEK
	{
		ifrnd 12
		ifcount 16 ife target -1
			ai AIZOMBIEWANDER
	}
	
	ifnotmoving
	{
		ife target -1
		ifcount 8
		// ifai AIZOMBIESEEK
		{
			ifn team 1 set crumbwait 150
			ai AIZOMBIEWANDER
		}
		ifrnd 64 operate
	}
	
	ifrnd 4
	{
		ifn target -1
		{
			ifl targetdist 8192
			{
				ai AIZOMBIECHARGE
				set spriteid target
				state facesprite
				ifrnd 128 sound Z_CHARGE1
				else sound Z_CHARGE2
				break
			}	
		}
		else
		ife gametype SURVIVAL
		ifn navpoint -1
		{
			ai AIZOMBIECHARGE
			ifrnd 128 sound Z_CHARGE1
			else sound Z_CHARGE2
			break
		}
	}
	else ife bluecarrier THISACTOR ai AIZOMBIECHARGE else
	ife redcarrier THISACTOR ai AIZOMBIECHARGE
	state zombieroamsounds
	

ends


useractor enemy ZOMBIE ZOMBIESTRENGTH

	fall
	ifai 0
	{
		cstator 257
		// sizeat 42 40
		clipdist 48
		ai AIZOMBIEWANDER
		ifspawnedby TEAMSPAWNER nullop else
		ife team 0
		set team 2
		else ife team 1 set navmode 4
		ifl sprite[].xrepeat 8
		{
			rand temp 4
			add temp 26
			seta[].xrepeat temp
			rand temp 2
			add temp 24
			seta[].yrepeat temp
			rand temp 4
			ife temp 1 spritepal 17 else
			ife temp 2 spritepal 19 else
			ife temp 3 spritepal 27 else
			ife temp 4 spritepal 28
		}
		ifn myspawner -1 addstrength 50
	}
	ifn bottarget -1 
	{
		ife actorvar[bottarget].monstatus 2 set bottarget -1
	}
	else ifn myspawner -1
	{
		set team 0
		ife sprite[myspawner].statnum 1024 set myspawner -1 else
		ife actorvar[myspawner].monstatus 2 set myspawner -1 else
		{
			ifg sprite[].htextra 0
			{
				ife sprite[].htowner myspawner { seta[].htextra -1 addstrength 20 } else
				ife sprite[].htpicnum RADPROJ { seta[].htextra -1 addstrength 20 }
			}
			ldist temp THISACTOR myspawner
			ifg temp 10240 setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z else
			ifg temp 4096
			{
				set spriteid myspawner
				state facesprite
			}
		}
	}
	ife initsprite NO set crumbwait 10
	
	
	ifai AIZOMBIEDYING { state zombiedyingstate break }
	ifai AIZOMBIEFROZEN { state zombiefrozenstate break }
	ifai AIZOMBIEGROW { state zombiegrowstate break }
	state monsterai
	ifai AIZOMBIESHRUNK state zombieshrunkstate
	else
	ife team 1 ifg FEMKILLCOUNT 0
	{
		ifg sprite[].htextra 0
		ifn sprite[].htowner -1
		ife sprite[sprite[].htowner].picnum BATBOSS
		ifvarand actorvar[sprite[].htowner].initflags 2
		{
			set FEMKILLCOUNT 1
		}
		sub FEMKILLCOUNT 1
		ife FEMKILLCOUNT 0
		{
			seta[].htextra 1000
			seta[].htpicnum HEADJIB1
			seta[].htowner THISACTOR
		}
	}
	
	ifhitweapon 
	{
		state checkzombiehit
		ifactor ZOMBIEHEADSHOT break
	}
	else state checksquished
	ifai AIZOMBIESTUN state zombiestunstate else
	ifai AIZOMBIEWANDER state zombiewalkstate else
	ifai AIZOMBIESEEK state zombiewalkstate else
	ifai AIZOMBIEATTK state zombieattkstate else
	ifai AIZOMBIECHARGE state zombiechargestate else
	ifai AIZOMBIEBITE state zombiebitestate

enda

useractor enemy ZOMBIEGIDUP ZOMBIESTRENGTH

	fall
	ifmove 0
	{
		cstator 257
		move STOPPED
		action GIDUPWAIT
		set team 2
		ifl sprite[].xrepeat 8
		{
			rand temp 4
			add temp 26
			seta[].xrepeat temp
			rand temp 2
			add temp 24
			seta[].yrepeat temp
			rand temp 4
			ife temp 1 spritepal 17 else
			ife temp 2 spritepal 19 else
			ife temp 3 spritepal 27 else
			ife temp 4 spritepal 28
		}
	}
	state monsterai
	ifhitweapon 
	{ 
		ifdead
		{
			cactor ZOMBIE
			state checkzombiehit
			break
		}
		else
		action GIDUP 
	}
	else state checksquished
	ifaction GIDUPWAIT
	{
		ifcount 10
		{
			ife mtype 0
			ifpdistl 1560 action GIDUP
			else ifrnd 1 action GIDUP
		}
		break
	}
	ifaction GIDUP
	ifactioncount 3
	{
		cactor ZOMBIE
		action AZOMBIEWALK
	}

enda

useractor enemy ZOMBIEHEADSHOT 0 AZOMBIEHEAD

	fall
	ifaction AZOMBIEHEAD
	{
		ifactioncount 7
		{
			sound THUD
			action AZOMBIEHEADDEAD
			strength 0
		}
	}
	ifaction AZOMBIEHEADDEAD
	{
		ifhitweapon
		{
			spritepal 8
			state standard_jibs
			getlastpal
			killit
		}
	}
	

enda

useractor enemy CRONEN 100

	fall
	ifai 0
	{
		cstator 257
		// sizeat 42 40
		clipdist 48
		ai AIZOMBIEWANDER
		ifspawnedby TEAMSPAWNER nullop else
		ife team 0 set team 2
		ifl sprite[].xrepeat 8
		{
			rand temp 4
			add temp 25
			seta[].xrepeat temp
			rand temp 3
			add temp 23
			seta[].yrepeat temp
			
		}
		// rand temp 13
		// ifrnd 224 { add temp 10 seta[].pal temp }
		
		
		
		ifvarand initflags 32
		{
			ifvarand monstflags 4096 nullop else
			{
				orvar monstflags 4096
				espawn RADAURA
				setav[RETURN].myspawner THISACTOR
			}
			addstrength 100
			set team 0
			espawn ZOMBIE
			setav[RETURN].myspawner THISACTOR
			
			espawn ZOMBIE
			setav[RETURN].myspawner THISACTOR
			
			ifrnd 64
			{
				espawn ZOMBIE
				setav[RETURN].myspawner THISACTOR
			}
			else
				spawn CRAZYLADY

		}
	}
	
	ifvarand initflags 32
	ifn bottarget -1
	ifrnd 4
	{
		sound COMET_FIRE
		rand zdist 2048
		sub zdist 2560
		ezshoot zdist RADPROJ
		
		rand angvar 512
		sub angvar 256
		add angvar sprite[].ang
		seta[RETURN].ang angvar
		shoot SPIT
	}

	ifai AIZOMBIEFROZEN { state zombiefrozenstate break }
	ifai AIZOMBIEGROW { state zombiegrowstate break }
	ifai AIZOMBIESHRUNK state zombieshrunkstate
	state monsterai
	ifhitweapon 
	{
		ifai AIZOMBIEWANDER ai AIZOMBIESEEK

		ifwasweapon SHRINKSPARK
		{	
		  sound ACTOR_SHRINKING
		  ai AIZOMBIESHRUNK
		  //set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
	
		ifg sprite[].extra 0
		{
			ifactorsound THISACTOR Z_PAIN1 nullop else
			ifactorsound THISACTOR Z_PAIN2 nullop else
			ifrnd 128 sound Z_PAIN1 else sound Z_PAIN2
		}

		ifdead
		{
			ifai AIZOMBIESHRUNK nullop else
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			 
			  ai AIZOMBIEFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  cactor ZOMBIE
			  ai AIZOMBIEGROW
			  break
			}
			state enemy_death
			sound SQUISHED
			spritepal 8
			state standard_jibs
			getlastpal
			killit
		}
	}
	else state checksquished
	ifai AIZOMBIEWANDER state zombiewalkstate else
	ifai AIZOMBIESEEK state zombiewalkstate else
	ifai AIZOMBIECHARGE state zombiechargestate else
	ifai AICRONENVOMIT state vomitstate
	
enda
