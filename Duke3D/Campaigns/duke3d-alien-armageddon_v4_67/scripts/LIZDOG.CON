action LIZDOGIDLE  0  1  5  1  10
action LIZDOGWALK  5  4  5  1  8
action LIZDOGRUN   5  4  5  1  4
action LIZDOGBITE1  35 3  5  1  20
action LIZDOGBITE2 40 1  5  1  40
action LIZDOGFROZEN 20  1  5  1  10
action LIZDOGSHT1  25 1  5  1  20
action LIZDOGSHT2  30 1  5  1  20
action LIZDOGSHT3  25 1  5  1  20
action LIZDOGEAT  -10 2  5  1  14 
action LIZDOGJMP1  50 1  5  1  20
action LIZDOGJMP2  55 1  5  1  20
action LIZDOGLAND  60 1  5  1  20

action LIZDOGHIT   -16319 1  5  1  18
action LIZDOGDIE   65 6  1  1  18
action LIZDOGDEAD  70 1  1  1  10

move LIZDOGWALKVEL 150
move LIZDOGRUNVEL 280
move LIZDOGJUMPVEL 280

ai AILIZDOGJUMP LIZDOGJMP1 LIZDOGJUMPVEL jumptoplayer
ai AILIZDOGSEEK LIZDOGWALK LIZDOGWALKVEL seekplayer
ai AILIZDOGSHRUNK LIZDOGWALK CYBERSHRUNKVELS fleeenemy
ai AILIZDOGFROZEN LIZDOGFROZEN STOPPED geth
ai AILIZDOGGROW LIZDOGFROZEN STOPPED geth
ai AILIZDOGDYING LIZDOGDIE STOPPED geth
ai AILIZDOGHIT LIZDOGHIT STOPPED geth
ai AILIZDOGATTK LIZDOGBITE1 LIZDOGWALKVEL geth
ai AILIZDOGSHOOT LIZDOGSHT1 STOPPED faceplayer
ai AILIZDOGEAT LIZDOGEAT STOPPED geth

state lizdogeatstate

	ife myvictim -1 { ai AILIZDOGSEEK break }
	ifg myvictim -1
	{
		ifn sprite[myvictim].statnum 1 set myvictim -1 else
		ifn actorvar[myvictim].monstatus 2 set myvictim -1 else
		ifn sprite[myvictim].picnum mtype set myvictim -1 else
		{
			dist temp THISACTOR myvictim
			ifg temp 1024 set myvictim -1
		}
		ifg myvictim -1
		{
			ifactioncount 2
			{
				resetactioncount

				set temp inithp
				div temp 10
				geta[].extra tempb
				add tempb temp
				seta[].extra tempb
				soundonce LIZEATSND
			}
			set dodgetime 3
			set spriteid myvictim
			state facesprite
		}
	
	}
	ifg sprite[].htextra 0
	ifn bottarget -1
	{
		ai AILIZDOGSEEK
		set myvictim -1
		ifactorsound THISACTOR LIZEATSND stopactorsound THISACTOR LIZEATSND
		break
	}
	set tempb inithp
	mul tempb 3 div tempb 2
	
	ifge sprite[].extra tempb // inithp 
	{ 
		ai AILIZDOGSEEK 
		ifactorsound THISACTOR LIZEATSND stopactorsound THISACTOR LIZEATSND
		ifg myvictim -1
		{
			seta[myvictim].htextra 10
			seta[myvictim].htpicnum RADIUSEXPLOSION
			seta[myvictim].htowner player[].i
			set myvictim -1
		}
		break 
	}
	
	
ends

state lizdogshootstate

	ifaction LIZDOGSHT1
	{
		ifspritepal 17
		{
			ifcount 2
			{
				geta[].z savz sub savz 1560 seta[].z savz
				resetcount
				sound TERMLASER 
				shoot GREENLASER
				add savz 1560
				seta[].z savz
			}
		}
		ifactioncount 1
		{
			ifn bottarget -1
			{
				set spriteid bottarget
				state facesprite
			}
			action LIZDOGSHT2
			ifspritepal 104
			{
				set xvel 832
				state rpg_targetprep
				set B targetdist
				shiftr B 2, ifg B 5120 set B 5120
				sub zdist B
				
				geta[].z savz, sub savz 4096, seta[].z savz
				zshoot zdist ARCBALLPROJ
				add savz 4096, seta[].z savz
				sound SHOCKBALLFIRE
			}
			else
			ifspritepal 17 nullop 
			else
			ifspritepal 3
			{
				geta[].z savz sub savz 1560 seta[].z savz
				resetcount
				sound TERMLASER 
				shoot GREENLASER
				add savz 1560
				seta[].z savz
			}
			else
			{
				set xvel 644
				state rpg_targetprep
				setprojectile[SHOCKBALL].drop -208
				set B targetdist
				shiftr B 2, ifg B 5120 set B 5120
				sub zdist B
				ezshoot zdist SHOCKBALL
				setav[RETURN].myspawner THISACTOR
				setprojectile[SHOCKBALL].drop 0
				sound SHOCKBALLFIRE
			}
			
		}
	}
	ifaction LIZDOGSHT2
	{
		ifactioncount 1
		action LIZDOGSHT3
	}
	ifaction LIZDOGSHT3
	{
		ifactioncount 1
		ai AILIZDOGSEEK
	}

ends


state lizdoggrowstate state genericgrowcode ends

state lizdogshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AILIZDOGSEEK
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 104 sizeto 56 52 else sizeto 26 24
	}
  else
    state genericshrunkcode

ends

state lizdogfrozenstate

    ifcount THAWTIME
    {
      ai AILIZDOGSEEK
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state lizdogdyingstate

	strength 0
	state deadsquished
	
	ifaction LIZDOGDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action LIZDOGDEAD
			iffloordistl 8 sound THUD
			ifrnd 128
				spawn BLOODPOOL
			break
		}
	}

	ifaction LIZDOGDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				ifspritepal 104 state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				ifspritepal 104 state standard_jibs
				killit
			}
		}

	}
	
ends

state lizdogroamsounds

	ifactorsound THISACTOR LIZDOG_ROAM break
	ifactorsound THISACTOR LIZDOG_JUMP break
	ifrnd 128 sound LIZDOG_ROAM else sound LIZDOG_JUMP

ends

state lizdogseekstate

	set temp NO
	ifge SKILL 4 { ifcount 15 set temp YES }
	else ifcount 30 set temp YES
	
	ifn bottarget -1 ife temp YES ifmove LIZDOGWALKVEL 
	{
		move LIZDOGRUNVEL seekplayer
		ifn sprite[].pal 104
		action LIZDOGRUN
	}
	
	ifspritepal 104 
	{
		add countvar 1
		ifge countvar 8
		{
			sound DOGSTEP
			set countvar 0
		}
	}
	
	ifg myvictim -1
	  {
		ife sprite[myvictim].statnum 1024 set myvictim -1
		else
		ifn actorvar[myvictim].monstatus 2 set myvictim -1
		
		ifg myvictim -1
		{
		  ifn dodgetime 0
		  ife actorvar[myvictim].monstatus 2
		  {
			set dodgetime 3
			set spriteid myvictim
			state facesprite
			
			dist temp THISACTOR myvictim
			ifl temp 644 { ai AILIZDOGEAT break }
			
			canseespr THISACTOR spriteid temp
			ife temp NO set myvictim -1 else
			ifnotmoving set myvictim -1 else
			break
		  }
		  else set myvictim -1
		}
	  }
	
	ifn myspawner -1 // incoming projectile
	{
		state incoming_eval
		set dodgetime 8
		set crumbwait 10
		seta[].ang angvel
		set myspawner -1
	}
	
	ifceilingdistl 96 nullop else
	ifcount 15
	{
		ifrnd 32
		{
			ifnotmoving { sound LIZDOG_JUMP ai AILIZDOGJUMP break }
			ife seemytarget YES ifp phigher
			ifl targetdist 6144 { sound LIZDOG_JUMP ai AILIZDOGJUMP break }
		}
	}
	
	ifcount 10
	ifnotmoving
	ife dodgetime 0
	ife bottarget -1
	{
		move LIZDOGWALKVEL randomangle
		set dodgetime 8
		set crumbwait 8
	}
	
	ife seemytarget YES
	{
		ifn bottarget -1 set target bottarget else 
		ife targetalive YES getp[].i target
		else set target -1
	}
	else set target -1
	
	ifn target -1
	ifactioncount 4
	{
		ifl targetdist 16384
		ifg targetdist 4096
		ifrnd 4
		{
			ai AILIZDOGSHOOT
			break
		}
	}
	

	ifrnd 1 state lizdogroamsounds
	
	ife targetalive YES
	ife seemytarget YES
	{
		ifl targetdist 1792 
		{ 
			ai AILIZDOGATTK 
			break 
		}
	}
	
	set temp inithp
	mul temp 3 div temp 2
	ifg targetdist 3072
	ife gametype 0
	ifcount 8
	ifl sprite[].extra temp // inithp
	ifstrength 1500
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife actorvar[spriteid].monstatus 2
			ifn spriteid myshelly
			ifge sprite[spriteid].z sprite[].z
			{
				ldist temp THISACTOR spriteid
				ifl temp 8192
				{
					canseespr THISACTOR spriteid temp
					ife temp YES
					{
						ife dodgetime 0 count 10
						set myvictim spriteid
						set mtype sprite[myvictim].picnum
						set dodgetime 5
						set bottarget -1
					}
				}
			}
			nextspritestat spriteid spriteid
		}
	  
	}


ends

state lizdogjumpstate

  ifaction LIZDOGJMP1
  {
	ifactioncount 2
	{
		action LIZDOGJMP2
		move LIZDOGJUMPVEL geth
	}
  } else
  ifaction LIZDOGJMP2
  {
	iffloordistl 4
	{
		sound THUD
		action LIZDOGLAND
		move STOPPED geth
	}
  } else
  ifaction LIZDOGLAND
  {
	ifactioncount 2 ai AILIZDOGSEEK
  }
  
ends

state lizdogbite

	ife targetalive NO
	{
		sound BITEMISS
		break
	}
	
	ifn bottarget -1
	ifn sprite[].sectnum sprite[bottarget].sectnum
	ifrnd 128
	{
		sound BITEMISS
		break
	}
	
	ifn bottarget -1
	ifn bottarget player[].i
	{
		ife seemytarget YES
		ifl targetdist 1596
		{
		  ifspritepal 104 seta[bottarget].htextra 22 else
		  seta[bottarget].htextra 11
		  seta[bottarget].htpicnum SHOTSPARK1
		  seta[bottarget].htowner THISACTOR
		  seta[bottarget].htang sprite[].ang
		  sound BEASTBITESND
		}
		else sound BITEMISS
	}
	else
	{
		ifpdistl 1596
		ife seeplayer YES
		{
		  getp[].i spriteid
		  seta[spriteid].htpicnum SHOTSPARK1
		  seta[spriteid].htowner THISACTOR
		  ifspritepal 104 seta[spriteid].htextra 22 else
		  seta[spriteid].htextra 11
		  seta[spriteid].htang sprite[].ang
		  
		  state checksaberblock
		  ifn sprite[spriteid].htextra -1
		  {
			  palfrom 16 16
			  ife pchar 1 sound B_GRUNT else
				sound DUKE_GRUNT
			  sound BEASTBITESND
		  }
		}
		else sound BITEMISS
	}

ends

state lizdogattkstate

	ifaction LIZDOGBITE1
	{
		ifactioncount 1
		{
			action LIZDOGBITE2
			state lizdogbite
		}
	}
	else
	ifaction LIZDOGBITE2
	{
		ifactioncount 1
		{
			action LIZDOGBITE1
			
			ife targetalive NO
			{
				ai AILIZDOGSEEK
				break
			}
			resetcount
			action LIZDOGBITE1
			ifg targetdist 2048 ai AILIZDOGSEEK
			ife bottarget -1 ifpdistg 2048 ai AILIZDOGSEEK
		}
	}

	ifn bottarget -1 
	{
		ifmove LIZDOGWALKVEL { ifl targetdist 1280 move STOPPED geth }
		else ifmove STOPPED { ifg targetdist 1560 move LIZDOGWALKVEL geth }
		set spriteid bottarget
		state facesprite
		
		ife actorvar[bottarget].monstatus 2 ai AILIZDOGSEEK
		
		ife bottarget player[].i
		ifpdistl 1500
		ife seeplayer YES
		{
			setp[].posxv 0 setp[].posyv 0 setp[].poszv 0 setp[].jumping_counter 0
		}
		
		ifl targetdist 1500
		seta[bottarget].xvel 0
	}
	else
	{
		ifmove LIZDOGWALKVEL { ifpdistl 1280 move STOPPED geth }
		else ifmove STOPPED { ifpdistg 1560 move LIZDOGWALKVEL geth }
		set spriteid player[].i
		state facesprite
	}

ends

eventloadactor LIZDOG ifspritepal 104 sizeat 56 52 else sizeat 26 24 cstator 257 
ifg sprite[].lotag 4 
{
	ifg sprite[].lotag 9
		geta[].lotag SPRITELOTAG
	seta[].lotag 0
}
else ife attmode YES seta[].lotag 0
ife sprite[].extra 1
{
	set initsprite sprite[].picnum
	geta[].hitag team seta[].hitag 0 cactor TEAMSPAWNER cstat 32768
}
ifn sprite[].xvel 0 geta[].xvel droptile
ifg sprite[].owner 0 
{
	geta[].owner monstflags ifvarand monstflags 1 xorvar monstflags 1
	seta[].owner -1
}
enda
eventloadactor LIZDOGSTAYPUT ifspritepal 104 sizeat 56 52 else sizeat 26 24 cstator 257 

ifg sprite[].lotag 4 
{
	ifg sprite[].lotag 9
		geta[].lotag SPRITELOTAG
	seta[].lotag 0
}
else ife attmode YES seta[].lotag 0
ife sprite[].extra 1
{
	set initsprite sprite[].picnum
	geta[].hitag team seta[].hitag 0 cactor TEAMSPAWNER cstat 32768
}
ifn sprite[].xvel 0 geta[].xvel droptile
ifg sprite[].owner 0 
{
	geta[].owner monstflags ifvarand monstflags 1 xorvar monstflags 1
	seta[].owner -1
}

enda

state lizdogcode
	ifai 0
	{
		ai AILIZDOGSEEK
		ifspritepal 104 { strength 800 sizeat 56 52 clipdist 80 orvar initflags 32 } else { sizeat 26 24 clipdist 42 }
		cstat 257
		geta[].htflags temp, orvar temp 8192, seta[].htflags temp
	}
	fall
	state monsterai

	ifai AILIZDOGDYING { state lizdogdyingstate break }
	
	ifai AILIZDOGFROZEN { state lizdogfrozenstate break }
	ifai AILIZDOGGROW { state lizdoggrowstate break }
	ifai AILIZDOGSHRUNK state lizdogshrunkstate else
	ifai AILIZDOGSEEK state lizdogseekstate else
	ifai AILIZDOGEAT state lizdogeatstate else
	ifai AILIZDOGATTK state lizdogattkstate else
	ifai AILIZDOGJUMP state lizdogjumpstate else
	ifai AILIZDOGSHOOT state lizdogshootstate
	
	set myspawner -1
	
	ifai AILIZDOGSHRUNK nullop else
	ifg stun 0 
		ai AILIZDOGHIT
	
	
	ifai AILIZDOGHIT
	{
		ifactioncount 2 ai AILIZDOGSEEK
	}

	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{	
		  sound ACTOR_SHRINKING
		  ai AILIZDOGSHRUNK
		  set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		
		sound LIZDOG_PAIN

		ifdead
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  ai AILIZDOGFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  ai AILIZDOGGROW
			  break
			}
			addkills 1
			state enemy_death
			ai AILIZDOGDYING
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			sound LIZDOG_DIE
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			
			
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				ifspritepal 104 state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state standard_jibs
				ifspritepal 104 state standard_jibs
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		
		ifai AILIZDOGHIT nullop else
		ifai AILIZDOGSHRUNK nullop else
		{
			ifg stun 0 { ai AILIZDOGHIT break }
			
			ifrnd 24 
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			{ 
				ai AILIZDOGHIT 
				break 
			}
		}
	}
	else state checksquished
ends

useractor enemy LIZDOG LIZDOGSTRENGTH state lizdogcode enda
useractor enemystayput LIZDOGSTAYPUT LIZDOGSTRENGTH cactor LIZDOG enda