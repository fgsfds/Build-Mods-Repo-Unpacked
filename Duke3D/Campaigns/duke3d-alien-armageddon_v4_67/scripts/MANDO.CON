// MANDO PHETT

action AMANDOSTAND  0  1  5  1  10
action AMANDOSTANDSHOOT2	5  1  5  1  10
action AMANDOSTANDSHOOT1	30 1  5  1  10
action AMANDORUN	35  4  5  1  6
action AMANDORUNSHOOT  10  4  5  1  6
action AMANDOMELEE1	55  1  5  1  22
action AMANDOMELEE2 60  1  5  1  20
action AMANDOMELEE3	55  1  5  1  22
action AMANDOJUMP	65  1  5  1  20
action AMANDOFALL	70  1  5  1  20
action AMANDOJETOFF  596  4  5  1  12
action AMANDODIE	120  6  1  1  16
action AMANDOCLOAK  120  1  1  1  25
action AMANDODEAD   125  1  1  1  10
action AMANDOJET	30  1  5  1  10
action AMANDOBOMB	30  1  5  1  10
action AMANDOROLL	596  4  5  1  12
action AMANDOROLLBACK  611  4  5  -1  12
action AMANDOCROUCH 0  1  5  1  10
action AMANDOCRAWL	5  4  5 1  12
action AMANDOCROUCHSHOOT  51  1  5  1  10
action AMANDOSWIM	100  4  5  1  12
action AMANDODOWN   1134   2  5  1  50
action AMANDOSTUN	4787  1  5  1  20
move MANDOSHOOTBOT
move MANDOSHOOTPLAYER

move MANDOSHRUNKVEL 40
move MANDOJUMPVEL 0 -96
move MANDOFALLVEL

move MANDOSTOPPED

gamevar mandotaunts 0 0
gamevar mandotaunt_time 0 0

ai AIMANDOHEAL AMANDOCROUCH MANDOSTOPPED geth
ai AIMANDOWAIT AMANDOSTAND MANDOSTOPPED geth
ai AIMANDOFROZEN AMANDOSTAND MANDOSTOPPED geth
ai AIMANDOSHRUNK AMANDORUN MANDOSHRUNKVEL furthestdir
ai AIMANDOGROW AMANDOMELEE1 MANDOSTOPPED geth
ai AIMANDORUN AMANDORUN STOPPED geth
ai AIMANDOJUMP AMANDOJUMP MANDOJUMPVEL geth getv
ai AIMANDODYING AMANDODIE MANDOSTOPPED geth
ai AIMANDOWANDER AMANDORUN STOPPED geth
ai AIMANDOMELEE AMANDOMELEE1 STOPPED geth
ai AIMANDOSTUN AMANDOSTUN STOPPED geth
ai AIMANDODOWN AMANDODOWN MANDOSTOPPED geth

state mandoroamsounds

	ifn mandotaunt_time 0 break
	ife pdown YES break
	ife gametype 0 ife seemytarget NO break
	ife team 0
	{
		switch mandotaunts
		case 0 globalsound MANDO_PERMISSION break
		case 1 globalsound MANDO_GOINGDOWN break
		case 2 globalsound MANDO_YOURPLACE break
		case 3 globalsound MANDO_COMPETITION break
		case 4 globalsound MANDO_AMAZING break
		endswitch
	}
	set mandotaunt_time 600
	add mandotaunts 1
	ifg mandotaunts 4 set mandotaunts 0
	

ends

state mandopainsounds

	ifsound MANDO_PAIN1 break
	ifsound MANDO_PAIN2 break
	ifsound MANDO_PAIN3 break
	ifsound MANDO_PAIN4 break
	rand temp 3
	ife temp 0 sound MANDO_PAIN1 else
	ife temp 1 sound MANDO_PAIN2 else
	ife temp 2 sound MANDO_PAIN3 else
	ife temp 3 sound MANDO_PAIN4
ends

state mando_body_jibs

	rand zdist 1024
	sub zdist 3500
	ezshoot zdist MANDOHEADPROJ
	rand angvar 512
	sub angvar 256
	add angvar sprite[].htang
	seta[RETURN].ang angvar
	seta[RETURN].pal sprite[].pal
	
	rand zdist 1024
	sub zdist 3500
	ezshoot zdist MANDOLEGPROJ
	rand angvar 2047
	seta[RETURN].ang angvar
	seta[RETURN].pal sprite[].pal
	
	rand zdist 1024
	sub zdist 3500
	ezshoot zdist MANDOCHESTPROJ
	rand angvar 2047
	seta[RETURN].ang angvar
	seta[RETURN].pal sprite[].pal
	
	rand zdist 1024
	sub zdist 3500
	ezshoot zdist MANDOARMPROJ
	rand angvar 2047
	seta[RETURN].ang angvar
	seta[RETURN].pal sprite[].pal
	
	rand zdist 1024
	sub zdist 3500
	ezshoot zdist MANDOARMPROJ
	rand angvar 2047
	seta[RETURN].ang angvar
	seta[RETURN].pal sprite[].pal
	seta[RETURN].cstat 132

ends

state mandoshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AIMANDOWAIT
  else
    ifcount SHRUNKCOUNT
      sizeto 26 24
  else
    state genericshrunkcode

ends

state startmandorollback

	geta[bottarget].x x
	geta[bottarget].y y
	sub x sprite[].x
	sub y sprite[].y
	geta[].z z
	sub z 8192
	getangle angle2 x y
	
	add angle2 1024
	cos mycos angle2
	sin mysin angle2
	
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 mysector temp spriteid x y zdist 4294901809
	sub x sprite[].x
	sub y sprite[].y
	mul x x
	mul y y
	add x y
	sqrt x x
	ifl x 1560 break
	
	set angvel angle2
	set angvar angvel
	sub angvar 1024
	seta[].ang angvar
	ai AIMANDORUN
	set dodgetime 12
	set crumbwait 10
	set float -40
	cactor MANDOCROUCH
	action AMANDOROLLBACK
	set myspawner -1
		
ends

state startmandododge

	ifn float 0 { set myspawner -1 break }
	ai AIMANDORUN
	ife myspawner -1 set myspawner player[].i
	geta[myspawner].x x
	geta[myspawner].y y
	sub x sprite[].x
	sub y sprite[].y
	geta[].z z
	sub z 8192
	getangle angvel x y

	add angvel 512
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 mysector temp spriteid x y zdist 4294901809
	sub x sprite[].x
	sub y sprite[].y
	mul x x
	mul y y
	add x y
	sqrt x x
	set xydist x
	
	sub angvel 1024
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 mysector temp spriteid x y zdist 4294901809
	sub x sprite[].x
	sub y sprite[].y
	mul x x
	mul y y
	add x y
	sqrt x x
	
	ifl x xydist add angvel 1024
	
	set dodgetime 12
	set crumbwait 10
	set float -50
	cactor MANDOCROUCH
	action AMANDOROLL
	ife sprite[].htextra -1 ifrnd 128
	ife emp_overlay 0
	ifn myspawner bottarget
	{
		ifrnd 84 soundonce MANDO_SLOW else
		ifrnd 128 soundonce MANDO_NOPE else
		soundonce MANDO_AIM
	}
	set myspawner -1
	
	ife team 0
	ifn bottarget -1
	ifle redscore bluescore
	ifrnd 42
	{
		espawn ROLLYTURRET
		seta[RETURN].pal 19
		geta[bottarget].x x2
		geta[bottarget].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvar x2 y2
		seta[RETURN].ang angvar
		setav[RETURN].mtype -2048
		setav[RETURN].myspawner THISACTOR
		setav[RETURN].dodgetime 40
		setav[RETURN].mlevel mlevel
	}
		
ends

state mandojumpcheck

	ifai AIMANDOJUMP break
	ifceilingdistl 96 break
	geta[].z z
	sub z 4096
	
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 512
		{
			sub z 8192
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			set xydist startx
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 1560 
			{ 
				cactor MANDOFETT ai AIMANDOJUMP 
				set startx angvel 
				set starty 0
				seta[].ang angvel 
			}
			else ifl xydist 384
			{
				set RETURN hitwall
				ifg navpoint -1 ifrnd 32 set navpoint -1
			}
		}
	}

ends

state rand_mandojumpcheck
	ifai AIMANDOJUMP break
	ifceilingdistl 96 break
	ifg navpoint -1
	{
		findnearactor JUMPAD 1280 spriteid
		ife spriteid -1
		ifn RETURN 0
		{
			set starty 0
			cactor MANDOFETT ai AIMANDOJUMP 
		}
		break
	}



ends

state mandojumpstate

	ife bottarget -1
	ife starty 0
	seta[].ang angvel
	
	ifmove MANDOJUMPVEL
	{
		ifcount 6 
		{
			move MANDOFALLVEL geth
			action AMANDOFALL
		}
	}
	ifaction AMANDOFALL
	{
		iffloordistl 4
		{
			ai AIMANDORUN
			// sound MANDOLAND
			ifnotmoving { set navpoint -1 set savedvalue -1 }
		}
	}

ends

state mando_assess_allies

ife team 1 
ifn gametype CTF
ifn gametype CONTROL
{
	ife pdown YES
	ifcansee ifpdistl 20480
	ifge sprite[].extra 75
	{
		ife initflags player[].i ifai AIMANDOHEAL
		{
			ldist xydist THISACTOR initflags
			ifg xydist 768 
			{ cactor MANDOCROUCH action AMANDOJET geta[].z z sub z 6144 seta[].z z }
			break
		}
		ife initflags -1 set initflags player[].i
	}
	else ife initflags player[].i set initflags -1
	
	ife initflags -1
	ifn myshelly -1
	ife actorvar[myshelly].monstatus 2
	ifge sprite[].extra 75
	{
		ldist xydist THISACTOR myshelly
		ifl xydist 16384
		{
			geta[].z z
			sub z 6144
			seta[].z z
			canseespr THISACTOR myshelly temp
			add z 6144
			seta[].z z
			ife temp YES
				set initflags myshelly
		}
	}
	
	ifn myshelly -1
	ife initflags myshelly ifai AIMANDOHEAL
	{
		ldist xydist THISACTOR initflags
		ifg xydist 768
		{ cactor MANDOCROUCH action AMANDOJET geta[].z z sub z 6144 seta[].z z }
	}
	ifn myshelly -1
	ife initflags myshelly ifn actorvar[myshelly].monstatus 2 set initflags -1
	
}

ends

state mandohealstate

	state mando_assess_allies
	
	ife initflags player[].i
	{
		soundonce AIRBURST
		addphealth 1
		addstrength -1
		ifstrength 1 strength 1
		// ifg sprite[player[].i].extra 50 addphealth 1
		palfrom 16 0 10 63
		espawn CAPTUREPLUS
		seta[RETURN].x player[].posx
		seta[RETURN].y player[].posy
		seta[RETURN].z sprite[player[].i].z
		seta[RETURN].pal 1
		rand x 512
		sub x 256
		add x sprite[RETURN].x
		seta[RETURN].x x
		rand y 512
		sub y 256
		add y sprite[RETURN].y
		seta[RETURN].y y
	}
	else ifn initflags -1 ife initflags myshelly
	{
		ife actorvar[initflags].monstatus 2 // make sure they are still down
		{
			soundonce AIRBURST
			addstrength -1
			ifstrength 10 strength 10
			setav[initflags].ikicked -2
		}
		else { set initflags -1 cactor MANDOFETT ai AIMANDOWAIT }
	}	
	else { set initflags -1 cactor MANDOFETT ai AIMANDOWAIT }

ends

state mandofrozenstate

    ifcount THAWTIME
    {
      ai AIMANDOWAIT
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  ife team 1 { strength 5 cactor MANDOCROUCH ai AIMANDODOWN set monstatus 2 break }
	  ife gametype 0
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
	  ifn team actorvar[player[].i].team
        state pkick_check
  
ends

state mandogrowstate state genericgrowcode ends

state mandokick

	ifn bottarget -1
	ifl targetdist 1340
	ife targetalive YES
	{
		set spriteid bottarget
		state facesprite
		cos xvel sprite[].ang
		sin yvel sprite[].ang
		ife bottarget player[].i
		{
			palfrom 32 63
			wackplayer
			// shiftvarl xvel 8
			// shiftvarl yvel 8
			mul xvel 384
			mul yvel 384
			setplayer[THISACTOR].posxv xvel
			setplayer[THISACTOR].posyv yvel
			// push player backwards
		}
		else
		{
			shiftr xvel 4 shiftr yvel 4
			movesprite bottarget xvel yvel 0 CLIPMASK0 RETURN
			getav[bottarget].stun temp
			add temp 10
			setav[bottarget].stun temp
		}
		seta[bottarget].htextra 20
		seta[bottarget].htpicnum KNEE
		seta[bottarget].htowner THISACTOR
		seta[bottarget].htang sprite[].ang
		sound KICK_HIT
	}
ends

state mandomeleestate

	ife targetalive NO
	{
		ai AIMANDORUN
		break
	}

	ifaction AMANDOMELEE1
	{
		ifactioncount 1
		{
			action AMANDOMELEE2
			state mandokick
		}
	}

	ifaction AMANDOMELEE2
	{
		ifactioncount 1
			action AMANDOMELEE3	
	}
	ifaction AMANDOMELEE3
	{
		ifactioncount 1
		{
			ifn bottarget -1
			ifl targetdist 4096
			ifn gametype CTF
			{
				ifl targetdist 1280
				{
					state startmandorollback
					ifaction AMANDOROLLBACK
					break
				}
				
				
				ai AIMANDOWAIT
				action AMANDOSTANDSHOOT1
				ifrnd 128
				{
					cactor MANDOCROUCH
					action AMANDOCROUCH
				}
				break
			}
			ai AIMANDORUN
			set botclip -5
		}
	}
ends

state mandodownstate

	// ifrnd 1 ife countvar 0 ifn ikicked -2 { soundonce DEA_REVIVEME set countvar 210 }
	
	ifcount 900 ifn ikicked -2 { resetcount set ikicked -2 }
	
	set burning 0
	ifhitweapon { } 
	ifstrength 5 strength 5
		
	ife ikicked -2 // healed by medic
	{
		addstrength 12
		ifspritepal 33 getlastpal else spritepal 33
	}
	else
	{
		ifpdistl 1280
		ifp pfacing
		ifp palive
		ifcansee
		{
			ifphealthl 30 { quote 775 break }
			quote 129
			ifhitspace
			{
				ifstrength 20
				{
					ife pchar 0
					{
					// 	rand temp 3
					// 	ife temp 0 globalsound DUKE_HEAL1
						ife temp 1 globalsound DUKE_HEAL2
					// 	ife temp 2 globalsound DUKE_HEAL3
					// 	ife temp 3 globalsound DUKE_HEAL4
					}
					else
					ife pchar 1
					{
						rand temp 2
						ife temp 0 globalsound B_TRYNOTODIE
						ife temp 1 globalsound B_HANGINTHERE
						ife temp 2 globalsound B_TRUSTME
					}
				}
				addstrength 12
				set countvarb 0
				ifspritepal 33 getlastpal else spritepal 33
				ifg player[].firstaid_amount 0
				{
					getp[].firstaid_amount temp
					sub temp 1
					setp[].firstaid_amount temp
				}
				else addphealth -1
				
			}
			else ifspritepal 33 getlastpal
		}
		else 
		{
			ifspritepal 33 getlastpal
			ife player[].ftq 129 setp[].fta 0
			ife player[].ftq 775 ifg player[].fta 30 setp[].fta 30
		}
	}
	
	ifstrength 350 nullop
	else
	{
		strength 350
		cactor MANDOFETT
		ai AIMANDOWAIT
		// ife countvar 0 { sound DEA_THANKYOU set countvar 120 }
		set ikicked 0
		set bottarget -1
		set navpoint -1
		seta[].htowner THISACTOR
		set monstatus 1
		cstat 257
		ife player[].ftq 129 setp[].fta 0
		ifspritepal 33 getlastpal
	}

ends

state mandodyingstate

	strength 0
	ifaction AMANDODIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action AMANDODEAD
			iffloordistl 8 sound THUD
			ifrnd 128 spawn BLOODPOOL
			ife team 1 { strength 5 cactor MANDOCROUCH ai AIMANDODOWN set monstatus 2 }
			break
		}
	}
	ifaction AMANDODEAD
	{
		state deadsquished
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spawn BLOODPOOL
				state mando_body_jibs
				state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spawn BLOODPOOL
				state mando_body_jibs
				state lite_jibs
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength MANDOSTRENGTH
		  // ai AIMANDOWAIT
		// }
	}

ends

state mandotargetmove

	ifai AIMANDOJUMP break
	
	// these move commands are being used as labels only
	// they indicate what the bot is supposed to be doing
	ifn bottarget -1
	{
		ifai AIMANDOMELEE
		{
			ifmove MANDOSHOOTBOT nullop else
			move MANDOSHOOTBOT geth
		}
		else
		ife bottarget player[].i
		{
			ifmove MANDOSHOOTPLAYER nullop else
			move MANDOSHOOTPLAYER faceplayersmart
		}
		else
		{
			ifmove MANDOSHOOTBOT nullop else
			move MANDOSHOOTBOT geth
		}
	}
	else
	{
		ifmove MANDOSHOOTPLAYER move STOPPED geth
		else
		ifmove MANDOSHOOTBOT move STOPPED geth
	}
	

ends

state mandostartwander
	
	cactor MANDOFETT
	ai AIMANDOWANDER
	set dodgetime 10
	set navpoint -1
	set savedvalue -1
	set B 0
	set xydist 2048
	whilevarn B 32
	{
		add B 1
		geta[].z z
		sub z 4096
		rand angvel 2047
		cos mycos angvel
		sin mysin angvel
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz clipmask
		
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		
		ifg startx xydist set B 32
	}
	seta[].ang angvel

ends

state mandowaitstate

	ifaction AMANDOCLOAK
	{
		set redbaseval 97
		ifspritepal 33 getlastpal else spritepal 33
		ifactioncount 3 
		{ 
			cactor MANDOFETT ai AIMANDORUN 
			set redbaseval 100 cstat 257
			ifspritepal 33 getlastpal
		} 
		else
		ifactioncount 2 { ifvarand player[].player_par 1 cstat 33025 else cstat 771 } else
		{ ifvarand player[].player_par 1 cstat 259 else cstat 771 }
		break
	}
	
	ifgapzl 64
	{
		ifaction AMANDOSTAND
		{
			cactor MANDOCROUCH
			action AMANDOCROUCH
		}
	}
	ifaction AMANDOSTANDSHOOT1
	{
		ifcount 60 // 30
		{
			cactor MANDOFETT
			ai AIMANDORUN
		}
	}
	ifaction AMANDOCROUCH
	{
		ifcount 60
		{
			cactor MANDOFETT
			ai AIMANDORUN
		}
	}
	ifaction AMANDOSTAND 
	{
		ifcount 6
		{
			cactor MANDOFETT
			ai AIMANDORUN
		}
	}
	orvar monstflags YES

ends

state mandoshootstate

	ifai AIMANDOMELEE break
	ifaction AMANDOJETOFF break
	ifaction AMANDOROLL break
	ifaction AMANDOROLLBACK break
	ifai AIMANDOJUMP break
	ifaction AMANDOCLOAK break
	
	ifl botclip 0 // reloading
	{
		add botclip 1
		ifvarand monstflags 16384 ifl botclip 0 add botclip 1
		ife botclip 0
		{
			// 0=plasma, 1=RPG, 2=flak, 3=chaingun, 4=shotgun
			ife mtype 0 set botclip 8 else
			ife mtype 1 set botclip 5 else
			ife mtype 2 set botclip 6 else
			ife mtype 3 set botclip 30 else
			ife mtype 4 set botclip 6 else
			set botclip 8
		}
		
		ife countvarb 0
		ifl targetdist 1152
		iffloordistl 4
		ifactor MANDOFETT
		{
			ai AIMANDOMELEE
			ife mtype 1 { set  mtype 0 set botclip -20 set countvar 0 }
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
			break
		}
		break
	}
	
	add countvar 1
	ifvarand monstflags 16384 add countvar 1
	
	ife targetalive YES
	ife seemytarget YES
	ife shootmytarget YES
	{
		ifaction AMANDOJET
		{
			set temp NO
			ifrnd 4 set temp YES
			ifl targetdist 4096 set temp YES
			ifg sprite[].htextra 0 set temp YES
			ifg dodgetime 0 set temp NO
			ife bluecarrier THISACTOR set temp NO
			ife temp YES
			{
				action AMANDOJETOFF
				move MANDOFALLVEL geth
				sound DUKE_JETPACK_OFF
				set countvarc -120
			}
			break
		}

		ife countvarb 0
		ifl targetdist 1152
		iffloordistl 4
		{
			cactor MANDOFETT
			ai AIMANDOMELEE
			ife mtype 1 { set  mtype 0 set botclip -20 set countvar 0 }
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
			break
		}
		// ife mtype 1
		// ifl targetdist 2560
		// {
			// set botclip -20
			// set mtype 0
			// set countvar 0
		// }
		switch mtype // weapon type
		
		case 0 // GREENLASER
		ifg countvar 5
		{
			ifg targetdist 1024
			{
				geta[].x savx
				geta[].y savy
				set x savx
				add x 256
				geta[].ang angvar
				add angvar 128
				rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
				seta[].x x2
				seta[].y y2
			}
			geta[].z savz set z savz sub z 1024 seta[].z z
			sound TERMLASER
			setprojectile[GREENLASER].extra 32
			shoot GREENLASER
			setprojectile[GREENLASER].extra 16
			ifg targetdist 1024
			{
				seta[].x savx
				seta[].y savy
			}
			seta[].z savz
			set countvar 0
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -40
				ifrnd 192
				{
					ifrnd 128 ifl targetdist 8192 set mtype 3
					else ifg targetdist 2292 set mtype 1
				}
			}
			set countvarb 1
		}
		break
		case 1 // RPG
		ifg countvar 19
		{
			geta[].z savz
			set z2 savz
			sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
			set countvar 0

			ifg targetdist 1024
			{
				geta[].x savx
				geta[].y savy
				set x savx
				add x 256
				rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
				seta[].x x2
				seta[].y y2
			}
			set xvel 644
			state rpg_targetprep
			ezshoot zdist RPG
			geta[RETURN].xvel xvel mul xvel 3 div xvel 2 seta[RETURN].xvel xvel
			geta[RETURN].zvel z mul z 3 div z 2 seta[RETURN].zvel z
			ife gametype 0 seta[RETURN].extra 80 else
			seta[RETURN].extra RPG_WEAPON_STRENGTH
			ifg targetdist 1024
			{
				seta[].x savx
				seta[].y savy
			}

			sound RPG_SHOOT
			seta[].z savz
			sub botclip 2
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 128 
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 0
					else
					set mtype 3
					
				}
			}
			set countvarb 1
		}
		else
		ifl targetdist 1560
		{
			set botclip -10
			ifrnd 128 
			{
				
				ifrnd 128 ifl targetdist 8192
					set mtype 0
				else
				set mtype 3
				
			}
		}
		break
		case 2 // DIVEBOMB
			
			ifaction AMANDOBOMB
			{
				geta[].z z
				sub z 4096
				cos mycos angvel
				sin mysin angvel
				hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
				set startx sprite[].x
				sub startx hitx
				mul startx startx
				set y2 sprite[].y
				sub y2 hity
				mul y2 y2
				add startx y2
				sqrt startx startx
				ifl startx 1024 set bottarget -1
				
				ife bottarget -1 { action AMANDOJET set botclip -20 ifrnd 128 set mtype 1 else set mtype 3 set dodgetime 0 }
				else
				{
					ldist xydist THISACTOR bottarget
					ifl xydist 6144 ifg countvar 5
					ifg botclip 0
					{ 
					
						geta[bottarget].x x2
						geta[bottarget].y y2
						sub x2 sprite[].x
						sub y2 sprite[].y
						getangle angvar x2 y2
						getincangle angle2 angvar sprite[].ang
						abs angle2
						ifl angle2 1024
						ifl sprite[].z sprite[bottarget].z
						{
							ife team 0
							shoot MORTER 
							else
							{
								set xvel 844
								geta[].z savz
								set z savz
								add z 8192
								seta[].z z
								state rpg_targetprep
								ezshoot zdist MORTER2
								seta[].z savz
							}
							sound RPG_SHOOT 
							set countvar 0 
							sub botclip 1
						}
					}
					seta[].ang angvel
					set dodgetime 3
					ifg xydist 7168 { action AMANDOJET set botclip -20 ifrnd 128 set mtype 1 else set mtype 3 set dodgetime 0  }
				}
			}
			else
			{ set botclip -20 ifrnd 128 set mtype 1 else set mtype 3 set dodgetime 0 cactor MANDOFETT ai AIMANDORUN }
			
		break
		case 3 // CHAINGUN
		ifg countvar 2
		{
			
			set countvar 0
			ifn bottarget -1
			{
				state hitscan_targetprep
				zshoot zdist SHOTSPARK1
				state hitscan_targetprep
				zshoot zdist SHOTSPARK1
				state hitscan_targetprep
				zshoot zdist SHOTSPARK1
			}
			else 
			{
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
			}
			
			sound M_GUN_SHOOT
			
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -40
				ifrnd 192 
				{
					set botclip -60
					ifrnd 128 ifl targetdist 8192 set mtype 0
					else ifg targetdist 2292 set mtype 1
					else set botclip -40 // no weapon change
				}
			}
			set countvarb 1
		}
		break
		case 4 // SHOTGUN
		ifg targetdist 8704
		{
			set botclip -20
			set countvar 0
			ifrnd 128 set mtype 3 else set mtype 1
		}
		ife countvar 14 sound NEWSHOTCOCK
		ifg countvar 29
		{
			
			sound NEWSHOTFIRE
			set countvar 0
			
			ifn bottarget -1
			{
				state hitscan_targetprep
				state randzshoot
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				zshoot tempb SHOTSPARK1
				// zshoot tempb SHOTSPARK1
				
			}
			else
			{
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				// shoot SHOTSPARK1
			}
			
			sub botclip 1
			ifle botclip 0 
			{
				set botclip -30
				ifrnd 128
				{
					set botclip -60
					ifrnd 128 ifg targetdist 2292 set mtype 1
					else set mtype 3
				}
			}
			set countvarb 1
		}
		break
		case 5 // shock rifle
		ifg countvar 22
		{
			geta[].z savz
			set z2 savz
			ifactor MANDOFETT sub z2 2048
			ifinwater add z2 4096
			seta[].z z2
			set countvar 0

			ifg targetdist 1024
			{
				geta[].x savx
				geta[].y savy
				set x savx
				add x 256
				rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
				seta[].x x2
				seta[].y y2
			}
			
			ifrnd 128 // shock ball
			{
				set xvel 644
				state rpg_targetprep
				ezshoot zdist SHOCKBALL
				setav[RETURN].myspawner THISACTOR
				sound SHOCKBALLFIRE
			}
			else
			{
				state hitscan_targetprep
				setprojectile[SHOCKBEAM].extra 16
				ezshoot zdist SHOCKBEAM
				setprojectile[SHOCKBEAM].extra 50
				sound LIGHTNINGFIRE
			}
			set countvarb 1
			
			ifg targetdist 1024
			{
				seta[].x savx
				seta[].y savy
			}
			
			seta[].z savz
			sub botclip 2
			ifle botclip 0 
			{
				set botclip -60
				ifrnd 128 
				{
					ifspritepal 15
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 4
						else
						set mtype 3
					}
					else
					{
						ifrnd 128 ifl targetdist 8192
							set mtype 2
						else
							set mtype 0
					}
				}
			}
		}
		else ifl targetdist 1560
		{
			set botclip -10
			ifrnd 128 
			{
				ifspritepal 15
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 4
					else
					set mtype 3
				}
				else
				{
					ifrnd 128 ifl targetdist 8192
						set mtype 2
					else
					set mtype 0
				}
			}
		}
		break
		
		default break
		endswitch
	}

ends
/*
state spritestaircheck
	
	// getzrange <x> <y> <z> <sector> <ceilingz> <ceilinghit> <floorz> <floorhit> <walldist> <clipmask>
	geta[].z z
	sub z 4096
	
	ifg sprite[].xvel 0 set angvar sprite[].ang else
	set angvar angvel
	
	set x sprite[].x
	add x 512
	rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
	updatesectorz x2 y2 z tempc
	ifn tempc -1
	{
		getzrange x2 y2 z tempc z2 temp tempd spriteid tempb CLIPMASK0
		ifg spriteid 49152 
		{
			sub spriteid 49152
			ifvarand sprite[spriteid].cstat 32
			ifvarand sprite[spriteid].cstat 1
			{
				set z sprite[].z
				sub z sprite[spriteid].z
				abs z
				ifle z 1280
				{
					set z sprite[spriteid].z
					sub z 512
					seta[].z z
					al spriteid
				}
			}
		}
	}

ends
*/

state mandomovement

	state mando_assess_allies
	ifn initflags -1
	{
		ldist xydist THISACTOR initflags
		ifle xydist 768
		{
			cactor MANDOCROUCH
			ai AIMANDOHEAL
			seta[].z sprite[initflags].z
			rand temp 3
			ife temp 0 sound MANDO_AMATEUR else
			ife temp 1 sound MANDO_AMAZING else
			ife temp 2 sound MANDO_PATHETIC else
			ife temp 3 sound MANDO_RESPAWNTOWN
			break
		}
	}
	
	ifai AIMANDOWANDER
	{
		ifn bottarget -1 ai AIMANDORUN else
		ifg navpoint -1 ai AIMANDORUN
	}

	ifinwater
	{
		ifaction AMANDOSWIM nullop else
		{
			ai AIMANDORUN
			action AMANDOSWIM
			cactor MANDOFETT
			move STOPPED geth
		}
		ifrnd 24 
		{
			geta[].z savz
			sub savz 6144
			seta[].z savz
			spawn WATERBUBBLE
			add savz 6144
			seta[].z savz
		}
	}
	else ifaction AMANDOSWIM { cactor MANDOFETT ai AIMANDORUN }
	
	ifaction AMANDOJETOFF
	{
		set countvarb 0
		seta[].ang angvel
		ifactioncount 3 nullop else
		{
			geta[].zvel z2
			sub z2 128
			seta[].zvel z2
		}
		// set dodgetime 3
		set crumbwait 3
		ifactioncount 4
		iffloordistl 8
		{
			action AMANDOCRAWL
			move STOPPED geth
			set dodgetime 0
			set crumbwait 0
			ifn bottarget -1 ifrnd 64
			ifn gametype CTF
			{
				ai AIMANDOWAIT
				action AMANDOCROUCH
				set botclip -1
			}
		}
	}
	ifaction AMANDOROLL
	{
		seta[].ang angvel
		set countvarb 0
		set dodgetime 3
		set crumbwait 3
		ifactioncount 5
		iffloordistl 8
		{
			action AMANDOCRAWL
			move STOPPED geth
			set dodgetime 0
			set crumbwait 0
			ifn bottarget -1 ifrnd 128
			{
				ai AIMANDOWAIT
				action AMANDOCROUCH
				set botclip -1
			}
		}
	}
	ifaction AMANDOROLLBACK
	{
		set angvar angvel
		add angvar 1024
		seta[].ang angvar
		set countvarb 0
		set dodgetime 3
		set crumbwait 3
		ifactioncount 5
		{
			action AMANDOCRAWL
			move STOPPED geth
			set dodgetime 0
			set crumbwait 0
			ifn bottarget -1
			{
				ai AIMANDOWAIT
				action AMANDOCROUCH
				
				set botclip -1
				set countvar 20
			}
		}
	}
	
	ifai AIMANDOWANDER nullop else
	ifaction AMANDORUN
	{
		state strafecheck
		
		ife countvarc 0
		ife countvarb 0
		ife bluecarrier THISACTOR
		ifn bottarget player[].i
			set bottarget -1
		
		ife bottarget -1
		ifactioncount 4
		ife countvarc 0
		ife countvarb 0
		{
			cactor MANDOCROUCH
			action AMANDOJET
			geta[].z z sub z 8192 seta[].z z
			sound DUKE_JETPACK_ON
		}
		else
		ifn initflags -1
		{
			cactor MANDOCROUCH
			action AMANDOJET
			geta[].z z sub z 8192 seta[].z z
			sound DUKE_JETPACK_ON
		}
		
		ifaction AMANDORUN
		ifn bottarget -1
		{
			ifl sprite[bottarget].z sprite[].z
			{
				geta[].z z sub z sprite[bottarget].z
				ldist xydist2 THISACTOR bottarget
				div z xydist2
				ifg z 16
					state startmandorollback
				else
				ife kicktarg THISACTOR
				{
					set stun 0
					state startmandorollback
				}
			}
		}
	}
	
	ifn padmove 0 { set angvel padang break }
	
	ife targetalive YES
	ife seemytarget YES
	ifn bottarget -1
	ife initflags -1
	ifl targetdist 10240
	ife dodgetime 0
	{
		ifl navpoint 0
		{
			geta[bottarget].x x2
			geta[bottarget].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2
			
			state avoidmines
			ife B YES 
			{
				add angvel 1024
				ife sprite[bottarget].picnum SPIDERWALK
				ifn mtype 2
				ifl countvar 2 set mtype 1
			}
			state strafechecklong
		}
		ifceilingdistl 128 nullop else
		ifai AIMANDORUN
		ifg targetdist 5120
		ife countvarb 0
		ife countvarc 0
		// ife bottarget player[].i
		ifn bluecarrier THISACTOR
		{
			set temp NO
			ife team 0
			ifrnd 32 set temp YES else
			ife team 1
			ifrnd 64 set temp YES
			ife temp YES
			{
				cactor MANDOCROUCH
				action AMANDOBOMB
				set mtype 2
				set botclip 10
				set spriteid bottarget
				state facesprite
				set angvel angvar
				set dodgetime 3
			}
		}
	}
	
	ifgapzl 64 
	{ 
		set temp sprite[].sectnum
		updatesectorz sprite[].x sprite[].y sprite[].z temp
		ifn temp -1
		{
			getflorzofslope temp sprite[].x sprite[].y z // z is the return var
			getceilzofslope temp sprite[].x sprite[].y z2
			sub z z2
			abs z
			ifl z 10240
			ifaction AMANDORUN 
			{
				cactor MANDOCROUCH 
				action AMANDOCRAWL 
			}
		}
	}
	else
	ifaction AMANDOCRAWL
	ife dodgetime 0
	{
		cactor MANDOFETT
		ai AIMANDORUN
	}
	set temp NO
	ifaction AMANDORUN set temp YES
	ifaction AMANDORUNSHOOT set temp YES
	ifaction AMANDOCRAWL set temp YES
	ifaction AMANDOSWIM set temp YES
	ifaction AMANDOJET set temp YES
	ifaction AMANDOJETOFF set temp YES
	ifaction AMANDOROLL set temp YES
	ifaction AMANDOJUMP set temp YES
	ifaction AMANDOFALL set temp YES
	ifaction AMANDOBOMB set temp YES
	ifaction AMANDOROLLBACK set temp YES
	// use movesprite to move at angvel
	ife temp YES
	{
		ifn initflags -1 // healing target
		{
			set bottarget -1
			set bluetimer 3 // don't look for new target
			set navpoint -1

			geta[initflags].x x2
			geta[initflags].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2
		}
		cos xvel angvel
		sin yvel angvel
		div xvel 80 // 96
		div yvel 80 // 96
		set zdist 0
		ifg stun 0 { shiftr xvel 1 shiftr yvel 1 }
		
		set B NO
		ifaction AMANDOJET set B YES
		ifaction AMANDOBOMB set B YES
		
		ife B YES
		{
			seta[].ang angvel
			geta[].z savz sub savz 4096 seta[].z savz
			spawn SMALLSMOKE
			add savz 4096 seta[].z savz
			ifvarand monstflags YES xorvar monstflags YES
			soundonce DUKE_JETPACK_IDLE
			shiftl xvel 1
			shiftl yvel 1
			ifaction AMANDOBOMB { mul xvel 4 divvar xvel 3 mul yvel 4 divvar yvel 3 }
			
			ifg navpoint -1 
			{ 
				geta[navpoint].z zdist sub zdist 6144 
				ifaction AMANDOBOMB sub zdist 16384
			} 
			else
			ifn bottarget -1 
			{ 
				geta[bottarget].z zdist sub zdist 10240 
				ifaction AMANDOBOMB { sub zdist 20480 set dodgetime 3 }
			} 
			else
			ifn initflags -1
			{
				geta[initflags].z zdist sub zdist 6144 
			}
			else
			geta[].z zdist
			
			
			sub zdist sprite[].z // if below target value will be negative
			
			
			set z2 zdist
			abs z2
			
			
			ifl z2 1024 set zdist 0 else
			ifl zdist 0 { ifaction AMANDOBOMB set zdist -3072 else set zdist -2560 } else
			ifg zdist 0 set zdist 2560
			
			iffloordistl 64 sub zdist 256 else
			ifceilingdistl 64 add zdist 256
		}
		else orvar monstflags YES
		
		ifaction AMANDOSWIM 
		{
			ifg navpoint -1 { geta[navpoint].z zdist add zdist 4096 } else
			ifn bottarget -1 geta[bottarget].z zdist else
			geta[].z zdist
			
			sub zdist sprite[].z // if below target value will be negative
			set z2 zdist
			abs z2
			ifl z2 2048 set zdist 0 else
			ifl zdist 0 set zdist -1280 else
			ifg zdist 0 set zdist 1280
			
		}
		else
		ifaction AMANDOCRAWL { mul xvel 3 div xvel 4 mul yvel 3 div yvel 4 }
		else
		ifaction AMANDOJETOFF { mul xvel 4 div xvel 3 mul yvel 4 div yvel 3 }
		else
		ifaction AMANDOROLL { mul xvel 5 div xvel 4 mul yvel 5 div yvel 4 }
		
		movesprite THISACTOR xvel yvel zdist CLIPMASK0 RETURN
		
		
		ifn RETURN 0
		{
			// bumping something
			ifaction AMANDOJET
			{
				cactor MANDOCROUCH
				action AMANDOJETOFF
				move MANDOFALLVEL geth
				sound DUKE_JETPACK_OFF
				set countvarc -150
			} else
			ifaction AMANDOJETOFF
			{
				iffloordistl 8
				{
					cactor MANDOCROUCH
					action AMANDOCRAWL
					set dodgetime 0
				}
			} else
			ifaction AMANDOBOMB
			{
				action AMANDOJETOFF
				move MANDOFALLVEL geth
				sound DUKE_JETPACK_OFF
				set countvarc -150
				set botclip -20 
				set mtype 3 
				set dodgetime 0
			}
			ifaction AMANDOROLL
			{
				iffloordistl 8
				{
					cactor MANDOCROUCH
					action AMANDOCRAWL
					set dodgetime 0
				}
			}
			ifaction AMANDORUN
			ifn bottarget -1
			{
				state avoidmines
				ife B YES
				{
					set myspawner bottarget
					state startmandododge
				}
			}
			sub RETURN 49152
			ifl RETURN 16384 ifg RETURN -1 // bumping a sprite
			{
				set B NO
				ife actorvar[RETURN].monstatus 1 
					ife actorvar[RETURN].team team set B YES
				ifg gametype 0 ifg navpoint -1
				ifvarand sprite[RETURN].cstat 16 set B YES
				ife B YES
				{
					set x2 sprite[].x
					add x2 256
					rotatepoint sprite[].x sprite[].y x2 sprite[].y angvel x y
					setsprite THISACTOR x y sprite[].z
					seta[].htmovflag 0
					set RETURN 0
				}
			}
			else 
			ifg navpoint -1 ifrnd 32 set navpoint -1
		}
		ifinwater break
		
		ife countvarb 0
		ife dodgetime 0
		{
			state mandojumpcheck
			state rand_mandojumpcheck
		}
		
		ifai AIMANDOWANDER ife dodgetime 0 
		iffloordistl 4
		{ 
			cactor MANDOFETT ai AIMANDOWAIT 
			ifgapzl 64
			{	
				cactor MANDOCROUCH
				action AMANDOCROUCH
			}
		}
	}
	
	ifai AIMANDOWANDER
	{
		ifcount 10
		ifn RETURN 0 state mandostartwander
	}
	else
	ife navpoint -1
	ife bottarget -1
	ife initflags -1
	ifcount 8
	ifaction AMANDORUN
		state mandostartwander

ends

state mandocode

	ifai 0
	{
		ai AIMANDOWAIT
		sizeat 26 24
		clipdist 40
		cstator 257
		geta[].mdflags temp
		orvar temp 16
		seta[].mdflags 16
		geta[].ang lastang
		orvar monstflags YES
		rand initsprite 4096
		set navmode 3
		ifspritepal 21 { set team 0 set navmode 4 } else
		ifspritepal 3 { set team 1 addstrength 100 set navmode 4 set mtype 1 } else
		spritepal 15
		set botclip 8
		ifn gametype SURVIVAL
		state mandoroamsounds
		set mandotaunt_time 600
		set redbaseval -300
		set initflags -1
		ife gametype DM
		{
			set temp bluescore
			sub temp redscore
			ifg temp 3 orvar monstflags 128
			ifg temp 2 orvar monstflags 8
			ifg temp 1 orvar monstflags 512
		}
		ife gametype CTF
		{
			set temp bluescore
			sub temp redscore
			ifg temp 350 orvar monstflags 2048
			ifg temp 250 orvar monstflags 128
			ifg temp 150 orvar monstflags 512
			strength 200
		}
		ife gametype CONTROL
		{
			set temp bluescore
			sub temp redscore
			ifg temp 350 orvar monstflags 1024
			ifg temp 250 orvar monstflags 64
			ifg temp 150 orvar monstflags 512
			strength 200
		}
	}
	
	ifn monstatus 2 ifg sprite[].extra 75 ife team 1 set shellyinmap 10
	
	seta[].ang lastang
	ifg countvarb 0
	{
		add countvarb 1
		ifg countvarb 2 set countvarb 0
	}
	ifl countvarc 0
		add countvarc 1
	ifl float 0
		add float 1
	
	ifinwater nullop else
	ifaction AMANDOJET nullop else
	ifaction AMANDOBOMB nullop else
	fall
	state checksquished
	ifai AIMANDODYING { state mandodyingstate break }
	ifai AIMANDOGROW { state mandogrowstate break }
	ifai AIMANDOFROZEN { state mandofrozenstate break }
	ifai AIMANDODOWN { state mandodownstate break }
	state monsterai
	
	ife team 1 state botblocking
	
	
	ifg mandotaunt_time 0 sub mandotaunt_time 1 // used as taunt counter
	ifrnd 1 ifrnd 96 ife bottarget -1 state mandoroamsounds
	ifai AIMANDOSHRUNK state mandoshrunkstate else
	{
		ifaction AMANDOCLOAK set myspawner -1 else
		ifaction AMANDOROLL set myspawner -1 else
		ifaction AMANDOJET set myspawner -1 else
		ifaction AMANDOJETOFF set myspawner -1
		else
		{
			ife redbaseval 0
			ifge bluescore redscore
			ife countvarb 0
			ifl gametime 12000
			ifactor MANDOFETT
			iffloordistl 4
			ifpdistl 6144
			ifn gametype CTF
			ifn gametype 0
			ife seeplayer YES
			{
				cactor MANDOFETT
				ai AIMANDOWAIT
				action AMANDOCLOAK
				globalsound TELEPORTER
				set redbaseval 1
				spawn GLAREANIM
			}
	
			findnearsprite BMFGPROJ 8192 spriteid
			ife spriteid -1
			findnearsprite BMFGIMPACT 6144 spriteid
			ifactor MANDOFETT
			ifn spriteid -1
			ife countvarc 0
			ife float 0
			{
				state startmandododge
				action AMANDOJET
				geta[].z z sub z 8192 seta[].z z
				sound DUKE_JETPACK_ON
				set dodgetime 16
			}
			ifn myspawner -1 state startmandododge else
			ifn team actorvar[player[].i].team
			ifp pfacing
			ifpdistl 8192
			ife bottarget player[].i
			ifn player[].kickback_pic 0
			ifl player[].curr_weapon RPG_WEAPON
			ife countvarb 0
				state startmandododge
		}
		ife bottarget -1 seta[].ang angvel else
		ifn dodgetime 0 seta[].ang angvel
		// state mandotargetmove
		
		// check for standing and waiting like a badass
		ifactor MANDOFETT ifai AIMANDORUN ifaction AMANDORUN
		ife targetalive YES ifl targetdist 2560 ifn bottarget -1
		ifn gametype CONTROL ifn gametype CTF
		ifrnd 16
		ifcount 30
		{
			ai AIMANDOWAIT
			action AMANDOSTANDSHOOT1
			ifrnd 128
			{
				cactor MANDOCROUCH
				action AMANDOCROUCH
			}
		}
		ifg redbaseval 0 // cloaked
		{
			add redbaseval 1
			ifge redbaseval 100 
			{ 
				cstat 771 
				seta[].alpha 160 
				ife player[].heat_on YES 
				{
					seta[].alpha 0
					cstat 259
				}
			}
			ifge redbaseval 900
			{
				set redbaseval -1200
				cstat 257
				seta[].alpha 0
			}
			// note that other cstat management handled in ANIMATESPRITES
		}
		else ifl redbaseval 0 add redbaseval 1
		
		set B NO
		
		ifn bottarget -1
		ifai AIMANDOWAIT 
		state avoidmines
		ife B YES
		{
			cactor MANDOFETT 
			ai AIMANDORUN
		}
		
		state mandoshootstate
		ifai AIMANDOJUMP state mandojumpstate
		ifai AIMANDOWAIT state mandowaitstate else
		ifai AIMANDOHEAL state mandohealstate else
		ifai AIMANDOMELEE state mandomeleestate else
		state mandomovement

	}
	geta[].ang lastang // to prevent glitched angles when bumping

	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{	
		  cactor MANDOFETT
		  sound ACTOR_SHRINKING
		  ai AIMANDOSHRUNK
		  set bottarget -1
		  set initflags -1
		  set navpoint -1
		  set redbaseval 0
		  seta[].alpha 0
		  ifspritepal 33 getlastpal
		  cstat 257
		  set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		ifdead
		{
			stopsound MANDO_PERMISSION
			stopsound MANDO_GOINGDOWN
			stopsound MANDO_YOURPLACE
			stopsound MANDO_COMPETITION
			stopsound MANDO_AMAZING
	
			set redbaseval 0
			seta[].alpha 0
			ifspritepal 33 getlastpal
			cstat 257
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor MANDOFETT
			  ai AIMANDOFROZEN
			  set bottarget -1
			  set initflags -1
			  set navpoint -1
			  strength 0
			  state freezeme
			  break
			}
			ife team 1 { ai AIMANDODYING cactor MANDOFETT cstat 0 break }
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  cactor MANDOFETT
			  ai AIMANDOGROW
			  set bottarget -1
		      set initflags -1
		      set navpoint -1
			  break
			}
			addkills 1
			state enemy_death
			cactor MANDOFETT
			ai AIMANDODYING
			set bottarget -1
		    set initflags -1
		    set navpoint -1
			cstat 0
			
			sound EDFL_PAIN3
			
			guts JIBS6 4
			
			ifwasweapon RPG
			{
				cactor MANDOFETT
				sound SQUISHED
				spawn BLOODPOOL
				state mando_body_jibs
				state lite_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				cactor MANDOFETT
				sound SQUISHED
				spawn BLOODPOOL
				state mando_body_jibs
				state lite_jibs
				killit
			}
			ifwasweapon SABERPROJ
			{
				espawn MANDOSABERDEATH
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				seta[RETURN].pal sprite[].pal
				killit
			}
			break
		}
		ifrnd 2
			spawn BLOODPOOL
		ifai AIMANDOSHRUNK nullop else
			state mandopainsounds
		
	}
	else state checksquished

ends

useractor enemy MANDOFETT MANDOSTRENGTH state mandocode enda

useractor enemy MANDOCROUCH MANDOSTRENGTH state mandocode enda

action AMANDOFLY  0  1  5  1  10
move MANDOFLYVEL  420
useractor notenemy MANDOFLYIN MANDOSTRENGTH AMANDOFLY

	ifmove 0
	{
		ifl sprite[].xrepeat 4 spawn GLARESTAR
		move MANDOFLYVEL geth
		sizeat 26 24
		clipdist 40
		cstator 257
		geta[].ang lastang
	}
	seta[].ang lastang
	geta[].z savz sub savz 4096 seta[].z savz
	spawn SMALLSMOKE
	add savz 4096 seta[].z savz
	soundonce DUKE_JETPACK_IDLE
	ifcount 90
	{
		espawn MANDOFETT
		seta[RETURN].pal sprite[].pal
		stopactorsound THISACTOR DUKE_JETPACK_IDLE
		killit
	}
	

enda
