
action ABATBOSSHANG  0  1  5  1  20
action ABATBOSSDEAD  -2  1  1  1  10
action ABATBOSSDYING -10  8  1  1  20
action ABATBOSSIDLEF -85  3  5  1  8
action ABATBOSSIDLEB -75  3  5  -1  8
action ABATBOSSFROZEN -65  1  5  1  10
action ABATBOSSSHOOTF -70  3  5  1  10
action ABATBOSSSHOOTB -60  3  5  -1  10
action ABATBOSSCLAW1  -55  2  5  1  12
action ABATBOSSCLAW2  -45  3  5  1  12 // hits when entering this frame
action ABATBOSSCLAW3  -30  1  5  1  18 // hits when entering this frame
action ABATBOSSSTUN  -25  1  5  1  20
action ABATBOSSSWOOP1  -20  1  5  1  20
action ABATBOSSSWOOP2  0  1  5  1  20

move BATHOVERVEL 0 -15
move BATKILLVEL 480 -15
move BATSWOOPVEL 384 -15
move BATBOOSTVEL 844 -15
move BATCLAWVEL 280 -12
move BATCLAWVEL2 96 -8
move BATRISEVEL 0 -72
move BATSEEKVEL 208 -15
move BATRETURNVEL 360 -20

ai AIBATHANG ABATBOSSHANG STOPPED geth
ai AIBATFROZEN ABATBOSSFROZEN STOPPED geth
ai AIBATHOVER ABATBOSSIDLEF BATHOVERVEL getv faceplayerslow
ai AIBATDYING ABATBOSSDYING STOPPED geth
ai AIBATSHRUNK ABATBOSSIDLEF SHRUNKVELS fleeenemy
ai AIBATGROW ABATBOSSSTUN STOPPED geth
ai AIBATSTUN ABATBOSSSTUN STOPPED geth
ai AIBATCLAW ABATBOSSCLAW1 BATCLAWVEL geth getv faceplayer
ai AIBATSHOOT ABATBOSSSHOOTF BATHOVERVEL geth getv faceplayerslow
ai AIBATSWOOP ABATBOSSSWOOP1 BATCLAWVEL2 geth getv faceplayerslow
ai AIBATRETURN ABATBOSSIDLEF BATRETURNVEL geth getv
ai AIBATHEAL ABATBOSSHANG STOPPED geth

state batreturnstate

	seta[].htextra -1
	ifstrength 100 strength 100
	set bottarget -1
	set targetdist 99999
	set seemytarget NO
	set seeplayer NO
	set targetalive NO
	set dodgetime 3
	ifl burning -1 set burning -1
	ifg burning 1 set burning 1
	add countvarc 1
	ifg countvarc 450
	{
		set countvarc 0
		ai AIBATHEAL
		sub botclip 1
		getp[].player_par TMP_A
		al TMP_A
		al botclip
		screensound BOSSPORT
		spawn GLARESTAR
		set z sprite[mysignpost].z
		sub z 2048
		setsprite THISACTOR sprite[mysignpost].x sprite[mysignpost].y z
		setav[mysignpost].initsprite -33
		break
	}
	ifg ikicked 0 sub ikicked 1
	
	dist targetdist THISACTOR mysignpost
	ifl targetdist 2048
	{
		seta[mysignpost].cstat 256
		seta[mysignpost].clipdist 1
		
		ifl targetdist 512
		{
			set countvarc 0
			ai AIBATHEAL
			sub botclip 1
			set z sprite[mysignpost].z
			sub z 2048
			setsprite THISACTOR sprite[mysignpost].x sprite[mysignpost].y z
			setav[mysignpost].initsprite -33
			sound DUMPSTER_MOVE
			break
		}
	}
	
	canseespr THISACTOR mysignpost temp
	ife temp YES
	ife ikicked 0
	{
		set spriteid mysignpost
		state facesprite
		
		
		set z sprite[mysignpost].z
		sub z sprite[].z
		set z2 targetdist
		div z2 768 ifl z2 1 set z2 1
		div z z2
		movesprite THISACTOR 0 0 z CLIPMASK0 RETURN	
	}
	
	ifnotmoving 
	{
		set temp NO
		set ikicked 8
	}
	
	ife temp NO
	{
		set xydist 99999
		set TMP_A -1
		headspritestat spriteid 980
		whilevarn spriteid -1
		{
			ldist xydist2 spriteid mysignpost
			canseespr THISACTOR spriteid tempb
			ife tempb YES
			ifl xydist2 xydist
			{
				set xydist xydist2
				set TMP_A spriteid
			}
			nextspritestat spriteid spriteid
		}
		ifn TMP_A -1
		{
			set spriteid TMP_A
			state facesprite
		}
	}

ends

defstate batfailsafe

	ife bottarget -1 add mtype 1
	else set mtype 0
	
	ifge mtype 600
	{
		set mtype 0
		set B -1
		headspritestat spriteid 999
		whilevarn spriteid -1
		{
			ldist tempc player[].i spriteid
			ifg tempc 2560 
			{
				canseespr spriteid player[].i temp
				ife temp YES
				set B spriteid
			}
			
			nextspritestat spriteid spriteid
			ifn B -1 set spriteid -1
		}
		ifn B -1
		{
			spawn GLARESTAR
			setsprite THISACTOR sprite[B].x sprite[B].y sprite[B].z
			spawn GLARESTAR
			sound BOSSPORT
			seta[].htactorstayput -1
		}
	}
ends

state bathealstate

	seta[].htextra -1
	add countvarc 1
	set TMP_B NO
	set TMP_A countvarc
	modvar TMP_A 70
	ife TMP_A 0
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum COFFIN
			ife sprite[spriteid].pal 3
			ife actorvar[spriteid].initsprite 0
			{
				setav[spriteid].initsprite 1
				switch botclip
				case 0
					setav[spriteid].droptile CYBERBEAST
					ifrnd 64
					setav[spriteid].monstflags 32768
					else
					setav[spriteid].monstflags 128
				break
				case 1
					setav[spriteid].droptile OCTABRAIN
					ifrnd 128
					setav[spriteid].monstflags 48
					else
					setav[spriteid].monstflags 2048
				break
				case 2
					setav[spriteid].droptile CRAZYLADY
					setav[spriteid].monstflags 256
				break
				default
					setav[spriteid].droptile CRAZYLADY
					setav[spriteid].monstflags 256
				break
				endswitch
				set TMP_B YES
			}
			nextspritestat spriteid spriteid
			ife TMP_B YES set spriteid -1
		}
	}

	set temp inithp
	div temp 900, ifl temp 1 set temp 1
	add temp 1
	geta[].extra tempb
	add tempb temp
	ifg tempb inithp set tempb inithp
	seta[].extra tempb
	
	seta[].ang sprite[mysignpost].ang
	set z sprite[mysignpost].z, sub z 2048
	setsprite THISACTOR sprite[mysignpost].x sprite[mysignpost].y z
	
	ifl countvarc 870
	{
	seta[mysignpost].cstat 257
	seta[mysignpost].clipdist 160
	}
	ife countvarc 870 
	{
		setav[mysignpost].initsprite 2
		seta[mysignpost].clipdist 80
	}
	ifge countvarc 900
	{
		// seta[].extra inithp
		set countvarc 0
		ai AIBATSWOOP
		sound ONYX_RECOG
	}
		
ends

state batfrozenstate 

	ifsquished	
	{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

    ifcount THAWTIME
    {
      ai AIBATHOVER
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }

      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 40
      state standard_jibs
      sound GLASS_BREAKING
      killit
    }
	ifn team actorvar[player[].i].team
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state batshrunkstate 

  ifcount SHRUNKDONECOUNT
    ai AIBATHOVER
  else
    ifcount SHRUNKCOUNT
	{
	  sizeto 40 38
	}
  else
    state genericshrunkcode

ends

state batdyingstate
  ifaction ABATBOSSDEAD
  {
	  set burning 0
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
		   ifn sprite[].pal 21
      {
        sound SQUISHED
        state standard_jibs
		state standard_jibs
        killit
      }
	  ifn initsprite 0
	  {
		add countvarc 1
		ifge countvarc countvarb
		{
			ife wall[initsprite].pal 2 setw[initsprite].pal 0
			else { setw[initsprite].pal 2 sub countvarb 1 }
			setw[initsprite].shade 0
			
			ifl countvarb 1 set countvarb 1
			set countvarc 0
		}
	  }
      break
  }
  guts JIBS6 2
  ifactioncount 8
  {
    iffloordistl 8
      sound THUD
    action ABATBOSSDEAD
    cstat 0
	
	ifspritepal 21
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ifg actorvar[spriteid].monxp 0
			ife actorvar[spriteid].team 0
			ife actorvar[spriteid].monstatus 1
			{
				seta[spriteid].htextra 3000
				seta[spriteid].htpicnum RPG
				seta[spriteid].htowner player[].i
			}
			nextspritestat spriteid spriteid
		}
		quake 2400
		set rainpal SCRAPFALL
		set raining 6
		set rainradius 20480
		set raincstat 0
		starttrackslot 6 44
	}
  }
ends

defstate batslam
			 
	sound CYBOSS_LAND
	cactor BATBOSS ai AIBATHOVER
	ifspritepal 21
	{
		ifpdistl 10240 quake 30
		hitradius 4096 15 30 50 100
		seta[].htextra -1
		ifpdistl 4096 seta[player[].i].htowner THISACTOR
	}
	else
	{
		ifpdistl 8192 quake 30
		hitradius 3072 20 35 60 80
		seta[].htextra -1
		ifpdistl 3072 seta[player[].i].htowner THISACTOR
	}
	set LIZBVAR 0
	whilevarn LIZBVAR 32
	{
		add LIZBVAR 1
		espawn BIGSMOKE
		rand LIZBVAR2 8192
		sub LIZBVAR2 4096
		add LIZBVAR2 sprite[].x
		seta[RETURN].x LIZBVAR2
		rand LIZBVAR2 8192
		sub LIZBVAR2 4096
		add LIZBVAR2 sprite[].y
		seta[RETURN].y LIZBVAR2
	}
ends

state batdodge
	
	ifg dodgetime 0
	{
		state makespeedblur
		seta[].xvel 0
		cos xvel angvel
		sin yvel angvel

		div xvel 80
		div yvel 80

		movesprite THISACTOR xvel yvel 0 CLIPMASK0 RETURN
		ifn RETURN 0 set dodgetime 0
	}
ends

state batzvel

	getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum temp temp z spriteid 192 CLIPMASK0

	sub z sprite[].z
	
	ifmove BATHOVERVEL
	{
		ifl z 6144 
		{
			movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
			set float YES
		}
		else iffloordistl 32
		{
			movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
			set float YES
		}
		else ifg z 8120 set float NO
	}
	else
	ifn bottarget -1
	{
		set float NO
		
		set z sprite[bottarget].z
		sub z sprite[].z
		set z2 targetdist
		ifg z2 512 sub z2 512
		div z2 768 ifl z2 1 set z2 1
		div z z2
		movesprite THISACTOR 0 0 z CLIPMASK0 RETURN
	}

ends

state batclawstate

	state batzvel
	state turntotarget
	
	ifn bottarget -1
	ifmove BATCLAWVEL
	{
		ldist xydist THISACTOR bottarget
		ifl xydist 896 move BATCLAWVEL2 getv geth
	}
	
	ifaction ABATBOSSCLAW1
	{
		ifactioncount 2
		{
			setprojectile[KNIFEPROJ].RANGE 1408
			action ABATBOSSCLAW2
			state hitscan_targetprep
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			state hitscan_targetprep
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			setprojectile[KNIFEPROJ].RANGE 1280
		}
	}
	else ifaction ABATBOSSCLAW2
	{
		ifactioncount 3
		{
			setprojectile[KNIFEPROJ].RANGE 1408
			action ABATBOSSCLAW3
			state hitscan_targetprep
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			state hitscan_targetprep
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			zshoot zdist KNIFEPROJ
			setprojectile[KNIFEPROJ].RANGE 1280
		}
	}
	else ifaction ABATBOSSCLAW3
	{
		ifactioncount 1
		{
			ife targetalive YES
			ife seemytarget YES
			ifl targetdist 1708
			ifrnd 192
			{
				ai AIBATCLAW
				sound ONYX_MELEE
			}
			else 
			{
				ai AIBATHOVER
				count 50
			}
		}
	}
ends

state batswoopstate

	ifaction ABATBOSSSWOOP1
	{
		ifactioncount 3
		{
			cactor BATBOSSSWOOPING
			action ABATBOSSSWOOP2
			ifmove BATHOVERVEL
			{
				move BATBOOSTVEL geth getv
				sound ONYX_SWOOP2
			}
			else
			{
				move BATSWOOPVEL geth getv
				add countvar 1
			}
		}
	}
	
	ifaction ABATBOSSSWOOP2
	{
		soundonce ACTORGLIDE
		state batzvel
		
		ifmove BATSWOOPVEL
		{
			set TMP_B 32
			state turnatrate
			
			ifcount 150 
			{
				cactor BATBOSS
				move BATHOVERVEL faceplayer getv
				action ABATBOSSSWOOP1
			}
			else
			ifl targetdist 5120
			{
				cactor BATBOSS
				move BATHOVERVEL faceplayer getv
				action ABATBOSSSWOOP1
			}
		}
		else
		ifmove BATBOOSTVEL
		{
			state makespeedblur
			ifn bottarget -1
			ifl targetdist 1024
			{
				state batslam
				sound SQUISHED
				set temp sprite[bottarget].htextra
				add temp 60
				seta[bottarget].htextra temp
				seta[bottarget].htpicnum RPG
				seta[bottarget].htowner THISACTOR
				seta[bottarget].htang sprite[].ang
				ife bottarget player[].i state playerpainsounds
			}
			else
			ifnotmoving state batslam
		}
		
		state hitscan_targetprep
		zshoot zdist KNIFEPROJ
		
		ifcount 300 { cactor BATBOSS ai AIBATHOVER }
		else
		ifcount 180 ifnotmoving { cactor BATBOSS ai AIBATHOVER }
		else
		ifn bottarget -1
		ifg sprite[bottarget].htextra 0
		ife sprite[bottarget].htowner THISACTOR
		{
			ifmove BATBOOSTVEL { state batslam sound SQUISHED }
			else
			sound RENDFLESH
			set temp sprite[bottarget].htextra
			ifspritepal 21
			add temp 60
			else add temp 40
			seta[bottarget].htextra temp
			seta[bottarget].htang sprite[].ang
			ife bottarget player[].i state playerpainsounds
			cactor BATBOSS ai AIBATHOVER
		}
	}

ends

defstate batshootstate 

	
	set dodgetime 3
	ifn bottarget player[].i
	{
		set TMP_B 6
		state turnatrate
	}
	ife countvar 3
	{
		ifactorsound THISACTOR SPELL2FIRE nullop else sound SPELL2FIRE
		ifcount 3
		{
			add countvarb 1
			resetcount
			espawn BATPROJ

			setav[RETURN].team team
			setav[RETURN].bottarget bottarget
			setav[RETURN].myspawner THISACTOR
			rand angvar 384, sub angvar 192
			add angvar sprite[RETURN].ang
			seta[RETURN].ang angvar
			geta[].z z, 
			ifspritepal 21
			sub z 18432
			else sub z 14700
			rand z2 5120
			sub z z2
			seta[RETURN].z z
			seta[RETURN].htbposz z
			
			ifspritepal 21 set TMP_A 90 else set TMP_A 60
			ifge countvarb TMP_A
			{
				add countvar 1
				set countvarb 30
				ai AIBATHOVER
			}
		}
	}
	else
	ife countvar 7
	{
		ife countvarb 0
		ifn bottarget -1
		{
			set spriteid bottarget
			state facesprite
			ifrnd 128 sub angvar 96 else add angvar 96
			seta[].ang angvar
			sound FLAMEBEAM
		}
		ife bottarget player[].i
		{
			set TMP_B 1
			state turnatrate
		}
		// ifactorsound THISACTOR LASERLOOP nullop else sound LASERLOOP
		ifcount 2
		{
			add countvarb 1
			ifspritepal 21 ife countvarb 35 sound FLAMEBEAM
			resetcount
			geta[].z z
			set savz z
			ifspritepal 21
			sub z 8192
			else sub z 6144
			seta[].z z
			state hitscan_targetprep
			setprojectile[FIREBEAM].extra 12
			setprojectile[FIREBEAM].offset -160
			setprojectile[FIREBEAM].spawns BURNING
			zshoot zdist FIREBEAM
			setprojectile[FIREBEAM].offset 160
			setprojectile[FIREBEAM].extra 22
			setprojectile[FIREBEAM].spawns LASERHIT
			seta[].z savz
			ifspritepal 21 set TMP_A 75 else set TMP_A 40
			ifge countvarb TMP_A
			{
				add countvar 1
				set countvarb 0
				ai AIBATHOVER
				// ifactorsound THISACTOR LASERLOOP stopactorsound THISACTOR LASERLOOP
			}
		}
	}

ends

state bathoverstate

	ife myspawner -1
	ife team 0 
	ife dodgetime 0
	ifp pfacing
	ifg player[].curr_weapon KNEE_WEAPON
	ifl player[].curr_weapon RPG_WEAPON
	ifn player[].kickback_pic 0
	ifrnd 32 set myspawner player[].i
	
	ifn myspawner -1
	{
		state incoming_eval
		ife actorvar[myspawner].monstatus 1
		{
			ifl x xydist sub angvel 256 else
			add angvel 256
		}
		ife dodgetime 0

		set dodgetime 12
		set crumbwait 10
		
		set myspawner -1
		sound DASHSOUND
	}
	ifg dodgetime 0 state batdodge
	
	ifmove BATRISEVEL
	{
		ifceilingdistl 144 
		{ 
			ife countvar 4 ai AIBATSWOOP else 
			ife countvar 8 ai AIBATSWOOP else
			ai AIBATSHOOT 
		}
		else ifcount 70 
		{ 
			ife countvar 4 ai AIBATSWOOP else 
			ife countvar 8 ai AIBATSWOOP else
			ai AIBATSHOOT 
		}
		else
		{
			getzrange sprite[].x sprite[].y sprite[].z sprite[].sectnum z temp temp spriteid 192 CLIPMASK0
			sub z sprite[].z
			// the more negative the greater the gap
			ifg z -49152 
			{ 
				ife countvar 4 ai AIBATSWOOP else 
				ife countvar 8 ai AIBATSWOOP else
				ai AIBATSHOOT  
			}
		 }
		 ifai AIBATSWOOP sound ONYX_SWOOP1
	}
	else
	state batzvel
	
	
	ifg countvar 8 set countvar 0
	
	ifmove BATSEEKVEL
	{
		ifn bottarget -1
		{
			move BATHOVERVEL getv faceplayerslow
			count 60
		}
		else
		{
			ifrnd 1 soundonce ONYX_ROAM2
			ifnotmoving state setfarang
		}
	}
	
	
	ifmove BATHOVERVEL
	ifcount 8
	{
		ife targetalive YES
		ife seemytarget YES
		ifcount 60
		{
			ifle countvar 2
				move BATKILLVEL getv geth seekplayer
			else
			ifle countvar 4
				move BATRISEVEL getv faceplayer
			else
			ifle countvar 6
				move BATKILLVEL getv geth seekplayer
			else
			ifle countvar 8
				move BATRISEVEL getv faceplayer
				
			ifmove BATKILLVEL sound ONYX_AGGRO else
			ifmove BATRISEVEL sound ONYX_TRANSITION
		}
		else ife bottarget -1
		{
			move BATSEEKVEL geth getv seekplayer
			soundonce ONYX_ROAM1
		}
	}
	
	ifmove BATKILLVEL
	{
		ife targetalive YES
		ife seemytarget YES
		ifl targetdist 1792
		{
			ai AIBATCLAW
			add countvar 1
			sound ONYX_MELEE
			break
		}
		ifcount 120
			ifrnd 8
		{
			add countvar 1
			move BATHOVERVEL getv faceplayerslow
		}
		else
		ife seemytarget YES
		ifl targetdist 4096
			state turntotarget
	}

ends

state batbosscode

	ifai 0
	{
		cactor BATBOSS
		ai AIBATHANG
		cstator 257
		ifspritepal 21
		sizeat 40 38
		else sizeat 32 30
		seta[].mdflags 16
		ifspritepal 21 
		{
			set botclip SPRITELOTAG
			set SPRITELOTAG 1996
			state AActivation // clear NOCNITSA dialog
			set SPRITELOTAG botclip

			addstrength 1500
			starttrackslot 6 43
			set botclip 3 // how many lives he has
			orvar monstflags 16384
			orvar initflags 2
		}
		ifn gametype 0 strength 1500
	}
	ifaction ABATBOSSDYING ifvarand player[].player_par 1 nullop else
	ifai AIBATHEAL nullop else
	fall
	ifaction ABATBOSSIDLEF ifactioncount 3 action ABATBOSSIDLEB else
	ifaction ABATBOSSIDLEB ifactioncount 3 
	{
		sound WINGSOUND
		action ABATBOSSIDLEF 
	}
	else
	ifaction ABATBOSSSHOOTF ifactioncount 3 action ABATBOSSSHOOTB else
	ifaction ABATBOSSSHOOTB ifactioncount 3 
	{
		sound WINGSOUND
		action ABATBOSSSHOOTF
	}
	
	ifai AIBATHANG
	{
		ifg sprite[].htextra 0 
		{
			ai AIBATSWOOP
			sound ONYX_RECOG
		}
		else
		ifcount 90
		{
			ai AIBATSWOOP
			sound ONYX_RECOG
		}
		else ifn sprite[].pal 21
		ifg sprite[].zvel 512
		{
			ai AIBATSWOOP
			sound ONYX_RECOG
		}
	}
	ifai AIBATHEAL { state bathealstate break }
	state monsterai
	
	ifai AIBATFROZEN { state batfrozenstate break }
	ifai AIBATGROW { state genericgrowcode break }
	ifai AIBATDYING { state batdyingstate break }
	ifai AIBATSHRUNK state batshrunkstate else
	ifai AIBATSTUN 
	{ 
		ifactioncount 1 ife stun 0 
		{
			ai AIBATHOVER
			cstat 257
		}
	} 
	else
	ifai AIBATHOVER state bathoverstate else
	ifai AIBATCLAW state batclawstate else
	ifai AIBATSHOOT state batshootstate else
	ifai AIBATSWOOP state batswoopstate else
	ifai AIBATRETURN state batreturnstate
	
	
	ifspritepal 21 state batfailsafe
	
	set B sprite[].extra
	div B 10
	set temp sprite[].htextra
	ifg temp B set TMP_A YES else set TMP_A NO
	
	ifhitweapon
	{
		ifrnd 2
		spawn BLOODPOOL
	  ifai AIBATRETURN
	  {
	    soundonce ONYX_PAIN1
		guts JIBS6 1
		ifstrength 50 strength 50
		break
	  }
	  else ifg botclip 0 ifstrength 1000
	  ifn mysignpost -1
	  {
	    soundonce ONYX_PAIN1
		guts JIBS6 1
		cactor BATBOSS
		ai AIBATRETURN
		set bleeding 0
		break
	  }
	  ifdead
	  {
		  ifn sprite[].pal 21
		  ife botclip 0
		  {
			  ifwasweapon FREEZEBLAST
			  {
				sound NEWFREEZE
				spritepal 1
				cactor BATBOSS
				ai AIBATFROZEN
				strength 0
				state freezeme
				break
			  }
			  ifwasweapon GROWSPARK
			  {
				cstat 0
				sound ACTOR_GROWING
				ai AIBATGROW
				break
			  }
		  }

		globalsound ONYX_DEATH
		ifspritepal 21 
		{
			stopallmusic
			addphealth 1000
			ife userdef[].player_skill 4
			setuserdef[].player_skill 3
			save 0
			
			addkills 1
			
			ifn SPRITELOTAG 0
			{
				setvar B 0	
				whilevarvarn B NUMWALLS
				{
					
					ife wall[B].hitag SPRITELOTAG
					{
						set initsprite B
					}
					add B 1
					ifn initsprite 0 set B NUMWALLS
				}
				set countvarc 0
				set countvarb 37
			}
		}
			
		state enemy_death
		cactor BATBOSS
		ai AIBATDYING
	  }
	  else
	  {
		
		ifn sprite[].pal 21
		ife botclip 0
		ifwasweapon SHRINKSPARK
		{
		  sound ACTOR_SHRINKING
		  cactor BATBOSS
		  ai AIBATSHRUNK
		  set shrunken 1
		  break
		}
		
		ifai AIBATSHRUNK nullop else
		ifai AIBATSHOOT nullop else
		ifai AIBATSWOOP nullop else
		{
			ifrnd 64
			ife TMP_A YES
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			{
			  ifmove BATKILLVEL add countvar 1
			  cactor BATBOSS
			  ai AIBATSTUN
			 
			}
			else
			ifg stun 8
			{
			  ifmove BATKILLVEL add countvar 1
			  ai AIBATSTUN
			  cactor BATBOSS
			}
			else
			ifmove BATHOVERVEL
			{
				count 80
			}
		}
		ife TMP_A NO soundonce ONYX_SMALLPAIN else
		soundonce ONYX_PAIN1
		guts JIBS6 1
	  }
	}
ends

useractor enemy BATBOSS BOSS2FLYSTRENGTH ABATBOSSHANG state batbosscode enda

useractor enemy BATBOSSSWOOPING BOSS2FLYSTRENGTH ABATBOSSSWOOP2 state batbosscode enda

eventloadactor COFFIN
	geta[].lotag SPRITELOTAG
	seta[].lotag 0
	geta[].hitag droptile
	seta[].hitag 0
	ifn sprite[].extra -1
	geta[].extra mtype
	set startx sprite[].x
	set starty sprite[].y
	cstator 257
	espawn COFFINLID
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	seta[RETURN].pal sprite[].pal
	seta[RETURN].ang sprite[].ang
	geta[].owner monstflags
	ife monstflags -1 set monstflags 0
	set myspawner RETURN
enda

useractor notenemy COFFIN 1000

	ifmove 0
	{
		move STOPPED
		set temp sprite[].xrepeat
		mul temp 2, ifg temp 255 set temp 255
		seta[].clipdist temp
	}
	ife myspawner -1 break
	
	ifn sprite[].pal 3
	ife SPRITELOTAG 0
	ife initsprite NO
	ifp pfacing
	ifp palive
	ifpdistl 1792
	ifhitspace
		set initsprite YES
		
	ife initsprite YES // just triggered
	{
		sound DUMPSTER_MOVE
		set initsprite 2
		espawn droptile
		ifn monstflags 0 setav[RETURN].monstflags monstflags
		seta[RETURN].pal sprite[].pal
		ifn mtype 0 setav[RETURN].SPRITELOTAG mtype
		setav[RETURN].mysignpost THISACTOR
		geta[].z z
		sub z 2048
		seta[RETURN].z z
		seta[RETURN].htbposz z
		seta[].clipdist sprite[].xrepeat
	}
	ifg initsprite 1
	ifl initsprite 33
	{
		// add angle to lid and move the lid to the right
		geta[myspawner].ang angvar
		add angvar 16
		seta[myspawner].ang angvar
		
		getactor[].ang angvar
		addvar angvar 256
		setvarvar x2 sprite[myspawner].x
		set temp sprite[myspawner].xrepeat
		div temp 3, ifl temp 4 set temp 4
		add x2 temp
		rotatepoint sprite[myspawner].x sprite[myspawner].y x2 sprite[myspawner].y angvar x2 y2
		setactor[myspawner].x x2
		setactor[myspawner].y y2
		add initsprite 1
		ife initsprite 33 seta[].clipdist sprite[].xrepeat
	}
	else
	ifl initsprite 0
	{
		ife initsprite -1
		{
			seta[myspawner].ang sprite[].ang
			seta[myspawner].x sprite[].x
			seta[myspawner].y sprite[].y
			sound HUGESTEP3
		}
		else
		{
			geta[myspawner].ang angvar
			sub angvar 16
			seta[myspawner].ang angvar
			
			getactor[].ang angvar
			subvar angvar 768
			setvarvar x2 sprite[myspawner].x
			set temp sprite[myspawner].xrepeat
			div temp 3, ifl temp 4 set temp 4
			add x2 temp
			rotatepoint sprite[myspawner].x sprite[myspawner].y x2 sprite[myspawner].y angvar x2 y2
			setactor[myspawner].x x2
			setactor[myspawner].y y2
			add initsprite 1
		}
	}
	ifspritepal 3 
	{
		ife initsprite 33 
		{
			add countvar 1
			ifge countvar 300
			{
				set initsprite -33
				set countvar 0
			}
		}
		else
		ife initsprite -1
		{
			add countvar 1
			ifge countvar 900
			{
				set initsprite 0
				set countvar 0
			}
		}
	}
enda
