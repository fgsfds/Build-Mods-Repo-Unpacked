// CYBERBEAST
// define CYBERBEAST 8555
// define CYBERBALL 8650

action CBIDLE  0  1  5  1  10
action CBWALK  5  4  5  1  8
action CBATTK  25 3  5  1  16
action CBATTK2 30 1  5  1  16
action CBATTK3 35 1  5  1  16
action CBFROZEN 35  1  5  1  10
action CBSHT1  40 1  5  1  20
action CBSHT2  45 1  5  1  20
action CBSHT3  50 1  5  1  20
action CBHYBE  55 1  5  1  10
action CBJMP1  60 1  5  1  20
action CBJMP2  65 1  5  1  20
action CBLAND  70 1  5  1  20
action CBSCW1  -15 4  5  1  6 // 80
action CBSCW2  5 4  5  1  6 // 100
action CBHIT   10287 1  5  1  18 // 120
action CBDIE   120 6  1  1  18
action CBDEAD  125 1  1  1  10
action ACBDECAP 7965  6  1  1  18
action ACBDECAPDEAD 7970 1  1  1  10

move CBWALKVEL 184
move CBWALKFAST 256
move CBJUMPVEL 256
move CBROLLVEL 480
move CBROLLFAST 562
move CYBERSHRUNKVELS 32

ai AICBJUMP CBJMP2 CBJUMPVEL jumptoplayer
ai AICBSEEK CBWALK CBWALKVEL seekplayer
ai AICBROLL CBHYBE STOPPED faceplayer
ai AICBSHRUNK CBWALK CYBERSHRUNKVELS fleeenemy
ai AICBFROZEN CBFROZEN STOPPED geth
ai AICBGROW CBFROZEN STOPPED geth
ai AICBDYING CBDIE STOPPED geth
ai AICBHIT CBHIT STOPPED geth
ai AICBATTK CBATTK CBWALKVEL geth
ai AICBHYBE CBHYBE STOPPED geth
ai AICBSHOOT CBSHT1 STOPPED faceplayer

state cbshootstate

	ifaction CBSHT1
	{
		ifspritepal 24 ifvarand player[].player_par 1
		{
			sound CBFIRE
			shoot CBPROJ
		}
		ifactioncount 1
		{
			action CBSHT2
			sound CBFIRE
			shoot CBPROJ
		}
	}
	ifaction CBSHT2
	{
		ifactioncount 1
		action CBSHT3
	}
	ifaction CBSHT3
	{
		ifactioncount 1
		ai AICBSEEK
	}

ends

state rollhitactor

	ifn bottarget -1 set target bottarget else
	set target player[].i
	
	sound SQUISHED
	
	ife target player[].i
	{
		wackplayer
		quake 12
		getp[].ang temp
		add temp 1024
		cos tempb temp
		sin tempc temp
		shiftvarl tempb 8
		shiftvarl tempc 8
		setp[].posxv tempb
		setp[].posyv tempc
		palfrom 40 0 48 16
	}
	else
	{
		seta[target].xvel -512
		getav[target].stun temp
		add temp 15
		setav[target].stun temp
		geta[target].z z
		sub z 1024
		seta[target].z z
		geta[target].zvel z
		sub z 1024
		seta[target].zvel z
	}
	ifspritepal 24 seta[target].htextra 45
	else
	seta[target].htextra 30
	seta[target].htpicnum RPG
	seta[target].htowner THISACTOR
	seta[target].htang sprite[THISACTOR].ang
	
	
	cactor CYBERBEAST
	ai AICBSEEK

ends

state rollhitobject

	ifpdistl 5120 quake 12
	sound LANDMETAL
	debris SCRAP1 3
	debris SCRAP2 3
	debris SCRAP3 3
	debris SCRAP4 3
	debris SCRAP5 3
	debris SCRAP6 3
	hitradius 1560 10 15 20 25
	// seta[].htextra -1
	ifpdistl 1560 seta[player[].i].htowner THISACTOR
	cactor CYBERBEAST
	ai AICBSEEK
	
ends

state cbrollstate

	ifspritepal 24 ifmove CBROLLVEL move CBROLLFAST faceplayerslow
	ifaction CBHYBE 
	{
		cactor CYBERBEAST // safety
		ifcount 6
		{
			cactor CYBERBALL
			geta[].z z
			sub z 2048
			seta[].z z
			action CBSCW1
			move CBROLLVEL faceplayerslow
		}
	}
	else
	{
		ifaction CBSCW1
		{
			cactor CYBERBALL // safety
			ifl targetdist 6144 action CBSCW2
		}
		else
		{
			ifaction CBSCW2 cactor CYBERBALL // safety
			
			ifactorsound THISACTOR MEGASHOCK nullop else sound MEGASHOCK
			espawn SPARKFALL
			seta[RETURN].pal 6
			seta[].shade -127
			ifl targetdist 1280
			state rollhitactor
			ifnotmoving
			state rollhitobject
		}
		
		ifcount 90
		{
			cactor CYBERBEAST
			ai AICBSEEK
		}
		else ifg countvarb 0
		{
			sub countvarb 1
			ife countvarb 0
			{
				cactor CYBERBEAST
				ai AICBSEEK
			}
		}
	}

ends

state cbgrowstate state genericgrowcode ends

state cbshrunkstate

  ifcount SHRUNKDONECOUNT
    ai AICBSEEK
  else
    ifcount SHRUNKCOUNT
	{
	  ifspritepal 24 sizeto 34 30 else
	  sizeto 32 28
	}
  else
    state genericshrunkcode

ends

state cbfrozenstate

    ifcount THAWTIME
    {
      ai AICBSEEK
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 
	  state enemy_death

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  state lite_jibs
	  state beast_body_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
  
ends

state cbdyingstate

	strength 0
	state deadsquished
	ifaction ACBDECAP
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action ACBDECAPDEAD
			iffloordistl 8 sound THUD
			ifrnd 128 
			{
				spritepal 6
				spawn BLOODPOOL
				getlastpal
			}
			break
		}
	}
	ifaction CBDIE
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstat 0
			action CBDEAD
			iffloordistl 8 sound THUD
			ifrnd 128 
			{
				spritepal 6
				spawn BLOODPOOL
				getlastpal
			}
			break
		}
	}
	ifaction ACBDECAPDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_armslegs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_armslegs
				
				killit
			}
		}
	}
	ifaction CBDEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_body_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_body_jibs
				
				killit
			}
		}
		// ifcount RESPAWNACTORTIME
		// ifrespawn ife gametype 0
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength CYBERBEASTSTRENGTH
		  // ai AICBSEEK
		// }
	}
	
ends

state cbroamsounds

	ifactorsound THISACTOR CBROAM1 break
	ifactorsound THISACTOR CBROAM2 break
	ifrnd 128 sound CBROAM1 else sound CBROAM2

ends

state cbseekstate

	ifspritepal 24 ifmove CBWALKVEL move CBWALKFAST seekplayer
	
	ifceilingdistl 96 nullop else
	ifcount 12
	{
		ifrnd 32
		{
			ifnotmoving { sound SPINJUMP ai AICBJUMP break }
			ifbulletnear { sound SPINJUMP ai AICBJUMP break }
			ife seemytarget YES ifp phigher
			ifl targetdist 6144 { sound SPINJUMP ai AICBJUMP break }
		}
	}
	
	ife seemytarget YES
	{
		ifn bottarget -1 set target bottarget else 
		ife targetalive YES getp[].i target
		else set target -1
	}
	else set target -1
	
	ifn target -1
	ifcount 8
	{
		ifl targetdist 16384
		ifrnd 3
		{
			ai AICBSHOOT
			break
		}
		ifg targetdist 4096
		{
			ifbulletnear 
				ai AICBROLL
			else
			ifrnd 1 ai AICBROLL
		}
	}
	
	ifrnd 1 state cbroamsounds
	
	ife targetalive YES
	ife seemytarget YES
	{
		ifl targetdist 1792 
		{ 
			ai AICBATTK 
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
			break 
		}
	}
	
	ifai AICBSEEK
	ifn myspawner -1
	ifge SKILL 4
	{
		state incoming_eval
		cactor CYBERBALL
		ai AICBROLL
		geta[].z z
		sub z 2048
		seta[].z z
		action CBSCW1
		seta[].ang angvel
		move CBROLLVEL geth
		set dodgetime 10
		set countvarb 10
	}
	set myspawner -1

ends

state cbjumpstate

  ifaction CBSCW2
  {
	cactor CYBERBALL // safety
    seta[].shade -127
	espawn SPARKFALL
	seta[RETURN].pal 6
	ifl targetdist 1280 ife seemytarget YES
	{
		ifn bottarget -1
		{
			seta[bottarget].htextra 2
			seta[bottarget].htpicnum RPG
			seta[bottarget].htowner THISACTOR
		}
		else
		{
			palfrom 40 0 48 16
			addphealth -2
		}
	}
    iffloordistl 8
	{
	  cactor CYBERBEAST
      ai AICBSEEK
	  sound LANDMETAL
	}
  }
  else
  {
    ifaction CBJMP2
	{
		ifcount 8
		{
			cactor CYBERBALL
			action CBSCW2
		}
    }
  }
	  
ends

state cbscratch

	ife targetalive NO
	{
		sound NEWBEAST_ATTACKMISS
		break
	}
	
	ifn bottarget -1
	ifn bottarget player[].i
	{
		ife seemytarget YES
		ifl targetdist 1596
		{
		  seta[bottarget].htextra 11
		  seta[bottarget].htpicnum SHOTSPARK1
		  seta[bottarget].htowner THISACTOR
		  seta[bottarget].htang sprite[].ang
		  sound RENDFLESH
		}
		else sound NEWBEAST_ATTACKMISS
	}
	else
	{
		ifpdistl 1596
		ife seeplayer YES
		{
		 
		  getp[].i spriteid
		  seta[spriteid].htpicnum SHOTSPARK1
		  seta[spriteid].htowner THISACTOR
		  seta[spriteid].htextra 11
		  seta[spriteid].htang sprite[].ang
		  
		  state checksaberblock
		  ifn sprite[spriteid].htextra -1
		  {
			   palfrom 16 16
			  ife pchar 1 sound B_GRUNT else
				sound DUKE_GRUNT
			  sound RENDFLESH
		  }
		}
		else sound NEWBEAST_ATTACKMISS
	}

ends

state cbattkstate

	ifaction CBATTK
	{
		ifactioncount 1
		{
			action CBATTK2
			state cbscratch
		}
	}
	else
	ifaction CBATTK2
	{
		ifactioncount 1
		{
			action CBATTK3
			state cbscratch
		}
	}
	else
	ifaction CBATTK3
	{
		ifactioncount 1
		{
			ife targetalive NO
			{
				ai AICBSEEK
				break
			}
			resetcount
			action CBATTK
			ifg targetdist 2048 ai AICBSEEK
			ife bottarget -1 ifpdistg 2048 ai AICBSEEK
			ifaction CBATTK ife bottarget myshelly setav[myshelly].myspawner THISACTOR
			break
		}
	}
	
	ifn bottarget -1 
	{
		ifmove CBWALKVEL { ifl targetdist 1280 move STOPPED geth }
		else ifmove STOPPED { ifg targetdist 1560 move CBWALKVEL geth }
		set spriteid bottarget
		state facesprite
		
		ife actorvar[bottarget].monstatus 2 ai AICBSEEK
	}
	else
	{
		ifmove CBWALKVEL { ifpdistl 1280 move STOPPED geth }
		else ifmove STOPPED { ifpdistg 1560 move CBWALKVEL geth }
		set spriteid player[].i
		state facesprite
	}

ends

state cbhybestate

	ifcount 10 ifrnd 16 ai AICBSEEK

ends

eventloadactor CYBERBEAST sizeat 32 28 ifspritepal 24 sizeat 34 30 action CBHYBE cstator 257 enda

state cbcode
	ifai 0
	{
		ai AICBHYBE
		sizeat 32 28
		ifspritepal 24 { sizeat 34 30 addstrength 300 }
		cstat 257
		clipdist 52
	}
	fall
	state monsterai

	ifai AICBDYING { state cbdyingstate break }
	
	ifai AICBFROZEN { state cbfrozenstate break }
	ifai AICBGROW { state cbgrowstate break }
	ifai AICBSHRUNK state cbshrunkstate else
	ifai AICBSEEK state cbseekstate else
	ifai AICBHYBE state cbhybestate else
	ifai AICBATTK state cbattkstate else
	ifai AICBJUMP state cbjumpstate else
	ifai AICBROLL state cbrollstate else
	ifai AICBSHOOT state cbshootstate
	
	ifai AICBSHRUNK nullop else
	ifg stun 0 
	{
		cactor CYBERBEAST
		ai AICBHIT
	}
	
	ifai AICBHIT
	{
		ifactioncount 2 ai AICBSEEK
	}

	ifhitweapon
	{
		ifai AICBHYBE ai AICBSEEK
		ifwasweapon SHRINKSPARK
		{	
		  cactor CYBERBEAST
		  sound ACTOR_SHRINKING
		  ai AICBSHRUNK
		  set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		state random_wall_jibs
		
	  ifwasweapon HEADJIB1
	  {
		addkills 1 state enemy_death
		sound SQUISHED
		cactor CYBERBEAST
		cstat 0
		strength -1
		state rf
		  ai AICBDYING
		  ifrnd 64
			spawn BLOODPOOL
		action ACBDECAP

		rand zdist 1024
		  sub zdist 3500
		  ezshoot zdist BEASTHEADPROJ
		  rand angvar 512
		  sub angvar 256
		  add angvar sprite[].htang
		  seta[RETURN].ang angvar
		  seta[RETURN].pal sprite[].pal
		
		break
	  }
		
		sound CBPAIN1

		ifdead
		{
			cactor CYBERBEAST
			ifactorsound THISACTOR MEGASHOCK stopactorsound THISACTOR MEGASHOCK
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  ai AICBFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ifwasweapon GROWSPARK
			{
			  cstat 0
			  sound ACTOR_GROWING
			  ai AICBGROW
			  break
			}
			addkills 1
			state enemy_death
			ai AICBDYING
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch SHRUNKPITCH
			sound CBDYING_SND
			ifg shrunken 10 ifl shrunken SHRUNKCOUNT
				setp[].sound_pitch 0
			spritepal 6
			guts JIBS6 6
			getlastpal
			
			ifwasweapon RPG
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_body_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				sound SQUISHED
				spritepal 6
				spawn BLOODPOOL
				state lite_jibs
				getlastpal
				state beast_body_jibs
				killit
			}
			break
		}
		ifrnd 2
		{
			spritepal 6
			spawn BLOODPOOL
			getlastpal
		}
		ifai AICBHIT nullop else
		ifai AICBSHRUNK nullop else
		{
			ifg stun 0 { cactor CYBERBEAST ai AICBHIT break }
			
			ifrnd 32 
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			{ 
				cactor CYBERBEAST
				ai AICBHIT 
				break 
			}
		}
	}
	else state checksquished
ends

useractor enemy CYBERBEAST CYBERBEASTSTRENGTH state cbcode enda

useractor enemy CYBERBALL CYBERBEASTSTRENGTH state cbcode enda

action CBPROJFRAMES 0  6  1  1  4
useractor notenemy CBPROJEXP 0 CBPROJFRAMES

	ifmove 0
	{
		sound THUD
		move STOPPED
		ifl sprite[].xrepeat 32
		sizeat 32 32
		cstator 128
		spawn MOLTENROCK
		spawn MOLTENROCK
		spawn MOLTENROCK
	}
	ifactioncount 6 killit

enda

spritenoshade MOLTENROCK
action MOLTENFRAME -1
useractor notenemy MOLTENROCK 0 MOLTENFRAME

ifmove 0
{
	move SLOWSPARK randomangle
	sizeat 16 16
	seta[].shade -127
	randvar mtype 512
	sub mtype 1024
	seta[].htflags 2048
	ifrnd 128 cstator 8
	ifrnd 128 cstator 4
}
ifceilingdistl 16 fall

movesprite THISACTOR 0 0 mtype CLIPMASK0 RETURN
ife initsprite -2 { ifg mtype 1560 killit }
else
iffloordistl 8
{
	ife monstatus 2 killit
	ifrnd 128 killit else
	{
		set monstatus 2
		geta[].z z
		sub z 1024
		seta[].z z
		seta[].zvel -1024
		set mtype 0
	}
}
add mtype 160
ifg mtype 3072 ifrnd 16 killit
ifnotmoving ifcount 3 { resetcount move SLOWSPARK randomangle ifrnd 64 killit }

enda
