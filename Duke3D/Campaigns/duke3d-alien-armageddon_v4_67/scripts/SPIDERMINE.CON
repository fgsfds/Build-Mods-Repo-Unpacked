// spider-mine

define SPIDERMINESTRENGTH 60

action AMINESIT 0  1  3  1  10
action AMINESIT2 -1807 1  3  1  10
action AMINEWALK 0  4  5  1  8
action AMINERUN  0  4  5  1  6
move MINEWALKVEL 160
move MINERUNVEL 256
move MINEJUMP 192
move MINEWAIT

state mine_explode
	sound PIPEBOMB_EXPLODE
	espawn MINEEXP
	seta[RETURN].xrepeat 40
	seta[RETURN].yrepeat 40
	debris SCRAP1 5
	debris SCRAP2 5
	hitradius 3072 30 70 100 120
	ifn bottarget -1
	{
		dist xydist THISACTOR bottarget
		set xydist2 3072
		sub xydist2 xydist
		ifg xydist2 0
		{
			div xydist2 14 // 12
			ifl xydist2 1 set xydist2 1
			geta[bottarget].htextra temp
			add temp 1
			add temp xydist2
			seta[bottarget].htextra temp
			seta[bottarget].htpicnum RPG
			seta[bottarget].htowner player[].i
		}
	}
	killit
ends

move ABOUTOBLOW

useractor notenemy SPIDERMINE SPIDERMINESTRENGTH AMINESIT

fall

ifmove ABOUTOBLOW
{
	ifcount 15 state mine_explode
	ifspritepal 121 getlastpal else spritepal 121
	break
}

ifl player[].ammo_amount TRIPBOMB_WEAPON MAXTRIPBOMBAMMO
ifpdistl 1280
ifp palive
ifp pfacing
ifcansee
{
	ifl player[].fta 60 quote 224
	ifhitspace
	{
		addweapon TRIPBOMB_WEAPON 1
		ife player[].ftq 224 setp[].fta 0
		globalsound DUKE_GET
		palfrom 16 0 32
		killit
	}
}
else ife player[].ftq 224 setp[].fta 0

ifmove 0
{
	move STOPPED
	set teamspawned 99999
	sizeat 16 16
	cstator 256
	// set monstatus 1
	set team 1
	set spawnprotect 0
	ifvarand dukeupgrades[TRIPBOMB_WEAPON] 2 set mtype YES
	ifvarand shellyupgrades[TRIPBOMB_WEAPON] 2 set mtype YES
	ifvarand wesupgrades[TRIPBOMB_WEAPON] 2 set mtype YES
}
ifactioncount 3
{
	ifaction AMINESIT action AMINESIT2
	else action AMINESIT
	resetactioncount
}

iffloordistl 4
{
	state targetsearch
	
	ife bottarget -1
	{
		findnearspritez CRACK1 2560 10240 target
		ifn target -1 ife sprite[target].hitag 0 set target -1
		ife target -1
		findnearspritez CRACK2 2560 10240 target
		ifn target -1 ife sprite[target].hitag 0 set target -1
		ife target -1
		findnearspritez CRACK3 2560 10240 target
		ifn target -1 ife sprite[target].hitag 0 set target -1
		ife target -1
		findnearspritez CRACK4 2560 10240 target
		ifn target -1 ife sprite[target].hitag 0 set target -1
		
		ifn target -1 { move ABOUTOBLOW sound LASERTRIP_ARMING break }
	}
	
	ifn bottarget -1
	{
		set monstatus 1
		cactor SPIDERWALK
		strength SPIDERMINESTRENGTH
		action AMINEWALK
		ife mtype YES
		{
			move MINERUNVEL randomangle 
			action AMINERUN
		}
		else
		move MINEWALKVEL randomangle
		sound SPIDUPSND
	}
	// ifn monstatus 2 ife mysignpost -1 state spawnmysignpost
}

ifhitweapon
{
	ifdead
	state mine_explode
}

enda

useractor notenemy SPIDERWALK SPIDERMINESTRENGTH

fall
ifmove 0
{
	move STOPPED
	set teamspawned 99999
	action ANULLACTION
	sizeat 16 16
	cstator 256
	set team 1
	set monstatus 1
	set spawnprotect 0
	ife pchar 0 ifvarand dukeupgrades[TRIPBOMB_WEAPON] 2 set mtype YES
	ife pchar 1 ifvarand shellyupgrades[TRIPBOMB_WEAPON] 2 set mtype YES
}
state targetsearch

// ifn monstatus 2 ife mysignpost -1 state spawnmysignpost

ifn bottarget -1
{
	// ife mysignpost -1 state spawnmysignpost
	
	ifaction AMINEWALK
	ifactioncount 2 { resetactioncount ifrnd 128 sound SPIDWALK1 else sound SPIDWALK2 }
	resetcount
	set spriteid bottarget
	state facesprite
	ifmove STOPPED 
	{ 
		move MINEWALKVEL geth action AMINEWALK 
		ife mtype YES
		{
			move MINERUNVEL geth 
			action AMINERUN
		}
	}
	
	geta[].htmovflag temp
	ifn temp 0
	{
		add temp 16384
		ifl temp 16384 ifg temp -1 
		{
			ife temp bottarget state mine_explode
		}
	}
	
	dist xydist THISACTOR bottarget
	ifl xydist 384
	state mine_explode
	else ifnotmoving
	ifl xydist 768
		state mine_explode
	else ifg sprite[bottarget].z sprite[].z
	ifl xydist 768
		state mine_explode
	else ifmove MINEJUMP
	{
		add countvar 1
		ifl xydist 768
			state mine_explode
		ifge countvar 10 { move MINEWALKVEL geth set countvar 0 }
	}
	else
	ifl sprite[bottarget].z sprite[].z
	iffloordistl 4
	ifl xydist 3072
	ife mtype YES
	{
		geta[].ang angvar
		move MINEJUMP jumptoplayer
		seta[].ang angvar
		geta[].zvel z sub z 1024 seta[].zvel z
		sound SPIDJUMP
	}
	else
	{
		ldist xydist2 THISACTOR bottarget
		ifl xydist2 384
		{
			move MINEWAIT
			action ANULLACTION
		}
		else ifmove MINEWAIT
		{
			move MINEWALKVEL geth
			action AMINEWALK
			ife mtype YES
			{
				move MINERUNVEL geth 
				action AMINERUN
			}
		}
	}
}
else 
{
	set temp NO
	ifmove MINEWALKVEL set temp YES
	ifmove MINERUNVEL set temp YES
	ifmove MINEWAIT set temp YES
	ifmove MINEJUMP set temp YES
	ife temp YES
	{
		move STOPPED
		action ANULLACTION
		set countvar 0
	}
}
ifcount 20 ifmove STOPPED
{
	cactor SPIDERMINE
	action AMINESIT
	set monstatus 0
}

ifhitweapon
{
	ifdead
	state mine_explode
}

enda

useractor notenemy NEWTRIPBOMB 0

ife countvar 0 soundonce LASERTRIP_ONWALL
add countvar 1

ife mtype 0 // not firing yet
{
	ifg countvar 9
	{
		
		ifp pfacing
		ifp palive
		ifpdistl 1024
		{
			ifhitspace
			{
				sound SWITCH_ON
				stopsound DUKE_GRUNT
				stopsound B_GRUNT2
				cactor TRIPBOMBSPRITE
				changespritestat THISACTOR 1
				set countvar 0
				cstat 0
				sizeat 32 32
				break
			}
			else ifl player[].fta 30 quote 125
		}
		else ifg player[].fta 0 ife player[].ftq 125 setp[].fta 0

		geta[].z z
		sub z 2048
		
		cos mycos sprite[].ang
		sin mysin sprite[].ang
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
		ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
		
		ifn hitsprite -1
		{
			ife hitsprite player[].i set mtype 1 else
			ife actorvar[hitsprite].monstatus 1 set mtype 1
			
			ife mtype 1 { sound LASERTRIP_ARMING set countvar 1 }
		}
	}
	break
}

ifge countvar 10
{
	setprojectile[BIGBOIPROJ].isound -1
	setprojectile[BIGBOIPROJ].extra 8
	setprojectile[BIGBOIPROJ].pal 2
	set temp player[].player_par
	modvar temp 5
	ife temp 0
	setprojectile[BIGBOIPROJ].spawns EXPLOSION4
	else setprojectile[BIGBOIPROJ].spawns -1
	shoot BIGBOIPROJ
	setprojectile[BIGBOIPROJ].isound MEGAIMPACT1
	setprojectile[BIGBOIPROJ].extra 4
	setprojectile[BIGBOIPROJ].pal 23
	setprojectile[BIGBOIPROJ].spawns GREEN_EXP
	soundonce LASERLOOP
	
	ifge countvar 300
	{
		spawn EXPLOSION2
		sound PIPEBOMB_EXPLODE
		hitradius TRIPBOMBBLASTRADIUS 25 50 75 100
		debris SCRAP1 6
		debris SCRAP2 6
		stopactorsound THISACTOR LASERLOOP
		killit
	}
}


enda

// NUKE EXPLOSION

spritenoshade NUKEEXPLOSION
spritenopal NUKEEXPLOSION
spritenoshade FIRERING
spritenopal FIRERING

useractor notenemy FIRERING 0

ifmove 0
{
	move STOPPED
	cstat 34
	geta[].z z
	ife mtype 2 sub z 8192 else
	sub z 14336
	seta[].z z
	seta[].shade -127
}
geta[].xrepeat x add x 4 seta[].xrepeat x
geta[].yrepeat y add y 4 seta[].yrepeat y
ifn mtype 0 ifcount 32 killit
ifcount 64 killit
geta[].ang angvar
add angvar 33
seta[].ang angvar


enda

move NUKEVELS 16
action NUKEFRAMES  0  21  1  1  16
useractor notenemy NUKEEXPLOSION

ifmove 0
{
	al mtype
	move NUKEVELS faceplayer
	action ANULLACTION
	sizeat 32 32
	ifspawnedby VERTROCKET { flash set mtype 1 }
	else palfrom 63 63 63 63
	cstat 32768
	ifrnd 128 cstator 4
	
	seta[].shade -127
	seta[].blend 1
	ifspritepal 0 spritepal 76
	
	ife mtype 1 
	{
		findnearactor BOSS3 5632 spriteid
		ifn spriteid -1
		geta[spriteid].htextra savz
		hitradius 5632 50 100 150 200
		ifn spriteid -1 seta[spriteid].htextra savz
	}
	else
	{
		// ifn mtype 2
		hitradius 6144 500 1000 1500 2000
		
		set x2 8192

		set spriteid 0
		whilevarn spriteid 16384
		{
			ifl sprite[spriteid].statnum 3
			ifg sprite[spriteid].statnum 0
			{
				geta[spriteid].z z
				sub z sprite[].z
				abs z
				ifl z 131072 // 65536
				{
					ldist xydist THISACTOR spriteid
					ifl xydist x2
					{
						set xydist2 2097152
						ife xydist 0 set xydist 1
						div xydist2 xydist
						ifl xydist2 256 set xydist2 256
						ifg xydist2 3000 set xydist2 3000
						add xydist2 256
						
						seta[spriteid].htextra xydist2
						seta[spriteid].htpicnum RPG
						ifge xydist2 sprite[spriteid].extra
							setav[spriteid].padang 1337
						seta[spriteid].htowner player[].i
						seta[spriteid].htang sprite[].ang
						getav[spriteid].burning tempb
						add tempb 180
						setav[spriteid].burning tempb
						getav[spriteid].stun tempb
						add tempb 90
						setav[spriteid].stun tempb
					}
					
				}
			}
			add spriteid 1
		}
	}
	set startx 2048
	ife rendmode 4
	{
		espawn SMALLSMOKE
		seta[RETURN].lotag 49
		seta[RETURN].hitag 16383
		seta[RETURN].xrepeat 2
		seta[RETURN].yrepeat 2
		seta[RETURN].xvel 255 // R value
		seta[RETURN].yvel 208 // G value
		seta[RETURN].zvel 208 // B value
		setav[RETURN].myspawner THISACTOR
		geta[].picnum picnum
		setav[RETURN].mtype picnum
		seta[RETURN].picnum SECTOREFFECTOR
		seta[RETURN].cstat 0
		changespritestat RETURN 3
	}
	seta[].mdflags 16
	iffloordistl 8 { geta[].z z sub z 3072 seta[].z z }
	globalsound EXPLODE4
}

ifaction ANULLACTION 
{
	ifactioncount 3 
	{ 
		// stopallsounds 
		globalsound NUKESOUND 
		action NUKEFRAMES 
		spawn EXPLOSION2
		quake 90
		espawn FIRERING
		ife mtype 1 setav[RETURN].mtype 1
		// ife mtype 2 setav[RETURN].mtype 1
	}
	break
}

// changespritesect THISACTOR player[].cursectnum

ifaction NUKEFRAMES
{
	ifactioncount 21 killit else
	ifactioncount 15 nullop else
	ifn mtype 1
	// ifn mtype 2
	{
		flash
		geta[].z savz
		sub savz 4096
		seta[].z savz
		set countvar 0
		add startx 256
		geta[].x savx
		add savx startx
		rand angvar 2047
		ifg framerate 30
		{
			whilevarn countvar 8
			{	
				espawn BIGSMOKE
				add angvar 256
				rotatepoint sprite[].x sprite[].y savx sprite[].y angvar x2 y2
				setsprite RETURN x2 y2 savz
				add countvar 1
			}
		}
		set x2 startx
		add x2 512
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			geta[spriteid].z z
			sub z savz
			abs z
			ifl z 65536
			{
				ldist xydist THISACTOR spriteid
				ifg xydist startx ifl xydist x2
				{
					geta[spriteid].htextra tempb
					add tempb 100
					seta[spriteid].htextra tempb
					seta[spriteid].htpicnum RPG
					ifge tempb sprite[spriteid].extra 
						setav[spriteid].padang 1337
					seta[spriteid].htowner player[].i
					seta[spriteid].htang sprite[].ang
					getav[spriteid].burning tempb
					add tempb 90
					setav[spriteid].burning tempb
					getav[spriteid].stun tempb
					add tempb 30
					setav[spriteid].stun tempb
				}
				
			}
			nextspritestat spriteid spriteid
		}
		
		
		add savz 4096
		seta[].z savz
	}
}

enda
