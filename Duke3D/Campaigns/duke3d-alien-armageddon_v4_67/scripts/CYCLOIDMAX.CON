// CYCLODIUS MAXIMUM JR

action ACYCLOIDMAXSTAND  		0  1  8  1  10
action ACYCLOIDMAXWALK   		8  4  8  1  20
action ACYCLOIDMAXSHOOTFIST1		40 1  8  1  100
action ACYCLOIDMAXSHOOTFIST2		48 1  8  1  80
action ACYCLOIDMAXSHOOTGUN1		56 1  8  1  18
action ACYCLOIDMAXSHOOTGUN2		64 1  8  1  12
action ACYCLOIDMAXDOWN		72  1  8  1  30	
action ACYCLOIDMAXDYING		80 7  1  1  24
action ACYCLOIDMAXSTUMBLE		80 5  1  1  24
action ACYCLOIDMAXDEAD			87 1  1  1  10

move CYCLOIDMAXWALKVEL 272
move CYCLOIDMAXGROW
move CYCLOIDMAXSTOPPED

ai AICYCLOIDMAXDOWN ACYCLOIDMAXSTUMBLE CYCLOIDMAXSTOPPED geth
ai AICYCLOIDMAXDYING ACYCLOIDMAXDYING CYCLOIDMAXSTOPPED geth
ai AICYCLOIDMAXSHOOTGUN ACYCLOIDMAXSHOOTGUN1 CYCLOIDMAXSTOPPED geth
ai AICYCLOIDMAXSHOOTFIST ACYCLOIDMAXSHOOTFIST1 CYCLOIDMAXSTOPPED geth
ai AICYCLOIDMAXWAIT ACYCLOIDMAXSTAND CYCLOIDMAXSTOPPED geth
ai AICYCLOIDMAXSEEK ACYCLOIDMAXWALK CANWALKVEL seekplayer

state cycloidmaxseekstate

	state strafecheck
	ifrnd 1 ifrnd 128 { ifrnd 128 globalsound CYCMAXROAM1 else globalsound CYCMAXROAM2 }
	
	ifcount 90
	{
		ife targetalive YES
		ifl targetdist MAXRANGE
			ife seemytarget YES
		{
			ife mtype 0 ai AICYCLOIDMAXSHOOTFIST
			else ai AICYCLOIDMAXSHOOTGUN
		}
		ifcount 300 ife gametype 0 ai AICYCLOIDMAXWAIT
	}
	
	
	ifactioncount 2
	{
		resetactioncount
		espawn SOUNDPLATE
		setav[RETURN].mtype 1
		rand temp 3
		add temp HUGESTEP1
		setav[RETURN].SPRITELOTAG temp
		seta[RETURN].cstat 32768
		hitradius 1792 40 40 40 40
		// seta[].htextra -1
	}
	ifg sprite[].yrepeat 40
	{
		ife targetalive YES
        ifpdistl 1280
		ife pdown NO
      {
        addphealth -1000
        palfrom 63 63
      }
	  
	  state squish_bot
	}

ends

state cycloidmaxshootgunstate

ifaction ACYCLOIDMAXSHOOTGUN2
{
	ifactioncount 1
	{
		// ife seemytarget YES
		// ifl targetdist MAXRANGE
		ifl countvarb 9
		{
			geta[].x savx
			geta[].y savy
			set x sprite[].x
			set temp sprite[].xrepeat
			mul temp 13
			add x temp
			geta[].ang angvar sub angvar 464
			rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
			seta[].x x2
			seta[].y y2
			
			geta[].z savz
			sub savz 4096
			ife countvarc 1 sub savz 2048
			seta[].z savz
			
			ife countvarc 0
			{
				sound RPG_SHOOT
				set xvel 644
				state rpg_targetprep
				ezshoot zdist RPG
				seta[RETURN].xvel 966
				geta[RETURN].zvel z mul z 3 div z 2 seta[RETURN].zvel z
				seta[RETURN].extra 100
				rand angvar 24
				sub angvar 12
				add angvar sprite[RETURN].ang
				seta[RETURN].ang angvar
			} else
			ife countvarc 1
			{
				sound TERMLASER
				setprojectile[GREENLASER].extra 24
				shoot GREENLASER shoot GREENLASER shoot GREENLASER eshoot GREENLASER
				setprojectile[GREENLASER].extra 16
			}
			
			add savz 4096
			ife countvarc 1 add savz 2048
			seta[].z savz
			
			seta[].x savx
			seta[].y savy
			
			
			action ACYCLOIDMAXSHOOTGUN1
			add countvarb 1
		}
		else
		{
			ai AICYCLOIDMAXWAIT
			set countvarb 0
			set mtype 0
			ife countvarc 0 set countvarc 1 else set countvarc 0
		}
	}
}
ifaction ACYCLOIDMAXSHOOTGUN1
{
	ifactioncount 1
		action ACYCLOIDMAXSHOOTGUN2
}

ends

state cycloidmaxshootfiststate

ifaction ACYCLOIDMAXSHOOTFIST2
{
	ifactioncount 2
	{
		set countvarb 0
		set mtype 1
		
		ifn bottarget -1
			ai AICYCLOIDMAXSHOOTGUN
		else
		
			ai AICYCLOIDMAXWAIT
			
		
		
	}
}
ifaction ACYCLOIDMAXSHOOTFIST1
{
	ifactioncount 1
	{
		action ACYCLOIDMAXSHOOTFIST2
		sound RPG_BIGSHOOT
		espawn FLYINGFIST
		setav[RETURN].myspawner THISACTOR
		setav[RETURN].bottarget bottarget
		geta[].yrepeat y
		mul y 644
		geta[].z z
		sub z y
		seta[RETURN].z z
		
		geta[].x savx
		geta[].y savy
		set x sprite[].x
		set temp sprite[].xrepeat
		mul temp 10
		add x temp
		geta[].ang angvar add angvar 462
		rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
		seta[RETURN].x x2
		seta[RETURN].y y2
	}
}

ends

state cycloidmaxdownstate

	set burning 0
	ifaction ACYCLOIDMAXSTUMBLE
	{
		ifactioncount 4
		{
			action ACYCLOIDMAXDOWN
			espawn FORCEBUBBLE
			setav[RETURN].myspawner THISACTOR
			seta[RETURN].xrepeat 24
			seta[RETURN].yrepeat 26
			
			ife wave 40
			{
				stopallsounds
				screensound CYCMAXSPEECH
				set cutcamtime player[].player_par
				set emp_overlay 180
				set emp_overlay_tile 15273
				set subtitle_start 811
				set subtitle_numlines 2
				set subtitle_time 180
			}
			
			ife wave 42
			{
				stopallsounds
				screensound CYCMAXSPEECH
				set cutcamtime player[].player_par
				set emp_overlay 240
				set emp_overlay_tile 15273
				set subtitle_start 816
				set subtitle_numlines 2
				set subtitle_time 240
			}
			
			ife gametype 0
			{
				ife countvar 1
				{
					ifg SPRITELOTAG 4
					state activatechannel
					ifg initsprite 1 set SPRITELOTAG initsprite
				}
				else
				ife countvar 2
				{
					ifg SPRITELOTAG 1
					state activatechannel
				}
  
			}
		}
		
	}
	ife gametype SURVIVAL 
	ife LEVEL 37
	{
		ifg monleft 0 resetcount
		else
		{
			ifcount 210 nullop else count 210
		}
		ife subtitle_time 1
		ife subtitle_start 811
		{
			stopallsounds
			screensound MANDO_ASSIST
			set cutcamtime player[].player_par
			set emp_overlay 300
			set emp_overlay_tile 15270
			set subtitle_start 813
			set subtitle_numlines 3
			set subtitle_time 300
			operaterespawns 30
		}
	}
	// strength 6500
	seta[].extra inithp
	seta[].htextra -1
	ifcount 210
	{
		ifspritepal 33 getlastpal else spritepal 33
	}
	ifcount 300 
	{ 
		ifspritepal 33 getlastpal 
		ifspritepal 33 seta[].pal initflags
		ai AICYCLOIDMAXWAIT 
		set monstatus 1 
		ife attmode YES 
		{
			ife countvar 1 orvar monstflags 128
			ife countvar 2 { xorvar monstflags 128 orvar monstflags 64 }
		}
	}

ends

state cycloidmaxwaitstate

	ifcount 30
	{
		ifrnd 128
		ife targetalive YES
			ifl targetdist MAXRANGE
				ife seemytarget YES
		{
			ife mtype 0 ai AICYCLOIDMAXSHOOTFIST
			else ai AICYCLOIDMAXSHOOTGUN
		}
		else 
		{
			ai AICYCLOIDMAXSEEK
			rand temp 3
			add temp HUGESTEP1
			soundvar temp
		}
	}
	

ends

state cycloidmaxdyingstate

	
	ifaction ACYCLOIDMAXDYING
	{
		ifactioncount 7
		{
			action ACYCLOIDMAXDEAD
			resetcount
			strength 0
			cstat 0
			set burning 0
			quake 20
			globalsound THUD
		}

	}
	ifaction ACYCLOIDMAXDEAD
	{
		ifcount 40 nullop else
		ifcount 39
		{
			ife VOLUME 6 ife LEVEL 37
			{
				stopallsounds
				starttrackslot 4 3
				screensound FIN_GLAD1
				set SCREENALPHA	255
				set midscreen 14817
				set cutend totalclock
				set cutstart cutend
				add cutend 1300
				set cutmash 2
				startscreen
				set SCREENALPHA	255
				set midscreen 14818
				set cutend totalclock
				set cutstart cutend
				add cutend 460
				stopsound FIN_GLAD1
				screensound FIN_GLAD2
				set cutmash 2
				startscreen
				set midscreen 14819
				set cutend totalclock
				set cutstart cutend
				add cutend 1700
				stopsound FIN_GLAD2
				screensound FIN_GLAD3
				set cutmash 2
				startscreen
				set SCREENALPHA	255
				set midscreen 14820
				set cutend totalclock
				set cutstart cutend
				add cutend 900
				stopsound FIN_GLAD3
				screensound FIN_GLAD4
				set cutmash 2
				startscreen
				set CUTSCENE_STEP 1
				set midscreen 14820
				set cutend totalclock
				set cutstart cutend
				add cutend 1400
				stopsound FIN_GLAD4
				screensound FIN_GLAD5
				set cutmash 2
				startscreen
				set CUTSCENE_STEP 0
				set SCREENALPHA	255
				set midscreen 14821
				set cutend totalclock
				set cutstart cutend
				add cutend 1000
				stopsound FIN_GLAD5
				screensound FIN_GLAD6
				set cutmash 2
				startscreen
				set SCREENALPHA	255
				set midscreen 14822
				set cutend totalclock
				set cutstart cutend
				add cutend 1600
				set cutmash 2
				starttrackslot 4 8
				startscreen
				set SCREENALPHA	255
				set midscreen 14823
				set cutend totalclock
				set cutstart cutend
				add cutend 9000
				set cutmash 2
				set crawlclock totalclock
				set cutstart crawlclock
				set crawlnumlines 38
				startscreen
				palfrom 63 0 0 0
				endofgame 2
			}
			else
			ifspritepal 0
			{
				ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
				{
					endofgame 60
					ife attmode YES set attmid 4
				}
			}
		}
	}

ends

state cycloidmaxcode

	ifmove 0
	{
		cstat 257
		ai AICYCLOIDMAXWAIT
		geta[].pal initflags
		ifspritepal 0 
		{
			sizeat 80 64
			clipdist 150
			stopallsounds
			globalsound CYCMAXSPEECH
			set cutcamtime player[].player_par
			set emp_overlay 150
			set emp_overlay_tile 15273
			set subtitle_start 809
			set subtitle_numlines 2
			set subtitle_time 150
			ife VOLUME 6 ife LEVEL 37
			{
				set gametype SURVIVAL
				set wave 39
				starttrackslot 7 11
			}
		}
		else
		{
			sizeat 40 32
			clipdist 128
			strength 5000
		}
	}
	
	fall
	ife monstatus 1 seta[].ang lastang
	
	ifai AICYCLOIDMAXDYING { state cycloidmaxdyingstate break }
	ifai AICYCLOIDMAXDOWN { state cycloidmaxdownstate break }
	
	state monsterai
	
	ifai AICYCLOIDMAXWAIT state cycloidmaxwaitstate else
	ifai AICYCLOIDMAXSEEK state cycloidmaxseekstate else
	ifai AICYCLOIDMAXSHOOTGUN state cycloidmaxshootgunstate else
	ifai AICYCLOIDMAXSHOOTFIST state cycloidmaxshootfiststate
	
	
	ifhitweapon
	{
		ifdead
		{
			ifge countvar 2
			{
				globalsound CYCMAXDEATH
				ai AICYCLOIDMAXDYING
				quake 150
				addkills 1
				state enemy_death
				// ifspritepal 0 final death stuff
				ife gametype SURVIVAL
				{
					set gametype 0
				}
			}
			else
			{
				// reset pathnode values
				getp[].player_par ikicked
				getp[].player_par curr_tick
				add curr_tick 1
				headspritestat spriteid 980
				whilevarn spriteid -1
				{
					setav[spriteid].redflagval 0
					setav[spriteid].rediteration 99999
					setav[spriteid].redtimer curr_tick
					nextspritestat spriteid spriteid
				}
				
				globalsound CYCMAXDEATH
				ai AICYCLOIDMAXDOWN
				set monstatus 2
				
				// spawn random goodies around
				set padang 2048
				whilevarn padang 0
				{
					
					set x2 sprite[].x
					add x2 3072
					
					rotatepoint sprite[].x sprite[].y x2 sprite[].y padang savx savy
					
					state spawn_goodie
					
					set savz sprite[].z
					sub savz 16384
					setsprite RETURN savx savy savz
					espawn EXPLOSION2
					setsprite RETURN savx savy savz
					
					sub padang 128 ifl padang 0 set padang 0
				}
				
				add countvar 1
				ife countvar 1
				{
					ife VOLUME 6 ife LEVEL 37
					{
						set spawnsleft 12
						set monleft 12
						set wave 40
					}
					headspritestat spriteid 1
					whilevarn spriteid -1
					{
						ife sprite[spriteid].picnum TEAMSPAWNER
							setav[spriteid].countvar 80
						nextspritestat spriteid spriteid
					}
				}
				else
				ife countvar 2
				{
					ife VOLUME 6 ife LEVEL 37
					{
						set spawnsleft 16
						set monleft 16
						set wave 42
					}
					headspritestat spriteid 1
					whilevarn spriteid -1
					{
						ife sprite[spriteid].picnum TEAMSPAWNER
							setav[spriteid].countvar 80
						nextspritestat spriteid spriteid
					}
				}
			}
			break
			
		}
		else
		{
			ifrnd 128 soundonce CYCMAXPAIN1 else
			soundonce CYCMAXPAIN2
			ifai AICYCLOIDMAXSEEK
			ife seemytarget YES
			ifn actorvar[sprite[].htowner].team team
			ifrnd 64
			{
				ife mtype 0 ai AICYCLOIDMAXSHOOTFIST
				else ai AICYCLOIDMAXSHOOTGUN
			}
		}
			
			
		
	}
ends

eventloadactor CYCLOIDMAX ifspritepal 0 sizeat 80 64 else sizeat 40 32 cstator 257 enda
eventloadactor CMAXSTAYPUT ifspritepal 0 sizeat 80 64 else sizeat 40 32 cstator 257 enda

useractor enemy CYCLOIDMAX 7000 state cycloidmaxcode enda
useractor enemystayput CMAXSTAYPUT 7000 cactor CYCLOIDMAX enda

action AFISTFLY  5  1  5  1  20
action AFISTRETURN 0  1  5  1  20
action AFISTSPIN  0  5  1  1  16

move FISTFLYVELS 644
move FISTRETURNVELS 512 -4
move FISTEXPLODING

spritenoshade FLYINGFIST
spritenopal FLYINGFIST

state fistexplode

	move FISTEXPLODING
	set monstatus 2
	cstat 32768
	hitradius 4096 40 80 120 160
	spawn EXPLOSION2
	debris SCRAP1 4
	debris SCRAP2 4
	debris SCRAP6 4
	globalsound NUKESOUND
	ifpdistl 8192 quake 90
	espawn FIRERING
	geta[RETURN].z z
	add z 10240
	seta[RETURN].z z
	flash
	
	ifn bottarget -1
	ifl targetdist 2048
	{
		seta[bottarget].htowner myspawner
		seta[bottarget].htpicnum RPG
		set B targetdist
		sub B 2048
		shiftr B 3
		ifl sprite[bottarget].htextra B seta[bottarget].htextra B
	}

ends

state fistexplodingstate

	rand angvar 2048
		
	rand savx 1024
	add savx sprite[].x
	
	geta[].z savz
	
	rand z 16384
	sub z 8192
	add z savz
	seta[].z z
	ifrnd 84
	debris SCRAP1 1
	else
	ifrnd 128
	debris SCRAP2 1
	else
	debris SCRAP6 1
	
	ifrnd 64
	ifcount 2
	{
		espawn EXPLOSION2
		rotatepoint sprite[].x sprite[].y savx sprite[].y angvar x2 y2
		setsprite RETURN x2 y2 z
		ifrnd 128
		soundonce PIPEBOMB_EXPLODE
		else
		soundonce ROBOT_EXPLODE
		
		hitradius 2048 15 30 45 60
	}
	
	seta[].z savz


	ifcount 15 killit

ends

useractor notenemy FLYINGFIST 125 AFISTFLY

	ifmove 0
	{
		move FISTFLYVELS geth getv
		ifspritepal 0
		sizeat 80 64
		else sizeat 40 32
		cstat 384
		clipdist 64
		seta[].shade -32
	}
	
	set burning 0
	ifmove FISTEXPLODING { state fistexplodingstate break }
	
	ifmove FISTFLYVELS spawn SPARKFALL
	
	state monsterai
	ifg spawnprotect 0
	{
	set spawnprotect 0 ifspritepal 33 getlastpal
	}
	ifn bottarget -1 state zveltotarget
	
	
	ifhitweapon
	{
		ifdead state fistexplode
	}
	ifn bottarget -1
	ifl targetdist 1024 state fistexplode else
	ifnotmoving 
	{
		ldist xydist THISACTOR myspawner
		ifg xydist 3072
		state fistexplode
	}
	
	ifstrength 100
	{
		ifrnd 24
		{
			espawn BIGSMOKE
			rand z2 8192
			sub z2 4096
			add z2 sprite[].z
			seta[RETURN].z z2
			seta[RETURN].htpicnum RECONWRECK
			rand x 384
			sub x 192
			add x sprite[].x
			seta[RETURN].x x
			rand y 384
			sub y 192
			add y sprite[].y
			seta[RETURN].y y
		}
	}
	
	ifcount 900 state fistexplode

enda

useractor notenemy FORCEBUBBLE 0

	ife myspawner -1 break
	clipdist 255
	cstat 257
	strength 1000
	ifhitweapon spritepal 33
	else ifspritepal 33 getlastpal
	ife monstatus 2 killit
	ife actorvar[myspawner].monstatus 1 killit

enda
