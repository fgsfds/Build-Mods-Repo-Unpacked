// BOMBSHELL NPC
// sprites by sebabdukeboss20, code by Dan Gaskill
// define SHELLY 6656
// define SHELLYCROUCH 6681
define SHELLYBIGHURT 40
define SHELLYHURT 80
define JUMPDIST 384

action ADUKESTAND			0  1  5  1  10
action AWESBOTSTAND			0  1  5  1  10
move WESVELS

ai AIDUKESTAND ADUKESTAND STOPPED geth
ai AIWESBOTSTAND AWESBOTSTAND WESVELS geth

spriteflags SHELLY 4202497 // 37756929
spriteflags SHELLYCROUCH 4202497

state shellybotpainsounds

	ifinwater break

	ifstrength SHELLYBIGHURT
	{
		ifactorsound THISACTOR SHELLYPAIN1 break
		ifactorsound THISACTOR SHELLYPAIN4 break
		ifrnd 128 sound SHELLYPAIN1 else
		sound SHELLYPAIN4
	}
	else
	ifstrength SHELLYHURT
	{
		ifactorsound THISACTOR SHELLYPAIN2 break
		ifactorsound THISACTOR SHELLYPAIN3 break
		ifrnd 128 sound SHELLYPAIN2 else
		sound SHELLYPAIN3
	}
ends

defstate healmybot
	set temp healthbuff
	sub temp sprite[].extra
	// temp is now amount that needs to be healed

	ifg player[].firstaid_amount 0
	{
		set tempb player[].firstaid_amount
		mul tempb 5
		
		ifge tempb temp
		{
			sub tempb temp
			div tempb 5
			setp[].firstaid_amount tempb
			seta[].extra healthbuff
		}
		else
		{
			sub temp tempb
			setp[].firstaid_amount 0
			set tempc healthbuff
			sub tempc temp
			seta[].extra tempc
		}
		ifactor SHELLY sound B_USEMEDKIT else
		ifactor SHELLYCROUCH sound B_USEMEDKIT else
		ifactor DUKEBOT sound DUKE_USEMEDKIT else
		ifactor DUKEBOTCROUCH sound DUKE_USEMEDKIT else
		ifactor WESBOT sound WESNICE else
		ifactor WESBOTCROUCH sound WESNICE
	}
	else
	ifg sprite[player[].i].extra MAXPLAYERHEALTH // healthbuff
	{
		set tempb sprite[player[].i].extra
		sub tempb MAXPLAYERHEALTH // healthbuff
		// tempb is amount available for healing
		mul tempb 5
		
		ifge tempb temp
		{
			sub tempb temp
			div tempb 5
			add tempb MAXPLAYERHEALTH // healthbuff
			seta[player[].i].extra tempb
			seta[].extra healthbuff
		}
		else
		{
			sub temp tempb
			seta[player[].i].extra MAXPLAYERHEALTH // healthbuff
			
			set tempc sprite[].extra
			add tempc temp
			seta[].extra tempc
		}
		ifactor SHELLY sound B_USEMEDKIT else
		ifactor SHELLYCROUCH sound B_USEMEDKIT else
		ifactor DUKEBOT sound DUKE_USEMEDKIT else
		ifactor DUKEBOTCROUCH sound DUKE_USEMEDKIT else
		ifactor WESBOT sound WESNICE else
		ifactor WESBOTCROUCH sound WESNICE
	}
	else quote 1136
ends

// state botpalchange

// ifspritepal 0 spritepal 3 else
// ifspritepal 3 spritepal 10 else
// ifspritepal 10 spritepal 11 else
// ifspritepal 11 spritepal 12 else
// ifspritepal 11 spritepal 12 else
// ifspritepal 12 spritepal 13 else
// ifspritepal 13 spritepal 14 else
// ifspritepal 14 spritepal 15 else
// ifspritepal 15 spritepal 16 else
// ifspritepal 16 spritepal 21 else
// ifspritepal 21 spritepal 22 else
// ifspritepal 22 spritepal 49 else
// ifspritepal 49 spritepal 17 else
// ifspritepal 17 spritepal 19 else
// ifspritepal 19 spritepal 43 else
// ifspritepal 43 spritepal 42 else
// ifspritepal 42 spritepal 35 else
// ifspritepal 35 spritepal 36 else
// ifspritepal 36 spritepal 3

// geta[].pal botpal

// ends

action ASHELLYSTAND			0  1  5  1  10
action ASHELLYWALK			5  4  5  1  14
action ASHELLYCROUCH		0  1  5  1  10
// all actions below this point include the gun

action ASHELLYSTANDGUN		30 1  5  1  10
action ASHELLYRUN			35 4  5  1  10
action ASHELLYRUNBACK		50 4  5  -1 10
action ASHELLYKICK1			55 1  5  1  15
action ASHELLYKICK2			60 1  5  1  25
action ASHELLYKICK3			55 1  5  1  15
action ASHELLYJUMP1			65 1  5  1  18
action ASHELLYJETPACK		65 1  5  1  20
action ASHELLYJUMP2			70 1  5  1  18
action ASHELLYFALL			70 1  5  1  20
action ASHELLYCROUCHGUN		50 1  5  1  10 
// from crouching actor
action ASHELLYCRAWL			55 4  5  1  18
action ASHELLYSWIM		   100 4  5  1  13
action ASHELLYFLINTCH	   120 1  1  1  10
action ASHELLYDIE		   120 6  1  1  18
action ASHELLYDEAD		   125 1  1  1  10
action ASHELLYDOWN		   114 2  5  1  50

// lastang is the angle she should face
// angvel is the angle she should move in

move BOTNOMOVE
move SHELLYVELS // placeholder
move SHELLYJUMPVELS 300
move SHELLYJUMPCTF 300 -108
move SHELLYWANDERVELS 256
move SHELLYSHRUNKVELS 38

ai AISHELLYFLINTCH ASHELLYFLINTCH BOTNOMOVE geth
ai AISHELLYNOFIGHT ASHELLYSTAND SHELLYVELS geth
ai AISHELLYRUN ASHELLYRUN SHELLYVELS geth
ai AISHELLYWANDER ASHELLYRUN SHELLYVELS geth
ai AISHELLYSTAND ASHELLYSTANDGUN SHELLYVELS geth
ai AISHELLYCROUCH ASHELLYCROUCHGUN SHELLYVELS geth
ai AISHELLYCRAWL ASHELLYCRAWL SHELLYVELS geth
ai AISHELLYJUMP ASHELLYJUMP1 SHELLYJUMPVELS jumptoplayer
ai AISHELLYDIE ASHELLYDIE SHELLYVELS geth
ai AISHELLYJETPACK ASHELLYJETPACK SHELLYVELS geth
ai AISHELLYSWIM ASHELLYSWIM SHELLYVELS geth
ai AISHELLYKICK ASHELLYKICK1 SHELLYVELS geth
ai AISHELLYSHRUNK ASHELLYRUN SHELLYSHRUNKVELS fleeenemy
ai AISHELLYFROZEN ASHELLYSTANDGUN SHELLYVELS geth
ai AISHELLYDOWN ASHELLYDOWN SHELLYVELS geth

state shellyreviveplayer
	ifai AISHELLYRUN { cactor SHELLYCROUCH ai AISHELLYCROUCH }
	ifai AISHELLYSTAND { cactor SHELLYCROUCH ai AISHELLYCROUCH }
	set bottarget -1
	set spriteid player[].i
	state facesprite
	addphealth 1
	// ifg sprite[player[].i].extra 50 addphealth 1
	addstrength -1
	ife sprite[].extra 30 setav[spriteid].ikicked -96
	ifstrength 1 strength 1
	ife sprite[player[].i].extra 10
	{
		rand temp 2
		ife temp 0 globalsound B_TRYNOTODIE
		ife temp 1 globalsound B_HANGINTHERE
		ife temp 2 globalsound B_TRUSTME		
	}
	palfrom 16 0 10 63
	espawn CAPTUREPLUS
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	seta[RETURN].z sprite[player[].i].z
	seta[RETURN].pal 1
	rand x 512
	sub x 256
	add x sprite[RETURN].x
	seta[RETURN].x x
	rand y 512
	sub y 256
	add y sprite[RETURN].y
	seta[RETURN].y y
ends

state shellyflintch

	ifcount 8
	ife stun 0
	{
		cactor SHELLY
		ai AISHELLYRUN
	}

ends

state shellybotweap

	ifl float 0 break // reloading SMG
	set TMP_A thirdbaseval // save the current value
	
	ifn target -1
	ifl xydist 2048
	{
		ifrnd 128 set thirdbaseval 9 else
		set thirdbaseval 3
	}
	else
	{
		rand temp 9
		switch temp
		case 0 case 1 
			set thirdbaseval 3 // SMG
			findnearactor ROLLYTURRET 10240 spriteid
			ife spriteid -1 set thirdbaseval 6
			
		break
		
		case 2 case 3 case 4
			set thirdbaseval 3 // SMG
		break
		
		case 5 case 6 case 7
			set thirdbaseval 9 // incinerator
		break
		
		case 8 case 9
			set thirdbaseval 4 // rpg
		break
		endswitch
	}
	
	ifn bottarget -1 ife sprite[bottarget].pal 1 set thirdbaseval 3
	
	ifn bottarget -1 ife sprite[bottarget].picnum GREENSLIME set thirdbaseval 3
	
	switch thirdbaseval
	case 3
		set botclip 30
	break
	case 4
		rand botclip 2 add botclip 1
	break
	case 6
		set botclip 2
	break
	case 9
		rand botclip 10 add botclip 10
	break
	endswitch
	
	ife TMP_A thirdbaseval set float 0 else
	set float -10

ends

state checkorders

	switch cmode
		case 1 // awaiting command
		break
		case 2 // player pressed forward
			ifhitspace
			{
				quote 141
				ifinwater nullop else
				sound B_GOTIT
				set countvarc 0
				set monstatus 70
				set countvarb 0
				seta[].htactorstayput sprite[].sectnum 
				set cmode 0
			}
		break
		case 3 // player pressed left
			ifhitspace
			{
				ifl sprite[].extra healthbuff
				{
					state healmybot
				}
				else quote 1135
				
				// ifvarand altcostume 1
				// {
					// xorvar altcostume 1 
					// orvar altcostume 4 // gladiator
				// }
				// else
				// ifvarand altcostume 4
				// {
					// xorvar altcostume 4
					// orvar altcostume 32 // bikini
				// }
				// else
				// ifvarand altcostume 32
				// {
					// xorvar altcostume 32
				// }
				// else orvar altcostume 1 // jeans
				
				set cmode 0
			}
		break
		case 4 // player pressed right
			ifhitspace
			{
				ifn player[].cursectnum -1
				ifn sprite[myshelly].sectnum -1
				ife shellysquish NO
				ifp palive
				ifg sprite[myshelly].xrepeat 22
				ifg sprite[player[].i].xrepeat 32
				{
					set x sprite[myshelly].x
					set y sprite[myshelly].y
					set z sprite[myshelly].z
					set angvar sprite[myshelly].ang
					set mysector sprite[myshelly].sectnum
					
					setav[myshelly].monstatus 60 // trigger to switch characters
					
					getp[].posz zdist add zdist 8192
					ife switchmode 0
					{
						setsprite myshelly player[].posx player[].posy z
						changespritesect myshelly player[].cursectnum
						setsprite player[].i x y z
						changespritesect player[].i mysector
						setp[].posx x
						setp[].posy y
						sub z 8192
						setp[].posz z
						setp[].ang angvar
					}
					
					globalsound SHELLYREADY set pchar 1 
					ifge shellycharge FULLBOTCHARGE
					{
						set shellycharge 0
						set switchboost SWITCHBOOSTTIME
					}
					
				}
				set cmode 0
			}
		break
	endswitch
	
	ifn cmode 0 
	{ 
		ife cmode -1 set cmode 0 
		ifpdistg 4096 set cmode 0
		// ifp pfacing nullop else set cmode 0
		ife pdown YES set cmode 0
		break 
	}

	add countvarb 1

	ife monstatus 70
	{
	
		ife pdown YES
		{
			set monstatus 30
			seta[].htactorstayput -1
			break
		}
		
		iffloordistl 32
		ifpdistl 1280
		ifp pfacing
		ifp palive
		ifg countvarb 30
		ife seeplayer YES
		{
			ifl player[].fta 30
			quote 143
			ifhitspace
			{
				ifinwater nullop else
				{
					ifrnd 128 sound SHELLYREADY
					else
					sound B_LETSROCK
				}
				set monstatus 30
				seta[].htactorstayput -1
				quote 145
				set countvarb 0
			}
		}
		else ife player[].ftq 143 setp[].fta 0
		break
	}
	else seta[].htactorstayput -1
	
	ifn vendor_screen 0 break
	
	ife subtitle_time 0
	ife pdown NO
	ife monstatus 30
	ifpdistl 1280
	ifp pfacing
	ifp palive
	ifg countvarb 30
	ifn gametype CTF ifn gametype CONTROL
	ife seeplayer YES
	{
		ifinwater ifl sprite[player[].i].z sprite[].z nullop else
		{
			ifl player[].fta 60
			quote 139
			ifhitspace
			{
				ifl player[].fta 60
				{ quote 139 setp[].fta 88 }
				ifhitspace	
				ife player[].newowner -1
				ifl player[].fta 90
				set cmode 1
			}
		}
	}
	else ife player[].ftq 139 setp[].fta 0
ends

state resetshellymultiplayer
	cactor SHELLY
	set monstatus 30
	// strength SHELLYSTRENGTH
	seta[].extra healthbuff
	ai AISHELLYSTAND
	ife gametype DM
	ifg pstarts 0
	{
		set tempb pstarts
		sub tempb 1
		rand temp tempb
		set mysector 0
		updatesectorz loadx[temp] loady[temp] loadz[temp] mysector
		ifn temp -1
		{
			seta[].x loadx[temp]
			seta[].y loady[temp]
			seta[].z loadz[temp]
			seta[].sectnum mysector
		}
	}
	else
	setsprite THISACTOR loadx[LEVEL] loady[LEVEL] loadz[LEVEL]
	set bottarget -1
	set navpoint -1
ends

state shellyshrunkstate

  ifcount SHRUNKDONECOUNT
  {
    seta[].htextra -1
    ai AISHELLYSTAND
	cactor SHELLY
	ifl sprite[].extra 1 strength 1
  }
  else
    ifcount SHRUNKCOUNT
	{
      sizeto 24 22
	  ifgapzl 48 
	  {
		cactor SHELLYCROUCH 
		action ASHELLYCROUCH	
	  }
	}
  else
    state genericshrunkcode

ends

state shellyfrozenstate

	ife ikicked -2 count 1000
	ifcount THAWTIME
    {
      ai AISHELLYSTAND
	  ife team 1
	  set monstatus 30
	  else set monstatus 1
	  set ikicked 0
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 30
	  // state lite_jibs
      sound GLASS_BREAKING
      // killit
	  cactor SHELLYCROUCH
	  ai AISHELLYDOWN
	  quote 132
	  set countvarc 0
	  strength 5
	  getlastpal
    }

ends


state moveshelly

	ifmove BOTNOMOVE break
	ifn padmove 0 { set angvel padang break }
	// ifg gametype 0 set lastangvel angvel
	getincangle temp lastangvel angvel
	abs temp
	shiftr temp 4
	add SPRITELOTAG temp
	sub SPRITELOTAG 8
	
	ifg SPRITELOTAG 128 set SPRITELOTAG 128
	ifl SPRITELOTAG 56 set SPRITELOTAG 56
	
	// ifg ptargeted 0 
	// ife pdown NO ife bottarget -1 
	// ifpdistl 1280 
	// add angvel 256
	
	ife pdown NO ife bottarget -1 
	ifpdistl 2560
	ifpdistg 844
	ife seeplayer YES
	ifn angvel player[].ang
	ifn player[].cursectnum -1
	ife sector[player[].cursectnum].lotag 0
	ife mtype 0
	{
		set B YES
		headspritesect spriteid player[].cursectnum
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum SECTOREFFECTOR
			ife sprite[spriteid].lotag 14
				set B NO
			nextspritesect spriteid spriteid
		}
		ife B YES
		{
			geta[].x x2
			geta[].y y2
			sub x2 player[].posx
			sub y2 player[].posy
			getangle angvar x2 y2
			
			getincangle tempb angvar player[].ang
			ifge tempb 0 add angvel 256 else sub angvel 256
		}
	}
	
	// hack to force bot to go towards player when down in matches
	ifg gametype 0
	ife pdown YES
	ifai AISHELLYRUN
	ifpdistl 5120
	ife seeplayer YES
	{
		geta[player[].i].x x2
		geta[player[].i].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvel x2 y2
	}
	
	cos xvel angvel
	sin tempb angvel
	// shiftr xvel 6
	// shiftr tempb 6
	div xvel SPRITELOTAG
	div tempb SPRITELOTAG
	ifaction ASHELLYCRAWL { shiftr xvel 1 shiftr tempb 1 }
	geta[].ang TMP_A
	movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
	seta[].ang TMP_A
	ifn mtype 0 set dodgetime 0
	
	set temp mtype
	ifn temp 0
	// ifg navpoint -1
	{
		// bumping something
		sub temp 49152
		ifl temp 16384 ifg temp -1 // bumping a sprite
		{
			set B NO
			ife actorvar[temp].monstatus 1
			ife actorvar[temp].team team set B YES
			ifg gametype 0 ifvarand sprite[temp].cstat 16 set B YES
			ife B YES
			{
				set x2 sprite[].x
				add x2 256
				rotatepoint sprite[].x sprite[].y x2 sprite[].y sprite[].ang x y
				setsprite THISACTOR x y sprite[].z
				seta[].htmovflag 0
			}
		}
	}

	set lastangvel angvel
	
ends

state startwander

	ifaction ASHELLYFALL break
	
	set B 0
	set xydist 2048
	whilevarn B 32
	{
		add B 1
		geta[].z z
		sub z 4096
		rand angvar 2047
		cos mycos angvar
		sin mysin angvar
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz CLIPMASK1
		
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		
		ifg startx xydist set B 32
		ife B 32 set angvel angvar
		
	}
	cactor SHELLY
	ai AISHELLYWANDER
	// ifgapzl 48 { cactor SHELLYCROUCH action ASHELLYCRAWL }
	ifl gametype 1
	{
		ifl countvarc 150
		set crumbwait 30
	}
	else { set crumbwait 6 set dodgetime 6 }

ends

state shellywander

	ifg gametype 0
	{
		state moveshelly
		ife crumbwait 0
		{
			cactor SHELLY
			ai AISHELLYRUN
		}
		break
	}
	
	ife seeplayer YES { cactor SHELLY ai AISHELLYRUN }
	
	ifai AISHELLYRUN break
	state moveshelly
	ifl crumbwait 1 sub crumbwait 1
	
	ifl crumbwait 0 
	{
	    ifpdistl 1280 { cactor SHELLY ai AISHELLYSTAND set SPRITELOTAG 128 break }
		ifrnd 2 state startwander else
		ifn mtype 0 { set navpoint -1 ifn gametype -1 state startwander }
	}
	

ends

state shellyhitscan

	
	geta[].sectnum mysector
	ife mysector -1 break
	getceilzofslope mysector sprite[].x sprite[].y z
	sub z sprite[].z
	ifg z -16384 break
	
	ifai AISHELLYCRAWL nullop else
	ifai AISHELLYRUN nullop else break

	// we want to scan at waist heigh in the direction angvel
	// then another scan much higher if there is an obstacle

	geta[].z z
	sub z 4096
	cos mycos angvel
	sin mysin angvel
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz CLIPMASK1
	
	set startx sprite[].x
	sub startx hitx
	mul startx startx
	set y2 sprite[].y
	sub y2 hity
	mul y2 y2
	add startx y2
	sqrt startx startx

	set temp NO
	ifl startx JUMPDIST set temp YES
	ifn mtype 0 { set startx 256 set temp YES }
	ifn hitsprite -1 
	{
		ife actorvar[hitsprite].monstatus 1 set temp NO
		ife sprite[hitsprite].picnum APLAYER set temp NO
	}
	ife pdown YES ifpdistl 644 set temp NO
	// iffloordistl 4
	ife sprite[].zvel 0
	ife temp YES
	{
		sub z 18432
		hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 tempd hitwall hitsprite hitx hity hitz CLIPMASK1
		
		set starty sprite[].x
		sub starty hitx
		mul starty starty
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add starty y2
		sqrt starty starty
		ifg starty startx // new scan goes farther
		{
			sub starty startx
			ifg starty 256 // enough space to stand on
			{
				cactor SHELLY
				ai AISHELLYJUMP
				ifg gametype 0 move SHELLYJUMPCTF geth
			}
		}
	}
	else ifg startx 384 ifl startx 1280 
	ife gametype 0
	ifn player[].cursectnum sprite[].sectnum ifn player[].cursectnum -1
	ifpdistg 1560 operate

ends

state findplayercrumb

	ife monstatus 70 break // ordered to wait for player
	ife seeplayer YES { set shotpitch 0 set countvarc 0 }
	else 
	{ 
		add shotpitch 1 add countvarc 1 
		ifg ptargeted 0 { add shotpitch 1 add countvarc 2 }
	}
	
	ifg shotpitch 120
	{
		set savedvalue -1
		set xydist2 0
		headspritestat spriteid 999
		ife spriteid -1
		{
			setsprite THISACTOR player[].posx player[].posy player[].posz
			cactor SHELLY
			ai AISHELLYRUN
			set shotpitch 0
		}
		else
		{
			whilevarn spriteid -1
			{
				canseespr spriteid player[].i temp
				
				ifg countvarc 180
				{
					ife temp YES set savedvalue spriteid
				}
				else
				{
					ife bottarget -1
					ife temp NO set savedvalue spriteid
				}
				
				
				nextspritestat spriteid spriteid
				ifn savedvalue -1 set spriteid -1
			}
			ifn savedvalue -1
			{
				setsprite THISACTOR sprite[savedvalue].x sprite[savedvalue].y sprite[savedvalue].z
				cactor SHELLY
				ai AISHELLYRUN
				ifp phigher ai AISHELLYJETPACK
				set shotpitch 0
			}
		}
	}

	ifg crumbwait 0 { sub crumbwait 1 break }
	

	ife seeplayer YES ifawayfromwall set xydist2 4096
	else set xydist2 2048

	set B -1 // reset crumb and search from scratch
	// linked list of sprites already in order from most recent to oldest
	headspritestat spriteid 999
	whilevarn spriteid -1
	{
		ldist xydist THISACTOR spriteid
		ifg xydist 384 ifl xydist xydist2
		ifge actorvar[spriteid].mtype lastcrumb
		{
			canseespr THISACTOR spriteid temp
			ife temp YES
			{ set B spriteid getav[B].mtype lastcrumb }
		}
		
		nextspritestat spriteid spriteid
		ifn B -1 set spriteid -1
	}

	ife B -1 // longer distance check
	{
		mul xydist2 2
		headspritestat spriteid 999
		whilevarn spriteid -1
		{
			ldist xydist THISACTOR spriteid
			ifg xydist 384 ifl xydist xydist2 
			ifge actorvar[spriteid].mtype lastcrumb
			{
				canseespr THISACTOR spriteid temp
				ife temp YES
				{ set B spriteid getav[B].mtype lastcrumb }
			}
			nextspritestat spriteid spriteid
			ifn B -1 set spriteid -1
		}
	}
	
	ife B -1
	ife seeplayer YES
	getp[].i B

	ifn B -1 // face crumb
	{
		// al lastcrumb
		// al B
		geta[B].x x
		geta[B].y y
		sub x sprite[].x
		sub y sprite[].y
		getangle angvel x y
		set droptile B
		ifai AISHELLYWANDER { cactor SHELLY ai AISHELLYRUN }
		
		ifai AISHELLYJETPACK nullop else
		ifn mtype 0 // bumping into something
		{
			set temp mtype
			add temp 16384
			ife temp player[].i break
				
			set crumbwait 20
			geta[].z z
			geta[B].z temp
			sub z temp
			ifg z 10240 // jump trigger
			{
				ifinwater nullop else
				ifai AISHELLYJUMP
				{
					ifaction ASHELLYJUMP2
					{
						cactor SHELLY
						ai AISHELLYJETPACK
						sound DUKE_JETPACK_ON
					}
				}
				else
				iffloordistl 4
				{
					cactor SHELLY
					ai AISHELLYJUMP
					ifg gametype 0 move SHELLYJUMPCTF geth
				}
			}
		}
	}
	else 
	{
		set droptile -1
		ifg shotpitch 90
		{
			ifai AISHELLYWANDER nullop else state startwander
		}
		
	}
	

ends

state shellyjetpack

	set FEMFALLDMG 0
	ifactorsound THISACTOR DUKE_JETPACK_IDLE nullop else sound DUKE_JETPACK_IDLE
	
	getincangle temp lastangvel angvel
	ifl temp 0 mul temp -1
	shiftr temp 4
	add SPRITELOTAG temp
	sub SPRITELOTAG 8
	
	ifg SPRITELOTAG 160 set SPRITELOTAG 160
	ifl SPRITELOTAG 80 set SPRITELOTAG 80
	
	cos xvel angvel
	sin tempb angvel

	div xvel SPRITELOTAG
	div tempb SPRITELOTAG
	set targethigher NO
	ifl gametype 1 { getp[].posz zdist ifp phigher set targethigher YES } else
	{
		ifg navpoint -1 geta[navpoint].z zdist
		else
		ifn bottarget -1 { geta[bottarget].z zdist sub zdist 8192 }
		ifl zdist sprite[].z set targethigher YES
	}
	sub zdist 2560
	
	sub zdist sprite[].z
	ifn mtype 0 
	{
		ife targethigher YES sub zdist 1560 else 
		iffloordistl 512
		{
			stopactorsound THISACTOR DUKE_JETPACK_IDLE
			sound DUKE_JETPACK_OFF
			ai AISHELLYSTAND
		}
	}
	
	ifl zdist -2560 set zdist -2560
	ifg zdist 2560 set zdist 2560
	ifceilingdistl 24 ifl zdist 0 set zdist 0
	
	// add adjustments for gapz and ceilingdist
	set dodgetime 0
	
	ife targethigher NO
	ifn droptile -1 // most recent crumb followed
	{
		geta[droptile].z z
		add z 4096
		ifl sprite[].z z add zdist 2048
		iffloordistl 64 nullop else
		{
			sub z 6144
			ifl z sprite[].z sub zdist 1560
		}
	}
	
	ifp pjetpack ifpdistl 1280 nullop else
	movesprite THISACTOR xvel tempb zdist 16385 mtype
	
	
	set lastangvel angvel
	
	ife targethigher NO
	ifpdistl 2048
	ife sprite[].sectnum player[].cursectnum
	ifp ponground
	{
		stopactorsound THISACTOR DUKE_JETPACK_IDLE
		sound DUKE_JETPACK_OFF
		ai AISHELLYSTAND
	}
	
	ifcount 30 iffloordistl 8
	{
		stopactorsound THISACTOR DUKE_JETPACK_IDLE
		sound DUKE_JETPACK_OFF
		ai AISHELLYSTAND
	}
	ifvarand player[].player_par 1
	{
		geta[].z z
		sub z 6144
		seta[].z z
		espawn BIGSMOKE
		seta[RETURN].cstat 514
		add z 6144
		seta[].z z
	}
	
	ifonwater nullop else
	ifg gametype 0
	ifrnd 8
	{
		stopactorsound THISACTOR DUKE_JETPACK_IDLE
		sound DUKE_JETPACK_OFF
		ai AISHELLYSTAND
	}
	
ends

state shellyswimstate

	set FEMFALLDMG 0
	set mysector sprite[].sectnum
	updatesectorz sprite[].x sprite[].y sprite[].z mysector
	ifn sprite[].sectnum mysector ifn mysector -1 { changespritesect THISACTOR mysector }
	
	set B YES
	ife pdown NO ife player[].cursectnum sprite[].sectnum ifpdistl 2048 set B NO
	ife B YES
	{
		getincangle temp lastangvel angvel
		ifl temp 0 mul temp -1
		shiftr temp 4
		add SPRITELOTAG temp
		sub SPRITELOTAG 8
		
		ifg SPRITELOTAG 160 set SPRITELOTAG 160
		ifl SPRITELOTAG 80 set SPRITELOTAG 80
		
		cos xvel angvel
		sin tempb angvel

		div xvel SPRITELOTAG
		div tempb SPRITELOTAG
		
		ifn droptile -1 // most recent crumb followed
			geta[droptile].z zdist
		else
		getp[].posz zdist
		add zdist 8192
		sub zdist sprite[].z
		
		ifl zdist -2560 set zdist -2560
		ifg zdist 2560 set zdist 2560
		ifceilingdistl 12 ifl zdist 0 set zdist 0
		iffloordistl 12 ifg zdist 0 set zdist 0
		
		set dodgetime 0
		ifpdistg 512
		movesprite THISACTOR xvel tempb zdist CLIPMASK0 mtype
		set lastangvel angvel
		
		ife pdown YES 
		ifge sprite[].extra 30
		ife team 1
		{
			ldist temp THISACTOR player[].i
			ifl temp 768
			ifpdistl 1560
			state shellyreviveplayer
		}
	}
	ifrnd 12 spawn WATERBUBBLE
	
	getplayer[].cursectnum mysector
		ifn mysector -1
		gets[mysector].lotag tempc
		else set tempc 0
	set tempd 0 // to be used as flag during loop below
	ifn tempc 2 // player not in water!
	{
		geta[].sectnum mysector
		ifn mysector -1
		{
			headspritesect spriteid mysector
			whilevarn spriteid -1
			{
				ife sprite[spriteid].picnum SECTOREFFECTOR
				ife sprite[spriteid].lotag 7
				{
					geta[spriteid].statnum tempb
					geta[spriteid].hitag B // B is shared by the SE in the sector it goes to
					headspritestat temp 9
					whilevarn temp -1
					{
						ife sprite[temp].lotag 7
						ifn spriteid temp
						ife sprite[temp].hitag B // the matching water SE
						ife sprite[temp].sectnum player[].cursectnum
						{
							geta[spriteid].x x
							sub x sprite[temp].x
							geta[spriteid].y y
							sub y sprite[temp].y
							changespritesect THISACTOR sprite[temp].sectnum
							geta[].x x2
							sub x2 x
							seta[].x x2
							geta[].y y2
							sub y2 y
							seta[].y y2
							seta[].z player[].posz
							set tempd YES
						}
						nextspritestat temp temp
						ife tempd YES set temp -1
					}
				}
				nextspritesect spriteid spriteid
				ife tempd YES set spriteid -1
			}
		}
	}

ends

state jetpackcheck

	ifp pjumping break
	ifp pfalling break
	ifceilingdistl 64 break

	getp[].posz z
	add z 32768 // 24576
	ifl z sprite[].z // player way above you
	// ifn sprite[].sectnum player[].cursectnum 
	ife player[].poszv 0
	ife seeplayer YES
	{
		cactor SHELLY
		ai AISHELLYJETPACK
		sound DUKE_JETPACK_ON
	}
	else
	ifp phigher
	ifp pjetpack
	ifpdistg 1560
	{
		cactor SHELLY
		ai AISHELLYJETPACK
		sound DUKE_JETPACK_ON
	}
	else
	ifg FEMFALLDMG 39
	iffloordistl 64
	{
		ifonwater nullop else
		{
			cactor SHELLY
			ai AISHELLYJETPACK
			sound DUKE_JETPACK_ON
		}
	}
ends

defstate startdodge

	state incoming_eval
	ife actorvar[myspawner].monstatus 1
	{
		ifl x xydist sub angvel 256 else
		add angvel 256
	}
	ife dodgetime 0
	ife sprite[].htextra -1 ife sprite[myspawner].statnum 4 ifrnd 32 soundonce B_NOPE
	ife team 0 set dodgetime 15 else
	set dodgetime 12
	set crumbwait 10
	
	set myspawner -1
	cactor SHELLY
	ai AISHELLYRUN
	
		
ends

state followplayer

	set B NO
	ife pdown YES
	ifpdistl 1560 // 768
	ife team 1
	{
		ifge sprite[].extra 30
		{
			ldist temp THISACTOR player[].i
			ifl temp 768
			{
				ifonwater set B YES
				ifinwater set B YES
				ifai AISHELLYSTAND set B YES
				ifai AISHELLYCROUCH set B YES
				ifai AISHELLYRUN set B YES
				ifn dodgetime 0 set B NO
				ife seeplayer NO
				{
					ifonwater nullop else set B NO
					ifinwater nullop else set B NO
				}
			}
		}
		else
		ifg player[].firstaid_amount 0
			state healmybot
		else
		ife bigmsgcount 0
		{
			set bigmsgcount 64
			set bigmsg 1159
		}
	}
	
	ife B YES
		state shellyreviveplayer
	

	// hack to make her leave standing position to kick an enemy if she is reloading
	ifgapzl 48 nullop else
	ifpdistl FOLLOWRANGE // MAXRANGE
	ifn bottarget -1
	ifn thirdbaseval 4 ifn thirdbaseval 9 // not using RPG or incinerator
	ife ikicked 0
	ife FEMKILLCOUNT 0
	ifn monstatus 70 // don't do this if ordered to wait for player
	iffloordistl 4
	ifg sprite[].extra SHELLYBIGHURT
	// don't run into bosses!
	ifl actorvar[bottarget].inithp 1000
	ifn sprite[bottarget].picnum GREENSLIME
	ifn sprite[bottarget].picnum ROTATEGUN
	ifl targetdist 4096 // 7168
	{
		ife avoid_melee YES
		ife actorvar[bottarget].shrunken 0
		{
			ifl float 0 
			{
				set myspawner bottarget
				state startdodge
			}
		}
		else
		{
			state moveshelly
			cactor SHELLY
			ifai AISHELLYRUN nullop else ai AISHELLYRUN
			set initflags bottarget
			break
		}
	}
	
	findplayer xydist
	ife monstatus 70 set xydist 64 // waiting for player, not following
	else ife team 1
	set monstatus 30 // failsafe
	else set monstatus 1
	
	ife player[].cursectnum -1 set xydist2 768
	else
	{
		ifg PSHRINKING 0 { set xydist 64 set xydist2 2048 } else
		ife pdown YES set xydist2 768 else
		ife bottarget -1 ifg ptargeted 0 set xydist2 768
		else
		ifn bottarget -1 ifg targetdist 4096 ifl targetdist 10240 set xydist2 FOLLOWRANGE
		else
		ife sprite[].sectnum player[].cursectnum set xydist2 FOLLOWRANGE else
		ifg sector[player[].cursectnum].lotag 2 set xydist2 768 else
		ifg sector[player[].cursectnum].hitag 0 set xydist2 768 else
		ife seeplayer YES 
		{
			ifp pfacing set xydist2 1280
			else
			set xydist2 3072 
		}
		else set xydist2 1560
	}
	
	ifg xydist xydist2
	{
		ifn FEMKILLCOUNT 0 ifai AISHELLYCROUCH ifg countvar 10 set countvar 9
		
		ifg countvar 10
		{
		
			ifai AISHELLYJETPACK nullop 
			else
				state jetpackcheck
			
			state moveshelly
			
			ifai AISHELLYSTAND ai AISHELLYRUN
			
			ifai AISHELLYRUN
			ifn mtype 0 ife gametype 0
			{
				set temp player[].player_par
				mod temp 15
				ife temp 0
				{
					cactor SHELLY
					ai AISHELLYJUMP
					move SHELLYJUMPCTF geth
				}
			}
		
			ifai AISHELLYCROUCH ai AISHELLYCRAWL
			
			ifai AISHELLYCRAWL
			{
				ifgapzl 48 nullop else
				{
					cactor SHELLY
					ai AISHELLYRUN
				}
			}
		}
		add countvar 1
	}
	else
	{
		ifp pfacing
		ifpdistl 1280
		ifg forwinput 2
		ife pdown NO
		{
			resetcount
			set angvel player[].ang
			state moveshelly
			
			ifaction ASHELLYFALL break
			
			ifai AISHELLYSTAND ai AISHELLYRUN
			
			
			ifai AISHELLYCROUCH ai AISHELLYCRAWL
			
			ifai AISHELLYCRAWL
			ife FEMKILLCOUNT 0
			{
				ifgapzl 48 nullop else
				{
					cactor SHELLY
					ai AISHELLYRUN
				}
			}
			break
		}
		set countvar 0
		
		ifaction ASHELLYFALL break
		
		ifai AISHELLYRUN 
		{
			ifonwater
			{
				cactor SHELLY
				ai AISHELLYSTAND
			}
			else
			ifrnd 128
			{
				cactor SHELLY
				ai AISHELLYSTAND
			}	
			else
			{
				cactor SHELLYCROUCH
				ai AISHELLYCROUCH
			}
			set SPRITELOTAG 128 
		}
		ifai AISHELLYCRAWL ai AISHELLYCROUCH
	}
ends

state shellykickcode

	ifn bottarget -1
	{
		geta[bottarget].x x2
		geta[bottarget].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvel x2 y2

		dist xydist THISACTOR bottarget
		
		ifg xydist 512
		{
			cos xvel angvel
			sin tempb angvel
			shiftr xvel 7
			shiftr tempb 7
			movesprite THISACTOR xvel tempb 0 CLIPMASK0 mtype
		}
	}

	set myspawner -1
	ifaction ASHELLYKICK1 ifactioncount 1
	{
		action ASHELLYKICK2
		ifn bottarget -1
		{
			ifl xydist 1024
			{
				sound KICKIMPACT
				seta[bottarget].htowner THISACTOR
				seta[bottarget].htang sprite[].ang
				seta[bottarget].htextra KNEE_WEAPON_STRENGTH
				seta[bottarget].htpicnum KNEE
				set ikicked 150
				set initsprite -1
			}
		}
	}
	ifaction ASHELLYKICK2 ifactioncount 1
	{
		action ASHELLYKICK3
	}
	ifaction ASHELLYKICK3 ifactioncount 1
	{
		ai AISHELLYSTAND
	}
	

ends

defstate botblocking
	ifinwater
	{
		geta[].cstat temp
		ifvarand temp 1
		{
			xorvar temp 1
			seta[].cstat temp
		}
	}
	else
	ifpdistl 1024
	{
		geta[].cstat temp
		ifvarand temp 1
		{
			xorvar temp 1
			seta[].cstat temp
		}
	}
	else
		cstator 1
ends

state shellyfallhandling

	ifai AISHELLYJETPACK break
	ifai AISHELLYSWIM break

	ifg sprite[].zvel 0
		add FEMFALLDMG 2
		
	ifg FEMFALLDMG 6
	{
		ifaction ASHELLYSTAND action ASHELLYFALL else
		ifaction ASHELLYWALK action ASHELLYFALL else
		ifaction ASHELLYSTANDGUN action ASHELLYFALL else
		ifaction ASHELLYRUN action ASHELLYFALL else
		ifactor SHELLYCROUCH { cactor SHELLY ai AISHELLYSTAND action ASHELLYFALL }
		// ifaction ASHELLYCROUCHGUN { cactor SHELLY ai AISHELLYSTAND action ASHELLYFALL }
	}
	
	ife globalnofall YES ifg FEMFALLDMG 8 set FEMFALLDMG 8
		
	iffloordistl 4
	{
		ifonwater set FEMFALLDMG 0
		ifinwater set FEMFALLDMG 0
		ifg FEMFALLDMG 0
		{
			sub FEMFALLDMG 40
			ifg FEMFALLDMG 0
			{
				state shellybotpainsounds
				// seta[].htextra FEMFALLDMG
				// seta[].htpicnum SHOTSPARK1
				// seta[].htowner THISACTOR
				// ifg FEMFALLDMG 15 { sound SQUISHED guts JIBS6 4 }
				ifonwater
				{
					cactor SHELLY
					ai AISHELLYSTAND
				}
				cactor SHELLYCROUCH	
				ai AISHELLYCROUCH
			}
			set FEMFALLDMG 0
		}
		ifaction ASHELLYFALL
		{
			ifonwater
			{
				cactor SHELLY
				ai AISHELLYSTAND
				break
			}
			ifai AISHELLYSTAND { cactor SHELLYCROUCH ai AISHELLYCROUCH }
			ifai AISHELLYRUN action ASHELLYRUN
		}
	}


ends

state shellymultiplayer

	ifai AISHELLYSTAND
	ife gametype SURVIVAL ifl player[].player_par 150 break

	// hack to make her leave standing position to kick an enemy if she is reloading
	ifgapzl 48 nullop else
	ifn bottarget -1
	ifn redcarrier THISACTOR
	ifn bluecarrier THISACTOR
	ifn thirdbaseval 4 ifn thirdbaseval 9 // not using RPG or incinerator
	ife ikicked 0
	ife FEMKILLCOUNT 0
	ifn monstatus 70 // don't do this if ordered to wait for player
	iffloordistl 4
	// don't run into bosses!
	ifl actorvar[bottarget].inithp 1000
	ifn sprite[bottarget].picnum GREENSLIME
	ifn sprite[bottarget].picnum ROTATEGUN
	{
		dist xydist THISACTOR bottarget
		ifl xydist 4096 // 8192
		{
			set angvel initsprite
			set navpoint -1
			state moveshelly
			cactor SHELLY
			ifai AISHELLYRUN nullop else ai AISHELLYRUN
			break
		}
	}

	// ifai AISHELLYJETPACK nullop 
	// else
		// state jetpackcheck
	
	ifai AISHELLYJUMP nullop else
	state moveshelly
	
	ifai AISHELLYSTAND ai AISHELLYRUN
	
	ifai AISHELLYCROUCH ai AISHELLYCRAWL
	
	ifai AISHELLYCRAWL
	ife FEMKILLCOUNT 0
	{
		ifgapzl 48 nullop else
		{
			cactor SHELLY
			ai AISHELLYRUN
		}
	}

	ifn mtype 0 { set navpoint -1 state startwander }
	ifl navpoint 0 ife crumbwait 0 state startwander
	
	ife gametype SURVIVAL
	iffloordistl 8 ifonwater
	ifl navpoint 0
		setsprite THISACTOR loadx[LEVEL] loady[LEVEL] loadz[LEVEL]

ends

state bombshellcode

	ifg stun 0 sub stun 1
	seta[].mdflags 16
	
	ife pchar 1 ife myshelly THISACTOR
	ifn monstatus 60
	{
		set monstatus 60
		ifstrength 10 strength 10
	}
	
	ife monstatus 60
	{
		set monstatus 30
		ifvarand startmode 1
		{
			cactor DUKEBOT
			ai AIDUKESTAND
			sizeat 42 36
			cstat 257
			set navpoint -1
			set float -20
			set botclip 3
			set thirdbaseval 2
			seta[].pal dukepal
			break
		}
		else
		ifvarand startmode 4
		{
			cactor WESBOT
			ai AIWESBOTSTAND
			sizeat 22 20
			cstat 257
			set navpoint -1
			set botclip 3
			set thirdbaseval 2
			set float -20
			seta[].pal wespal
			break
		}
	}

	ifai AISHELLYJETPACK seta[].zvel 0 else
	ifai AISHELLYSWIM seta[].zvel 0 else
	fall
	ifai 0
	{
		orvar monstflags YES
		set SPRITELOTAG 128
		ifvarand monstflags 131072 
		{ 
			spritepal 17 clipdist 32 
			seta[].extra bluebaseval
		}
		else
		{
			seta[].pal shellypal
			clipdist 20
		}
		set botclip 30
		set thirdbaseval 3 // SMG
		set initflags -1
		cstator 257
		sizeat 23 21
		ifn gametype 0 { ifvarand altcostume 1 xorvar altcostume 1 orvar altcostume 4 }
		geta[].ang lastang
		
		
		ifvarand monstflags 131072 nullop else
		{
			ife gametype SURVIVAL { strength 200 set shellyhp 200 }
			else ife attmode YES { set shellyhp pdamage seta[].extra pdamage }
		}
		ifspawnedby SHELLYCHUTE
		{
			strength 150
			set shellyhp 150
			set monstatus 30
			set team 1
			ai AISHELLYSTAND
			sound SHELLYREADY
			getp[].max_actors_killed temp
			sub temp 1
			setp[].max_actors_killed temp
			set temp sprite[].htflags
			ifvarand temp 32 xorvar temp 32
			seta[].htflags temp
			break
		}
		ife team 1
		{
			ifl shellyhp 10 set shellyhp 10
			ifspawnedby APLAYER { seta[].extra shellyhp orvar startmode 2 }
		}
		
		ifvarand monstflags 131072 ai AISHELLYSTAND
		else
		ifcansee ifpdistl 4096
		{
			set monstatus 30
			set team 1
			ai AISHELLYSTAND
			ifg player[].player_par 60
				sound SHELLYREADY
			getp[].max_actors_killed temp
			sub temp 1
			setp[].max_actors_killed temp
		}
		else { seta[].extra shellyhp seta[].htextra -1 break }
		// ife VOLUME 6 spritepal 21
	}
	
	
	ife spawnprotect 0
	ife monstatus 30 ifspritepal 33 seta[].pal shellypal
	
	
	ife team 1 
	{
		ifcansee set seeplayer YES else set seeplayer NO
		set myshelly THISACTOR
		orvar charsel 2
		orvar startmode 2
		ife monstatus 1 set monstatus 30
		set mlevel plevel
		ifn monstatus 2 ifge sprite[].extra 30 set shellyinmap 10
		set inithp player[].max_player_health
		state checksectvisited
		state checkorders
		ifn myshelly -1 ife sprite[].htextra -1 ifg sprite[].extra 0 geta[].extra shellyhp
	}
	else state monsterai
	
	// ifn monstatus 2 ife mysignpost -1 state spawnmysignpost

	ifai AISHELLYSHRUNK { state shellyshrunkstate break }
	ifai AISHELLYFROZEN { state shellyfrozenstate break }
	ifai AISHELLYDIE
	{
		strength 0
		ifaction ASHELLYDIE
		{
			ifinwater fall // falls too slow under water otherwise
			ifactioncount 5
			{
				action ASHELLYDEAD
				ifhitweapon { }
			}
		}
		ifaction ASHELLYDEAD
		{
			// iffloordistl 4
			ife team 0 
			{
				ifwasweapon RADIUSEXPLOSION seta[].htpicnum RPG
				ifhitweapon
				{
				  sound SQUISHED
				  state standard_jibs
				  killit
				}
				break
			}
			ifactioncount 4
			{
				set B NO
				ifl gametype 1 set B YES
				ife gametype SURVIVAL set B YES
				ife B YES
				{
					cactor SHELLYCROUCH
					ai AISHELLYDOWN
					quote 132
					set countvarc 0
					strength 5
				}
				else ifcount 150
				{
					state resetshellymultiplayer
					break
				}
			}

		}
		break
	}
	ifai AISHELLYDOWN
	{
		set shellysquish NO
		set monstatus 2
		ifg gametype 0
		ifn gametype SURVIVAL
		{
			ifcount 120
			state resetshellymultiplayer
			break
		}
		set burning 0
		set shellycharge 0
		ifhitweapon 
		{
			ifstrength 5 strength 5
		}
		
		ife ikicked -2 // healed by medic
		{
			addstrength 4 // 5
			ifspritepal 33 getlastpal else spritepal 33
		}
		else
		{
			set TMP_A player[].player_par
			sub TMP_A ikicked
			
			set temp NO
			
			ife pdown NO
			
			ifpdistl 1280
			ifp pfacing
			ifp palive
			ife seeplayer YES
			ifhitspace
			set temp YES
			
			ifphealthl 100 nullop else
			ifvarand perks 1024
			ifge TMP_A 300 set temp YES
			
			ife temp NO
			ife pdown YES
			ifvarand perks 1024
			ifg player[].firstaid_amount 0
			state healmybot
			
			ife temp YES
			{
				ifvarand perks 1024
				ifge TMP_A 300 nullop else
				quote 136
				
				set temp NO
				ifhitspace set temp YES
				
				ifvarand perks 1024 ifge TMP_A 300 set temp YES
				
				ife temp YES
				{
					resetcount
					ifphealthl 5
					{
						quote 130
						ifspritepal 33 getlastpal
						
						cactor SHELLYCROUCH
						ai AISHELLYCROUCH
						sound SHELLYREADY
						set monstatus 30
						seta[].htextra -1 set burning 0 set bleeding 0
						set ikicked 0
						set shotpitch 0
						set mtype 0
						set bottarget -1
						seta[].htowner THISACTOR
						cstat 257
						ife player[].ftq 130 setp[].fta 0
						ife player[].ftq 129 setp[].fta 0
						seta[].pal shellypal
						ifstrength 10 strength 10
						set countvarc 0
						break
						
					}
					else
					{
						ifstrength 10
						ife pchar 0
						{
							rand temp 3
							ife temp 0 globalsound DUKE_HEAL1
							ife temp 1 globalsound DUKE_HEAL2
							ife temp 2 globalsound DUKE_HEAL3
							ife temp 3 globalsound DUKE_HEAL4
						}
						addstrength 5
						set countvarb 0
						ifspritepal 33 getlastpal else spritepal 33
						ifg player[].firstaid_amount 0
						{
							getp[].firstaid_amount temp
							sub temp 1
							setp[].firstaid_amount temp
						}
						else addphealth -1
					}
				}
				else ifspritepal 33 getlastpal
			}
			else 
			{
				ifcount 360
				ifp palive
				{
					ife player[].fta 0 quote 133
					ifhitspace add countvarc 1
					else set countvarc 0
					ifg countvarc 25
					{
						spawn TRANSPORTERSTAR
						globalsound TELEPORTER
						setsprite THISACTOR player[].posx player[].posy player[].posz
						ifn player[].cursectnum -1 changespritesect THISACTOR player[].cursectnum
						spawn TRANSPORTERSTAR
						resetcount
						set countvarc 0
						ife player[].ftq 133 setp[].fta 0
					}
				}
				// ifpdistg 1280 ife player[].fta 0
				// {
					// setp[].ftq 132
					// setp[].fta 40
				// }
				ifspritepal 33 getlastpal
				ife player[].ftq 130 setp[].fta 0
				ife player[].ftq 129 setp[].fta 0
			}
		}
		set temp NO
		ifge sprite[].extra player[].max_player_health set temp YES
		ife pdown YES ifge sprite[].extra 100 set temp YES
		ife temp YES
		{
			set shellyhp sprite[].extra
			// strength SHELLYSTRENGTH
			ife gametype SURVIVAL { strength 200 set shellyhp 200 }
			
			cactor SHELLYCROUCH
			ai AISHELLYCROUCH
			sound SHELLYREADY
			set ikicked 0
			set shotpitch 0
			set mtype 0
			seta[].htextra -1 set burning 0 set bleeding 0
			set bottarget -1
			seta[].htowner THISACTOR
			set monstatus 30
			cstat 257
			ife player[].ftq 130 setp[].fta 0
			ife player[].ftq 129 setp[].fta 0
			ifspritepal 33 getlastpal
			set spawnprotect 150
		}
		break
	}
	
	findnearspritez LASERLINE 1024 10240 spriteid
	ifn spriteid -1 
	{
		geta[spriteid].x x2
		geta[spriteid].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvar x2 y2
		getincangle temp angvar sprite[].ang
		abs temp
		ifl temp 512
		set FEMKILLCOUNT 8
	}
	
	ifg FEMKILLCOUNT 0 sub FEMKILLCOUNT 1
	
	ifg dodgetime 0 sub dodgetime 1
	ife gametype -1 set shellycharge 0 else
	
	ife team 1
	{
		add shellycharge 1
		ife gametype 0
		ifge shellycharge FULLBOTCHARGE
		ifl damagecount BOOSTMIN sub shellycharge 1
	}
	
	ife myspawner -1
	ife dodgetime 0
	{
		ifp pfacing ife seeplayer YES 
		{
			ifpdistl 1408 ifg player[].curr_weapon KNEE_WEAPON ifvarand bits 4
			set myspawner player[].i
			
			ifn team actorvar[player[].i].team
			ifp pfacing
			ifpdistl 8192
			ife bottarget player[].i
			ifn player[].kickback_pic 0
			ifl player[].curr_weapon RPG_WEAPON
				set myspawner player[].i
			
		}
	}
		
	ifai AISHELLYKICK set myspawner -1
	ifai AISHELLYJETPACK set myspawner -1
	ifai AISHELLYFLINTCH set myspawner -1
	ifaction ASHELLYFALL set myspawner -1
	ifn myspawner -1 state startdodge
	
	state shellyhitscan
	seta[].ang lastang
	ifl gametype 1
	{
		state findplayercrumb
		getp[].cursectnum mysector
		ifn mysector -1
		ifn mysector sprite[].sectnum
		ifn monstatus 70
		ife switchmode 0
		{
			ife seeplayer NO
			ifg sector[mysector].lotag 2
			ifpdistg 6144
			ife bottarget -1
				setsprite THISACTOR player[].posx player[].posy sprite[player[].i].z
		}
	}
	else
	{
		set navmode 2
		ife dodgetime 0
		state navigationsmart
	}
	state spawnprotectcode
	ifg padmove 0 state jumppadmove
	
	add float 1
	ifl float 0
	{
		ife float -75 
		{
			sound SHELLYCLIPOUT
			espawn SHELL
			setav[RETURN].mtype CLIPFALL
		}
		ife float -45 sound SHELLYCLIPIN
		ife float -1 state shellybotweap
	}
	
	ife pdown YES ifpdistl 1024 set bottarget -1 else
	state targetsearch
	
	ifn bottarget -1 
	{
		ife team actorvar[bottarget].team set bottarget -1 else
		dist targetdist THISACTOR bottarget
	}
	
	ifg bot_taunt 0 sub bot_taunt 1
	ifg ikicked 0 { sub ikicked 1 ifn bottarget -1 ifn actorvar[bottarget].shrunken 0 ifg ikicked 10 set ikicked 10 }
	ifai AISHELLYFLINTCH set bottarget -1
	ife bottarget -1 ifactorsound THISACTOR LASERLOOP stopactorsound THISACTOR LASERLOOP
	ifn thirdbaseval 9 ifactorsound THISACTOR LASERLOOP stopactorsound THISACTOR LASERLOOP
	ifn bottarget -1
	{
		dist xydist THISACTOR bottarget
		
		ifinwater nullop else
		ife bot_taunt 0 
		{ 
			ife VOLUME 6 ifl LEVEL 10
			{
				ifrnd 192 ifoutside sound B_SUNASSWIPE else
				sound B_JIB10 
			}
			set bot_taunt 2000 
		}
		geta[bottarget].x x2
		geta[bottarget].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle initsprite x2 y2

		ifl gametype 1
		ifpdistl FOLLOWRANGE // MAXRANGE
			ifn thirdbaseval 4 ifn thirdbaseval 9
			ife ikicked 0
				ife initflags bottarget
			{
				set angvel initsprite
				set navpoint -1
				state avoidboss
			}
				
		
		
		ifai AISHELLYKICK nullop else
		ifai AISHELLYJETPACK nullop else
		ifai AISHELLYSWIM nullop else
		ifgapzl 48 nullop else
		iffloordistl 4
		ifl xydist 1024
		ife ikicked 0
		ife actorvar[bottarget].stun 0
		{
			cactor SHELLY
			ai AISHELLYKICK
		}
		
		ifai AISHELLYKICK nullop else
		ife pdown YES
		ife dodgetime 0
		ifpdistl 4096
		ife seeplayer YES
		{
			getp[].posx x2
			getp[].posy y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle angvel x2 y2
		}
		
		
		ifle botclip 0 state shellybotweap
		
		getincangle temp lastang initsprite
		ifl temp 0 mul temp -1
		ifai AISHELLYKICK nullop else
		ifl temp 64
		{
			ife sprite[bottarget].picnum GREENSLIME set thirdbaseval 3
			switch thirdbaseval
			case 3 // SMG
			
				ifge float 3
				{
					set float 0
					sub botclip 1
					ifactor SHELLYCROUCH { geta[].z z add z 3072 seta[].z z }
					ifai AISHELLYSWIM { geta[].z z add z 4096 seta[].z z }
					spawn SHELL
					spawn AIRFLASH1
					ifactor SHELLYCROUCH { geta[].z z sub z 3072 seta[].z z }
					ifai AISHELLYSWIM { geta[].z z sub z 4096 seta[].z z }
					
					// ifactorsound THISACTOR SHELLYFIRE nullop else 
					sound SHELLYFIRE
					ifg plevel 3 sound M4FIRE
					state hitscan_targetprep
					state randzshoot
					zshoot tempb CHAINGUN
					ifg plevel 3 
					{
						state hitscan_targetprep
						state randzshoot
						zshoot tempb CHAINGUN
						spawn SHELL
					}
					sub botclip 1
					ife botclip 0 set float -90
				}
			break
			case 4 // RPG
				ifge float 30
				{
					set float 0
					
					set target bottarget
					state friendlyfirecheck
					ifn target -1
					{
						ifg plevel 5 sound ROCKETFIRE2 else
						sound RPG_SHOOT
						sub botclip 1
						geta[].x savx
						geta[].y savy
						set x savx
						add x 256
						rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang x2 y2
						seta[].x x2
						seta[].y y2
						
						set xvel 644
						state rpg_targetprep
						
						ezshoot zdist RPG
						seta[RETURN].extra RPG_WEAPON_STRENGTH
						ifg plevel 5 
						{
							setav[RETURN].mtype TARGETLOCK
							setav[RETURN].bottarget bottarget
						}
						
						seta[].x savx
						seta[].y savy
					}
					ifle xydist 2048 state shellybotweap
				}
			break
			case 6 // rolly turret
				ifge float 30
				{
					set float 0
					sub botclip 1
					ife botclip 0 set float -60
					espawn ROLLYTURRET
					setav[RETURN].team team
					ifvarand shellyupgrades[HANDBOMB_WEAPON] 1 setav[RETURN].initsprite 1
					setav[RETURN].mtype -2048
					setav[RETURN].dodgetime 40
					setav[RETURN].myspawner THISACTOR
					ife team 1 setav[RETURN].mlevel plevel else
					setav[RETURN].mlevel mlevel
				}
			break
			case 9 // freezer
			
				set temp NO
				ifg plevel 4 ifge float 4 set temp YES else
				ifge float 5 set temp YES
				ife temp YES
				{
					set float 0
					
					set target bottarget
					state friendlyfirecheck
					ifn target -1
					{
						sub botclip 1
						
						ifactor SHELLYCROUCH { geta[].z z add z 3072 seta[].z z }
						ifai AISHELLYSWIM { geta[].z z add z 4096 seta[].z z }
						spawn AIRFLASH1
						ifactor SHELLYCROUCH { geta[].z z sub z 3072 seta[].z z }
						ifai AISHELLYSWIM { geta[].z z sub z 4096 seta[].z z }
						
						ifg plevel 4
						{
							state hitscan_targetprep
							zshoot zdist FIREBEAM
							ifactorsound THISACTOR LASERLOOP nullop else sound LASERLOOP
						}
						else
						{
							sound FLAMER_SHOOT
							
							state hitscan_targetprep
							state randzshoot
							zshoot zdist FIREBOLT
							
							geta[].ang TMP_A, sub TMP_A 8, seta[].ang TMP_A
							state hitscan_targetprep
							state randzshoot
							zshoot zdist FIREBOLT
							
							geta[].ang TMP_A, add TMP_A 16, seta[].ang TMP_A
							state hitscan_targetprep
							state randzshoot
							zshoot zdist FIREBOLT
							
							geta[].ang TMP_A, sub TMP_A 8, seta[].ang TMP_A
						}
					}
				}
			break
			endswitch

		}
	
	}
	else
	set initsprite angvel
	
	ife dodgetime 0
	ifcount 2
	ifn sprite[].ang initsprite
	{
		geta[].ang angvar
		getincangle temp angvar initsprite
		set B MAXSHELLYTURN
		mul B -1
		ifg temp B ifl temp MAXSHELLYTURN
			seta[].ang initsprite
		else
		{
			ifl temp 0 sub angvar MAXSHELLYTURN
			else add angvar MAXSHELLYTURN
			seta[].ang angvar
		}
		set lastang sprite[].ang
	}
	
	ifaction ASHELLYJUMP1
	{
		set countvar 0
		ifactioncount 2
		{
			action ASHELLYJUMP2
			move SHELLYJUMPVELS geth
		}
	}
	ifaction ASHELLYJUMP2
	{
		iffloordistl 4
		{
			cactor SHELLYCROUCH
			ai AISHELLYCROUCH
			
			set countvar 11
			ifonwater 
			{
				cactor SHELLY
				ai AISHELLYSTAND
				sound LANDWATER
			}
			else
			ifg FEMFALLDMG 40
			sound LANDNORMAL
		}
	}
	
	ifgapzl 48 
	{ 
		// ifgapzl 24 nullop else
		ifaction ASHELLYRUN 
		{
			cactor SHELLYCROUCH 
			ai AISHELLYCRAWL
		} else
		ifai AISHELLYSTAND
		{
			cactor SHELLYCROUCH
			ai AISHELLYCROUCH
		}
	}
	else
	ifaction ASHELLYCRAWL
	ife FEMKILLCOUNT 0
	{
		cactor SHELLY
		ai AISHELLYRUN
	}
	
	state botblocking
	
	state shellyfallhandling
	ifg dodgetime 0 state moveshelly else
	ifai AISHELLYWANDER state shellywander
	else ifai AISHELLYKICK state shellykickcode
	else ifai AISHELLYFLINTCH state shellyflintch
	else ifai AISHELLYJETPACK state shellyjetpack
	else ifai AISHELLYSWIM state shellyswimstate
	else 
	{
		ife pdown YES state followplayer else
		ifg gametype 0 state shellymultiplayer
		else state followplayer
	}
	
	ifg FEMKILLCOUNT 0 ifai AISHELLYRUN
	{
		cactor SHELLYCROUCH 
		ife seeplayer YES ifpdistl 8192 ai AISHELLYCROUCH else
		ai AISHELLYCRAWL
	}

	geta[].htflags temp orvar temp 134234112 seta[].htflags temp
	
	ifinwater { ifai AISHELLYSWIM nullop else { cactor SHELLY ai AISHELLYSWIM } }
	else ifai AISHELLYSWIM
	{
		ifgapzl 48 { cactor SHELLYCROUCH ai AISHELLYCROUCH }
		else
		ai AISHELLYJUMP
	}
	
	
	// ifsquished
	// {
		// seta[].htextra 200
		// seta[].htpicnum RPG
		// seta[].htowner THISACTOR
		// set shellysquish YES
		// sound SQUISHED
		// quote 163
	// }
	// else 
	
	ife team 1
	{
		ifn monstatus 2
		ifgapzl 24 set shellysquish YES
		else set shellysquish NO
		ifg burning 0 state imonfire
		state predamage
	}
	
	ifg sprite[].htextra 0
	{
		set blueiteration 90
		ife sprite[].htextra 1
		ife sprite[].htpicnum SHOTSPARK1
		ife sprite[].extra 0 // hardcoded falling damage
		ife team 1
		{
			seta[].htextra -1
			seta[].extra shellyhp
		}
		
		geta[].htowner spriteid
		geta[].htextra temp
		ife gametype DM
		{
			ife team actorvar[spriteid].team set temp -1
		}
		else
		ifn sprite[].htpicnum BURNING
		shiftr temp 1
		
		ife team 1 ife spriteid player[].i shiftr temp 1
		ifl temp 1 set temp 1
		seta[].htextra temp
		
	}
	else ifg blueiteration 0 sub blueiteration 1
	
	ifhitweapon
	{	
		ife team 1
		ifn myshelly -1 geta[].extra shellyhp
		
		ife team 1
		ifwasweapon SHRINKSPARK
		{	
		  cactor SHELLY
		  sound ACTOR_SHRINKING
		  ai AISHELLYSHRUNK
		  // set shrunken 1
		  break
		}
		else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		ifwasweapon FREEZEBLAST nullop else state random_wall_jibs
		ifdead
		{
			cactor SHELLY
			getp[].player_par ikicked
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  cactor SHELLY
			  ai AISHELLYFROZEN
			  strength 0
			  state freezeme
			  break
			}
			ife team 1 set monstatus 2
			else state enemy_death
			ifg gametype 0 
			ife team 1
			{ 
				ife gametype DM set tempb 1 else set tempb 5
				ife team 0 add bluescore tempb else ife team 1 add redscore tempb 
				state playerscorecheck
			}
			
			ifinwater nullop else
			{
				rand temp 3
				ife temp 0 globalsound SHELLYDIE1 else 
				ife temp 1 globalsound SHELLYDIE2 else
				ife temp 2 globalsound B_KILLED4 else
				globalsound B_KILLED5
			}
			guts JIBS6 4
			ife gametype 0 money 6
			cstat 0
			strength 0
			ai AISHELLYDIE
		}
		else 
		{
			state shellybotpainsounds
			ifg stun 0
			{
				set TMP_B plevel
				mul TMP_B 8
				rand TMP_A 100
				ifl TMP_A TMP_B
				set stun 0
				else
				{
					cactor SHELLY
					ai AISHELLYFLINTCH
				}
			}
		}
	}
	// state shellycostume
ends

useractor enemy SHELLY SHELLYSTRENGTH ASHELLYSTAND state bombshellcode enda
useractor enemy SHELLYCROUCH SHELLYSTRENGTH ASHELLYSTAND state bombshellcode enda

appendevent EVENT_KILLIT

switch sprite[].picnum
	case SHELLY 
	case SHELLYCROUCH
	ife team 1
	{
	hitradius 768 2 2 2 2
	cactor SHELLYCROUCH
    ai AISHELLYDOWN
	set monstatus 2
	getp[].player_par ikicked
    quote 132
    set countvarc 0
    strength 5
	set RETURN 1
	cstat 257
	sizeat 23 21
	}
	break
endswitch

endevent

action ADUMMYSTAND 0  1  5  1  1
useractor notenemy SHELLYDUMMY 0 ADUMMYSTAND

ife player[].newowner -1 killit
getp[].i myspawner
seta[].mdflags 16
setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y sprite[myspawner].z
// seta[].ang player[].ang
seta[].ang angvel
sizeat 42 36

enda

gamevar shellyintro NO 0
move CHUTEDRIFT 48 -16
useractor notenemy SHELLYCHUTE 0

	ifmove 0
	{
		ife wesreplace 2 { spawn WESBOT killit }
		seta[].z sector[].ceilingz
		move CHUTEDRIFT randomangle
		sizeat 23 21
		geta[].z initsprite
	}
	fall

	geta[].ang angvar
	add angvar 12
	seta[].ang angvar

	iffloordistl 128 soundonce CHUTESOUND

	iffloordistl 4
	{
		spawn SHELLY
		globalsound THUD
		set shellyintro YES
		set midscreen HELLOSHELLY
		set cutend totalclock add cutend 1200
		set cutmash 4
		globalsound B_HAILQUEEN
		globalsound DEAGLE_SLIDE
		startscreen
		killit
	}

	// failsafe
	ifle sprite[].z initsprite
	{
		spawn SHELLY
		globalsound THUD
		killit
	}
	geta[].z initsprite

enda

useractor notenemy EDFTROOPCHUTE 0

	ifmove 0
	{
		seta[].z sector[].ceilingz
		move CHUTEDRIFT randomangle
		sizeat 38 35
		geta[].z initsprite
	}
	fall

	geta[].ang angvar
	add angvar 12
	seta[].ang angvar

	iffloordistl 128 soundonce CHUTESOUND

	iffloordistl 4
	{
		espawn EDFTROOP
		seta[RETURN].pal sprite[].pal
		globalsound THUD
		killit
	}

enda

useractor notenemy EDFSNIPERCHUTE 0

	ifmove 0
	{
		seta[].z sector[].ceilingz
		move CHUTEDRIFT randomangle
		sizeat 29 26
		geta[].z initsprite
	}
	fall

	geta[].ang angvar
	add angvar 12
	seta[].ang angvar

	iffloordistl 128 soundonce CHUTESOUND

	iffloordistl 4
	{
		espawn EDFSNIPER
		seta[RETURN].pal sprite[].pal
		globalsound THUD
		killit
	}

enda

useractor enemy MIMIC 1000

ifrnd 128
cactor SHELLY
// else
// cactor DUKEBOT
orvar monstflags 131072
set inithp plevel
mul inithp 150
add inithp 150
set bluebaseval inithp
set monxp inithp
seta[].extra inithp
seta[].htpicnum MIMIC
set spawnprotect 0
set monstatus 1

enda
