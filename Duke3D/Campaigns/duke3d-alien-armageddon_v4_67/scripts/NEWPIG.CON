
// define NEWPIGPISS 17020
// define NEWPIG 17036
// define NEWPIGSTAYPUT 17037
// define NEWPIGDIVE 17081
// define NEWPIGDONUT 17101

action ANEWPWALK     0  4 5 1 20
action ANEWPRUN      0  4 5 1 11
action ANEWPSHOOT    30 2 5 1 58
action ANEWPSHOOT2	 35 1 5 1 40
action ANEWPCOCK1    30 3 5 -1 7
action ANEWPCOCK2	20 3 5  1 7
action ANEWPSTAYSTAND 93 1 5 1 10
action ANEWPSTAND    94 1 5 1 10
action ANEWPDIVE     40 2 5 1 40 // -5
action ANEWPDIVESHOOT  0 2 5 1 52
action ANEWPDIVESHOOT2 5 1 5 1 40
action ANEWPMELEE	55 2 5 1 20


action ANEWPDYING    68 6 1 1 18
action ANEWPHIT      1851 1 5 1 10 // 74
// action ANEWPHIT2	 68 1 1 1 10
action ANEWPDEAD     73 1 1 1 1
action ANEWPFROZEN    0 1 5
action ANEWPGROW      0
action ANEWPDECAP    80 5 1 1 18
action ANEWPDECAPDEAD 84 1 1 1 1
action ANEWPDYING2   74 6 1 1 20
action ANEWPDEAD2    79 1 1 1 1 
action ANEWPDOWN	85 4 1 1 20

action ANEWPDONUT	65 1 1 1 10
action ANEWPISS	-16 2 5 1 4
action ANEWPISSTART -6 1 5 1 30
action ANEWPISSEND  -1 1 1 1 30

action NEWPDOWNBREATH 0 2 1 1 40
action NEWPDOWNSHOOT1  2 1 1 1 20
action NEWPDOWNSHOOT2  3 1 1 1 16
action NEWPDOWNDIE	   4 2 1 1 18
action NEWPDOWNDEAD	   5 1 1 1 10
action NEWPIGPLAYDEAD  5 1 1 1 10

move NEWPWALKVELS 72
move NEWPRUNVELS 108
move NEWPMELEEVEL 96
move NEWPDEADVEL

ai AINEWPSEEKENEMY ANEWPWALK NEWPWALKVELS seekplayer
ai AINEWPSHOOTENEMY ANEWPSHOOT STOPPED faceplayer
ai AINEWPFLEEENEMY ANEWPWALK NEWPWALKVELS fleeenemy
ai AINEWPSHOOT ANEWPSHOOT STOPPED faceplayer
ai AINEWPDODGE ANEWPRUN NEWPRUNVELS dodgebullet
ai AINEWPCHARGE ANEWPRUN NEWPRUNVELS seekplayer
ai AINEWPDIVING ANEWPDIVE STOPPEDFORPLAYER faceplayer
ai AINEWPDYING ANEWPDYING STOPPED geth
// ai AINEWPSHRINK ANEWPWALK SHRUNKVELS fleeenemy
ai AINEWPGROW ANEWPGROW STOPPEDFORPLAYER faceplayerslow
ai AINEWPHIT ANEWPHIT STOPPEDFORPLAYER faceplayer
ai AINEWPMELEE ANEWPMELEE NEWPMELEEVEL faceplayer

ai AINEWPDOWN NEWPDOWNBREATH STOPPEDFORPLAYER faceplayer


useractor enemy NEWPIGDOWN 20 NEWPDOWNBREATH

	fall
	ifaction NEWPDOWNDIE
	{
		ifactioncount 1 action NEWPDOWNDEAD
		strength 0
		move STOPPED geth
		ifhitweapon { }
		break
	}
	ifaction NEWPDOWNDEAD
	{
		set burning 0
		ifhitweapon
		{
			ifwasweapon RADIUSEXPLOSION
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
			else
			  ifwasweapon RPG
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
		}
		break
	}
	ifaction NEWPIGPLAYDEAD
	{
		ifhitweapon
		{
			ifwasweapon RADIUSEXPLOSION
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
			else
			  ifwasweapon RPG
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
		}
		ifcount 30 ifrnd 2 { action NEWPDOWNBREATH strength 20 cstator 257 }
		break
	}
	// state monsterai
	ifai 0
	{
		cstator 257
		sizeat 28 26
		ai AINEWPDOWN
		set mtype NEWPIGDOWN
		set monxp 50 
		set monstatus 1
	}
	ifrnd 1 sound PIG_PAIN
	ifaction NEWPDOWNBREATH
	ife seemytarget YES ifl targetdist 8192
	ife targetalive YES
	ifactioncount 2
	{
		resetactioncount
		ifrnd 64
		{
			action NEWPDOWNSHOOT1
			rand botclip 3
			add botclip 3
		}
	}
	
	ifaction NEWPDOWNSHOOT1
	{
		ifactioncount 1
		{
			ifg botclip 0
			{
				action NEWPDOWNSHOOT2
				sound NEW_PISTOL_FIRE
				state hitscan_targetprep
				state randzshoot
				zshoot tempb SHOTSPARK1
				state hitscan_targetprep
				state randzshoot
				zshoot tempb SHOTSPARK1
				sub botclip 1
				spawn SHELL
			}
			else action NEWPDOWNBREATH
		}
	}
	ifaction NEWPDOWNSHOOT2
	{
		ifactioncount 1 action NEWPDOWNSHOOT1
	}
	
	set temp NO
	ifonwater set temp YES
	ifinwater set temp YES
	ife temp YES 
	{
		ifrnd 8 addstrength -1
		ifle sprite[].extra 0
		{
			seta[].htextra 2
			seta[].htowner player[].i
			seta[].htpicnum KNEE
		}
	}
	state predamage
	ifhitweapon
	{
		ifwasweapon SHRINKSPARK
		{
		  cactor NEWPIG
		  sound ACTOR_SHRINKING
		  ai AINEWPSEEKENEMY
		  strength 5
		  set shrunken 1
		  break
		}
		else
		  ifwasweapon GROWSPARK
			sound SPARKLESOUND
		
		
		ifdead
		{
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  move 0
			  cactor NEWPIG
			  action ANEWPFROZEN
			  strength 0
			  set monstatus 1
			  state freezeme
			  break
			}
			state random_wall_jibs
			ifwasweapon GROWSPARK
			{
			  sound ACTOR_GROWING
			  cactor NEWPIG
			  ai AINEWPGROW
			  break
			}
			ife mtype NEWPIGDOWN state enemy_death
			ifwasweapon RADIUSEXPLOSION
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
			else
			  ifwasweapon RPG
			{
			  sound SQUISHED
			  state lite_jibs
			  state pig_body_jibs
			  killit
			}
			sound PIG_DYING
			action NEWPDOWNDIE
			move STOPPED faceplayer
			cstat 0
			
			set monstatus 2
		}
	}
	else ifrnd 1
	{
		seta[].htextra 1
		seta[].htpicnum SHOTSPARK1
		seta[].htowner player[].i
	}
	
enda

state newpmeleestate

	ifactioncount 1
	ifmove NEWPMELEEVEL
	{
		move STOPPED geth
		
		ifn bottarget -1
		{
			ifn bottarget player[].i
			{
				ife seemytarget YES
				ifl targetdist 1596
				{
				  seta[bottarget].htextra 25
				  seta[bottarget].htpicnum SHOTSPARK1
				  seta[bottarget].htowner THISACTOR
				  seta[bottarget].htang sprite[].ang
				  sound PIGBSMACK
				  
				  seta[bottarget].xvel -844
					getav[bottarget].stun temp
					add temp 20
					setav[bottarget].stun temp
					geta[bottarget].z z
					sub z 2048
					seta[bottarget].z z
					geta[bottarget].zvel z
					sub z 1560
					seta[bottarget].zvel z
				}
				else sound PIGBSWING
			}
			else
			{
				ifpdistl 1596
				ifcansee
				{
				  getp[].i spriteid
				  seta[spriteid].htpicnum SHOTSPARK1
				  seta[spriteid].htowner THISACTOR
				  seta[spriteid].htextra 25
				  seta[spriteid].htang sprite[].ang
				  
				  state checksaberblock
				  ifn sprite[spriteid].htextra -1
				  {
					  palfrom 30 16
					  ife pchar 1 sound B_GRUNT else
						sound DUKE_GRUNT
					  sound PIGBSMACK
					  
					wackplayer
					getp[].ang angvar
					add angvar 1024
					cos PIGBVAR2 angvar
					sin PIGBVAR3 angvar
					shiftvarl PIGBVAR2 9
					shiftvarl PIGBVAR3 9
					setplayer[].posxv PIGBVAR2
					setplayer[].posyv PIGBVAR3
					// push player backwards  
				  }
				}
				else sound PIGBSWING
			}
		}
		else sound PIGBSWING
			
	}
	ifactioncount 2 
	{
		ife seemytarget YES
		ifrnd 128
		ai AINEWPSHOOTENEMY
		else
		ai AINEWPSEEKENEMY
	}

ends

state newpseekenemystate
  
  ifn myspawner -1
  {
	set myspawner -1
	ifrnd 32
	{
	  ifonwater
	  {
		ai AINEWPDODGE
		set dodgetime 6
		break
	  }
	  // cactor NEWPIGDIVE
	  ai AINEWPDIVING
	  break
	}
  }
  ifai AINEWPCHARGE
  {
    ife seemytarget YES
      // ifl targetdist 3084
    {
      ifnotmoving
	  {
		
        ife targetalive YES
          ife shootmytarget YES
			ifl targetdist 10240
		  {
		    state lowlineofsight
			ifrnd 128 ife B YES 
			ifn sector[].lotag 1
			{ 
				// cactor NEWPIGDIVE
				ai AINEWPDIVING 
			}
			else
			ai AINEWPSHOOTENEMY
			break
		  }
		  else
	      ai AINEWPSEEKENEMY
	  }
	  else
	  // ifl targetdist 3500
	  {
	    ifle SKILL 3 ifg targetdist 3500 break
		ifl targetdist 1560
		ifrnd 128
		ife shrunken 0
		{
			ai AINEWPMELEE
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
		}
		else
		{
			set temp NO
			ifl targetdist 3500 set temp YES
			else ifrnd 8 ifl targetdist 16384 set temp YES
			ife temp YES
			{
				state lowlineofsight
				ifrnd 160 ife B YES
				ifn sector[].lotag 1
				{
					// cactor NEWPIGDIVE
					ai AINEWPDIVING
				}
				else 
				ai AINEWPSHOOTENEMY
			}
		}
	  }
    }
	else ifcount 90
		ai AINEWPSEEKENEMY
	
    break
  }
  else iffloordistl 32
  {
    ifnotmoving
	  {
		
        ife targetalive YES
          ife shootmytarget YES
		  ifl targetdist 16384
		  {
			state lowlineofsight
			ifrnd 128 ife B YES
			ifn sector[].lotag 1
			{ 
				// cactor NEWPIGDIVE
				ai AINEWPDIVING 
			}
			else
			ai AINEWPSHOOTENEMY
			break
		  }
		  else
		  ifcount 8 ifrnd 32
		  {
			ai AINEWPDODGE
			set dodgetime 10
			state setfarang
			break
		  }
	  }
    
    ifrnd 8
    {
      ifbulletnear
	  {
        ai AINEWPDODGE
		set dodgetime 6
	  }
    }
	else
	ifactor NEWPIG
	ifrnd 16
	{
	  ifbulletnear
	  {
		// cactor NEWPIGDIVE
        ai AINEWPDIVING
		break
	  }
	}
	ifg targetdist 5120
	ifrnd 8
    {
      ifactornotstayput
      ai AINEWPCHARGE
    }
  }

  ifrnd 128
    ife seemytarget YES
  {
    ifai AINEWPDODGE
    {
	  ifl dodgetime 2 set dodgetime 2
      ifcount 32
        ai AINEWPCHARGE
          break
    }
    iffloordistl 32
    {
      ifl targetdist 1024
        ife targetalive YES
          ife shootmytarget YES
      {
	    ifrnd 192 ife shrunken 0 
		{
			ai AINEWPMELEE
			ife bottarget myshelly setav[myshelly].myspawner THISACTOR
		}
		else
        ai AINEWPSHOOTENEMY
        break
      }
	  ifactornotstayput ifg targetdist 12288 break
	  
	  ifcount 40 nullop else
	  ifge SKILL 4 count 40
	  
      ifcount 48
      {
        ifrnd 8
          ife targetalive YES
            ife shootmytarget YES
        {
		  state lowlineofsight
			ifrnd 64 ife B YES
		  ifn sector[].lotag 1
		  {
			// cactor NEWPIGDIVE
            ai AINEWPDIVING
		  }
          else
            ai AINEWPSHOOTENEMY
          break
        }
      }
    }
  }
ends

// state newpshootenemystate

    // ifcount 12 nullop
    // else
      // ifcount 11
    // {
      // ife shootmytarget YES
		// state pigfiring
      // else
        // ai AINEWPSEEKENEMY
    // }
    // ifcount 25 nullop
    // else
      // ifcount 24
      // {
        // action ANEWPCOCK1
		// sound SHOTGUN_COCK
		// ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH

      // }
    // ifcount 48 nullop
    // else
      // ifcount 47
    // {
      // ife shootmytarget YES
		// state pigfiring
      // else
        // ai AINEWPSEEKENEMY
    // }
    // ifcount 60 nullop
    // else
      // ifcount 59
      // {
        // action ANEWPCOCK1
		// sound SHOTGUN_COCK
		// ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
      // }
    // ifcount 72
    // {
      // ifrnd 64
        // resetcount
      // else
      // {
	    // ifl targetdist 1560
		// ife shrunken 0
		// ifrnd 128 ai AINEWPMELEE
		// else
        // ifl targetdist 844
          // ai AINEWPFLEEENEMY
        // else
          // ai AINEWPSEEKENEMY
      // }
    // }
    // ifaction ANEWPCOCK1
	// {
      // ifactioncount 3
	    // action ANEWPCOCK2
	// }
	// ifaction ANEWPCOCK2
	// {
	  // ifactioncount 3
	    // action ANEWPSHOOT
	// }

// ends

defstate newpshootenemystate

ifaction ANEWPSHOOT
{
	ifactioncount 1
	{
		ife shootmytarget YES
		{
			action ANEWPSHOOT2
			state pigfiring
			add countvar 1
		}
		else
		{
			ai AINEWPSEEKENEMY
			set countvar 0
		}
	}
}
else
ifaction ANEWPSHOOT2
{
	ifactioncount 1
	{
		action ANEWPCOCK1
		sound SHOTGUN_COCK
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
	}
}
ifaction ANEWPCOCK1
{
	ifactioncount 3 action ANEWPCOCK2
}
else
ifaction ANEWPCOCK2
{
	ifactioncount 3
	{
		ifl countvar 2
		{
		  ife shootmytarget YES
			action ANEWPSHOOT
		  else
			ai AINEWPSEEKENEMY
		}
		else
		{
			set countvar 0
			ifrnd 64
			ife shootmytarget YES
				action ANEWPSHOOT
			else
			{
				ifl targetdist 1560
				ife shrunken 0
				ifrnd 128 
				{
					ai AINEWPMELEE
					ife bottarget myshelly setav[myshelly].myspawner THISACTOR
				}
				else
				ifl targetdist 844
				  ai AINEWPFLEEENEMY
				else
				  ai AINEWPSEEKENEMY
			}
		}
	}
}

ends

state newpfleeenemystate

  ifl dodgetime 2 set dodgetime 2
  ifactioncount 8
    ai AINEWPSEEKENEMY
  else
    ifnotmoving
	  {
        ife targetalive YES
          ife shootmytarget YES
		  {
			state lowlineofsight
			ifrnd 128 ife B YES
			ifn sector[].lotag 1
			{ 
				//  cactor NEWPIGDIVE
				ai AINEWPDIVING 
			}
			else
			ai AINEWPSHOOTENEMY
			break
		  }
		  else
	      ai AINEWPSEEKENEMY
	  }
ends

// state newpdivestate
  // ifaction ANEWPDIVESHOOT
  // {
    // ife seemytarget YES
    // {
      // ifcount 12 nullop
      // else
        // ifcount 11
      // {
        // ife shootmytarget YES
        // state pigfiring
        // else
		// {
		  // cactor NEWPIG
          // ai AINEWPSEEKENEMY
		// }
      // }
      // ifcount 25 nullop
      // else
        // ifcount 24
		// {
		  // sound SHOTGUN_COCK
		  // ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
		// }
      // ifcount 48 nullop
      // else
        // ifcount 47
      // {
        // ife shootmytarget YES
        // state pigfiring
        // else
		// {
		  // cactor NEWPIG
          // ai AINEWPSEEKENEMY
		// }
      // }
      // ifcount 60 nullop
      // else
        // ifcount 59
      // {
	    // sound SHOTGUN_COCK
		// ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
        // ifgapzl 32
          // ai AINEWPDIVING
        // else
        // {
          // ifl targetdist 4096
		  // {
		    // cactor NEWPIG
            // ai AINEWPFLEEENEMY
			// set dodgetime 6
		  // }
          // else
		  // {
		    // cactor NEWPIG
            // ai AINEWPSEEKENEMY
		  // }
        // }
      // }
    // }
    // else
      // ifgapzl 32
        // ai AINEWPDIVING
      // else
	  // {
	    // cactor NEWPIG
        // ai AINEWPSEEKENEMY
	  // }
  // }
  // else
  // {
    // ifactioncount 2
	// {
		// ife targetalive YES
		// {
		  // resetcount
		  // action ANEWPDIVESHOOT
		// }
		// else
		// {
		  // cactor NEWPIG
		  // ai AINEWPSEEKENEMY
		// }
	// }
  // }
// ends


defstate newpdivestate

ifaction ANEWPDIVE
{
	ifactioncount 2
	{
		cactor NEWPIGDIVE
		ife targetalive YES
		{
		  resetcount
		  action ANEWPDIVESHOOT
		}
		else
		{
		  cactor NEWPIG
		  ai AINEWPSEEKENEMY
		}
	}
}
else
ifaction ANEWPDIVESHOOT
{
	ifactioncount 1
	{
		ifge countvar 4
		{
			set countvar 0
			ifgapzl 32
			{
				action ANEWPDIVESHOOT
				resetactioncount
				break
			}
			else
			{
			  ifl targetdist 4096
			  {
				cactor NEWPIG
				ai AINEWPFLEEENEMY
				set dodgetime 6
			  }
			  else
			  {
				cactor NEWPIG
				ai AINEWPSEEKENEMY
			  }
			}
			break
		}
		ife countvar 2
		{
			add countvar 1
			action ANEWPDIVESHOOT
			resetactioncount
			break
		}
		ife shootmytarget YES
		{
			action ANEWPDIVESHOOT2
			state pigfiring
			add countvar 1
		}
		else
		{
			cactor NEWPIG
			ai AINEWPSEEKENEMY
			set countvar 0
		}
	}
}
else
ifaction ANEWPDIVESHOOT2
{
	ifactioncount 1
	{
		action ANEWPDIVESHOOT
		sound SHOTGUN_COCK
		ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR SHOTGUN_COCK SHRUNKPITCH
		add countvar 1
	}
}

ends

state checknewphitstate
	// ifn sprite[].picnum NEWPCOPDIVE
	// ifn sprite[].htpicnum FREEZEBLAST
	// ifn sprite[].htpicnum SHRINKSPARK
	// ife shrunken 0
  // spawn BLOOD
  ifdead
  {
    cactor NEWPIG

    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action ANEWPFROZEN
      strength 0
	  state freezeme
      break
    }
	state random_wall_jibs
	ife gametype 0
	{
    ifrnd 16
      spawn SHIELD
    else
      state drop_shotgun
	}
	addkills 1 state enemy_death
	
    ifwasweapon GROWSPARK
    {
      sound ACTOR_GROWING
      ai AINEWPGROW
      break
    }
	
    ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      state lite_jibs
	  state pig_body_jibs
      killit
    }
    else
      ifwasweapon RPG
    {
      sound SQUISHED
      state lite_jibs
	  state pig_body_jibs
      killit
    }
    else
      ai AINEWPDYING
	  
	ifwasweapon SABERPROJ
	{
	    espawn NEWPIGSABERDEATH
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].pal sprite[].pal
		killit
	}
	  
	  ifrnd 36
	  ife shrunken 0
	  {
		sound PIG_DYING
		action ANEWPDOWN
		break
	  }
	  ifrnd 128 cstator 4
	  ifrnd 128
	  action ANEWPDYING2
	  sound PIG_DYING
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PIG_DYING SHRUNKPITCH
  }
  else
  {
    sound PIG_PAIN
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PIG_PAIN SHRUNKPITCH
    state random_wall_jibs

    ifwasweapon SHRINKSPARK
    {
	  // cactor NEWPCOP
      sound ACTOR_SHRINKING
      // ai AINEWPSHRINK
	  set shrunken 1
    }
    else
      ifwasweapon GROWSPARK
        sound SPARKLESOUND
    
	ifvarg stun 0
      {
		set countvar 0
	      ai AINEWPHIT
	      cactor NEWPIG
		  // ifrnd 128 action ANEWPHIT2
      }
    else
    ifrnd 32
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
	{
	    ifmove STOPPED ifcount 13 nullop else
		{
		  set countvar 0
	      cactor NEWPIG
          ai AINEWPHIT
		  // ifrnd 128 action ANEWPHIT2
		}
	}
	else
	ifrnd 112
	{
	  ifai AINEWPSHOOTENEMY nullop else
	  ifai AINEWPDIVING nullop else
	  {
		  state lowlineofsight
		  ifrnd 128 ife B YES
		  ifn sector[].lotag 1
		  {
			// cactor NEWPIGDIVE
			ai AINEWPDIVING
		  }
		  else
		  {
			cactor NEWPIG
			ai AINEWPSHOOTENEMY
		  }
		 
	  }
	}
  }
ends

state newpshrinkstate
  ifcount SHRUNKDONECOUNT
    ai AINEWPSEEKENEMY
  else
    ifcount SHRUNKCOUNT
      sizeto 28 26
  else
    state genericshrunkcode
ends

state newpdyingstate

    state deadsquished
	ifmove NEWPDEADVEL
	{
		// ifrespawn ife gametype 0
		  // ifcount RESPAWNACTORTIME
		// {
		  // addkills -1 set monstatus 1
		  // spawn TRANSPORTERSTAR
		  // cstat 257
		  // strength NEWPCOPSTRENGTH
		  // ai AINEWPSEEKENEMY
		// }
		// else
		// {
		  strength 0
		  ifhitweapon
			ifwasweapon RADIUSEXPLOSION
		  {
			sound SQUISHED
			state lite_jibs
			ifaction ANEWPDEAD
				state pig_body_jibs
			else
			ifaction ANEWPDEAD2
			    state pig_body_jibs
			killit
		  }
		  
		// }
		break
	}
	
	ifaction ANEWPDOWN
	{
		ifactioncount 3
		{
			cactor NEWPIGDOWN
			cstat 0
			ai AINEWPDOWN
			ifrnd 128 action NEWPDOWNDEAD
			else
				action NEWPIGPLAYDEAD
			sound THUD
			ifhitweapon { }
			strength 20
		}
		break
	}
	  ifaction ANEWPDECAP
	  {
		ifactioncount 4
		{
			ifrnd 64
			  spawn BLOODPOOL2
			iffloordistl 8
			  sound THUD
			action ANEWPDECAPDEAD
			move NEWPDEADVEL
		}
		break
	  }
	  ifactioncount 5
	  {
	    ifvarand sprite[].cstat 4 cstat 4 else cstat 0
		ifrnd 64
		  spawn BLOODPOOL2
		iffloordistl 8
		  sound THUD
		ifaction ANEWPDYING action ANEWPDEAD
		else
		ifaction ANEWPDYING2 action ANEWPDEAD2
		move NEWPDEADVEL
	  }
  
ends

state newpcopcode
  fall
  state monsterai
  ife monstatus -1 break
  state checksquished
  ifaction APIGSTAND
  {
    ai AINEWPSEEKENEMY
	sizeat 28 26
	cstator 257
	sound PIG_RECOG
  }
  else
  ifaction ANEWPSTAND
  {
    ai AINEWPSEEKENEMY
	sizeat 28 26
	cstator 257
	sound PIG_RECOG
  }
  else
  ifai 0
  {
	ai AINEWPSEEKENEMY
	sizeat 28 26
	cstator 257
	sound PIG_RECOG
  }
  else
    ifaction ANEWPDEAD { state newpdyingstate break }
  else
    ifaction ANEWPDECAPDEAD { state newpdyingstate break }
  else
    ifaction ANEWPDEAD2 { state newpdyingstate break }
  else
    ifaction ANEWPFROZEN
  {
    ifcount THAWTIME
    {
      ai AINEWPSEEKENEMY
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
        ifrnd 8
          spawn WATERDRIP

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  lotsofglass 10
      state standard_jibs
      ifrnd 84 spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else
    ifai AINEWPDYING
      state newpdyingstate
  // else
    // ifai AINEWPSHRINK
      // state newpshrinkstate
  else
  { 
    ifai AINEWPHIT
    {
	  ifvarg stun 0 resetactioncount
      ifactioncount 3
      ai AINEWPSEEKENEMY
    }
	else
    ifai AINEWPSEEKENEMY
      state newpseekenemystate
    else
      ifai AINEWPDODGE
        state newpseekenemystate
    else
      ifai AINEWPSHOOTENEMY
        state newpshootenemystate
    else
	  ifai AINEWPMELEE
	    state newpmeleestate
	else
      ifai AINEWPGROW
        state genericgrowcode
    else
      ifai AINEWPFLEEENEMY
        state newpfleeenemystate
    else
      ifai AINEWPDIVING
        state newpdivestate
    else
      ifai AINEWPCHARGE
        state newpseekenemystate
    ifhitweapon
	{
	    ifwasweapon HEADJIB1
		{
			cactor NEWPIG
			sound SQUISHED
			guts JIBS2 2
			state enemy_death
			state drop_shotgun
			state random_wall_jibs
			addkills 1
			strength 0
			ai AINEWPDYING
			action ANEWPDECAP
			rand zdist 1024
			sub zdist 3500
			ezshoot zdist PIGHEADPROJ
			rand angvar 512
			sub angvar 256
			add angvar sprite[].htang
			seta[RETURN].ang angvar
			seta[RETURN].pal sprite[].pal
			cstat 0
		}
		else
        state checknewphitstate
	}
	else
	{
		ife monstatus 1
		ifvarg stun 0
		{
			set countvar 0
			ai AINEWPHIT
			cactor NEWPIG
			// ifrnd 128 action ANEWPHIT2
		}
		ifrnd 1
		{
		  ifrnd 32
			soundonce PIG_ROAM
		  else
			ifrnd 64
			  soundonce PIG_ROAM2
		  else
			soundonce PIG_ROAM3
		}
	}
  }
ends

useractor enemystayput NEWPIGDIVE PIGCOPSTRENGTH
  ifai 0
  {
    sizeat 28 26
	cstator 257
	ai AINEWPDIVING
	action ANEWPDIVESHOOT
	sound PIG_RECOG
  }
  state newpcopcode
enda

useractor enemystayput NEWPIGSTAYPUT PIGCOPSTRENGTH ANEWPSTAYSTAND
  sizeat 28 26
  cstator 257
  ai AINEWPSEEKENEMY
  cactor NEWPIG
  sound PIG_RECOG
enda

useractor enemy NEWPIGGUARD PIGCOPSTRENGTH AENEMYGUARD

	cstator 257
	sizeat 28 26
	fall
	set monstatus -2
	ifpdistl 20480 ifcansee ifg ptargeted 0 seta[].htextra 0
	ifg sprite[].htextra -1
	{
		cactor NEWPIG
		ife sprite[].htextra 0
		sound PIG_RECOG
		else
		sound PIG_PAIN
		state wakeupguards
	}

enda

useractor enemy NEWPIG PIGCOPSTRENGTH ANEWPSTAND

  state newpcopcode
  
enda

action ANEWPISSACTOR	0 2 5 1 4

useractor enemy NEWPIGPISS PIGCOPSTRENGTH ANEWPISSACTOR

fall
ifmove 0
{
    cstator 257
	sizeat 28 26
	ifcansee move PISSING
	break
}
ifmove PISSING
{
	soundonce DUKE_URINATE
	ifcount 50 nullop else ifg sprite[].htextra 1 
	{
		count 50
		state predamage
	}
	ifcount 60 
	{ 
		stopsound DUKE_URINATE // sound FLUSH_TOILET 
		move 0
		cactor NEWPIG
		action ANEWPSTAND
		sound PIG_RECOG
	}
	
}

enda

eventloadactor NEWPIGDONUT sizeat 28 26 cstator 257 enda
useractor enemy NEWPIGDONUT PIGCOPSTRENGTH

ifmove 0
{
	cstator 257
	sizeat 28 26
	move STOPPED faceplayer
}
fall
ifcansee nullop else resetcount
set temp NO
ifhitweapon set temp YES
ifbulletnear set temp YES
ifcount 180 set temp YES
ife temp YES
{
	move 0
	cactor NEWPIG
	action ANEWPSTAND
	sound PIG_RECOG
}

enda
