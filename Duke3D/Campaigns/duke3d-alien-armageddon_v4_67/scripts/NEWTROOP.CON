//
//
//    NEW LIZARD TROOPER CODE
//
//

action ANEWTSTAND     0  1  5  1  1
action ANEWTGROW    0  1  5  1  1
action ANEWTSTAYSTAND  -1  1  5  1  1
action ANEWTWALKING   5  4  5  1   12
action ANEWTWALKINGBACK 20   4  5   -1   12
action ANEWTRUNNING   40  4  5  1  6
action ANEWTSHOOT    25  1  5  1   25
action ANEWTSHOOTDONE	25  1  5  1   20
action ANEWTJETPACK  30  1  5  1  1
action ANEWTJETPACKILL 30  1  5  1   50
action ANEWTJUMP1 60 1  5  1   20
action ANEWTJUMP2 65 1  5  1   20
action ANEWTLAND 70 1  5  1   20
action ANEWTPOINT1  75  1  5  1  50
action ANEWTPOINT2  80  1  5  1  70
action ANEWTFLINTCH  1971  1  5  1  16
action ANEWTDYING    101  6  1  1   16
action ANEWTDEAD     106
action ANEWTPLAYDEAD   106
action ANEWTSUFFERDEAD 98  1  1   1   30
action ANEWTSUFFERING  99  2  1  1   21
// action ANEWTDUCK     35  1  5  1  3
// action ANEWTDUCKSHOOT  35  1  5  1   25

action ANEWTDUCK     0  1  5  1  3
action ANEWTDUCKSHOOT  0  1  5  1   25
action ANEWTDUCKDONE 0  1  5  1   20

action ANEWTABOUTHIDE  95  2  1  1   25
action ANEWTHIDE     96  1  1  1   25
action ANEWTREAPPEAR   95  1  1  1   25
action ANEWTFROZEN    0  1  5
action ANEWTDECAP    113  5  1  1  16
action ANEWTDECAPDEAD  118
action ANEWTDYING2		107  6  1  1  16
action ANEWTDEAD2		112
action ANEWTDYING2FALL 107  2  1  1  8

move NEWTWALKVELS 96
move NEWTWALKVELSBACK -96
move NEWTJETPACKVELS 90 -84
move NEWTJETUPVELS 90 -96
move NEWTJETDOWN 80 8
move NEWTJETPACKILLVELS 192 -38
move NEWTJETBACKVELS -96 -12
move NEWTRUNVELS 232
move NEWTBOOSTVELS 480 -20
move NEWTJUMPVEL 208 -96
move NEWTLANDVEL 180

ai AINEWTAUNT ANEWTPOINT1 STOPPED faceplayer
ai AINEWTSEEKENEMY  ANEWTWALKING   NEWTWALKVELS seekplayer
ai AINEWTSEEKPLAYER ANEWTWALKING   NEWTWALKVELS seekplayer
ai AINEWTFLEEING  ANEWTRUNNING   NEWTRUNVELS fleeenemy
ai AINEWTFLEEINGBACK ANEWTWALKINGBACK   NEWTWALKVELSBACK faceplayer
ai AINEWTDODGE    ANEWTRUNNING   NEWTRUNVELS  dodgebullet
ai AINEWTSHOOTING   ANEWTSHOOT   STOPPED  faceplayer
ai AINEWTDUCKING  ANEWTDUCK    STOPPED  faceplayer
ai AINEWTJETPACK  ANEWTJETPACK   NEWTJETPACKVELS seekplayer
// ai AINEWTSHRUNK   ANEWTWALKING   SHRUNKVELS fleeenemy
ai AINEWTHIDE     ANEWTABOUTHIDE STOPPED faceplayer
ai AINEWTGROW     ANEWTGROW    DONTGETUP faceplayerslow
ai AINEWTBOOST ANEWTJETPACK NEWTBOOSTVELS geth getv
ai AINEWTDECAP ANEWTDECAP STOPPED geth
ai AINEWTJUMP ANEWTJUMP1 NEWTJUMPVEL geth getv

state newtauntstate

ifaction ANEWTPOINT1
{
	ifactioncount 1 action ANEWTPOINT2
}
ifaction ANEWTPOINT2
{
	ifactioncount 1
	{
		ife shootmytarget YES ai AINEWTSHOOTING
		else ai AINEWTSEEKENEMY
	}
}

ends

state newt_painsounds

	set countvar 30
	ifg shrunken 10
	{
		sound NEWT_PAIN1
		setactorsoundpitch THISACTOR NEWT_PAIN1 1560
		break
	}
	
	rand temp 3
	switch temp
	case 0 sound NEWT_PAIN1 break
	case 1 sound NEWT_PAIN2 break
	case 2 sound NEWT_PAIN3 break
	case 3 sound NEWT_PAIN4 break
	endswitch
	
ends

state newtjumpcheck

	ifceilingdistl 96 break
	geta[].z z
	sub z 4096
	
	cos mycos sprite[].ang
	sin mysin sprite[].ang
	hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
	ifn hitsprite -1 ifvarand sprite[hitsprite].cstat 16 set hitsprite -1
	
	ife hitsprite -1
	{
		set startx sprite[].x
		sub startx hitx
		mul startx startx
		set y2 sprite[].y
		sub y2 hity
		mul y2 y2
		add startx y2
		sqrt startx startx
		ifl startx 512
		{
			sub z 8192
			hitscan sprite[].x sprite[].y z sprite[].sectnum mycos mysin 0 hitsector hitwall hitsprite hitx hity hitz 16777281
			
			set startx sprite[].x
			sub startx hitx
			mul startx startx
			set y2 sprite[].y
			sub y2 hity
			mul y2 y2
			add startx y2
			sqrt startx startx
			
			ifg startx 1560 
			{ 
				cactor NEWTROOP ai AINEWTJUMP geta[].ang mtype
				ife countvar 0 sound NEWT_JUMP
			}
		}
	}


ends

state newtjumpstate

	seta[].ang mtype
	ifaction ANEWTJUMP1 { ifactioncount 1 action ANEWTJUMP2 } else
	ifaction ANEWTJUMP2
	{
		ifactioncount 1
		{
			action ANEWTLAND
			move NEWTLANDVEL geth
		}
	}
	ifaction ANEWTLAND
	{
		ifonwater
		  iffloordistl 64
		  {
			ai AINEWTJETPACK
			ifvarand initflags 16 move NEWTJETUPVELS seekplayer
			break
		  }
		iffloordistl 8
			 ai AINEWTSEEKENEMY	
	}

ends

defstate checknewthit

  
  ifwasweapon HEADJIB1 // HEADSHOT!
  {
    // sound PRED_DYING
	sound SQUISHED
	cstat 0
    strength -1
	cactor NEWTROOP
	ifai AINEWTHIDE { sizeat 24 22 ifvarand monstflags 256 { seta[].xrepeat init_xrepeat seta[].yrepeat init_yrepeat } cstat 0 }
	ifaction ANEWTSUFFERING stopsound LIZARD_BEG
	ai AINEWTDECAP
	// guts HEADJIB1 1
	rand zdist 1024
	  sub zdist 3500
	  ezshoot zdist NEWTROOPHEADPROJ
	  rand angvar 512
	  sub angvar 256
	  add angvar sprite[].htang
	  seta[RETURN].ang angvar
	  seta[RETURN].pal sprite[].pal
	state random_wall_jibs
	ife mtype 3 { set monstatus 2 break }
	ife gametype 0
	{
		ifvarand initflags 16 spawn LASERPISTOL else
		ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		state drop_ammo
	}
    
	addkills 1 state enemy_death
	break
  }
  ifaction ANEWTSUFFERING
  {
      stopactorsound THISACTOR LIZARD_BEG
	  ifg shrunken 20 ifl shrunken SHRUNKCOUNT
	  {
		sound NEWT_DEATH1
		setactorsoundpitch THISACTOR NEWT_DEATH1 1560
	  }
	  else
	  {
		rand temp 2
		switch temp
		case 0 sound NEWT_DEATH1 sound NEWT_DEATH1 break
		case 1 sound NEWT_DEATH2 sound NEWT_DEATH2 break
		case 2 sound NEWT_DEATH3 sound NEWT_DEATH3 break
		endswitch
	  }
    cstat 0
    strength 0
    action ANEWTSUFFERDEAD
	ife mtype 3 set mtype 0
	ifwasweapon RPG nullop else ifwasweapon RADIUSEXPLOSION nullop else
    break
  }
  ifdead
  {
    stopactorsound THISACTOR NEWT_BURNING
	stopactorsound THISACTOR NEWT_TAUNT1
	stopactorsound THISACTOR NEWT_AGGRO1
	stopactorsound THISACTOR NEWT_AGGRO2
	stopactorsound THISACTOR NEWT_ROAM1
	stopactorsound THISACTOR NEWT_ROAM2
	stopactorsound THISACTOR NEWT_ROAM3
	stopactorsound THISACTOR NEWT_ROAM4
	stopactorsound THISACTOR NEWT_TAUNT2
	
    cactor NEWTROOP
	ifai AINEWTHIDE { ai AINEWTSEEKPLAYER sizeat 24 22 ifvarand monstflags 256 { seta[].xrepeat init_xrepeat seta[].yrepeat init_yrepeat } cstat 256 }
	
    ifwasweapon FREEZEBLAST
    {
      sound NEWFREEZE
      spritepal 1
      move 0
      action ANEWTFROZEN
      strength 0
	  state freezeme
      break
    }
	ife gametype 0
	{
		ifvarand initflags 16 spawn LASERPISTOL else
		ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
		state drop_ammo
	}
    state random_wall_jibs

    ifwasweapon GROWSPARK
    {
      cstat 0
      sound ACTOR_GROWING
      ai AINEWTGROW
      break
    }

    addkills 1 state enemy_death

    ifwasweapon RPG
    {
      sound SQUISHED
      state troop_body_jibs
      state lite_jibs
      killit
    }
    else
      ifwasweapon RADIUSEXPLOSION
    {
      sound SQUISHED
      state troop_body_jibs
      state lite_jibs
      killit
    }
    else
    {
	  ifg shrunken 20 ifl shrunken SHRUNKCOUNT
	  {
		sound NEWT_DEATH1
		setactorsoundpitch THISACTOR NEWT_DEATH1 1560
	  }
	  else
	  {
		  switch temp
			case 0 sound NEWT_DEATH1 sound NEWT_DEATH1 break
			case 1 sound NEWT_DEATH2 sound NEWT_DEATH2 break
			case 2 sound NEWT_DEATH3 sound NEWT_DEATH3 break
		  endswitch
	  }
	  ifwasweapon SABERPROJ
	  {
	    espawn NEWTSABERDEATH
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].pal sprite[].pal
		killit
	  }
      ifrnd 64
        iffloordistl 32
      {
        sound LIZARD_BEG
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR LIZARD_BEG SHRUNKPITCH
        spawn BLOODPOOL2
        strength 0
        move 0
        action ANEWTSUFFERING
		set mtype 3 // suffering flag for headshots
        break
      }
	  ifawayfromwall 
	  ifrnd 128
	  { 
		ifrnd 128 cstator 4 
		action ANEWTDYING2 
		// guts ARMJIB1 1 
	  } 
	  else
      action ANEWTDYING
	  move STOPPED
      break
    }
  }
  else
  {
    ifai AINEWTHIDE break
    state random_wall_jibs
	  
	ife countvar 0
		state newt_painsounds

	
    ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      // ai AINEWTSHRUNK
	  set shrunken 1
    }
    else
      ifai AINEWTBOOST nullop else
      iffloordistl 32
      {
	    ifwasweapon BURNING nullop else
		ifaction ANEWTFLINTCH nullop else
        ifrnd 96
		{
			set temp sprite[].extra
			mul temp 2
			ifle temp inithp
			ifn sprite[].htpicnum BURNING
			ifn sprite[].htpicnum RADWOUND
			  action ANEWTFLINTCH
		}
        else ifvarg stun 0 action ANEWTFLINTCH
		ifaction ANEWTFLINTCH 
		{
			ai AINEWTSEEKPLAYER 
			cactor NEWTROOP 
			move STOPPED geth 
			action ANEWTFLINTCH
		}
		else ifvarand initflags 32
		ifrnd 64
		ifn sector[].lotag 1
		{
			ai AINEWTDUCKING
			cactor NEWTROOPDUCKING
		}
      }
	  else
	  {
		ifvarg stun 0
		{
			ai AINEWTSEEKPLAYER
			action ANEWTFLINTCH
			move STOPPED geth
		}
	  }
			
	  
     ifwasweapon GROWSPARK
        sound SPARKLESOUND
  
  }
ends

state newthidestate
  ifaction ANEWTREAPPEAR
  {
    ifactioncount 2
    {
      sound TELEPORTER
      ai AINEWTSHOOTING
	  sizeat 24 22
	  ifvarand monstflags 256 { seta[].xrepeat init_xrepeat seta[].yrepeat init_yrepeat }
	  seta[].htg_t 2 2
      cstat 257
    }
    else
    {
	  set burning 0
	  seta[].htextra -1
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		sizeto MINXSTRETCH MINYSTRETCH
		sizeto MINXSTRETCH MINYSTRETCH
	  }
	  else
	  {
		  ifvarand monstflags 256 { sizeto 48 44 sizeto 48 44 }
		  else
		  {
			sizeto 24 22 sizeto 24 22
		  }
	  }
      spawn FRAMEEFFECT1
    }
  }
  else
    ifaction ANEWTWALKING
  {
    set burning 0
	seta[].htextra -1
    ifl targetdist 2448
      ifg targetdist 1024
    {
      ifceilingdistl 48
        break
      ifp pfacing
        break

      ifgapzl 64 nullop
      else
        ifawayfromwall
      {
        spawn GLARESTAR
        action ANEWTREAPPEAR
        move 0
        break
      }
    }
	ife bluecarrier THISACTOR
	{
		spawn GLARESTAR
        action ANEWTREAPPEAR
        move 0
        break
	}
	ife redcarrier THISACTOR
	{
		spawn GLARESTAR
        action ANEWTREAPPEAR
        move 0
        break
	}
  }
  else
    ifaction ANEWTHIDE
  {
    ifactioncount 2
    {
      // spawn GLARESTAR
      // sound TELEPORTER
      action ANEWTWALKING
      move NEWTWALKVELS faceplayer
      cstat 32768
    }
    else
    {
	  ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	  {
		sizeto 2 MINYSTRETCH
		sizeto 2 MINYSTRETCH
	  }
	  else
	  {
		  sizeto 2 22
		  sizeto 2 22
		  sizeto 2 22  
	  }
      spawn FRAMEEFFECT1
	  ifhitweapon
      state checknewthit
    }
  }
  else
    ifaction ANEWTABOUTHIDE
	{
		ifactioncount 2
		{
		  action ANEWTHIDE
		  cstat 0
		  spawn GLARESTAR
		  sound TELEPORTER
		}
		ifhitweapon
        state checknewthit
	}
ends

state newtgunnashoot

  ife targetalive YES
{
	set temp NO
	ifvarand initflags 16 ifrnd 64 ife seemytarget YES set temp YES
	
    ifl targetdist 1024
      ai AINEWTSHOOTING
    else
      ifactornotstayput
      ifvare temp NO
    {
      ifactioncount 12
        ifrnd 16
          ife shootmytarget YES
      {
        ifspritepal 21
          ifrnd 5
            ifg targetdist 4096
				ifn redcarrier THISACTOR ifn bluecarrier THISACTOR
              ai AINEWTHIDE
				
        else
        {
          ifl targetdist 1100
		  {
            ai AINEWTFLEEING
			set dodgetime 8
		  }
          else
          {
            ifl targetdist 4096
              ife seemytarget YES
                ife shootmytarget YES
                  ai AINEWTSHOOTING
            else
            {
              move NEWTRUNVELS seekplayer
              action ANEWTRUNNING
            }
          }
        }
      }
	  else ifge SKILL 4
	  ife shootmytarget YES
	  ifl targetdist 20480
	  ifrnd 12
	  {
          ifrnd 96
		  ifn sector[].lotag 1
		  {
			ai AINEWTDUCKING
			cactor NEWTROOPDUCKING
		  }
		  else
		  ai AINEWTSHOOTING
          break
	  }
    }
    else
      ifcount 26
        ifrnd 32
        {
          ifrnd 96
		  ifn sector[].lotag 1
		  {
			ai AINEWTDUCKING
			cactor NEWTROOPDUCKING
		  }
		  else
		  ai AINEWTSHOOTING
        }
		
	  ifvarand initflags 16
	  ifactornotstayput
	  {
		  ifai AINEWTSHOOTING nullop else
		  ifai AINEWTDUCKING nullop else
		  {
			  getactor[].ang angvar
			  ifrnd 32
			  ife scannedsprite THISACTOR ai AINEWTBOOST else
			  ifbulletnear { ai AINEWTBOOST set dodgetime 8 }
			  
			  ifai AINEWTBOOST
			  {
				  ifrnd 128 addvar angvar 256 else subvar angvar 256
				  seta[].ang angvar
			  }  
		  }
	  }
}
ends

state newtbooststate

	ifl dodgetime 2 set dodgetime 2
	cstat 514
	spawn FRAMEEFFECT1
	cstat 257
	seta[].ang lastang
	soundonce DUKE_JETPACK_IDLE
	ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DUKE_JETPACK_IDLE SHRUNKPITCH
	ifcount 30
	{
		set dodgetime 0
		iffloordistl 32 
		ifn sector[].lotag 1
		{
			ai AINEWTDUCKING 
			cactor NEWTROOPDUCKING
		}
		else { ai AINEWTJETPACK count 36 }
	}
	else ifnotmoving
	{
		set dodgetime 0
		iffloordistl 32
		ifn sector[].lotag 1
		{
			ai AINEWTDUCKING 
			cactor NEWTROOPDUCKING
		}
		else { ai AINEWTJETPACK move NEWTJETDOWN faceplayer getv count 40 }
	}

ends

state newtseekstate

	ifg sprite[].zvel 0
	{
	  ifceilingdistl 128 nullop
	  else
	  iffloordistl 128 nullop else
		ifactornotstayput
		{
		  ai AINEWTJETPACK
		  ifvarand initflags 16 move NEWTJETUPVELS seekplayer
		  break
		}
	  
	}

  state newtgunnashoot
  ifinwater
  {
    ai AINEWTJETPACK
	ifvarand initflags 16 move NEWTJETUPVELS seekplayer
    break
  }
  else ifonwater
  iffloordistl 96
  {
	ai AINEWTJETPACK
	ifvarand initflags 16 move NEWTJETUPVELS seekplayer
    break
  }
  ife seemytarget YES
  {
    ifmove NEWTRUNVELS
      ifl targetdist 1596
	  {
		ifn sector[].lotag 1
		ai AINEWTSHOOTING
		else
		{
        ai AINEWTDUCKING
		cactor NEWTROOPDUCKING
		}
	  }
    ife targethigher YES
    {
      ifceilingdistl 128 nullop
      else
        ifactornotstayput
		{
          ai AINEWTJETPACK
		  ifvarand initflags 16 move NEWTJETUPVELS seekplayer
		}
      break
    }
    else
      ifrnd 2
    {
      ifspritepal 21
        ifg targetdist 1596
		ifn redcarrier THISACTOR ifn bluecarrier THISACTOR
      {
        ai AINEWTHIDE
        break
      }
	  ife dodgetime 0
	  ifcount 8
      ifbulletnear
      {
        ifrnd 128
		ifn sector[].lotag 1
		{
          ai AINEWTDUCKING
		  cactor NEWTROOPDUCKING
		}
		else
		{
          ai AINEWTDODGE
		  set dodgetime 8
		}
		
        break
      }
	  ifceilingdistl 128 nullop
      else
      ifrnd 2
      ifvarand initflags 16
      ifactornotstayput
      {
      	ai AINEWTJETPACK
      	ifvarand initflags 16 move NEWTJETUPVELS seekplayer
  	  }
    }
	else
	ifn myspawner -1
	{
		ifrnd 128
		ifn sector[].lotag 1
		{
          ai AINEWTDUCKING
		  cactor NEWTROOPDUCKING
		}
		else
		{
          ai AINEWTDODGE
		  state incoming_eval
		  seta[].ang angvel
		  set dodgetime 8
		}
		set myspawner -1
        break
	}
  }
  ifactornotstayput state newtjumpcheck
  ifai AINEWTJUMP break
  ifnotmoving
  {
    ifrnd 32
      operate
    else
      ifcount 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
              ai AINEWTSHOOTING
	ifai AINEWTSHOOTING nullop else
	ifg crumbwait 20
	{
		ai AINEWTJETPACK
		  move NEWTJETUPVELS geth
		set crumbwait 0
	}
  }
  
  ifrnd 2
  ife countvar 0
  {
    ifg shrunken 20 ifl shrunken SHRUNKCOUNT
	{
		sound NEWT_ROAM1
		setactorsoundpitch THISACTOR NEWT_ROAM1 1560
	}
	else
	{
		rand temp 3
		switch temp
		case 0 sound NEWT_ROAM1
		break
		case 1 sound NEWT_ROAM2
		break
		case 2 sound NEWT_ROAM3
		break
		case 3 sound NEWT_ROAM4
		break
		endswitch
	}
	set countvar 150
  }
  
ends

state newtduckstate
  ifaction ANEWTDUCK
  {
    ifactioncount 8
    {
      ife targetalive YES
      {
        ifrnd 128
          action ANEWTDUCKSHOOT
      }
      else
        ifmove DONTGETUP
          break
        else
		{
          ai AINEWTSEEKPLAYER
		  cactor NEWTROOP
		}
    }
  }
  else
    ifaction ANEWTDUCKSHOOT
  {
    ifn bottarget -1 { set spriteid bottarget state facesprite }
    ifvarand initflags 32 ifactioncount 1 seta[].htg_t 2 3
    ifcount 64
    {
      ifmove DONTGETUP
        resetcount
      else
        action ANEWTDUCKDONE
      
    }
    else
      ifactioncount 2
      {
        ife shootmytarget YES
        {
			
		  ifvarand initflags 32
		    state shootbigplasma
		  else
		  ifvarand initflags 16
			state shootBIGFIRELASER
		  else ifspritepal 21
			state shootBIGFIRELASER
		  else
		  {  
			sound PRED_ATTACK
			ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK SHRUNKPITCH
			set savx sprite[].x
			set savy sprite[].y
			set newx sprite[].x
			add newx 96
			set angvar sprite[].ang
			sub angvar 512
				
			rotatepoint sprite[].x sprite[].y newx sprite[].y angvar x y
			seta[].x x
			seta[].y y
			shoot FIRELASER
			seta[].x savx
			seta[].y savy
			
		  }
		  resetactioncount
		  ifactornotstayput ifrnd 64 action ANEWTDUCKDONE
			
        }
        else 
			action ANEWTDUCKDONE
      }
  }
  else
  ifaction ANEWTDUCKDONE
  ifactioncount 1
  {
	ifl targetdist 1100
	{
	  ai AINEWTFLEEING
	  cactor NEWTROOP
	  set dodgetime 8
	}
	else
	{
	  ai AINEWTSEEKPLAYER
	  cactor NEWTROOP
	}
  }
ends

state newtshootstate
  ifvarand initflags 32 ifaction ANEWTSHOOT ifactioncount 1 seta[].htg_t 2 3
  ifaction ANEWTSHOOT
  ifactioncount 2
  {
    ife shootmytarget YES
    {
	  ifvarand initflags 32
		state shootbigplasma
	  else
      ifvarand initflags 16
		state shootBIGFIRELASER
	  else ifspritepal 21
		state shootBIGFIRELASER
	  else
	  {
		set savx sprite[].x
		set savy sprite[].y
		set newx sprite[].x
		add newx 80
		set angvar sprite[].ang
		sub angvar 512
			
		rotatepoint sprite[].x sprite[].y newx sprite[].y angvar x y
		seta[].x x
		seta[].y y
	    shoot FIRELASER
		seta[].x savx
		seta[].y savy
	    sound PRED_ATTACK
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK SHRUNKPITCH
      }
      resetactioncount
      ifrnd 96
        action ANEWTSHOOTDONE
      ifcount 24
		action ANEWTSHOOTDONE
    }
    else
      action ANEWTSHOOTDONE
  }
  ifaction ANEWTSHOOTDONE
  ifactioncount 1
  {
	ifg targetdist 2048 ifrnd 192 ai AINEWTSEEKPLAYER
	else
	{
		ifg targetdist 1596
		{
		  ai AINEWTFLEEING
		  set dodgetime 8
		}
		else
		{
		  ai AINEWTFLEEINGBACK
		  set dodgetime 8
		}
	}
  }
ends

state newtfleestate
  ifl dodgetime 2 set dodgetime 2
  ifactioncount 7
  {
    ifg targetdist 3084
    {
      ai AINEWTSEEKPLAYER
      break
    }
    else
      ifrnd 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
      {
	    set dodgetime 0
        ifrnd 128
		ifn sector[].lotag 1
		{
          ai AINEWTDUCKING
		  cactor NEWTROOPDUCKING
		}
        else
          ai AINEWTSHOOTING
          break
      }
  }
  ifactornotstayput state newtjumpcheck
  ifai AINEWTJUMP break
  ifnotmoving
  {
    ifrnd 32
      operate
    else
      ifcount 32
        ife targetalive YES
          ife seemytarget YES
            ife shootmytarget YES
      {
	    set dodgetime 0
        ifrnd 128
		ifn sector[].lotag 1
		{
          ai AINEWTDUCKING
		  cactor NEWTROOPDUCKING
		}
		else
          ai AINEWTSHOOTING	
      }
  }
ends

state newtdying
  set damflash 0
  iffloordistl 32
  {
    ifaction ANEWTDYING2FALL action ANEWTDYING2
    ifactioncount 5
    {
	  ifvarand sprite[].cstat 4 cstat 4 else
      cstat 0
      iffloordistl 8
      sound THUD
      ifrnd 64
        spawn BLOODPOOL2
      ifaction ANEWTDYING2 nullop else state rf
      strength 0
      move STOPPED geth
	  ifaction ANEWTDECAP action ANEWTDECAPDEAD else
	  ifaction ANEWTDYING2
	  {
		action ANEWTDEAD2 
		ife shrunken 0
		{
		geta[].yrepeat y
		ife upscaled YES sub y 2 else
		sub y 4
		seta[].yrepeat y
		}
	  }
	  else
      action ANEWTDEAD
	  
	  geta[].cstat temp
	  ifvarand temp 256 xorvar temp 256
	  seta[].cstat temp
    }
    break
  }
  else
  {
    move 0
    ifaction ANEWTDYING2 action ANEWTDYING2FALL
	else ifaction ANEWTDYING2FALL nullop
	else
	{
		state rf
		ifai AINEWTDECAP action ANEWTDECAP else
		action ANEWTDYING
	}
  }
  ifhitweapon
  ifwasweapon RPG
  {
      sound SQUISHED
      state troop_body_jibs
      state lite_jibs
      killit
  }
ends

state newtjetpackstate

  
  ifaction ANEWTJETPACKILL
  {
    ifvarand initflags 32 ifactioncount 1 seta[].htg_t 2 3
    ife seemytarget YES
      ifactioncount 2
    {
      resetactioncount
	    
	  ifvarand initflags 32
		state shootbigplasma
	  else
      ifvarand initflags 16
	  	state shootBIGFIRELASER
	  else ifspritepal 21
	    state shootBIGFIRELASER
	  else
	  {
	      sound PRED_ATTACK
		  ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR PRED_ATTACK SHRUNKPITCH
	      shoot FIRELASER
      }
	 
    }

    ife targethigher YES
	{
      ai AINEWTJETPACK
	  ifvarand initflags 16 move NEWTJETUPVELS seekplayer
	}
    else
      ifinwater
        ai AINEWTJETPACK
    else
	{
      ifcount 26
        iffloordistl 32
		{
          ai AINEWTSEEKPLAYER
		  break
		}
		
      ifn bottarget -1
	  {
	    ldist temp THISACTOR bottarget
		ifmove NEWTJETBACKVELS
		ifg temp 6144
		{
			action ANEWTJETPACKILL
			move NEWTJETPACKILLVELS seekplayer
		}
		else
		ifl temp 2560
			move NEWTJETBACKVELS faceplayer getv
	  }
	  ifcount 90
	  {
	    ife seemytarget YES nullop else
		{
			ai AINEWTJETPACK
			move NEWTJETDOWN seekplayer getv
		}
	  }
	}
  }
  else
    ifcount 48
  {
      ife seemytarget YES
	  {
		action ANEWTJETPACKILL
		move NEWTJETPACKILLVELS seekplayer
	  }
	  else
	  	  ifcount 150
	  {
		  ifmove NEWTJETDOWN nullop
		  else move NEWTJETDOWN seekplayer getv
	  }
  }
  getactor[].ang angvar
  ifvarand initflags 16
  {
	  ifbulletnear 
	  ifactornotstayput
	  {
		  set dodgetime 8
		  ai AINEWTBOOST
		  ifrnd 128 addvar angvar 256 else subvar angvar 256
		  seta[].ang angvar
		  count 10
	  }
  }
ends

state newtsufferingstate
  ifactioncount 2
  {
    ifrnd 16
      spawn WATERDRIP
    ifactioncount 14
    {
      stopsound LIZARD_BEG
      cstat 0
      strength 0
      action ANEWTSUFFERDEAD
      break
    }
  }
ends

state newtshrunkstate
  ifcount SHRUNKDONECOUNT
    ai AINEWTSEEKENEMY
  else
    ifcount SHRUNKCOUNT
	{
	  ifvarand monstflags 256 sizeto 48 44
	  else
      sizeto 24 22
	}
  else
    state genericshrunkcode
ends

state newtdeadstate
  
    strength 0
    // ifrespawn ife gametype 0
      // ifcount RESPAWNACTORTIME
    // {
	  // addkills -1 set monstatus 1
      // spawn GLARESTAR
      // cstat 257
      // strength NEWTROOPSTRENGTH
      // ai AINEWTSEEKENEMY
    // }
    ifhitweapon
    {
      ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
		ifaction ANEWTDECAPDEAD
		{
			guts LEGJIB1 2
			guts ARMJIB1 1
		  ifrnd 48
		  ife shrunken 0
			spawn BLOODPOOL2
		}
		else
        state troop_body_jibs
        state lite_jibs
        killit
      }
      break
    }
    else
      state checksquished
 ends


state newtcode fall

 state monsterai
 ifg countvar 0 sub countvar 1
 
  ifinwater
    ifrnd 1
      spawn WATERBUBBLE
  ifaction ATROOPSTAND action ANEWTSTAND
  ifaction ATROOPSTAYSTAND action ANEWTSTAND
  ifai 0 
  {
	 ifn dodgetime 0 ai AINEWTDODGE
	 else
	 action ANEWTSTAND
  }
  ifaction ANEWTSTAND
  {
    ifrnd 128
      ai AINEWTSHOOTING
    else
	  ifrnd 160 ife sprite[].htextra -1 
	  ifpdistg 6144
	  {
		ai AINEWTAUNT
		ifrnd 128 { sound NEWT_TAUNT1  sound NEWT_TAUNT1 } else { sound NEWT_TAUNT2 sound NEWT_TAUNT2 }
		set countvar 60
	  }
    else
	{
      ai AINEWTSEEKPLAYER
	  ifrnd 128
	  {
		sound NEWT_AGGRO1 sound NEWT_AGGRO1 
	  }
		else 
	  {
		sound NEWT_AGGRO2 sound NEWT_AGGRO2
	  }
	  set countvar 60
	}
  }
  else
    ifaction ANEWTFROZEN
  {
    ifcount THAWTIME
    {
      ai AINEWTSEEKENEMY
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
	  ife gametype 0
	  {
		  ifvarand initflags 16 spawn LASERPISTOL else
		  ifspritepal 21 ifrnd SPAWNAMMOODDS spawn LASERPISTOL else
			state drop_ammo
	  }

      ifrnd 84
        spawn BLOODPOOL
	  lotsofglass 10
      state troop_body_jibs
	  state lite_jibs
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  else ifaction ANEWTPLAYDEAD
  {
    ifhitweapon
    {
      ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state troop_body_jibs
        state lite_jibs
        killit
      }
      break
    }
    else
      state checksquished

    ifcount PLAYDEADTIME
    ifg targetdist 2560
    ife seemytarget YES
    {
      addkills -1 set monstatus 1
	 
      state newt_painsounds
      cstat 257
      strength 1
	  set monxp 100
      ai AINEWTSHOOTING
      
    }
    else
      ifp pfacing
        resetcount

    break
  }
  else
    ifaction ANEWTDEAD
	{
     state newtdeadstate
	 break
	}
  else
    ifaction ANEWTDECAPDEAD
	{
	 state newtdeadstate
	 break
	}
  else
    ifaction ANEWTDEAD2
	{
	 state newtdeadstate
	 break
	}
  else
    ifaction ANEWTSUFFERDEAD
  {
    ifactioncount 2
    {
      ifrnd 64
	  ife gametype 0
      {
        resetcount
        action ANEWTPLAYDEAD
      }
      else
      {
	    ifg shrunken 20 ifl shrunken SHRUNKCOUNT
		{
			sound NEWT_DEATH1
			setactorsoundpitch THISACTOR NEWT_DEATH1 1560
		}
		else
		{
			rand temp 2
			switch temp
			case 0 sound NEWT_DEATH1 break
			case 1 sound NEWT_DEATH1 break
			case 2 sound NEWT_DEATH1 break
			endswitch

		}
        action ANEWTDEAD
      }
    }
  }
  else
    ifaction ANEWTDYING
  {
    state newtdying
    break
  }
  else
    ifaction ANEWTDYING2
  {
    state newtdying
	break
  }
  else
    ifaction ANEWTDYING2FALL
  {
    state newtdying
	break
  }
  else
    ifai AINEWTDECAP
  {
    state newtdying
	break
  }
  else
    ifaction ANEWTSUFFERING
  {
    state newtsufferingstate
    ifhitweapon
      state checknewthit
    break
  }
  else
    ifaction ANEWTFLINTCH
  {
    move STOPPED geth
    ifvarg stun 0 resetactioncount
    ifactioncount 3
      ai AINEWTSEEKENEMY
	
  }
  else
  {
	 ifg burning 0
	 {
		stopactorsound THISACTOR NEWT_TAUNT1
		stopactorsound THISACTOR NEWT_AGGRO1
		stopactorsound THISACTOR NEWT_AGGRO2
		stopactorsound THISACTOR NEWT_ROAM1
		stopactorsound THISACTOR NEWT_ROAM2
		stopactorsound THISACTOR NEWT_ROAM3
		stopactorsound THISACTOR NEWT_ROAM4
		stopactorsound THISACTOR NEWT_TAUNT2
		ifactorsound THISACTOR NEWT_BURNING nullop else sound NEWT_BURNING
		set countvar 60
	 }
  
  
    ifai AINEWTSEEKPLAYER
      state newtseekstate
	else
	  ifai AINEWTJUMP
	  state newtjumpstate
	else
	  ifai AINEWTAUNT
	  state newtauntstate
    else
      ifai AINEWTJETPACK
    {
      state newtjetpackstate
      ifinwater nullop
      else
	  {
        soundonce DUKE_JETPACK_IDLE
		ifg shrunken 20 ifl shrunken SHRUNKCOUNT setactorsoundpitch THISACTOR DUKE_JETPACK_IDLE SHRUNKPITCH
	  }
    }
    else
      ifai AINEWTSEEKENEMY
        state newtseekstate
    else
      ifai AINEWTSHOOTING
        state newtshootstate
    else
      ifai AINEWTFLEEING
        state newtfleestate
    else
      ifai AINEWTFLEEINGBACK
        state newtfleestate
    else
      ifai AINEWTDODGE
        state newtseekstate
    else
      ifai AINEWTDUCKING
        state newtduckstate
    else
	  ifai AINEWTBOOST
        state newtbooststate
	else
      // ifai AINEWTSHRUNK
        // state newtshrunkstate
      ifai AINEWTGROW
	  {
        state genericgrowcode
		break
	  }
    else
      ifai AINEWTHIDE
    {
      state newthidestate
      break
    }
  }

  ife monstatus -1 break
  ifhitweapon
    state checknewthit
  else
  {
    state checksquished
	ife monstatus 1
	{
		ifaction ANEWTFLINTCH nullop else
		{
			ifvarg stun 0
			{
				ai AINEWTSEEKPLAYER 
				cactor NEWTROOP 
				move STOPPED geth 
				action ANEWTFLINTCH
			}
		}
	}
  }
ends

state checknewtpalette
  ifai 0
  {
	sizeat 24 22
	cstator 257
	// geta[].htflags temp, orvar temp 4194304, orvar temp 8192
	// seta[].htflags temp
    ifspritepal 0
	{
		ifrnd 128 spritepal 12 else
        spritepal 22
	}
    else
        ifspritepal 21
          addstrength TROOPSTRENGTH
	else
	ifspritepal 24
	{
		addstrength TROOPSTRENGTH
		orvar initflags 32
	}
  }
ends

useractor enemy NEWTROOPGUARD NEWTROOPSTRENGTH AENEMYGUARD
	cstator 257
	sizeat 24 22
	fall
	set monstatus -2
	ifpdistl 20480 ifcansee ifg ptargeted 0 seta[].htextra 0
	ifg sprite[].htextra -1
	{
		cactor NEWTROOP
		ife sprite[].htextra 0
		{
		ifrnd 128 sound NEWT_TAUNT1 else sound NEWT_TAUNT2
		}
		else
		sound NEWT_PAIN1
		state wakeupguards
	}
enda

useractor enemy NEWTROOPJETPACK NEWTROOPSTRENGTH
  state checknewtpalette
  ai AINEWTJETPACK
  ifvarand initflags 16 move NEWTJETUPVELS seekplayer
  cactor NEWTROOP
enda

useractor enemy NEWTROOPDUCKING NEWTROOPSTRENGTH ANEWTDUCK
ifai 0
{
  state checknewtpalette
  ai AINEWTDUCKING
  ifgapzl 48
    move DONTGETUP
}
else state newtcode
enda

useractor enemystayput NEWTROOPSTAYPUT NEWTROOPSTRENGTH ANEWTSTAYSTAND
  state checknewtpalette
  ife sprite[].htextra -1
  ifrnd 64 
  ifpdistg 6144
  {
	ai AINEWTAUNT
	ifrnd 128 { sound NEWT_TAUNT1 sound NEWT_TAUNT1 } else { sound NEWT_TAUNT2 sound NEWT_TAUNT2 }
	set countvar 60
  }
  else
  ifrnd 128
  {
      ai AINEWTSHOOTING
	  sound NEWT_AGGRO1 sound NEWT_AGGRO1
  }
  else
  {
	ai AINEWTSEEKPLAYER
	ifrnd 128 { sound NEWT_AGGRO1 sound NEWT_AGGRO1 } else { sound NEWT_AGGRO2 sound NEWT_AGGRO2 }
	set countvar 60
  }
  cactor NEWTROOP
enda

useractor enemy NEWTROOPRUNNING NEWTROOPSTRENGTH
	state checknewtpalette
	cstator 257
	cactor NEWTROOP sizeat 24 22 
	set dodgetime 16
	ai AINEWTDODGE
enda

useractor enemy NEWTROOPONTOILET NEWTROOPSTRENGTH
  ifcount 24
  {
    sound FLUSH_TOILET
	ifg sprite[].htextra sprite[].extra spawn FECES
    operate
    ai AINEWTSEEKPLAYER
    cactor NEWTROOP
  }
  else
    ifcount 2
      nullop
  else
    state checknewtpalette
	
	ifg sprite[].htextra 0 count 40
enda

useractor enemy NEWTROOPJUSTSIT NEWTROOPSTRENGTH
  ifcount 30
  {
    operate
    ai AINEWTSEEKPLAYER
    cactor NEWTROOP
  }
  else
    ifcount 2
      nullop
  else
    state checknewtpalette
	
	ifg sprite[].htextra 0 count 40
enda

useractor enemy NEWTROOP NEWTROOPSTRENGTH ANEWTSTAND
  state checknewtpalette
  state newtcode
enda
