// NEW BATTLELORD BOSS	

action ABATLORDSTAND		20  1  5  1  10
action ABATLORDWALK        0  4  5  1  14
action ABATLORDFROZEN      0  1  5  1  10
action ABATLORDRUN         0  4  5  1  8
action ABATLORDSHOOT         20  2  5  1  4
action ABATLORDLOB         30 2  5  1  35
action ABATLORDJUMP			45  1  5  1  20
action ABATLORDFALL			50  1  5  1  20
action ABATLORDLAND			40  1  5  1  30
action ABATLORDDYING        75 6  1  1  35
action BATLORDFLINTCH       60 1  5  1  8
action ABATLORDDEAD         80

move PALBATLORDSHRUNKRUNVELS 32

move BATLORDJUMPVELS 360 -96
move BATLORDFALLVELS 360
move BATLORDWALKVELS 224
move BATLORDRUNVELS 312

ai AIBATLORDSEEKENEMY ABATLORDWALK BATLORDWALKVELS seekplayer // seekplayer
ai AIBATLORDRUNENEMY ABATLORDRUN BATLORDRUNVELS geth // faceplayer
ai AIBATLORDSHOOTENEMY ABATLORDSHOOT STOPPED geth // faceplayer
ai AIBATLORDLOBBED ABATLORDLOB STOPPED geth // faceplayer
ai AIBATLORDDYING ABATLORDDYING STOPPED geth
ai AIBATLORDJUMP ABATLORDJUMP BATLORDJUMPVELS jumptoplayer
ai AIBATLORDGROW BATLORDFLINTCH STOPPED geth

eventloadactor NEWBATLORD 

ifspritepal 0 
	sizeat 72 60 
else
	sizeat 36 30

cstator 257

enda

eventloadactor NEWBATLORDSTAYPUT

ifspritepal 0 
	sizeat 72 60 
else
	sizeat 36 30

cstator 257

enda

state batlordjumpstate

	set myspawner -1
	set dodgetime 3
	state turntotarget
	ifaction ABATLORDJUMP 
	{
		ifactioncount 2
		{
			action ABATLORDFALL
			move BATLORDFALLVELS geth
		}
	} 
	else
	ifaction ABATLORDFALL
	{
		iffloordistl 4
		{
			action ABATLORDLAND
			sound LBM_JUMPSLAM
			move STOPPED geth
			debris SCRAP2 4
			debris SCRAP3 4
			debris SCRAP4 4
			debris SCRAP5 4
			spawn BIGSMOKE
			ifpdistl 8192 quake 30
			ife seeplayer YES
			ifpdistl 3072 wackplayer
			ifspritepal 0
			{
				hitradius 5120  40 80 120 160 
				// seta[].htextra -1
				ifpdistl 5120 seta[player[].i].htowner THISACTOR
			}
			else
			{
				ifvarand perks 8
				{
					hitradius 3500  30 60 90 120
					// seta[].htextra -1
					ifpdistl 3500 seta[player[].i].htowner THISACTOR
				}
				else
				ife team 1 ifpdistl 3500 ife seeplayer YES nullop else
				{
					hitradius 3500  30 60 90 120
					// seta[].htextra -1
					ifpdistl 3500 seta[player[].i].htowner THISACTOR
				}
			}

		}
	} else
	ifaction ABATLORDLAND
	{
		ifactioncount 1
		{
		    ife seemytarget YES
			ife targetalive YES
			ifl targetdist 5120 ai AIBATLORDSHOOTENEMY
			else ai AIBATLORDSEEKENEMY
		}
	}

ends

state batlordrunenemystate

  set myspawner -1
  state strafechecklong
  ifn bottarget -1 { set spriteid bottarget state facesprite }
  ifl targetdist 4096
  ife seemytarget YES
  ifrnd 8
  ife targetalive YES
  { 
    ai AIBATLORDSHOOTENEMY
    break
  }
  else
  {
    ifactioncount 4
    {
        resetactioncount
        sound BOS1_WALK
    }
  }
  ifcount 150
    ai AIBATLORDSEEKENEMY
	
   ifactornotstayput
   ifnotmoving ifrnd 32 
   ife FEMFALLDMG 0
   iffloordistl 8
   {
	ai AIBATLORDJUMP
	set FEMFALLDMG 240
   }
ends

state batlordseekenemystate

  state strafechecklong
  ifrnd 1
    soundonce BOS1_ROAM
  

  ifactioncount 4
  {
    resetactioncount
    sound BOS1_WALK
  }
  
  ifl targetdist 3072
    ife targetalive YES
	ife seemytarget YES
  {
    set myspawner -1
    ai AIBATLORDSHOOTENEMY
    break
  }
  
  ifn myspawner -1
  {
	ifactornotstayput
	iffloordistl 8
	ifn sprite[].pal 0 
	{
		ai AIBATLORDJUMP
		move BATLORDJUMPVELS geth getv
		geta[myspawner].x x2
		geta[myspawner].y y2
		sub x2 sprite[].x
		sub y2 sprite[].y
		getangle angvar x2 y2
		ifrnd 128 add angvar 512 else sub angvar 512
		seta[].ang angvar
		set dodgetime 3
		set myspawner -1
		break
	}
	set myspawner -1
  }
  
  set temp NO
  ifge SKILL 4 { ifcount 25 set temp YES }
  else ifcount 50 set temp YES

  ife seemytarget YES
  ife targetalive YES
    ife temp YES
  {
    ifrnd 32
    {
	  ifg targetdist 4096 ifl targetdist 8192
	  ifrnd 32 iffloordistl 8 ifn sprite[].pal 0 ai AIBATLORDJUMP else
	  ai AIBATLORDSHOOTENEMY	
    }
    else
      ifg targetdist 3072
        ifrnd 192
    {
      ifrnd 64
	  ifactornotstayput
	  {
	    ifrnd 64 ifl targetdist 8192 iffloordistl 8 ifn sprite[].pal 0 ai AIBATLORDJUMP else
        ai AIBATLORDRUNENEMY
	  }
      else
        ai AIBATLORDLOBBED
    }
  }
  ifai AIBATLORDSEEKENEMY ifnotmoving ifrnd 32 ife FEMFALLDMG 0 iffloordistl 8 { ai AIBATLORDJUMP set FEMFALLDMG 240 }

ends

state batlorddyingstate
  ifaction ABATLORDDEAD
  {
	  set burning 0
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
		ifspritepal 0 state standard_jibs
        killit
      }
      break
  }
  ifactioncount 5
  {
    iffloordistl 8
      sound THUD
    action ABATLORDDEAD
    cstat 0
	ifspritepal 0
	{
		setuserdef[].display_bonus_screen 1
		ife attmode YES ifl attmid 3 setp[].timebeforeexit 150 else
		ife marathon YES 
		{
			setp[].timebeforeexit 150
			set cutscene_ready 1252
		}
		else
		{
			endofgame 60
			set cutscene_ready 1252
			setuserdef[].volume_number 0
			ife attmode YES set attmid 4
		}
	}
  }
ends

state batlordlobbedstate
  
  state turntotarget
  set myspawner -1
  ife seemytarget YES
  {
    ifactioncount 2
    {
		set x2 sprite[].x
		ifspritepal 0 add x2 768 else add x2 384
		set angvar sprite[].ang

		geta[].x savx 
		geta[].y savy
		geta[].z countvar
		set z2 countvar
		sub z2 4096
		
		rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
		seta[].x x 
		seta[].y y
		seta[].z z2
		
		ife bottarget -1 
		{
			ife team 1 { ai AIBATLORDSEEKENEMY break }
			set bottarget player[].i
		}
		state calczdist
		ldist temp THISACTOR bottarget
		ifvare temp 0 setvar temp 1
		mulvar zdist 1024
		sub zdist 2048
		divvarvar zdist temp
		ifn sprite[].pal 0 
		{
			setprojectile[BIGGRENADEPROJ].extra 50
			setprojectile[BIGGRENADEPROJ].xrepeat 32
			setprojectile[BIGGRENADEPROJ].yrepeat 32
		}
		ezshoot zdist BIGGRENADEPROJ
		ifn sprite[].pal 0 
		{
			setprojectile[BIGGRENADEPROJ].extra 100
			setprojectile[BIGGRENADEPROJ].xrepeat 48
			setprojectile[BIGGRENADEPROJ].yrepeat 48
		}
		setav[RETURN].bottarget bottarget

		seta[].x savx
		seta[].y savy
		seta[].z countvar
        resetactioncount
        sound GRENADE_SHOOT  
    }
    else
      ifcount 64
        ifrnd 16
         ai AIBATLORDSEEKENEMY
  }
  else
    ai AIBATLORDSEEKENEMY
ends

state batlordshootenemy

  state turntotarget
  set myspawner -1
 
  ife targetalive NO count 80
  ifcount 72
    ai AIBATLORDSEEKENEMY
  else
    ifaction ABATLORDSHOOT
      ifactioncount 2
  {
    resetactioncount
	ifg shrunken 10 ifl shrunken SHRUNKCOUNT
	{
		sound SHELLYFIRE
		setactorsoundpitch THISACTOR BOS1_ATTACK1 SHRUNKPITCH
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		shoot SHRUNKBULLET
		break
	}
	sound SHELLYFIRE
	setactorsoundpitch THISACTOR SHELLYFIRE -512

	// geta[].x savx
	// geta[].y savy
	// set x2 sprite[].x
	// set angvar sprite[].ang
	// add angvar 384 ifspritepal 0 add x2 512 else add x2 320

	// rotatepoint sprite[].x sprite[].y x2 sprite[].y angvar x y
	// seta[].x x seta[].y y
	
	ifn bottarget -1
	{
		ifvarand initflags 32
		{
			set xvel 768
			state rpg_targetprep
			ezshoot zdist STICKYPROJ
			ifrnd 128 ezshoot zdist STICKYPROJ
		}
		else
		{
			state hitscan_targetprep
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			state hitscan_targetprep
			zshoot zdist SHOTSPARK1
			zshoot zdist SHOTSPARK1
			ifspritepal 0
			{
				state hitscan_targetprep
				zshoot zdist SHOTSPARK1
				zshoot zdist SHOTSPARK1
				zshoot zdist SHOTSPARK1
			}
		}
	}
	else
	{
		shoot SHOTSPARK1
		shoot SHOTSPARK1
		shoot SHOTSPARK1
		shoot SHOTSPARK1
		ifspritepal 0
		{
			shoot SHOTSPARK1
			shoot SHOTSPARK1
			shoot SHOTSPARK1
		}
	}
	
	// seta[].x savx
	// seta[].y savy
	
  }
ends

state checkbatlordhitstate
 
  ifrnd 2
    spawn BLOODPOOL
  ifdead
  {
    ifspritepal 0
	{
	  ife pchar 0
      globalsound DUKE_TALKTOBOSSFALL
	}
    else
    {
	  ifwasweapon FREEZEBLAST
      {
        sound NEWFREEZE
        spritepal 1
        move 0
        action ABATLORDFROZEN
        strength 0
		state freezeme
        break
      }
	  ifwasweapon GROWSPARK
		{
		  cstat 0
		  sound ACTOR_GROWING
		  ai AIBATLORDGROW
		  break
		}
	  
	  ife pchar 0
      ifrnd 64
        globalsound DUKE_TALKTOBOSSFALL
    }
    
    sound BOS1_DYING

    addkills 1
	state enemy_death
    ai AIBATLORDDYING
  }
  else
  {
    ifspritepal 0 nullop
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
	  set shrunken 1
      break
    }
    ifrnd 32
	ifn sprite[].htpicnum BURNING
	ifn sprite[].htpicnum RADWOUND
    {
      action BATLORDFLINTCH
      move 0
    }
	else
	ifg stun 0
	{
	  action BATLORDFLINTCH
      move 0
	}
	else
	ifrnd 16
	ife seemytarget YES
	{
		ifrnd 48 ifl targetdist 8192 iffloordistl 8 ifn sprite[].pal 0 ai AIBATLORDJUMP else
		ifg targetdist 4096 ifrnd 128
		ai AIBATLORDLOBBED else
		ai AIBATLORDSHOOTENEMY
	}

    soundonce BOS1_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends

state batlordcode

  ifai AIBATLORDGROW { state genericgrowcode break }

  ifaction ABATLORDFROZEN
  {
    ifcount THAWTIME
    {
      ai AIBATLORDSEEKENEMY
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
      addkills 1 state enemy_death
      state standard_jibs
	  state standard_jibs
	  lotsofglass 10
      ifrnd 84
        spawn BLOODPOOL
      sound GLASS_BREAKING
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check
    break
  }
  // al THISACTOR
  // geta[].extra temp al temp
  // geta[].htextra tempb al tempb
  // geta[].htpicnum picnum al picnum
  ifai 0
  {
	globalsound BOS1_RECOG
    cstator 257
	ifl sprite[].xrepeat 4 spawn GLARESTAR
	
    ifspritepal 0
	{
      ai AIBATLORDRUNENEMY
	  sizeat 72 60
	  clipdist 120
	}
	else
	ifspritepal 42
	{
	  strength 1500
	  orvar initflags 32
	  sizeat 36 30
	  ifcansee ai AIBATLORDSHOOTENEMY else
	  ai AIBATLORDSEEKENEMY
	  clipdist 80
	}
    else
    {
      strength 1200
	  sizeat 36 30
      ifcansee 
	  {
		set spriteid player[].i
		state facesprite
		ai AIBATLORDSHOOTENEMY 
	  }
	else
	  ai AIBATLORDSEEKENEMY
	  clipdist 80
    }
  }
  
  ifai AIBATLORDDYING
  {
    state batlorddyingstate break
  }
  
  state monsterai 
  ifg FEMFALLDMG 0 sub FEMFALLDMG 1
  ifaction BATLORDFLINTCH
  {
    ifspritepal 0 ifg stun 15 set stun 15
    ifg stun 0 resetactioncount
    ifactioncount 3
	{
		ife targetalive YES
		ife seemytarget YES
		ai AIBATLORDSHOOTENEMY
		else
		ai AIBATLORDSEEKENEMY
	}
  }
  else
    ifai AIBATLORDSEEKENEMY
      state batlordseekenemystate
  else
    ifai AIBATLORDRUNENEMY
      state batlordrunenemystate
  else
    ifai AIBATLORDJUMP
	  state batlordjumpstate
  else
    ifai AIBATLORDSHOOTENEMY
      state batlordshootenemy
  else
    ifai AIBATLORDLOBBED
      state batlordlobbedstate

	ifn sprite[].htowner -1
	ifg sprite[].htextra 0
	ife team actorvar[sprite[].htowner].team
		seta[].htextra -1
		
    ifhitweapon
      state checkbatlordhitstate
    else
      ife targetalive YES
        ifspritepal 0
          ifpdistl 1280
		  ife pdown NO
				ife spawnprotect 0
	{
	  addphealth -1000
      palfrom 63 63
	}
	else
	ife monstatus 1
	{
		ifg stun 0
		{
		  ai AIBATLORDSEEKENEMY
		  action BATLORDFLINTCH
		  move 0
		}
	}
	ifspritepal 0
	state squish_bot
  
ends

useractor enemy NEWBATLORD 6000 ABATLORDSTAND
fall state batlordcode 
enda

useractor enemystayput NEWBATLORDSTAYPUT 6000

 cactor NEWBATLORD
 ifai 0
  {
	globalsound BOS1_RECOG
    cstator 257
	
    ifspritepal 0
	{
      ai AIBATLORDSHOOTENEMY
	  sizeat 72 60
	  clipdist 120
	}
    else
    {
      strength 1200
	  sizeat 36 30
      ai AIBATLORDSHOOTENEMY
	  clipdist 80
    }
  }

enda
