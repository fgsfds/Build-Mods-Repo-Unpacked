// CANHEAD BOSS ROBOT
action ACANHEADSTAND  		0  1  5  1  10
action ACANHEADWALK   		5  4  5  1  20
action ACANSHOOTBLUE1		25 1  5  1  6
action ACANSHOOTBLUE2		30 1  5  1  6
action ACANSHOOTRED1		35 1  5  1  18
action ACANSHOOTRED2		40 1  5  1  18
action ACANHEADDYING		25 9  1  1  24
action ACANHEADDEAD			33 1  1  1  10

move CANWALKVEL 300
move CANRUNVEL 400
move CANHEADGROW

ai AICANHEADDYING ACANHEADSTAND STOPPED geth
ai AICANSHOOTRED ACANSHOOTRED2 STOPPED faceplayer
ai AICANSHOOTBLUE ACANSHOOTBLUE2 STOPPED faceplayer
ai AICANHEADWAIT ACANHEADSTAND STOPPED geth
ai AICANSEEK ACANHEADWALK CANWALKVEL seekplayer

state canheadseekstate

	ife gametype 0 state strafecheck
	
	ifactor CANHEAD
	{
		set temp NO
		ifge SKILL 4 { ifcount 50 set temp YES }
		else ifcount 90 set temp YES
		
		ife temp YES
		{
			ife targetalive YES
			ifl targetdist MAXRANGE
				ife seemytarget YES
			{
				ife mtype 0 ai AICANSHOOTBLUE
				else ai AICANSHOOTRED
			}
			ifcount 150 ife gametype 0 ai AICANHEADWAIT
		}
	}
	else ifmove CANWALKVEL move CANRUNVEL seekplayer
	
	ifactioncount 2
	{
		resetactioncount
		espawn SOUNDPLATE
		setav[RETURN].mtype 1
		setav[RETURN].initsprite -1
		setav[RETURN].SPRITELOTAG CANHEADSTEP
		seta[RETURN].cstat 32768
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			set temp NO
			ifn actorvar[spriteid].monstatus 0 { ifn actorvar[spriteid].team team set temp YES } else
			set temp YES
			
			ife temp YES
			{
				dist xydist THISACTOR spriteid
				ifle xydist 1792
				{
					seta[spriteid].htpicnum RPG
					seta[spriteid].htowner THISACTOR
					seta[spriteid].htang sprite[].ang
					seta[spriteid].htextra 40
				}
			}
			
			nextspritestat spriteid spriteid
		}
		ifpdistl 1792
		ife team 0
		{
			seta[player[].i].htpicnum RPG
			seta[player[].i].htowner THISACTOR
			seta[player[].i].htang sprite[].ang
			seta[player[].i].htextra 40
		}
		// sound CANHEADSTEP
	}
	ifg sprite[].yrepeat 48
	{
		ife targetalive YES
        ifpdistl 1280
		ife pdown NO
      {
        addphealth -1000
        palfrom 63 63
      }
	  
	  state squish_bot
	}

ends

state canshootredstate

ifaction ACANSHOOTRED2
{
	ifactioncount 1
	{
		// ife seemytarget YES
		// ifl targetdist MAXRANGE
		ifl countvarb 7
		{
			geta[].x savx
			geta[].y savy
			set x sprite[].x
			set temp sprite[].xrepeat
			mul temp 12
			add x temp
			geta[].ang angvar sub angvar 480
			rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
			seta[].x x2
			seta[].y y2
			
			eshoot RPG
			seta[RETURN].extra 100
			
			seta[].x savx
			seta[].y savy
			
			sound RPG_BIGSHOOT
			action ACANSHOOTRED1
			add countvarb 1
		}
		else
		{
			ai AICANHEADWAIT
			set countvarb 0
			set mtype 0
		}
	}
}
ifaction ACANSHOOTRED1
{
	ifactioncount 1
		action ACANSHOOTRED2
}

ends

state canshootbluestate

ifaction ACANSHOOTBLUE2
{
	ifactioncount 1
	{
		// ife seemytarget YES
		ifl targetdist MAXRANGE
		ifl countvarb 11
		{
			ife team 0 ife bottarget -1 set bottarget player[].i
			
			geta[].x savx
			geta[].y savy
			set x sprite[].x
			set temp sprite[].xrepeat
			mul temp 12
			add x temp
			geta[].ang angvar add angvar 448
			rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
			seta[].x x2
			seta[].y y2
			
			set xvel 844
			state rpg_targetprep
			ezshoot zdist BIGPLASMA
			
			set xvel 844
			state rpg_targetprep
			ezshoot zdist BIGPLASMA
			
			set xvel 844
			state rpg_targetprep
			ezshoot zdist BIGPLASMA
			
			seta[].x savx
			seta[].y savy
			
			sound LASERSHOOT
			action ACANSHOOTBLUE1
			add countvarb 1
		}
		else
		{
			ai AICANHEADWAIT
			set countvarb 0
			set mtype 1
		}
	}
}
ifaction ACANSHOOTBLUE1
{
	ifactioncount 1
		action ACANSHOOTBLUE2
}

ends

state canheadwaitstate

	ifmove CANHEADGROW // growing from beam
	{
		seta[].htextra -1
		ife PROGRESSION YES { set inithp 10000 strength 10000 } else
		strength 5000
		ifspritepal 0 spritepal 121 else spritepal 0
		ifvarand player[].player_par 1 sizeto 80 80 
		ifactorsound THISACTOR ELECTRICLOOP nullop else sound ELECTRICLOOP
		ife sprite[].xrepeat 80 ifspritepal 0 
		{ 
			ai AICANSEEK sound CANHEADSTEP 
			stopactorsound THISACTOR ELECTRICLOOP 
			setav[myspawner].monstatus 2
		}
		break
	}
	ifcount 30
	{
		ifactor CANHEAD
		ifrnd 128
		ife targetalive YES
			ifl targetdist MAXRANGE
				ife seemytarget YES
		{
			ife mtype 0 ai AICANSHOOTBLUE
			else ai AICANSHOOTRED
		}
		else 
		{
			ai AICANSEEK
			sound CANHEADSTEP
		}
	}
	

ends

state canheaddyingstate

	ifaction ACANHEADSTAND
	{
		ifcount 90 { action ACANHEADDYING sound CANHEADFALL }
		else
		{
			rand angvar 2048
			
			set x sprite[].x
			add x 640
			geta[].z savz
			sub savz 16384
			seta[].z savz
			ifrnd 128
			debris SCRAP1 1
			else
			debris SCRAP2 1
			ifrnd 32
			{
				espawn EXPLOSION2
				rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
				setsprite RETURN x2 y2 savz
				ifrnd 128
				sound PIPEBOMB_EXPLODE
				else
				sound ROBOT_EXPLODE
			}
			add savz 16384
			seta[].z savz
		}
	}
	ifaction ACANHEADDYING
	{
		ifactioncount 8
		{
			action ACANHEADDEAD
			resetcount
			ifhitweapon { }
			strength 0
			cstat 0
		}
		else
		ifactioncount 5 nullop else
		ifactioncount 4 { ifactorsound THISACTOR SQUISHED4 nullop else sound SQUISHED4 }
	}
	ifaction ACANHEADDEAD
	{
		ifspritepal 0
		{
			ifcount 30
			{
				ifg sprite[].xrepeat 32
				{
					sizeto 32 32
					soundonce ACTOR_SHRINKING
				}
				
				ifcount 91 nullop else
				ifcount 90
				{
					// globalsound VACA_ENDMUSIC
					set midscreen DC_ENDCUT
					set cutend totalclock
					add cutend 9999
					set cutmash 2
					startscreen
					
					set midscreen 10893
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					
					set midscreen 10894
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					
					set midscreen 10895
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					
					set midscreen 10896
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					
					set midscreen 10897
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					
					set midscreen 10898
					set cutmash 4
					set cutend totalclock
					add cutend 9999
					startscreen
					al VOLUME
					endofgame 10
				}
			}
		}
		else
		ifhitweapon
		{
			set B NO
			ifwasweapon RPG set B YES
			ifwasweapon RADIUSEXPLOSION set B YES
			ife B YES
			{
			sound PIPEBOMB_EXPLODE
			sound ROBOT_EXPLODE
			debris SCRAP1 8
			debris SCRAP2 8
			debris SCRAP6 8
			state tech_debris
			spawn EXPLOSION2
			killit
			}
		}
		
	}

ends

state canheadcode

	ifmove 0
	{
		cstator 257
		ai AICANHEADWAIT
		ifl sprite[].xrepeat 4 spawn GLARESTAR
		ifspritepal 0 
		{
			sizeat 26 26
			move CANHEADGROW
			espawn 581
			setav[RETURN].myspawner THISACTOR
			set myspawner RETURN
		}
		else ifspritepal 21
		{
			sizeat 42 40
			clipdist 96
			strength 1500
		}
		else
		{
			sizeat 64 64
			clipdist 128
			strength 5000
		}
	}
	
	// ifactor CANHEAD
	// ifhitspace ifcount 10
	// {
		// resetcount
		// ifaction ACANHEADSTAND action ACANHEADWALK else
		// ifaction ACANHEADWALK action ACANSHOOTBLUE else
		// ifaction ACANSHOOTBLUE action ACANSHOOTRED else
		// ifaction ACANSHOOTRED { cactor CANHEADNOARMS action ACANHEADSTAND }
	// }
	
	// ifactor CANHEADNOARMS
	// ifhitspace ifcount 10
	// {
		// resetcount
		// ifaction ACANHEADSTAND action ACANHEADWALK else
		// ifaction ACANHEADWALK action ACANHEADDYING else
		// ifaction ACANHEADDYING action ACANHEADDEAD else
		// ifaction ACANHEADDEAD { cactor CANHEAD action ACANHEADSTAND }
	// }
	fall
	state monsterai
	
	ifai AICANHEADDYING { state canheaddyingstate break }
	
	ifai AICANHEADWAIT state canheadwaitstate else
	ifai AICANSEEK state canheadseekstate else
	ifai AICANSHOOTRED state canshootredstate else
	ifai AICANSHOOTBLUE state canshootbluestate
	
	ifstrength 1000
	{
		ifrnd 12
		{
			espawn BIGSMOKE
			set z2 sprite[].z
			sub z2 16384
			seta[RETURN].z z2
			seta[RETURN].htpicnum RECONWRECK
			rand x 512
			sub x 256
			add x sprite[].x
			seta[RETURN].x x
			rand y 512
			sub y 256
			add y sprite[].y
			seta[RETURN].y y
		}
	}
	
	ifhitweapon
	{
		ifactor CANHEAD
		{
			ifdead
			{
				rand zdist 1024
				sub zdist 3500
				ezshoot zdist REDARMPROJ
				rand angvar 256
				sub angvar 640
				add angvar sprite[].ang
				seta[RETURN].ang angvar
				seta[RETURN].pal sprite[].pal
				ifn sprite[].pal 0
				{
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				}
				rand zdist 1024
				sub zdist 3500
				ezshoot zdist BLUEARMPROJ
				rand angvar 256
				add angvar 640
				add angvar sprite[].ang
				seta[RETURN].ang angvar
				seta[RETURN].pal sprite[].pal
				ifn sprite[].pal 0
				{
				seta[RETURN].xrepeat sprite[].xrepeat
				seta[RETURN].yrepeat sprite[].yrepeat
				}

				rand angvar 2048
				set countvar 0
				sound PIPEBOMB_EXPLODE
				sound ROBOT_EXPLODE
				set x sprite[].x
				add x 640
				geta[].z savz
				sub savz 16384
				seta[].z savz
				debris SCRAP1 8
				debris SCRAP2 8
				state tech_debris
				whilevarn countvar 4
				{
					espawn EXPLOSION2
					rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
					setsprite RETURN x2 y2 savz
					add angvar 512
					add countvar 1
				}
				add savz 16384
				seta[].z savz
				ifl SPRITELOTAG 10 seta[].htactorstayput -1
				cactor CANHEADNOARMS
				ifvarand monstflags 131072
				{
					ai AICANHEADDYING
					quake 90
					addkills 1
					state enemy_death
					break
				}
				strength 1500
				ifspritepal 0 ife PROGRESSION YES
				{
					addstrength 1000
					set inithp sprite[].extra
					orvar monstflags 512
					espawn SIDEGUN
					setav[RETURN].myspawner THISACTOR
					setav[RETURN].botclip -384
					rand safecount 5
					setav[RETURN].mtype safecount
					espawn SIDEGUN
					setav[RETURN].myspawner THISACTOR
					setav[RETURN].botclip 384
					setav[RETURN].mtype safecount
					ife gametype DM setav[RETURN].teamspawned 99999
				}
				ai AICANHEADWAIT
				
				ifspritepal 21 strength 1000
				ifg SPRITELOTAG 9
					state activatechannel
				break
			}
			else
			ifai AICANSEEK
			ife seemytarget YES
			ifn actorvar[sprite[].htowner].team team
			ifrnd 64
			{
				ife mtype 0 ai AICANSHOOTBLUE
				else ai AICANSHOOTRED
			}
		}
		else
		ifdead
		{
			ai AICANHEADDYING
			quake 90
			addkills 1
			state enemy_death
		}
	}
ends

useractor enemy CANHEAD 5000 state canheadcode enda

useractor enemy CANHEADNOARMS 1500 state canheadcode enda

action AREACTORSPARKF -1  1  1  1  1
useractor notenemy 581 0

ifn myspawner -1
{
	ifmove 0
	{
		seta[].blend 1
		cstator 2
		ifrnd 128 cstator 4
		move STOPPED
		action AREACTORSPARKF
		sizeat 128 128
		ifceilingdistl 128 nullop else
		{
			espawn 581
			setav[RETURN].myspawner THISACTOR
			geta[RETURN].z z sub z 16384 seta[RETURN].z z
			set myvictim RETURN
		}
	}
	ifceilingdistl 128 spawn SPARKFALL
	ife monstatus 2 { ifn myvictim -1 setav[myvictim].monstatus 2 killit }
	ife sprite[myspawner].statnum 1024 killit
}

enda

eventloadactor 10900 geta[].z initsprite enda
useractor notenemy 10900 seta[].z initsprite enda
