// DR PROTON
// SPRITES BY SEBABDUKEBOSS20, CODE BY DAN GASKILL

define MAXSPIDERTURN 24

spriteflags PROTONSTAND 8192
spriteflags DRPROTON 8192
spriteflags PSPIDER 1056768
spriteflags PSPIDERJUMPING 1056768

action APROTONSTAND  -5  1  5  1  10
action APROTONFROZEN  -5  1  5  1  10
action APROTONWALK    0  4  5  1  12
action APROTONAIM	  20 1  5  1  8
action APROTONFIRE	  25 1  5  1  8
action APROTONAIM2	  30 1  5  1  20
action APROTONFIRE2	  35 1  5  1  12
action APROTONHIT	  40 1  5  1  24
action APROTONJUMP	  45 1  5  1  20
action APROTONLAND	  50 1  5  1  20
action APROTONDYING   55 5  1  1  24
action APROTONDEAD    59 1  1  1  10

move PROTONWALKVEL 208


ai AIPROTONSTAND APROTONSTAND STOPPED geth
ai AIPROTONFROZEN APROTONSTAND STOPPED geth
ai AIPROTONWALK APROTONWALK PROTONWALKVEL seekplayer
ai AIPROTONSHOOT APROTONAIM STOPPED faceplayersmart
ai AIPROTONCHEST APROTONAIM2 STOPPED faceplayer
ai AIPROTONHIT APROTONHIT STOPPED geth
ai AIPROTONJUMP APROTONJUMP PROTONWALKVEL jumptoplayer
ai AIPROTONDYING APROTONDYING STOPPED geth

state killplayertaunt

	ifl countvarb 0 { add countvarb 1 break }
	ifp palive nullop else
	{
		stopallsounds
		globalsound PROT_COMB6
		set countvarb -1000
	}

ends

state protonroamsounds

	ifp palive
	ifrnd 1
	ifpdistl 16384
	{
		ifsound PROT_SEEK1 nullop else
		ifsound PROT_SEEK2 nullop else
		ifsound PROT_COMB2 nullop else
		ifsound PROT_COMB4 nullop else
		ifsound PROT_COMB5 nullop else
		ifsound PROT_COMB7 nullop else
		ifsound PROT_TAUNT1 nullop else
		ifsound PROT_TAUNT2 nullop else
		{
			rand temp 3
			ife gametype 0
			{
				ife temp 0 globalsound PROT_TAUNT1 else
				ife temp 1 globalsound PROT_TAUNT2 else
				ife temp 2 globalsound PROT_COMB2 else
				ife temp 3 globalsound PROT_COMB4	
			}
			else
			{
				ife temp 0 sound PROT_TAUNT1 else
				ife temp 1 sound PROT_TAUNT2 else
				ife temp 2 sound PROT_COMB2 else
				ife temp 3 sound PROT_COMB4
			}
		}
	}

ends

state protonstandstate

	ifg cutcamtime 0
	ifn cutcam -1
		break
		
	ifcount 8
	{
		findplayer xydist
		set xydist2 20480
		sub xydist2 xydist
		shiftr xydist2 11
		rand temp 100
		ifl temp xydist2
		{
			ifcansee
				ifcanshoottarget
					ifp palive
			{
				ifrnd 85 ai AIPROTONCHEST else
				ai AIPROTONSHOOT
				
			}
		}
	}
	ifcount 20 ai AIPROTONWALK
	
ends

state protonfrozenstate

	ifsquished	
	{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

	 ifcount THAWTIME
    {
      ai AIPROTONSTAND
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  

      
	  lotsofglass 30
      
      sound GLASS_BREAKING
	  ai AIPROTONDYING
      
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check

ends

state protonwalkstate
	ifcount 30
	{
		findplayer xydist
		set xydist2 20480
		sub xydist2 xydist
		shiftr xydist2 10
		rand temp 100
		ifl temp xydist2
		{
			ifcansee
				ifcanshoottarget
					ifp palive
			{
				ifrnd 85 ai AIPROTONCHEST else
				ai AIPROTONSHOOT
			}
		}
	}
	ifnotmoving ifrnd 32 ai AIPROTONJUMP
	ifcount 300 ai AIPROTONSTAND
ends

state protonshootstate
	ifaction APROTONAIM
	{
		ifactioncount 1
		{
			action APROTONFIRE
			sound LASERSHOOT
			geta[].z savz
			sub savz 3072
			seta[].z savz
			eshoot FIRELASER3
			add savz 3072
			seta[].z savz
			add botclip 1
			ifg botclip 5
			{
				set botclip 0
				ai AIPROTONSTAND
			}
		}
	}
	ifaction APROTONFIRE
	{
		ifactioncount 1
		action APROTONAIM
	}
	
ends

state protoncheststate

	ifaction APROTONAIM2
	{
		ifactioncount 1
		{
			action APROTONFIRE2
			move STOPPED geth
		}
	}
	ifaction APROTONFIRE2
	{
		seta[].shade -100
		ifactioncount 3
		{
			geta[].z savz
			sub savz 10240
			seta[].z savz
			spawn INFERNO
			add savz 10240
			seta[].z savz
			ai AIPROTONSTAND
		}
	}
	
ends

state protonhitstate
	
	ifcount 6 nullop else 
	ifge SKILL 4 count 6
	
	ifcount 10
	{
		ifrnd 96
		ifcansee
			ifcanshoottarget
		{
			ifrnd 85 ai AIPROTONCHEST else
			ai AIPROTONSHOOT
		}
		else ai AIPROTONWALK
	}
ends

state protonjumpstate
	ifaction APROTONJUMP
	{
		ifactioncount 2
		action APROTONLAND
	}
	ifaction APROTONLAND
	{
		iffloordistl 16 
		{
			sound SPIDERLAND
			ai AIPROTONWALK
		}
	}
ends

state protondyingstate

	ifaction APROTONDYING
	{
		ifactioncount 4
		{
			action APROTONDEAD
			// SOUND
			cstat 0
		}
		break
	}
ends

useractor enemy PROTONSTAND DRPROTONSTRENGTH cactor DRPROTON enda

useractor enemy DRPROTON DRPROTONSTRENGTH 

fall
ifai 0
{
	sizeat 42 38
	clipdist 64
	cstator 257
	ifcansee
	ai AIPROTONSTAND
	else
	{
		ifhitweapon { }
		strength DRPROTONSTRENGTH
	}
}
ifai AIPROTONFROZEN { state protonfrozenstate break }
ifai AIPROTONDYING { state protondyingstate break }
ifai AIPROTONSTAND state protonstandstate else
ifai AIPROTONWALK state protonwalkstate else
ifai AIPROTONSHOOT state protonshootstate else
ifai AIPROTONCHEST state protoncheststate else
ifai AIPROTONHIT state protonhitstate else
ifai AIPROTONJUMP state protonjumpstate

state killplayertaunt

ifaction APROTONWALK
{
	state protonroamsounds
	ife mtype 0 // first step
	{
		ifactioncount 1
		{
			sound TERMSTEP1
			set mtype 1
		}
	}
	else ife mtype 1
	{
		ifactioncount 2
		{
			sound SERVOSND
			set mtype 2
		}
	}
	else ife mtype 2
	{
		ifactioncount 3
		{
			sound TERMSTEP2
			set mtype 3
		}
	}
	else ife mtype 3
	{
		ifactioncount 4
		{
			set mtype 0
			resetactioncount
			sound SERVOSND
		}
	}
}


// ifcount 10
// ifhitspace
// {
	// resetcount
	// ifaction APROTONSTAND action APROTONWALK else
	// ifaction APROTONWALK action APROTONAIM else
	// ifaction APROTONAIM action APROTONFIRE else
	// ifaction APROTONFIRE action APROTONAIM2 else
	// ifaction APROTONAIM2 action APROTONFIRE2 else
	// ifaction APROTONFIRE2 action APROTONHIT else
	// ifaction APROTONHIT action APROTONJUMP else
	// ifaction APROTONJUMP action APROTONLAND else
	// ifaction APROTONLAND action APROTONDYING else
	// ifaction APROTONDYING action APROTONDEAD else
	// ifaction APROTONDEAD action APROTONSTAND
// }
state monsterai

ifhitweapon
{
    // ifwasweapon SHRINKSPARK
	// {
		  // sound ACTOR_SHRINKING
		  // set shrunken 1
		  // break
	// }
	// else
        ifwasweapon GROWSPARK
          sound SPARKLESOUND
	
	ifdead
	{
		// ifwasweapon FREEZEBLAST
		// {
		  // sound NEWFREEZE
		  // spritepal 1
		  // ai AIPROTONFROZEN
		  // strength 0
		  // state freezeme
		  // setp[].sound_pitch 0
		  // break
		// }
		ai AIPROTONDYING
		addkills 1 
		espawn PROTONTOP
		ifg SPRITELOTAG 9 setav[RETURN].SPRITELOTAG SPRITELOTAG
		set SPRITELOTAG 0
	    state enemy_death
		sound PROT_BEATFORM
		sound PROT_LEGCRASH
		
		geta[].z z
		sub z 7500
		seta[RETURN].z z
		break
	}
	
	ifai AIPROTONHIT nullop else
	ifg stun 0 ai AIPROTONHIT
	
	ifai AIPROTONHIT nullop else
	// ifai AIPROTONSHRUNK nullop else
	ifp palive
	{
		ifsound PROT_PAIN1 nullop else
		ifsound PROT_PAIN2 nullop else
		{
			ifrnd 128 sound PROT_PAIN1
			else sound PROT_PAIN2
		}
		ifrnd 16
		ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
		{
			
			
			ifsound PROT_SEEK1 nullop else
			ifsound PROT_SEEK2 nullop else
			ifsound PROT_COMB2 nullop else
			ifsound PROT_COMB4 nullop else
			ifsound PROT_COMB5 nullop else
			ifsound PROT_COMB7 nullop else
			ifsound PROT_TAUNT1 nullop else
			ifsound PROT_TAUNT2 nullop else
			ifn sprite[].htpicnum SHOTSPARK1
			ifp palive
			{
				stopsound PROT_PAIN1
				stopsound PROT_PAIN2
				ife gametype 0
				globalsound PROT_COMB5
				else sound PROT_COMB5
			}
			ai AIPROTONHIT 
			break 
		}
		ifrnd 64 
		{
			
			ifai AIPROTONSHOOT nullop else 
			ifai AIPROTONCHEST nullop else
			{
				findnearactor INFERNO MAXRANGE spriteid
				ife spriteid -1
				ifrnd 64 ai AIPROTONCHEST else
				ai AIPROTONSHOOT
			}
		}
	}
}

ifp palive
ife bottarget -1
{
	ifcansee set countvarb 0
	else 
		add countvarb 1
		
	ifg countvarb 60
	{
		ifrnd 3
		ifpdistl 49152
		{
			ifsound PROT_SEEK1 nullop else
			ifsound PROT_SEEK2 nullop else
			ifsound PROT_COMB2 nullop else
			ifsound PROT_COMB4 nullop else
			ifsound PROT_COMB5 nullop else
			ifsound PROT_COMB6 nullop else
			ifsound PROT_COMB7 nullop else
			ifsound PROT_TAUNT1 nullop else
			ifsound PROT_TAUNT2 nullop else
			{
				ife gametype 0
				{
					ifrnd 84 globalsound PROT_COMB7 else
					ifrnd 128 globalsound PROT_SEEK1 else
					globalsound PROT_SEEK2
				}
				else
				{
					ifrnd 84 sound PROT_COMB7 else
					ifrnd 128 sound PROT_SEEK1 else
					sound PROT_SEEK2
				}
				set countvarb -180
			}
		}
	}
}

enda

action ASPIDERSTAND  0  1  5  1  20
action ASPIDERFROZEN  0  1  5  1  10
action ASPIDERSHOOT1  0  1  5  1  6
action ASPIDERSHOOT2  5  1  5  1  6 // his gun
action ASPIDERWALK  0  4  5  1  10 // referenced from pspiderjumping actor
action ASPIDERJUMP 0  1  5  1  20 // referenced from pspiderjumping actor
action ASPIDERLAND 15  1  5  1  20
action ASPIDERAIM  30  1  5  1  20
action ASPIDERLOB2  35  1  5  1  16 // spider cannon
action ASPIDERLOB  40  1  5  1  12 
action ASPIDERLOB2F  35  1  5  1  4 // spider cannon
action ASPIDERLOBF  40  1  5  1  3
action ASPIDERDIE   45  6  1  1  50 
action ASPIDERDEAD  50  1  1  1  10

move SPIDJUMPVEL 480
move SPIDLANDVEL 400
move SPIDRUNVEL 320

ai AISPIDERTURN ASPIDERWALK STOPPED geth
ai AISPIDERWAIT ASPIDERSTAND STOPPED geth
ai AISPIDERSHOOT ASPIDERSHOOT1 STOPPED geth
ai AISPIDERLOB ASPIDERLOB STOPPED geth
ai AISPIDERJUMP ASPIDERJUMP SPIDJUMPVEL jumptoplayer
ai AISPIDERRUN ASPIDERWALK SPIDRUNVEL faceplayerslow
ai AISPIDERDYING ASPIDERDIE STOPPED geth
ai AISPIDERFROZEN ASPIDERFROZEN STOPPED geth

state spiderfrozenstate

	ifsquished	
	{ geta[].htextra temp add temp 10 seta[].htextra temp seta[].htpicnum RPG }

	 ifcount THAWTIME
    {
      ai AISPIDERWAIT
	  // sound TERMON
      getlastpal
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }
    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
	    resetcount resetactioncount
        strength 0
        break
      }
	  

      
	  lotsofglass 30
      
      sound GLASS_BREAKING
	  addkills 1 
	  state enemy_death
	  ifrnd 128 sound PROT_DEATH1 else sound PROT_DEATH2
	  getlastpal
	  ai AISPIDERDYING
	  break
      
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        state pkick_check

ends

state spiderrunstate

	ifcount 60
	{
		cactor PSPIDER
		// ai AISPIDERWAIT
		ai AISPIDERTURN
	}
	ifcount 10
	ifnotmoving
	ifrnd 96 ai AISPIDERJUMP

ends

state spiderturn

	seta[].ang lastang
	geta[bottarget].x x2
	geta[bottarget].y y2
	sub x2 sprite[].x
	sub y2 sprite[].y
	getangle initsprite x2 y2
	
	getincangle temp sprite[].ang initsprite
	
	ifn sprite[].ang initsprite
	{
		geta[].ang angvar
		getincangle temp angvar initsprite
		set B MAXSPIDERTURN
		mul B -1
		ifg temp B ifl temp MAXSPIDERTURN
			seta[].ang initsprite
		else
		{
			ifl temp 0 sub angvar MAXSPIDERTURN
			else add angvar MAXSPIDERTURN
			seta[].ang angvar
		}
		set lastang sprite[].ang
	}
	
	ifl temp 0 mul temp -1

ends

state pspidershoot // proton firing his gun

	ifaction ASPIDERSHOOT1
	{
		ifactioncount 1
		{
			action ASPIDERSHOOT2
		}
	}
	ifaction ASPIDERSHOOT2
	{
		ifactioncount 2
		{
			resetactioncount
			add botclip 1
			ifg botclip 5
			{
				set botclip 0
				add mtype 1
				ifg mtype 3 set mtype 0
				ai AISPIDERWAIT
				break
			}
			else 
			{
				sound LASERSHOOT
				action ASPIDERSHOOT2
				
				geta[].x savx
				geta[].y savy
				set x sprite[].x
				set temp sprite[].xrepeat
				mul temp 10
				add x temp
				geta[].ang angvar
				rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
				seta[].x x2
				seta[].y y2

				
				geta[].z savz
				sub savz 3072
				seta[].z savz
				
				orvar monstflags 128
				
				eshoot FIRELASER3
				add savz 3072
				seta[].z savz
				
				seta[].x savx
				seta[].y savy
				
				geta[RETURN].ang angvar
				rand temp 32
				sub temp 16
				add angvar temp
				seta[RETURN].ang angvar
				
				xorvar monstflags 128
			}
		}
	}
	

ends

state pspiderlob

	ifaction ASPIDERLOBF
	{
		ifactioncount 1
		{
			add botclip 1
			ifg botclip 9
			{
				set botclip 0
				add mtype 1
				ifg mtype 3 set mtype 0
				ai AISPIDERWAIT
				break
			}
			else
			{
				action ASPIDERLOB2F
				sound M4FIRE
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
				shoot SHOTSPARK1
			}
		}
	}
	ifaction ASPIDERLOB2F
	{
		ifactioncount 1 action ASPIDERLOBF
	}
	ifaction ASPIDERLOB
	{
		ifactioncount 1
		{
			add botclip 1
			
			ifg botclip 3
			{
				set botclip 0
				add mtype 1
				ifg mtype 3 set mtype 0
				ai AISPIDERWAIT
				break
			}
			else
			{
				sound FLAMER_SHOOT
				action ASPIDERLOB2
				geta[].z savz
				sub savz 8192
				seta[].z savz
				
				geta[].x savx
				geta[].y savy

				ife countvarc 0
				{
					set x sprite[].x
					set temp sprite[].xrepeat
					mul temp 16
					add x temp
					geta[].ang angvar, sub angvar 256
					rotatepoint sprite[].x sprite[].y x sprite[].y angvar x2 y2
					setsprite THISACTOR x2 y2 savz
				
					ifn bottarget -1 dist zdist THISACTOR bottarget else
					findplayer zdist
					shiftr zdist 2
					setprojectile[RADPROJ].vel 708
					
					eshoot RADPROJ
					seta[RETURN].ang sprite[].ang
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
					
					geta[].ang angvar
					sub angvar 32
					eshoot RADPROJ
					seta[RETURN].ang angvar
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
					add angvar 64
					
					eshoot RADPROJ
					seta[RETURN].ang angvar
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
					
					setprojectile[RADPROJ].vel 512
					
					seta[].x savx
					seta[].y savy
				}
				else
				ifspritepal 46
				{
					set xvel 844
					state rpg_targetprep
					ezshoot zdist FREEZEBOLT
					ezshoot zdist FREEZEBOLT
					ezshoot zdist FREEZEBOLT
					geta[RETURN].ang angvar
					sub angvar 20
					seta[RETURN].ang angvar
					ezshoot zdist FREEZEBOLT
					geta[RETURN].ang angvar
					add angvar 40
					seta[RETURN].ang angvar
				}
				else
				{
					ifn bottarget -1 dist zdist THISACTOR bottarget else
					findplayer zdist
					shiftr zdist 2

					eshoot FTPROJ
					seta[RETURN].ang sprite[].ang
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
					
					geta[].ang angvar
					sub angvar 20
					eshoot FTPROJ
					seta[RETURN].ang angvar
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
					add angvar 40
					
					eshoot FTPROJ
					seta[RETURN].ang angvar
					geta[RETURN].zvel z
					sub z zdist
					seta[RETURN].zvel z
				}
				add savz 8192
				seta[].z savz
			}
			
		}
	}

	ifaction ASPIDERLOB2
	{
		ifactioncount 1 action ASPIDERLOB
	}
	

ends

state spiderjumpstate

	ifaction ASPIDERJUMP
	{
		ifactioncount 3 
		{
			action ASPIDERLAND
			move SPIDLANDVEL geth
		}
		geta[].zvel z
		sub z 256 seta[].zvel z
	}
	else
	{
		iffloordistl 16 
		{
			sound SPIDERLAND
			cactor PSPIDER
			ai AISPIDERWAIT
		}
	}

ends

state pspidercode

	ifai 0
	{
		ifl sprite[].xrepeat 4 spawn GLARESTAR
		sizeat 40 36
		clipdist 128
		cstator 257
		fall
		ai AISPIDERWAIT
		ifspritepal 3 { strength 2500 sizeat 32 30 clipdist 80 }
		ifspritepal 46 { strength 2000 sizeat 32 30 clipdist 80 }
	}
	fall
	state monsterai
	
	ifai AISPIDERDYING
	{
		ifaction ASPIDERDIE
		{
			ifactioncount 5
			{
				action ASPIDERDEAD
				seta[].htextra -1
				strength 0
				cstat 0
				globalsound THUD
				resetcount
			}
		}
		ifaction ASPIDERDEAD
		{
			ifspritepal 0
			{
				ifcount 10 nullop
				else ifcount 9
				{
					stopallsounds
					starttrack 10
					screensound PROT_DEATH_MONO
					set midscreen PROTON_DEATHCUT
					set cutend totalclock
					add cutend 5700
					startscreen
					set midscreen 8866
					set cutmash 4
					set cutend totalclock
					add cutend 2600
					stopsound PROT_DEATH_MONO
					starttrackslot 0 0
					startscreen
					set midscreen 8870
					set cutmash 5
					set cutend totalclock
					add cutend 2600
					startscreen
					endofgame 10
				}
			}
			else
			{
				strength 0
				ifhitweapon
				{
					sound SQUISHED
					sizeat 24 24
					debris SCRAP1 10
					debris SCRAP2 10
					debris SCRAP3 10
					debris SCRAP6 10
					state tech_debris
					state tech_debris
					state standard_jibs
					state standard_jibs
					killit
				}
			}
		}
		break
	}
	ifai AISPIDERFROZEN
	{
		state spiderfrozenstate
		break
	}
	state killplayertaunt
	ife myshelly -1
	getp[].i bottarget
	else
	{
		dist temp THISACTOR player[].i
		dist tempc THISACTOR myshelly
		ifvarvarl tempc temp
		{
			geta[].z z
			subvar z 8192
			seta[].z z
			geta[myshelly].z tempb
			subvar tempb 8192
			seta[myshelly].z tempb
			canseespr THISACTOR myshelly temp
			add z 8192
			seta[].z z
			add tempb 8192
			seta[myshelly].z tempb
			ife temp YES
				set bottarget myshelly
			set bottarget player[].i
		}
		set bottarget player[].i
	}
	
	
	ifai AISPIDERTURN
	{
		state spiderturn
		ifcount 10
		ifl temp 16
		{
			ifcansee
			{
				ifrnd 64
				ifpdistl 10240
				ifpdistg 3072
				{
					sound SPIDERJUMP
					cactor PSPIDERJUMPING
					ai AISPIDERJUMP
					ifspritepal 0
					{
						espawn ROLLYTURRET
						seta[RETURN].pal 19
					}
				}
				else
				{
					ife mtype 0
					{
						ai AISPIDERLOB
						ife countvarc 0 set countvarc 1 else set countvarc 0
					}
					else
					ife mtype 1
					ai AISPIDERSHOOT
					else
					ife mtype 2
					{
						ai AISPIDERLOB
						action ASPIDERLOBF
					}
					else
					ife mtype 3
					ai AISPIDERSHOOT
				}
			}
			else ai AISPIDERWAIT
		}
	}
	
	ifai AISPIDERSHOOT
		state pspidershoot
	else
	ifai AISPIDERLOB
		state pspiderlob
	else
	ifai AISPIDERJUMP
		state spiderjumpstate
	else
	ifai AISPIDERRUN
		state spiderrunstate
	else
	ifai AISPIDERWAIT
	{
		ifcount 10 nullop else
		ifge SKILL 4 count 10
		
		ifcount 14
		{
			geta[bottarget].x x2
			geta[bottarget].y y2
			sub x2 sprite[].x
			sub y2 sprite[].y
			getangle initsprite x2 y2
			
			getincangle temp sprite[].ang initsprite
			ifl temp 0 mul temp -1
			ifl temp 16 
			{
				ifcansee
				{
					ifrnd 64
					ifpdistl 10240
					ifpdistg 3072
					{
						cactor PSPIDERJUMPING
						ai AISPIDERJUMP
						ifspritepal 0
						{
							espawn ROLLYTURRET
							seta[RETURN].pal 19
						}
					}
					else
					{
						ife mtype 0
						ai AISPIDERLOB
						else
						ife mtype 1
						ai AISPIDERSHOOT
						else
						ife mtype 2
						{
							ai AISPIDERLOB
							action ASPIDERLOBF
						}
						else
						ife mtype 3
						ai AISPIDERSHOOT
					}
				}
				else 
				{
					cactor PSPIDERJUMPING
					ai AISPIDERRUN
				}
			}
			else 
			{
				ifrnd 96
				{
					cactor PSPIDERJUMPING
					ai AISPIDERRUN
				}
				else
				ai AISPIDERTURN
			}
		}
		// else // jump check
		// ifcount 8
		// {
			// ifrnd 12
			// ifpdistl 10240
			// ifpdistg 3072
			// ifcansee
			// {
				// sound SPIDERJUMP
				// cactor PSPIDERJUMPING
				// ai AISPIDERJUMP
			// }
			// else
			// ifrnd 12
			// {
				// cactor PSPIDERJUMPING
				// ai AISPIDERRUN
			// }
		// }
	}
	
	ifaction ASPIDERWALK
	{
		state protonroamsounds
		add float 1
		ifg float 5
		{
			sound SPIDERSTEP
			set float 0
		}
	}
	
	// make sure the code below does not trigger when dying
	ifp palive
        ifspritepal 0
          ifpdistl 1280
			ife pdown NO
    {
      addphealth -1000
      palfrom 63 63
    }
	state squish_bot
	
	ifhitweapon
	{
		// ifwasweapon SHRINKSPARK
		// {
			  // sound ACTOR_SHRINKING
			  // set shrunken 1
			  // break
		// }
		// else
			ifwasweapon GROWSPARK
			  sound SPARKLESOUND
		
		ifdead
		{
			cactor PSPIDER
			ifwasweapon FREEZEBLAST
			{
			  sound NEWFREEZE
			  spritepal 1
			  ai AISPIDERFROZEN
			  strength 0
			  state freezeme
			  setp[].sound_pitch 0
			  break
			}
			ai AISPIDERDYING
			addkills 1 
			state enemy_death
			ifrnd 128 sound PROT_DEATH1 else sound PROT_DEATH2
			break
		}

		ifsound PROT_PAIN1 nullop else
		ifsound PROT_PAIN2 nullop else
		{
			ifrnd 128 sound PROT_PAIN1
			else sound PROT_PAIN2
		}
		ifsound PROT_SEEK1 nullop else
		ifsound PROT_SEEK2 nullop else
		ifsound PROT_COMB2 nullop else
		ifsound PROT_COMB4 nullop else
		ifsound PROT_COMB5 nullop else
		ifsound PROT_COMB7 nullop else
		ifsound PROT_TAUNT1 nullop else
		ifsound PROT_TAUNT2 nullop else
		ifrnd 16
		ifn sprite[].htpicnum SHOTSPARK1
		ifn sprite[].htpicnum BURNING
	    ifn sprite[].htpicnum RADWOUND
		ifp palive
		{
			stopsound PROT_PAIN1
			stopsound PROT_PAIN2
			{
				ifg gametype 0 sound PROT_COMB5
				else
				globalsound PROT_COMB5
			}
		}

		
	}

	ifp palive
	{
		ifcansee set countvarb 0
		else 
		{
			add countvarb 1
			ifn myshelly -1 ife bottarget myshelly set countvarb 0
		}
		ifg countvarb 60
		{
			ifrnd 3
			ife gametype 0
			{
				ifsound PROT_SEEK1 nullop else
				ifsound PROT_SEEK2 nullop else
				ifsound PROT_COMB2 nullop else
				ifsound PROT_COMB4 nullop else
				ifsound PROT_COMB5 nullop else
				ifsound PROT_COMB6 nullop else
				ifsound PROT_COMB7 nullop else
				ifsound PROT_TAUNT1 nullop else
				ifsound PROT_TAUNT2 nullop else
				{
					ifrnd 84 globalsound PROT_COMB7 else
					ifrnd 128 globalsound PROT_SEEK1 else
					globalsound PROT_SEEK2
					set countvarb -180
				}
			}
		}
	}

ends

useractor enemy PSPIDER PROTONSPIDERSTRENGTH

	state pspidercode

enda

useractor enemy PSPIDERJUMPING PROTONSPIDERSTRENGTH

	state pspidercode

enda

action ASPIDTRANSFRAME -5  1  5  1  10
useractor enemy PSPIDERTRANSPORT PROTONSPIDERSTRENGTH ASPIDTRANSFRAME

ifmove 0
{
	sizeat 40 36
	clipdist 128
	cstat 514
	move STOPPED
}

ifcount 31 nullop else ifcount 30
{
	cstat 2
	globalsound BOSSPORT
}

ifcount 61 nullop else ifcount 60
cstat 1

ifspritepal 33 spritepal 34 else
ifspritepal 4 spritepal 0 else
spritepal 33

ifcount 100
{
	espawn PSPIDER
	setav[RETURN].SPRITELOTAG SPRITELOTAG
	setav[RETURN].monstflags 80
	killit
}

enda

action FBALLFRAMES 0 6 1 1 8
spritenoshade FIREBALL
useractor notenemy FIREBALL 0 FBALLFRAMES

ifmove 0
{
	move STOPPED
	cstator 128
	seta[].shade -127
	ifrnd 128 spawn BURNING else spawn BURNING2
	sizeat 36 36
	findplayer xydist
	ifl xydist 1560
	{
		getp[].i spriteid
		getav[spriteid].burning temp
		add temp 30
		setav[spriteid].burning temp
	}
}

ifactioncount 6 killit

enda


spritenoshade INFERNO
spritenopal INFERNO
move INFERNOVELS 480
useractor notenemy INFERNO 16

ifmove 0
{
	sizeat 32 32
	clipdist 80
	cstat 256
	cstator 128
	move INFERNOVELS faceplayerslow
	seta[].shade -100
	spritepal 110
	
	ife rendmode 4
	{
		espawn SMALLSMOKE
		seta[RETURN].lotag 49
		seta[RETURN].hitag 10240
		seta[RETURN].xrepeat 2
		seta[RETURN].yrepeat 2
		seta[RETURN].xvel 255 // R value
		seta[RETURN].yvel 48 // G value
		seta[RETURN].zvel 48 // B value
		setav[RETURN].myspawner THISACTOR
		geta[].picnum picnum
		setav[RETURN].mtype picnum
		seta[RETURN].picnum SECTOREFFECTOR
		seta[RETURN].cstat 0
		changespritestat RETURN 3
	}
}

sizeto 64 64

ifactorsound THISACTOR ROCKETLOOP nullop else sound ROCKETLOOP
ifcount 240
{
	strength 0
	seta[].htextra 10
	seta[].htpicnum SHOTSPARK1
	seta[].htowner player[].i
}
ifhitweapon
{
	ifdead
	{
		//spawn ELEC_EXP
		spawn RANGERTRAIL
		sound EXPLODE4
		hitradius 2048 24 28 32 36
		stopactorsound THISACTOR ROCKETLOOP
		ifrnd 128 spawn BURNING else spawn BURNING2
		killit
	}
}
ifcansee
ifpdistl 1280
{
	spawn ELEC_EXP
	sound EXPLODE4
	hitradius 2048 24 28 32 36
	palfrom 63 63 16 16
	stopactorsound THISACTOR ROCKETLOOP
	ifrnd 128 spawn BURNING else spawn BURNING2
	killit
}

findplayer xydist
setvar xydist2 4096
subvarvar xydist2 xydist
ifvarg xydist2 0
{
	shiftvarr xydist2 6
	addvar xydist2 16
	ifvarg xydist2 63 setvar xydist2 63
	ifvarvarl player[THISACTOR].pals_time xydist2
	setplayer[THISACTOR].pals_time xydist2
	setplayer[THISACTOR].pals 0 63
	setplayer[THISACTOR].pals 1 16
	setplayer[THISACTOR].pals 2 16
}
espawn SPARKFALL
seta[RETURN].pal 110

getp[].posz zdist
geta[].z z
sub zdist z
ifl zdist -2048
{
	ifceilingdistl 8 nullop else
	{
		sub z 644
		seta[].z z
	}
}
else ifg zdist 2048
{
	iffloordistl 8 nullop else
	{
		add z 644
		seta[].z z
	}
}

enda

move PROTONESCAPE 0 -128
useractor notenemy PROTONTOP 0

ifmove 0
{
	move STOPPED
	sizeat 42 38
}

ifmove STOPPED
ifcount 30
{
	move PROTONESCAPE geth getv
	sound DUKE_JETPACK_ON
	ifg SPRITELOTAG 9
		state activatechannel
}

ifmove PROTONESCAPE
ifvarand player[].player_par 1
{
	geta[].z z
	sub z 2048
	seta[].z z
	espawn BIGSMOKE
	seta[RETURN].cstat 514
	add z 2048
	seta[].z z
}


ifceilingdistl 4 killit

enda
