/*
--------------------------------------------------------------------------------
Duke Nukem 3D - Blast Edition Version 0.1
By Marcos
2022 - Space In a Bottle
*/

//Pistol configuration code

var WEAPON1_FLAGS 49152
var WEAPON1_TOTALTIME 4

define PISTOLBURST_FIRE 401
definesound PISTOLBURST_FIRE snd/PISTOLB.VOC 0 0 254 0 0

define EXPTRACER 3985
define EXPTRACERTRAIL 3989
define PLASMAMICRO_IMPACT 404

defineprojectile EXPTRACER PROJ_WORKSLIKE 1
defineprojectile EXPTRACER PROJ_EXTRA 15
defineprojectile EXPTRACER PROJ_EXTRA_RAND 17
defineprojectile EXPTRACER PROJ_HITRADIUS 1024
defineprojectile EXPTRACER PROJ_SPAWNS EXPLOSION3
defineprojectile EXPTRACER PROJ_SXREPEAT 14
defineprojectile EXPTRACER PROJ_SYREPEAT 14
defineprojectile EXPTRACER PROJ_SOUND PLASMAMICRO_IMPACT
defineprojectile EXPTRACER PROJ_DECAL -1

/*
action AEXPTRACER 0 1 5 1 1
useractor notenemy EXPTRACER 0 AEXPTRACER enda 
*/

useractor notenemy EXPTRACERTRAIL 0

	ife w_temp5 0
	{
		set w_temp5 6
		seta[].yoffset -16
	}
		
	ifg w_temp5 1
	ifle w_temp5 6
	{
		seta[].xrepeat w_temp5
		seta[].yrepeat w_temp5
		sub w_temp5 1
	}
	
	ifcount 6
		killit
	
enda

state draw_pistol // PISTOL_WEAPON:
    // hud_temp4 = FIREDELAY
    // hud_temp3 = RELOAD

    state reset_hud_weapon_y_coordinate

    setvar hud_tilenum FIRSTGUN
	
	add hud_angle weapon_xoffset
	sub hud_angle looking_angSR1
	sub  hud_angle 10
	shiftvarl hud_angle 1

    addvar hud_totaltime 1
    ifvarvarl weaponcount hud_totaltime
    {
        setvar hud_x 183 // 195-12
        addvarvar hud_x weapon_xoffset
        subvarvar hud_x looking_angSR1

        addvar hud_y 244

        ifvarvare weaponcount 4
			subvar hud_x 3

        ifvarl weaponcount 4
            addvarvar hud_tilenum weaponcount
		else
		{
			ifvare weaponcount 4
				add hud_tilenum -24
			else ifvare weaponcount 5
				add hud_tilenum -23
		}

        setvar hud_orientation 2

        guniqhudid currentweapon

        state G_DrawWeaponTile

        guniqhudid 0
    }
    else
    {
        //orvar hud_orientation 512

        subvarvar hud_temp3 weaponcount

        ifvarg hud_temp3 17
        {
            guniqhudid currentweapon

            setvar hud_x 194
            subvarvar hud_x looking_angSR1

            addvar hud_y 230

            addvar hud_tilenum 4

            state G_DrawWeaponTile

            guniqhudid 0
        }
        else ifvarg hud_temp3 12
        {
            addvar hud_tilenum 6

            setvarvar hud_temp weaponcount
            shiftvarl hud_temp 3

            setvar hud_x 244
            subvarvar hud_x hud_temp
            subvarvar hud_x looking_angSR1

            setvarvar hud_temp2 weaponcount
            shiftvarl hud_temp2 4

            addvar hud_y 130
            addvarvar hud_y hud_temp2

            state G_DrawWeaponTile

            subvar hud_y 130
            subvarvar hud_y hud_temp2
            subvar hud_tilenum 6

            guniqhudid currentweapon

            setvar hud_x 224
            subvarvar hud_x looking_angSR1

            addvar hud_y 220

            addvar hud_tilenum 5

            state G_DrawWeaponTile

            guniqhudid 0
        }
        else ifvarg hud_temp3 7
        {
            addvar hud_tilenum 6

            setvarvar hud_temp weaponcount
            shiftvarl hud_temp 1

            setvar hud_x 124
            addvarvar hud_x hud_temp
            subvarvar hud_x looking_angSR1

            setvarvar hud_temp2 weaponcount
            shiftvarl hud_temp2 3

            addvar hud_y 430
            subvarvar hud_y hud_temp2

            state G_DrawWeaponTile

            subvar hud_y 430
            addvarvar hud_y hud_temp2
            subvar hud_tilenum 6

            guniqhudid currentweapon

            setvar hud_x 224
            subvarvar hud_x looking_angSR1

            addvar hud_y 220

            addvar hud_tilenum 5

            state G_DrawWeaponTile

            guniqhudid 0
        }
        else ifvarg hud_temp3 4
        {
            setvar hud_x 184
            subvarvar hud_x looking_angSR1

            addvar hud_y 235

            addvar hud_tilenum 8

            state G_DrawWeaponTile

            subvar hud_y 235
            subvar hud_tilenum 8

            guniqhudid currentweapon

            setvar hud_x 224
            subvarvar hud_x looking_angSR1

            addvar hud_y 210

            addvar hud_tilenum 5

            state G_DrawWeaponTile

            guniqhudid 0
        }
        else ifvarg hud_temp3 2
        {
            setvar hud_x 164
            subvarvar hud_x looking_angSR1

            addvar hud_y 245

            addvar hud_tilenum 8

            state G_DrawWeaponTile

            subvar hud_y 245
            subvar hud_tilenum 8

            guniqhudid currentweapon

            setvar hud_x 224
            subvarvar hud_x looking_angSR1

            addvar hud_y 220

            addvar hud_tilenum 5

            state G_DrawWeaponTile

            guniqhudid 0
        }
        else ifvarg hud_temp3 0
        {
            guniqhudid currentweapon

            setvar hud_x 194
            subvarvar hud_x looking_angSR1

            addvar hud_y 235

            addvar hud_tilenum 5

            state G_DrawWeaponTile

            guniqhudid 0
        }
    }
ends

appendevent EVENT_GETSHOTRANGE

	ife player[].curr_weapon PISTOL_WEAPON
	{
		ife userdef.pistolauto 1
		{
			set ANGRANGE 24
			set ZRANGE 72
		}
		else
		{
			set ANGRANGE 8
			set ZRANGE 32
		}
		
		ife alt_start 2
		{
			set ANGRANGE 128
			set ZRANGE 96
		}
	}

endevent

appendevent EVENT_EGS

	ife sprite[].picnum EXPTRACER
	ifspawnedby APLAYER
	{
		displayrandvar atemp 48
		displayrandvar atemp2 -48
		geta[].ang atemp3
		set atemp4 atemp3
		add atemp3 atemp
		add atemp3 atemp2
		seta[].ang atemp3
		displayrandvar atemp -2048
		displayrandvar atemp2 2048
		add atemp atemp2
		geta[].zvel atemp2
		add atemp atemp2
		seta[].zvel atemp
	}

endevent
