/*
--------------------------------------------------------------------------------
Duke Nukem 3D - Blast Edition Version 2.0
By Marcos
2022 - Space In a Bottle
*/

//Scorpion Tank

define SCORPIONTANKSTRENGTH 400

//           off-set, numofframes, numofrot, skipframes, delay
action ASTANKSPIN   0 1 7 1 4
action ASTANKSHOOTING 0 2 7 1 10
action ASTANKNAPALM 14 2 7 1 10
action ASTANKFREEZE 28 2 7 1 10
action ASTANKWAIT 0 1 7 1 1
action ASTANKDEAD 0 1 7 1 1

// move TANKVEL horiz vert
move STANKFORWARD 200
move STANKSTOP

useractor enemy SCORPIONTANK SCORPIONTANKSTRENGTH fall

  ifaction 0
  {
    sizeat 48 48
    action ASTANKWAIT
    cstat 257
    clipdist 100
  }
  else
    ifaction ASTANKSPIN
  {
    soundonce TANK_ROAM

    ifactioncount 20
    {
        ifcansee
          ifcanshoottarget
      {
	    ifrnd 48
		{
			move STANKSTOP geth
			action ASTANKFREEZE
			stopsound TANK_ROAM
		}
		else ifrnd 48
		{
			move STANKSTOP geth
			action ASTANKSHOOTING
			stopsound TANK_ROAM
		}
		else ifrnd 16
		{
			move STANKSTOP geth
			action ASTANKNAPALM
			stopsound TANK_ROAM
		}
      }
    }

    ifrnd 16
      move STANKFORWARD seekplayer
  }
  else
    ifaction ASTANKSHOOTING
  {
    ifactioncount 22
    {
      ifpdistg 8192
        action ASTANKNAPALM
		
      resetcount
      move 0 action ASTANKWAIT
    }
    else
      ifactioncount 2
    {
      ifcansee
      {
        ifpdistl 16384
        {
          ifrnd 128
          {
            sound CHAINGUN_FIRE
            state ShootSpark
          }
        }
        else
          ifrnd 128
        {
          sound PRED_ATTACK
          shoot FIRELASER
        }
      }
      else
      {
	    ifrnd 96
			action ASTANKNAPALM
		else
		{
			move STANKFORWARD seekplayer
			action ASTANKSPIN
		}
      }
    }

    ifrnd 16
    {
      stopsound TANK_ROAM
      move STANKSTOP faceplayerslow
    }
  }
  else ifaction ASTANKNAPALM
  {
    move STANKSTOP faceplayerslow
  
	ifactioncount 1
	{
		sound FLAMEALT_SOUND
		setprojectile[FLAMEPROJ].extra 6
		setprojectile[FLAMEPROJ].drop -48
		setprojectile[FLAMEPROJ].pal 2
		setprojectile[FLAMEPROJ].workslike 86082
		setprojectile[FLAMEPROJ].hitradius 1024
		setprojectile[FLAMEPROJ].range 0
		setprojectile[FLAMEPROJ].spawns FLAMECLUSTERSPAWNER
		eshoot FLAMEPROJ
		seta[RETURN].ang sprite[].ang
		setprojectile[FLAMEPROJ].extra FLAMEDAMAGE
		setprojectile[FLAMEPROJ].drop -256
		setprojectile[FLAMEPROJ].pal 0
		setprojectile[FLAMEPROJ].workslike 118850
		setprojectile[FLAMEPROJ].hitradius 768
		setprojectile[FLAMEPROJ].range 12
		setprojectile[FLAMEPROJ].spawns FLAMEPROJSPAWNED
		
		ifrnd 128
			resetactioncount
		else 
			action ASTANKWAIT
	}
  }
  else ifaction ASTANKFREEZE
  {
	ifcount 40
    {
      ifpdistg 8192
        action ASTANKNAPALM
		
      resetcount
      move 0 action ASTANKWAIT
    }
    else
      ifactioncount 1
    {
      ifcansee
      {
        ifpdistl 16384
        {
           sound CAT_FIRE
           eshoot FREEZEBLAST
		   seta[RETURN].ang sprite[].ang
        }
      }
      else
      {
	    ifrnd 96
			action ASTANKNAPALM
		else
		{
			move STANKFORWARD seekplayer
			action ASTANKSPIN
		}
      }
	  
	  resetactioncount
    }

    ifrnd 16
    {
      stopsound TANK_ROAM
      move STANKSTOP faceplayerslow
    }
  }
  else
    ifaction ASTANKWAIT
  {
    ifactioncount 32
    {
      move STANKFORWARD seekplayer
      action ASTANKSPIN
    }
  }
  else
    ifaction ASTANKDEAD
  {
    addkills 1
    hitradius 2560 TOUGH TOUGH TOUGH TOUGH
    sound LASERTRIP_EXPLODE
    debris SCRAP1 15
    spawn EXPLOSION2
    ifrnd 128 spawn ELITEPIGCOP
    killit
  }

  ifhitweapon
  {
    ifdead
      action ASTANKDEAD
    else
    {
      debris SCRAP1 1
      ifaction ASTANKSHOOTING break
	  else ifaction ASTANKNAPALM break
	  else ifaction ASTANKFREEZE break

      ifrnd 192
      {
        move STANKSTOP geth
        action ASTANKSHOOTING
        stopsound TANK_ROAM
      }
    }
  }
enda