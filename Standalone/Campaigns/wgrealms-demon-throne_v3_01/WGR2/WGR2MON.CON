action ASHARKCRUZING 0 3 5 1 18
action ASHARKFLEE  0 3 5 1 10
action ASHARKATACK   0 3 5 1 12
action ASHARKBITE   -10 2 5 1 12
action ASHARKSHRUNK  0 3 5 1 12
action ASHARKGROW  0 1 5 1 24
action ASHARKFROZEN  0 1 5 1 12
action ASHARKDEAD 15 1 1 1 12

move SHARKVELS 24
move PALSHARKVELS 48
move SHARKFASTVELS 72
move PALSHARKFASTVELS 144
move SHARKFLEEVELS 40
move PALSHARKFLEEVELS 80
move FLOATUP 0 -8

state goodfishcode

  ifaction ASHARKCRUZING
  {

    ifcount 32
      ifnotmoving
    {
      ifrnd 128
        move SHARKFASTVELS randomangle geth
      else
        move PALSHARKVELS randomangle geth 
    }
  }

  ifvarn cursed 0
  {
	  strength 0
	  setactor[THISACTOR].htextra 10
	  setactor[THISACTOR].htpicnum SHOTSPARK1
	  setactor[THISACTOR].htowner player[THISACTOR].i
  }
  ifhitweapon
  {
    ifdead
    {
	    setvar monstatus 2
        state randsquishsound
        guts JIBS6 3
        killit
    }
    else
      move PALSHARKFLEEVELS randomangle geth
  }
  else ifsquished
  {
	  state randsquishsound
	  guts JIBS6 3
      killit
  }
  
ends

state sharkcode

  ifaction ASHARKDEAD
  {
	ifmove STOPPED nullop else
	ifceilingdistl 32 { cstator 8 move STOPPED geth  }
	
	ifinwater
	{
		ifceilingdistl 16 fall
	}
	else fall
	  strength 0
	  ifhitweapon
	  {
		  sound HITFLESH
		  guts JIBS6 6
		  killit
	  }
	break
  }

  ifinwater
  {
	ifceilingdistl 4 fall
  }
	else killit

  ifaction ASHARKFLEE
  {
    ifcount 16
      ifrnd 48
    {
      action ASHARKCRUZING
      move SHARKVELS randomangle geth
      ifspritepal 2 move PALSHARKVELS randomangle geth
    }
  }
  else
    ifaction ASHARKCRUZING
  {
	ifspritepal 2 ifpdistl 2048
	{
		ifinwater
		ifp phigher movesprite THISACTOR 0 0 -256 CLIPMASK0 RETURN
		else movesprite THISACTOR 0 0 256 CLIPMASK0 RETURN
	}
    ifcansee
      ifcount 48
        ifrnd 2
          ifcanshoottarget
    {
      action ASHARKATACK
      move SHARKFASTVELS faceplayerslow getv 
      ifspritepal 2 move PALSHARKFASTVELS faceplayerslow getv
      break
    }
    ifcount 32
      ifnotmoving
    {
      ifrnd 128
      {
        move SHARKVELS randomangle geth
        ifspritepal 2 move PALSHARKVELS randomangle geth
      }
      else
      {
        move SHARKFASTVELS randomangle geth
        ifspritepal 2 move PALSHARKFASTVELS randomangle geth
       }
    }
  }
  else
    ifaction ASHARKATACK
  {
    ifinwater
	ifp phigher movesprite THISACTOR 0 0 -512 CLIPMASK0 RETURN
	else movesprite THISACTOR 0 0 512 CLIPMASK0 RETURN
	
	ifpdistl 2048 action ASHARKBITE
    ifcount 4
    {
      ifpdistl 1280
      {
        ifp palive ifcanshoottarget
        ifvare cursed 0
		ifvarvarn swordtarg THISACTOR 
        {
	      ifvare character 1 sound BARBPAIN2 else
          sound DUKE_GRUNT
          palfrom 28 32
         // setvar tempd SHARKBITESTRENGTH
         // state directdamage
		 ife invulntime 0
         addphealth SHARKBITESTRENGTH
          sound FISHBITE
        }
        action ASHARKFLEE
        move SHARKFLEEVELS fleeenemy
        ifspritepal 2 move PALSHARKFLEEVELS fleeenemy
      }
    }
    else
      ifnotmoving
    {
      ifcount 32
      {
        action ASHARKCRUZING
        move SHARKVELS randomangle geth
        ifspritepal 2 move PALSHARKVELS randomangle geth
      }
    }
    else
      ifcount 48
        ifrnd 2
    {
      action ASHARKCRUZING
      move SHARKFASTVELS randomangle geth
      ifspritepal 2 move PALSHARKFASTVELS randomangle geth
    }
  }
  else ifaction ASHARKBITE
  {
	ifinwater
	ifp phigher movesprite THISACTOR 0 0 -512 CLIPMASK0 RETURN
	else movesprite THISACTOR 0 0 512 CLIPMASK0 RETURN
	
	ifpdistg 2560 action ASHARKATACK
    ifcount 4
    {
      ifpdistl 1280
      {
        ifp palive ifcanshoottarget
        ifvare cursed 0
		ifvarvarn swordtarg THISACTOR 
        {
	      ifvare character 1 sound BARBPAIN2 else
          sound DUKE_GRUNT
          palfrom 28 32
         // setvar tempd SHARKBITESTRENGTH
         // state directdamage
		 ife invulntime 0
         addphealth SHARKBITESTRENGTH
          sound FISHBITE
        }
        action ASHARKFLEE
        move SHARKFLEEVELS fleeenemy
        ifspritepal 2 move PALSHARKFLEEVELS fleeenemy
      }
    }
    else
      ifnotmoving
    {
      ifcount 32
      {
        action ASHARKCRUZING
        move SHARKVELS randomangle geth
        ifspritepal 2 move PALSHARKVELS randomangle geth
      }
    }
    else
      ifcount 48
        ifrnd 2
    {
      action ASHARKCRUZING
      move SHARKFASTVELS randomangle geth
      ifspritepal 2 move PALSHARKFASTVELS randomangle geth
    }
  }

  ifvarn cursed 0
  {
	  strength 0
	  setactor[THISACTOR].htextra 10
	  setactor[THISACTOR].htpicnum SHOTSPARK1
	  setactor[THISACTOR].htowner player[THISACTOR].i
  }
  ifhitweapon
  {
    ifdead
    {
	    setvar monstatus 2
        state randsquishsound
        guts JIBS6 3
        action ASHARKDEAD
		geta[].yrepeat y sub y 4 seta[].yrepeat y
        move FLOATUP getv
		ifrnd 128 cstator 8
		ifrnd 128 cstator 4
    }
    else
    {
      move PALSHARKFASTVELS randomangle geth
      ifspritepal 2 move PALSHARKVELS randomangle geth
    }
  }
  else ifsquished
  {
	  state randsquishsound
	  guts JIBS6 3
      killit
  }
  
ends

actor SHARK SHARKSTRENGTH ASHARKCRUZING SHARKVELS randomangle geth

state monsterai
state sharkcode

enda

useractor notenemy GOODFISH 20 ASHARKCRUZING SHARKFASTVELS randomangle geth

sizeat 24 24
seta[].blend 1
seta[].alpha 16
cstator 256
state monsterai
state goodfishcode

enda

useractor notenemy BADFISH 15 ASHARKCRUZING SHARKVELS randomangle geth

ife mtype 0
{
	set mtype 1
	sizeat 30 28
	cstator 256
}
state monsterai
state sharkcode

enda

action ACACOFRAME  0  1  5  1  10
useractor notenemy CACOTRAIL 0 ACACOFRAME

ifmove 0
{
	move STOPPED
	seta[].xrepeat sprite[myspawner].xrepeat
	seta[].yrepeat sprite[myspawner].yrepeat
	seta[].alpha 128
	seta[].ang sprite[myspawner].ang
	seta[].pal sprite[myspawner].pal
}
ifcount 3 killit

enda

action ATROOPSTAND     0  1  5  1  1
action ATROOPSTAYSTAND  -2  1  5  1  1
action ATROOPFLINTCH  31  1  1  1  6
action ATROOPDYING    79  6  1  1   18
action ATROOPJIBBING  36  6  1  1   18
action ATROOPDEAD 84 1  1  10

action ATROOPWALK 0 3 5 1 12
action ATROOPWALK2 10 3 5 -1 12
action ATROOPSHOOT1 15 2 5 1 60
action ATROOPSHOOT2 26 1 5 1 80
action ATROOPSHOOT3 20 2 5 -1 40

move TROOPJETPACKVELS 64 -64
move TROOPFASTVELS 160 -64
move TROOPJETPACKVELSDOWN 72 -36
move TROOPFASTVELSDOWN 192 -36
move TROOPJETPACKILLVELS 128 -36
move TROOPFASTKILLVELS 320 -36
move TROOPDODGEVELS 192 -48
move TROOPJETPACKVELSBACK -64 -36
move SHRUNKVELS 32

move CACOCHARGEVELS 768 -36 
move CACOCHARGEPREP -64 -48

ai AITROOPJETPACK  ATROOPWALK   TROOPJETPACKVELS seekplayer faceplayerslow
ai AICACOCHARGE ATROOPSHOOT3 CACOCHARGEPREP faceplayerslow getv

state cacochargestate

	ifn htg0 -1 
	ifl burning 0
	{
		action ATROOPFLINTCH ai AITROOPJETPACK
		break
	}
	
	ifaction ATROOPSHOOT3
	{
		// ifspritepal 121 getlastpal else spritepal 121
		ifceilingdistl 160
		{
			fall
			ifmove CACOCHARGEPREP
				move TROOPJETPACKVELSBACK faceplayerslow getv
		}
		set x initx
		rand mx 256
		sub mx 128
		add x mx
		seta[].x x
		set y inity
		rand my 256
		sub my 128
		add y my
		seta[].y y
		ifcount 30
		{
			sound CACOCHARGE
			action ATROOPSHOOT2
			move CACOCHARGEVELS geth getv
			// ifspritepal 121 getlastpal
		}
	}
	else
	ifaction ATROOPSHOOT2
	{
		espawn CACOTRAIL
		setav[RETURN].myspawner THISACTOR
		ifcount 15 nullop else state gradualturn
		
		ife target -1 set target player[].i
		getactor[].z z
		getactor[target].z mz
		subvarvar mz z
		shiftr mz 2
		movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
		
		ife target player[].i { ifcansee set temp xydist else set temp 16384 }
		else set temp xydist2
		
		ifl temp 1280
		{
			ifpdistl 8192 quake 20
			sound BIGTHUD
			hitradius 2048 30 40 50 60
			
			state randsquishsound
			rand tempb 20
			add tempb 65
			seta[target].htextra tempb
			seta[target].htpicnum RPG
			seta[target].htowner THISACTOR
			seta[target].htang sprite[].ang
			
			ife target player[].i { wackplayer wackplayer }
			action ATROOPFLINTCH ai AITROOPJETPACK 
		}
		else
		ifnotmoving 
		{ 
			action ATROOPFLINTCH ai AITROOPJETPACK 
			ifpdistl 8192 quake 20
			sound BIGTHUD
			hitradius 2048 30 40 50 60
			
			spritepal 0
			debris SCRAP3 3
			debris SCRAP4 3
			debris SCRAP5 3
			getlastpal
		} 
		else
		ifcount 120 { action ATROOPFLINTCH ai AITROOPJETPACK } else
		ife target player[].i ifangdiffl 512 { action ATROOPFLINTCH ai AITROOPJETPACK }
		ife target player[].i set target -1
	}

ends

state troopdying

	
    ifactioncount 5
    {
      ifrnd 64
	  {
	    spritepal 8
        spawn BLOODPOOL
		getlastpal
	  }
      action ATROOPDEAD
	  iffloordistl 16 sound THUD
    }

ends

state troopjibbing

ifactioncount 5
    {
	  ifn sprite[].pal 88
      spritepal 8
      ifrnd 64
        spawn BLOODPOOL
      state standard_jibs
      state randsquishsound
      killit
    }
ends

state troopdead

	strength 0
	ifhitweapon
	{
		ifwasweapon RPG
		{
			spritepal 8
		  ifrnd 64
			spawn BLOODPOOL
		  state standard_jibs
		  state randsquishsound
		  killit
		}
		ifwasweapon RADIUSEXPLOSION
		{
			spritepal 8
		  ifrnd 64
			spawn BLOODPOOL
		  state standard_jibs
		  state randsquishsound
		  killit
		}
	}
	ifspritepal 88 sizeto 72 36 else
	ifspritepal 7 sizeto 64 30 else
	ifspritepal 24 sizeto 20 10 else
	sizeto 40 19

ends

state checktroophit
  ifdead
  {
    ifspritepal 121 getlastpal
    ifwasweapon FREEZEBLAST
		 ifdead spawn BIGSMOKE
    
    ifwasweapon BURNING nullop else
		state random_wall_jibs
	addkills 1 add globalkills 1
	state specialdeath
    
	cstat 0
	ifrnd 128 cstator 4
	move STOPPED
    ifwasweapon RPG
    {
	  action ATROOPJIBBING
      sound PRED_DYING
    }
    else
      ifwasweapon RADIUSEXPLOSION
    {
	  action ATROOPJIBBING
      sound PRED_DYING
    }
    else
    {
      sound PRED_DYING
      action ATROOPDYING
    }
  }
  else
  {
    ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifactorsound THISACTOR PRED_PAIN nullop else sound PRED_PAIN
	
	ifaction ATROOPFLINTCH break
	
	ifvarl fear 0 
		action ATROOPFLINTCH 
	else
    ifrnd 96
	ifn sprite[].htpicnum ARROWPROJ
	ifn sprite[].htpicnum BURNING
        action ATROOPFLINTCH
		
	ifaction ATROOPFLINTCH
	
	ifai AICACOCHARGE
		ai AITROOPJETPACK
  }
ends

state troopjetpackstate

ifspritepal 24
{
	ifmove TROOPJETPACKVELS move TROOPFASTVELS seekplayer faceplayerslow else
	ifmove TROOPJETPACKVELSDOWN move TROOPFASTVELSDOWN seekplayer faceplayerslow else
	ifmove TROOPJETPACKILLVELS move TROOPFASTKILLVELS seekplayer faceplayerslow
}

ifmove TROOPDODGEVELS 
{ 
	ifcount 26 ai AITROOPJETPACK 
	break 
} 
else
ifwasweapon SHOTSPARK1 nullop
else
ifbulletnear move TROOPDODGEVELS dodgebullet

ifmove TROOPJETPACKVELSBACK  
{
   ifaction ATROOPSHOOT2 nullop else
   {
	   ifpdistg 8192
	   {
		ifspritepal 24 move TROOPFASTVELS seekplayer faceplayerslow else
	   	move TROOPJETPACKVELS seekplayer faceplayerslow
   	   }
	   else
	   ifnotmoving
	   {
		ifspritepal 24 move TROOPFASTVELS seekplayer faceplayerslow else
	   	move TROOPJETPACKVELS seekplayer faceplayerslow
   	   }
   }
} 
else
    ifawayfromwall
	ifpdistl 3584 ifcansee move TROOPJETPACKVELSBACK faceplayerslow

set temp NO
ifaction ATROOPWALK set temp YES
ifaction ATROOPWALK2 set temp YES

add countvar 1
ife temp YES
ifvare fear 0
ifg countvar 30
{
	ifrnd 6
	{
		ifcansee
		ifvare target -1
		ifvare cursed 0
			  action ATROOPSHOOT1
		else
		ifvarn target -1 action ATROOPSHOOT1		  
	}
	else ifvare target -1 ifpdistl 3072 ifvare cursed 0
	ifrnd 8
	action ATROOPSHOOT1
}

ifaction ATROOPSHOOT1
ifspritepal 88
{
	ai AICACOCHARGE
	geta[].x initx, geta[].y inity
	break
}
      
ifaction ATROOPSHOOT1 ifactioncount 2
{
	action ATROOPSHOOT2
	set countvar 0
}
    
ifaction ATROOPSHOOT2 
{
	
	ifcount 13
	{
		geta[].z initz
		set zdist sprite[].yrepeat
		// shiftl zdist 7
		mul zdist 96
		set z initz
		add z zdist
		seta[].z z
	
		resetcount
		sound PRED_ATTACK
    	ifspritepal 24
    	{
	    	setprojectile[DEMONSPIT].workslike 32770
	    	setprojectile[DEMONSPIT].extra 5
	    	shoot DEMONSPIT
	    	setprojectile[DEMONSPIT].workslike 36866
	    	setprojectile[DEMONSPIT].extra 9
	    	ifmove TROOPFASTVELS move TROOPFASTVELS faceplayerslow else
	    	ifmove TROOPJETPACKVELSBACK move TROOPJETPACKVELSBACK faceplayerslow else
	    	ifmove TROOPFASTVELSDOWN move TROOPFASTVELSDOWN faceplayerslow
    	}
    	else
    	{
    	    ifspritepal 7
    	    {
    	    	ifvarn target -1
		    	{
			    	state calczdist
					ldist xydist THISACTOR target
					ifvare xydist 0 setvar xydist 1
					mulvar zdist 644
					divvarvar zdist xydist
					ezshoot zdist NOVAPROJ
				}
				else
		    		shoot NOVAPROJ
    	    }
    	    else
    	    {
		    	ifvarn target -1
		    	{
			    	state calczdist
					ldist xydist THISACTOR target
					ifvare xydist 0 setvar xydist 1
					mulvar zdist 768
					divvarvar zdist xydist
					ezshoot zdist FIRELASER
				}
				else
		    	shoot FIRELASER
		     }
	    	ifmove TROOPJETPACKVELS move TROOPJETPACKVELS faceplayerslow else
	    	ifmove TROOPJETPACKVELSBACK move TROOPJETPACKVELSBACK faceplayerslow else
	    	move TROOPJETPACKVELSDOWN faceplayerslow
    	}
		
		seta[].z initz
	}
	ifactioncount 2
	action ATROOPSHOOT3
}
ifaction ATROOPSHOOT3 ifactioncount 2
action ATROOPWALK

ifp phigher 
{
	ifspritepal 24
	{
		ifmove TROOPFASTVELS nullop
		else move TROOPFASTVELS seekplayer faceplayerslow
	}
	else
	{
		ifmove TROOPJETPACKVELS nullop 
		else move TROOPJETPACKVELS seekplayer faceplayerslow
	}
} 
else
{
	ifspritepal 24
	{
		ifmove TROOPFASTVELSDOWN 
		{
			ifcount 26
			iffloordistl 64 ifrnd 16
			{
				resetcount
				move TROOPFASTVELS seekplayer faceplayerslow
			}
			
		}
		else
		ifrnd 6
		ifcount 26
		{
			resetcount
			move TROOPFASTVELSDOWN seekplayer faceplayerslow
		}
	}
	else
	{
		ifmove TROOPJETPACKVELSDOWN 
		{
			ifcount 26
			iffloordistl 64 ifrnd 16
			{
				resetcount
				move TROOPJETPACKVELS seekplayer faceplayerslow
			}
			
		}
		else
		ifrnd 6
		ifcount 26
		{
			resetcount
			move TROOPJETPACKVELSDOWN seekplayer faceplayerslow
		}
	}
}


ends

state checksquished
  ifsquished
  {
    addkills 1 add globalkills 1
	state specialdeath
    
    sound SQUISHED
    ifactor PIGCOP
    spritepal 8
    state standard_jibs
    state random_ooz
    killit
  }
ends


state troopcode 

ifaction ATROOPWALK { ifactioncount 3 action ATROOPWALK2 } else
ifaction ATROOPWALK2 { ifactioncount 3 action ATROOPWALK }

state monsterai

ifaction ATROOPJIBBING nullop else
fall
  
  // ifvarl burning 0 ifvare monstatus 1 cstat 257 else
  ifaction ATROOPDYING nullop else
  ifaction ATROOPJIBBING nullop else
  ifaction ATROOPDEAD nullop else
  ifvare player[THISACTOR].heat_on YES cstat 257 else
  ifaction ATROOPWALK cstat 514 else
  ifaction ATROOPWALK2 cstat 514 else
  cstat 257

  

  ifinwater
    ifrnd 1
      spawn WATERBUBBLE
  ifaction ATROOPSTAND
  {
    ai AITROOPJETPACK
  }
  else
    ifaction ATROOPDYING
  {
    state troopdying
    break
  }
  else
    ifaction ATROOPDEAD
  {
    state troopdead
	break
  }
  else ifaction ATROOPJIBBING
  {
	  state troopjibbing
	  break
  }
  else
    ifaction ATROOPFLINTCH
  {
	ifvarl fear 0 resetactioncount
    ifactioncount 4
      ai AITROOPJETPACK
  }
  else
      ifai AITROOPJETPACK
      state troopjetpackstate
  else
	ifai AICACOCHARGE
	state cacochargestate
  
  ifhitweapon
    state checktroophit
  else
    state checksquished
ends

state checktrooppalette
  ifai 0
  {
    sizeat 40 38
	setvar gold GOLD4
    ifspritepal 0
        nullop
    else
        ifspritepal 7
        {
	      addstrength TROOPSTRENGTH
	      addstrength TROOPSTRENGTH
	      addvar gold GOLD4
	      sizeat 64 60
        }
        else
        ifspritepal 88
        {
		  sizeat 72 70
		  set inithp TROOPSTRENGTH
		  mul inithp 5
		  seta[].extra inithp
          addvar gold GOLD4
		  ifg SKILL 3 state randomaura
        }
    else ifspritepal 24
    {
	    strength WEAKTROOPSTRENGTH
	    sizeat 20 18
    }
          
  }
ends

actor LIZTROOPSTAYPUT TROOPSTRENGTH ATROOPSTAYSTAND
  state checktrooppalette
  ai AITROOPJETPACK
  cactor LIZTROOP
enda


actor LIZTROOP TROOPSTRENGTH ATROOPSTAND
  state checktrooppalette
  state troopcode
enda


action ALIZWALK 0 4 8 1 18
action ALIZRUN 0 4 8 1 10
action ALIZSPRINT 0 4 8 1 6
action ALIZWAIT -8 1 8 1 20
action ALIZSTRIKE 32 3 8 1 6
action ALIZSTRUCK 48 1 8 1 4
action ALIZRETRACT 48 3 8 -1 8
action ALIZFLINTCH 56 1 8 1 20
action ALIZDYING 64 7 1 1 14
action ALIZDEAD 70 1 1 1 20
action ALIZRISE 70 7 1 -1 20
action ALIZJUMP 8 1 8 1 40
action ALIZFALL 16 1 8 1 40
action ALIZLAND -8 1 8 1 50

move LIZWALKVELS 128
move LIZCHARGEVELS 256
move LIZSPRINTVEL 768 // 644
move LIZJUMPVEL 256 -24
move LIZLANDVEL 224

ai AILIZ_SEEK ALIZWALK LIZWALKVELS seekplayer faceplayerslow
ai AILIZ_CHARGE ALIZRUN LIZCHARGEVELS faceplayer
ai AILIZ_SPRINT ALIZLAND STOPPED faceplayer
ai AILIZ_WANDER ALIZWALK LIZWALKVELS geth
ai AILIZ_STRIKE ALIZSTRIKE STOPPED geth
ai AILIZ_THROW ALIZSTRIKE STOPPED geth
ai AILIZ_FLINTCH ALIZFLINTCH STOPPED geth
ai AILIZ_DYING ALIZFLINTCH STOPPED geth
ai AILIZ_DODGE ALIZRUN LIZCHARGEVELS dodgebullet
ai AILIZJUMPENEMY ALIZJUMP LIZJUMPVEL jumptoplayer

state stopsprint

ifspritepal 33 getlastpal
action ALIZSTRUCK
move STOPPED geth

ifvarn target -1
{
	ifvarl xydist2 1280
	{
		move STOPPED geth
		sound CHOP
		state facetarget
		getactor[target].htextra temp
		addvar temp 51
		setactor[target].htextra temp
		setactor[target].htpicnum SHOTSPARK1
		setactor[target].htowner THISACTOR
		setactor[target].htang sprite[THISACTOR].ang
	}
	else sound SWISH
}
else
{
	ifcansee ifpdistl 1280
	ifvarvarn swordtarg THISACTOR 
	{
		move STOPPED faceplayer
		sound CHOP
		ifvarg roxq 0 nullop else
		tip
		wackplayer
		wackplayer
		palfrom 32 63
		setvar tempd LIZCHOPPINGPLAYER
		mulvar tempd 3
		state directdamage
		state dukepainsounds
	} 
	else sound SWISH
}
	
ends

state lizsprintstate

ifvarn target -1 setvarvar xydist xydist2

ifaction ALIZSTRUCK nullop else
{
	ifspritepal 33 getlastpal else spritepal 33
}
ifaction ALIZLAND
{
	ifcount 20
	{
		action ALIZSPRINT
		move LIZSPRINTVEL faceplayerslow
		// sound
	}
} else
ifaction ALIZSPRINT
{
	state motionblur
	spawn SMALLSMOKE
	ifvarl xydist 1280 state stopsprint else
	ifnotmoving state stopsprint else
	ifcount 40 state stopsprint
} else
ifaction ALIZSTRUCK
{
	ifspritepal 33 getlastpal
	ifcount 20 ai AILIZ_SEEK
}


ends

state lizjumpstate

ifvarn target -1 setvarvar xydist xydist2


  ifaction ALIZLAND
  {
	ifactioncount 2
	  ai AILIZ_SEEK
  }
  else
  ifaction ALIZFALL
  {
    iffloordistl 16
    {
      action ALIZLAND
      move STOPPED geth
    }
    else ifvarl xydist 1408
    {
	    ifvarn cursed 0 ifvare target -1 break
    	ai AILIZ_STRIKE
    	sound SWISH
	}
  }
  else
    ifactioncount 2
    {
      action ALIZFALL
      move LIZLANDVEL geth
    }
ends

state lizdodgestate

ifcount 18 ai AILIZ_SEEK

ends

state lizdyingstate 

strength 0

ifaction ALIZDEAD
{
	ifhitweapon
	{
		ifwasweapon RADIUSEXPLOSION
		{
			spritepal 0
			state randsquishsound
			state standard_jibs
			killit
		}
		ifwasweapon RPG
		{
			spritepal 0
			state randsquishsound
			state standard_jibs
			killit
		}
	}
}

ifaction ALIZDYING ifactioncount 6
{
	action ALIZDEAD
	// setvar command 65536
    // setactor[THISACTOR].mdflags 16
	espawn FLIES
	setactorvar[RETURN].myspawner THISACTOR
	ifwasweapon SHOTSPARK1 spawn BLOODPOOL
}

ifaction ALIZFLINTCH ifcount 3
	action ALIZDYING


ifaction ALIZRISE
{
	ifactioncount 7
	{
		ai AILIZ_FLINTCH
		strength LIZSTRENGTH
	}
}

ends

state lizseekstate

ifvarn target -1
{
	ai AILIZ_CHARGE
	break
}

ifcount 12 ifcansee
{
	
	ifbulletnear ifrnd 64
	{
		ifrnd 128
		ai AILIZJUMPENEMY else
		ai AILIZ_DODGE
		break
	}
	ifpdistl 5120
	{
		ai AILIZ_CHARGE
		break
	}
	ifrnd 2
	{
		ai AILIZ_CHARGE
		break
	}
	ifpdistg 8192 ifcount 130 ifrnd 2
	{
		ai AILIZ_WANDER
		// move LIZWALKVELS randomangle
		break
	}
	ifp phigher ifrnd 8 ai AILIZJUMPENEMY else
	ifnotmoving ifrnd 8 ai AILIZJUMPENEMY
}
else
	ifcount 260 ifrnd 2
		ai AILIZ_WANDER	
ends

state lizchargestate 

ifvarn target -1
{
	ifvarl xydist2 896
	{
		move STOPPED geth
		ai AILIZ_STRIKE
		sound SWISH
		break
	}
	ifspritepal 7
	ifvarl xydist2 8192
	ifrnd 8
	{
		ai AILIZ_SPRINT
		sound LIZSPRINTSND
		break
	}
	ifbulletnear ifrnd 32
	{
		ai AILIZJUMPENEMY
		break
	}
	ifvarl xydist2 3840 ifvarl xydist2 2816
	ifcount 20 ifrnd 64
	ai AILIZJUMPENEMY
	ifnotmoving ifrnd 8 ai AILIZJUMPENEMY
	break
}

ifspritepal 7 
 ifvare cursed 0
 ifcansee 
  ifcanshoottarget 
   ifpdistl 8192
    ifrnd 4
{
	ai AILIZ_SPRINT
	sound LIZSPRINTSND
	break
}

ifbulletnear ifrnd 64
{
	ifrnd 128
	ai AILIZJUMPENEMY else
	ai AILIZ_DODGE
	break
}
ifcount 52
ifpdistg 6144 { ai AILIZ_SEEK break }

ifpdistl 1280
ifvare cursed 0
ifcanshoottarget
{
	move STOPPED faceplayer
	ai AILIZ_STRIKE
	sound SWISH
	break 
}

ifpdistl 3840 ifpdistg 2816
ifcount 20 ifrnd 64
	ai AILIZJUMPENEMY


ifcount 156 ai AILIZ_SEEK

ifnotmoving ifrnd 8 ai AILIZJUMPENEMY


ends

state lizwanderstate 

ifvarn target -1
{
	ai AILIZ_CHARGE
	break
}

ifaction ALIZWAIT
{
	ifactioncount 2
	{
		action ALIZWALK
		move LIZWALKVELS randomangle
		resetcount
	}
	break
} else
ifnotmoving ifcount 20
{
	resetcount
	action ALIZWAIT
	move STOPPED
	break
}
ifpdistg 8192
{
	ifcansee ifrnd 1
			ai AILIZ_SEEK
}
else ifcount 26
{
	ifpdistl 5120
	{
		ai AILIZ_CHARGE
		break
	}
	ifcansee ifrnd 8
		ai AILIZ_SEEK
}

ends

state lizwaitstate 
ifactioncount 4 ai AILIZ_SEEK
ends

state lizstrikestate

state motionblur
ifaction ALIZSTRIKE 
{
	ifactioncount 3
	{
		// shoot stuff
		state monsterbite state monsterbite
		action ALIZSTRUCK		
	}
} else
ifaction ALIZSTRUCK
{
	ifactioncount 2
	action ALIZRETRACT
} else
ifaction ALIZRETRACT
{
	ifactioncount 3
	{
		ifcansee ifcanshoottarget
		ifp palive
		ifpdistl 1280
		ifvare cursed 0
		{
			setvar target -1
			action ALIZSTRIKE 
			move STOPPED faceplayerslow
			sound SWISH
			break
		}
		ifvarn target -1
		ifvarl xydist2 896
		{
			action ALIZSTRIKE 
			move STOPPED geth
			sound SWISH
			break
		}
		ai AILIZ_CHARGE
	}
}

ends

state lizflintchstate 
ifvarl fear 0 resetactioncount
ifactioncount 2
{
	ifpdistl 5120 ifcansee ai AILIZ_CHARGE else
	ai AILIZ_SEEK
}
ends

state lizcode

ifmove 0
{
	ifn gold -1
	setvar gold GOLD3
	sizeat 30 25
	cstator 257
	ai AILIZ_SEEK
	sound CAPT_RECOG
	ifspritepal 7 { addstrength 100 ifn gold -1 addvar gold GOLD2 }
	ifvare SKILL 5 ifrnd 32 ifspritepal 0 { spritepal 7 addstrength 100 }
}
state monsterai
	  

ifai AILIZ_DYING { state lizdyingstate break }
ifai AILIZ_SEEK state lizseekstate else
ifai AILIZ_CHARGE state lizchargestate else
ifai AILIZ_WANDER state lizwanderstate else
ifai AILIZ_STRIKE state lizstrikestate else
ifai AILIZ_FLINTCH state lizflintchstate else
ifai AILIZ_DODGE state lizdodgestate else
ifai AILIZJUMPENEMY state lizjumpstate else
ifai AILIZ_SPRINT state lizsprintstate

ifhitweapon
{
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		ifspritepal 33 getlastpal
		sound CAPT_DYING
		ifrnd 128 cstat 0 else cstat 4
		addkills 1 add globalkills 1
		state specialdeath
		
		
		ai AILIZ_DYING
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
    		killit
    		break
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
		    killit
		    break
	    }
		
	}
	else // ifrnd 96
	{
		ifai AILIZ_SPRINT nullop else
		ai AILIZ_FLINTCH
	}
	soundonce CAPT_PAIN
}
else ifrnd 1 ifrnd 128 ifvare burning 0 soundonce CAPT_RECOG

ends

action LIZSTATUEFRAME 0 1 8 1 1

 actor LIZMANSTAYPUT LIZSTRENGTH 
	ifn gold -1
	setvar gold GOLD3
    sizeat 30 25 // 48 40
	cstator 257
	ai AILIZ_SEEK
	sound CAPT_RECOG
	ifspritepal 7 { addstrength 100 addvar gold GOLD2 }
	cactor LIZMAN 
  enda
  
// actor LIZMANSPITTING LIZSTRENGTH ai AILIZSPIT cactor LIZMAN enda
// actor LIZMANJUMP LIZSTRENGTH ai AILIZJUMPENEMY cactor LIZMAN enda

actor LIZMAN LIZSTRENGTH

ifspritepal 17
{
	action LIZSTATUEFRAME
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength LIZSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		spritepal 0
		setvar command 0
		ifvare SKILL 5 ifrnd 32 
		{
			spritepal 7
			addstrength 100
		}
	}
	else break
}
  fall
  state lizcode
enda

useractor enemy LIZMANSTATUE LIZSTRENGTH
ifspritepal 17
{
	action LIZSTATUEFRAME
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength LIZSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		spritepal 0
		setvar command 0
		ifvare peractor1 1 
		{
			setvar peractor1 0
			getactor[THISACTOR].sectnum mysector
			setactor[THISACTOR].htactorstayput mysector
			cactor LIZMANSTAYPUT 
		}
		else
		cactor LIZMAN
	}
	else break
}
enda

eventloadactor LIZMAN

ifspritepal 17 
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	cactor LIZMANSTATUE
	action LIZSTATUEFRAME
	sizeat 30 25 // 48 40
	state eventloadactorstate
}

enda

eventloadactor LIZMANSTAYPUT

ifspritepal 17 
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	cactor LIZMANSTATUE
	action LIZSTATUEFRAME
	sizeat 30 25 // 48 40
	state eventloadactorstate
	setvar peractor1 1 // stayput
}

enda

action APIGWALK     0  4 5 1 20
action APIGRUN      0  4 5 1 11
action APIGSHOOT    30 2 5 1 58
action APIGCOCK     25 1 5 1 16
action APIGSTAND    25 1 5 1 1
// action APIGDIVE     40 2 5 1 40
// action APIGDIVESHOOT  45 2 5 1 58
action APIGDIVE     -5 2 5 1 40
action APIGDIVESHOOT  0 2 5 1 58
action APIGDYING    55 5 1 1 15
action APIGHIT      55 1 1 1 10
action APIGDEAD     60 1 1 1 1
action APIGGROW      0
action APIGWAIT    30 1 5 1 1

action APIGWALK2    -45 4 5 1 20 // DECAPITATED
action APIGDYING2   -25 5 1 1 15 // DECAPITATED
action APIGDEAD2    -21 1 1 1 1 // DECAPITATED

move PIGWALKVELS 72
move PIGRUNVELS 108

ai AIPIGWAIT APIGWAIT STOPPED geth
ai AIPIGSEEKENEMY APIGWALK PIGWALKVELS seekplayer faceplayerslow
ai AIPIGSHOOTENEMY APIGSHOOT STOPPED faceplayer
ai AIPIGFLEEENEMY APIGWALK PIGWALKVELS fleeenemy
ai AIPIGSHOOT APIGSHOOT STOPPED faceplayer
ai AIPIGDODGE APIGRUN PIGRUNVELS dodgebullet
ai AIPIGCHARGE APIGRUN PIGRUNVELS seekplayer faceplayerslow
ai AIPIGDIVING APIGDIVE STOPPED faceplayer
ai AIPIGDYING APIGDYING STOPPED faceplayer
ai AIPIGGROW APIGGROW STOPPED faceplayerslow
ai AIPIGHIT APIGHIT STOPPED faceplayer

state pigwaitstate
// respawned pigcops wait until they see the player

ifvare peractor1 1 ai AIPIGSHOOTENEMY


ends

state pigcursedseekstate

 ifai AIPIGCHARGE
  {
	ifvarn target -1
    ifvarl xydist2 3084
    {
	    ifnotmoving
	     ai AIPIGSEEKENEMY
	    else
		{
		 cactor PIGCOPDIVE
	     ai AIPIGDIVING
		}
    }
    else
    ifvare peractor1 1
      ifpdistl 3084
      ifnotmoving
        ai AIPIGSEEKENEMY
         
    break
  }
  else iffloordistl 32
  {
	ifvarn target -1
	ifvarg xydist2 4096
	{
      ifactornotstayput
      ai AIPIGCHARGE
    }
	else
    ifpdistg 4096
    {
      ifactornotstayput
      ai AIPIGCHARGE
    }
    ifrnd 8
    {
      ifbulletnear
        ai AIPIGDODGE
    }
  }
  else ifvarand sprite[THISACTOR].cstat 8
  {
	  ifvarn target -1
	ifvarg xydist2 4096
	{
      ifactornotstayput
      ai AIPIGCHARGE
    }
	else
    ifpdistg 4096
    {
      ifactornotstayput
      ai AIPIGCHARGE
    }
    ifrnd 8
    {
      ifbulletnear
        ai AIPIGDODGE
    }
 }

  
  ifrnd 128
  {
    ifai AIPIGDODGE
    {
      ifcount 32
        ai AIPIGCHARGE
          break
    }
    iffloordistl 32
    ifvarn target -1
    {
      ifvarl xydist2 1024
      {
	      ai AIPIGSHOOTENEMY
	      break
      }
      ifcount 48
      {
        ifrnd 8
        {
          ifrnd 192
            ai AIPIGSHOOTENEMY
          else
		  {
		    cactor PIGCOPDIVE
            ai AIPIGDIVING
		  }
          break
        }
      }
    }
    else
    ifvarand sprite[THISACTOR].cstat 8
    ifvarn target -1
    {
	      ifvarl xydist2 1024
	      {
		      ai AIPIGSHOOTENEMY
		      break
	      }
	      ifcount 48
	      {
	        ifrnd 8
	        {
	          ifrnd 192
	            ai AIPIGSHOOTENEMY
	          else
			  {
			    cactor PIGCOPDIVE
	            ai AIPIGDIVING
			  }
	          break
	        }
	      }
	  }
  }

ends

state pigseekenemystate

ifvarn cursed 0 
{
	state pigcursedseekstate 
	break 
}

  ifai AIPIGCHARGE
  {
    ifvare peractor1 1
      ifpdistl 3084
    {
      ifnotmoving
        ai AIPIGSEEKENEMY
      else
	  {
		cactor PIGCOPDIVE
        ai AIPIGDIVING
	  }
    }
    else
    ifvarn target -1
    ifvarl xydist2 3084
    {
	    ifnotmoving
	     ai AIPIGSEEKENEMY
	    else
		{
		 cactor PIGCOPDIVE
	     ai AIPIGDIVING
		}
    }
	ifvare peractor1 1
	  ifpdistg 3084
	  ifpdistl 16384
	  ifrnd 3 ifrnd 128
	  {
          ifrnd 192
            ai AIPIGSHOOTENEMY
          else
		  {
		    cactor PIGCOPDIVE
            ai AIPIGDIVING
		  }
          break
      }

    break
  }
  else iffloordistl 32
  {
    ifpdistg 4096
    {
      ifactornotstayput
      ai AIPIGCHARGE
    }
    ifrnd 8
    {
      ifbulletnear
        ai AIPIGDODGE
    }
  }
  else ifvarand sprite[THISACTOR].cstat 8
  {
	    ifpdistg 4096
	    {
	      ifactornotstayput
	      ai AIPIGCHARGE
	    }
	    ifrnd 8
	    {
	      ifbulletnear
	        ai AIPIGDODGE
	    }
   }

  setvar temp 0
  ifvare peractor1 1 setvar temp 1
  ifvarn target -1 setvar temp 1
  
  ifrnd 128
    ifvare temp 1
  {
    ifai AIPIGDODGE
    {
      ifcount 32
        ai AIPIGCHARGE
          break
    }
 
      ifpdistl 1024
        ifp palive
          ifvare mtype 1
      {
        ai AIPIGSHOOTENEMY
        break
      }
      else ifvarn target -1
      ifvarl xydist2 1024
      {
	      ai AIPIGSHOOTENEMY
	      break
      }
      ifcount 48
      {
        ifrnd 8
          ifp palive
            ifvare mtype 1
        {
          ifrnd 192
            ai AIPIGSHOOTENEMY
          else
		  {
		    cactor PIGCOPDIVE
            ai AIPIGDIVING
		  }
          break
        }
      }
    
  }
ends

state pigzshoot

state calczdist
ldist xydist THISACTOR target
ifvare xydist 0 setvar xydist 1
shiftvarl zdist 10
divvarvar zdist xydist

zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET
zshoot zdist SLOWBULLET

ifspritepal 10
{
	zshoot zdist SLOWBULLET
	zshoot zdist SLOWBULLET
	zshoot zdist SLOWBULLET
	zshoot zdist SLOWBULLET
	zshoot zdist SLOWBULLET
	zshoot zdist SLOWBULLET
}

ifvarg zdist 6144 setactor[THISACTOR].xvel -192
ifvarl zdist -6144 setactor[THISACTOR].xvel -192

ends

state pigfiring

ifspritepal 23 { shoot ENEMYSLUGPROJ break }

ifvarn target -1 { state pigzshoot break }
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
shoot SLOWBULLET
ifspritepal 10
{
   shoot SLOWBULLET
   shoot SLOWBULLET
   shoot SLOWBULLET
   shoot SLOWBULLET
   shoot SLOWBULLET
   shoot SLOWBULLET
}

ends

state pigshootenemystate

ifvarn cursed 0 ifvare target -1 { ai AIPIGSEEKENEMY break }

    ifcount 12 nullop
    else
      ifcount 11
    {
      ifvare mtype 1
      {
	    ifspritepal 10 sound DB_FIRE else
        sound PIG_ATTACK
        ifvarn target -1 { state shotgunathero break }
        state pigfiring
      }
      else
        ai AIPIGSEEKENEMY
    }
    ifcount 25 nullop
    else
      ifcount 24
      {
        action APIGCOCK
        ifspritepal 10 { sound DBCLOSE spawn SHOTGUNSHELL } else
        sound SHOTCOCK2
        spawn SHOTGUNSHELL
      }
    ifcount 48 nullop
    else
      ifcount 47
    {
      ifvare mtype 1
      {
	    ifspritepal 10 sound DB_FIRE else
        sound PIG_ATTACK
        ifvarn target -1 { state shotgunathero break }
        state pigfiring
      }
      else
        ai AIPIGSEEKENEMY
    }
    ifcount 60 nullop
    else
      ifcount 59
      {
        action APIGCOCK
        ifspritepal 10 { sound DBCLOSE spawn SHOTGUNSHELL } else
        sound SHOTCOCK2
        spawn SHOTGUNSHELL
      }
    ifcount 72
    {
      ifrnd 64
      {
	    ifvarn cursed 0
	    {
		    ifvarn target -1 resetcount
		    else ai AIPIGSEEKENEMY
	    }
	    else
        resetcount
      }
      else
      {
        ifpdistl 768
          ai AIPIGFLEEENEMY
        else
          ai AIPIGSEEKENEMY
      }
    }
    ifaction APIGCOCK
      ifactioncount 2
        action APIGSHOOT

ends

state pigfleeenemystate
  ifactioncount 8
    ai AIPIGSEEKENEMY
  else
    ifnotmoving
      ai AIPIGSEEKENEMY
ends

state pigdivestate

ifvarn cursed 0 ifvare target -1 { cactor PIGCOP ai AIPIGSEEKENEMY break }

  ifaction APIGDIVESHOOT
  {
    ifvare peractor1 1
    {
      ifcount 12 nullop
      else
        ifcount 11
      {
        ifvare mtype 1
        {
	      ifspritepal 10 sound DB_FIRE else
          sound PIG_ATTACK
          ifvarn target -1 { state shotgunathero break }
          state pigfiring
        }
        else
		{
		  cactor PIGCOP
          ai AIPIGSEEKENEMY
		}
      }
      ifcount 25 nullop
      else
        ifcount 24
        {
	      ifspritepal 10 { sound DBCLOSE spawn SHOTGUNSHELL } else
          sound SHOTCOCK2
          spawn SHOTGUNSHELL
        }
      ifcount 48 nullop
      else
        ifcount 47
      {
        ifvare mtype 1
        {
	      ifspritepal 10 sound DB_FIRE else
          sound PIG_ATTACK
          ifvarn target -1 { state shotgunathero break }
          state pigfiring
        }
        else
		{
		  cactor PIGCOP
          ai AIPIGSEEKENEMY
		}
      }
      ifcount 60 nullop
      else
        ifcount 59
      {
	    ifspritepal 10 { sound DBCLOSE spawn SHOTGUNSHELL } else
        sound SHOTCOCK2
        spawn SHOTGUNSHELL
        ifgapzl 32
          ai AIPIGDIVING
        else
        {
          ifpdistl 4096
		  {
		    cactor PIGCOP
            ai AIPIGFLEEENEMY
		  }
          else ifvarn target -1 ifvarl xydist2 4096
		  {
		    cactor PIGCOP
            ai AIPIGFLEEENEMY
		  }
          else
		  {
		    cactor PIGCOP
            ai AIPIGSEEKENEMY
		  }
        }
      }
    }
    else
      ifgapzl 32
        ai AIPIGDIVING
      else
	  {
	    cactor PIGCOP
        ai AIPIGSEEKENEMY
	  }
  }
  else
    ifactioncount 2
    ifp palive
    {
      resetcount
      action APIGDIVESHOOT
    }
ends

state checkpighitstate

  ifdead
  {
    cactor PIGCOP
    ifwasweapon BURNING nullop else
		state random_wall_jibs

      state drop_shotgun
	  
	addkills 1 add globalkills 1
	state specialdeath
    
    ifwasweapon FREEZEBLAST
		 ifdead spawn BIGSMOKE
    
    ifwasweapon RADIUSEXPLOSION
    {
      state randsquishsound
      state standard_jibs
      killit
    }
    else
      ifwasweapon RPG
    {

      state randsquishsound
      state standard_jibs
      killit
    }
    else
    {

	  // guts JIBS6 10
	  cstat 256
	  ifmove STOPPED setvar saveit NO else setvar saveit YES
      ai AIPIGDYING
      sound PIG_DYING
        
      ifvare command 1
	  {
		ifvare saveit NO
		action APIGDYING2
		else { action APIGWALK2 move PIGWALKVELS geth }
		ifmultiplayer nullop else ifvare powcount 0 ifvare powmode 0 
		ifl combo combomax
			addvar combo 1
		state aimjib
		ezshoot zdist HEADJIBPROJ
		state setjib
		
	  }
	  
	  
    }
  }
  else
  {
    sound PIG_PAIN
	setvar command 0
    ifwasweapon BURNING nullop else
		state random_wall_jibs

	ifai AIPIGWAIT { cactor PIGCOP ai AIPIGSEEKENEMY }
      ifvarl fear 0 { cactor PIGCOP ai AIPIGHIT } else
      ifrnd 32
	  {
	    cactor PIGCOP
        ai AIPIGHIT
	  }
    else ifwasweapon KNEE
      ifrnd 53
	  {
	    cactor PIGCOP
        ai AIPIGHIT
	  }
    else
      ifrnd 64
      {
	    ifvarn cursed 0 ifvare target -1 nullop else
	    ifai AIPIGSHOOTENEMY nullop else
		ifactor PIGCOP
        ai AIPIGSHOOTENEMY
      }
    else
      ifrnd 64
      {
	    ifvarn cursed 0 ifvare target -1 nullop else
	    ifai AIPIGDIVING nullop else
	    {
		    cactor PIGCOPDIVE
	        ai AIPIGDIVING
	        action APIGDIVESHOOT
        }
      }
  }
ends

state pigdyingstate

  ifaction APIGWALK2
  {
	  ifcount 16
	  {
		  resetcount
		  getactor[THISACTOR].z saveit
		  setvarvar z saveit
		  subvar z 10240
		  ifspritepal 14 addvar z 2048
		  ifspritepal 10 subvar z 2048
		  setactor[THISACTOR].z z
		  setvar countvar 6
		  whilevarn countvar 0
		  {
			  espawn JIBS6
			  randvar zdist 512
			  subvar zdist 1560
			  randvar xvel 32
			  setactor[RETURN].xrepeat 12
			  setactor[RETURN].yrepeat 12
			  setav[RETURN].htg3 999999
			  setactor[RETURN].zvel zdist
			  setactor[RETURN].xvel xvel
			  subvar countvar 1
		  }
		  setactor[THISACTOR].z saveit
		  
		  
	  }
	  strength 0
	  setvar monstatus 1
	  cstator 257
	  setvar temp 0
	  ifhitweapon setvar temp 1
	  ifactioncount 16 setvar temp 1
	  ifvare temp 1
	  {
		  action APIGDYING2
		  move STOPPED
		  setvar monstatus 2
		  
		  ifwasweapon RADIUSEXPLOSION
		  {
		      state randsquishsound
		      state standard_jibs
		      killit
		  }
		  else
		    ifwasweapon RPG
		  {
		      state randsquishsound
		      state standard_jibs
		      killit
		  }
		  ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
	  }
	  break
  }
  ifactioncount 5
  {
    ifrnd 64
      spawn BLOODPOOL
    state rf
    iffloordistl 8
      sound THUD
    action APIGDEAD
    // setvar command 65536
    // setactor[THISACTOR].mdflags 16
    espawn FLIES
	setactorvar[RETURN].myspawner THISACTOR
    move STOPPED
    cstat 0
    break
  }
ends

action STATUEFRAME -5 1 5 1 1

eventloadactor PIGCOP

ifspritepal 17 
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	// action APIGSTAND
	cactor 2025
	sizeat 32 27 // 48 40
	state eventloadactorstate
}
sizeat 32 27 // 48 40
ifspritepal 10 sizeat 36 31 // 54 46
ifspritepal 14 sizeat 27 23 // 40 34
ifvare SKILL 5 ifspritepal 0 ifrnd 64 spritepal 23
enda

eventloadactor PIGCOPSTAYPUT
ifspritepal 17 
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	// action APIGSTAND
	cactor 2025
	sizeat 32 27 // 48 40
	state eventloadactorstate
	setvar peractor1 1 // stayput
}
sizeat 32 27 // 48 40
ifspritepal 10 sizeat 36 31 // 54 46
ifspritepal 14 sizeat 27 23 // 40 34
ifvare SKILL 5 ifspritepal 0 ifrnd 64 spritepal 23
enda

// actor PIGCOPDIVE PIGCOPSTRENGTH
  // ai AIPIGDIVING
  // action APIGDIVESHOOT
  // setvar gold GOLD4
  // cactor PIGCOP
  // ifspritepal 10
  // {
	  // addstrength PIGCOPSTRENGTH
	  // sizeat 54 46
	  // addvar gold GOLD2
  // }
  // ifspritepal 14
  // {
	  // strength 46
	  // sizeat 40 34
	  // subvar gold GOLD2
  // }
// enda

state pigcopcode

ifvarand sprite[THISACTOR].cstat 8 nullop else
{
	getactor[THISACTOR].z z
	subvar z 4096
	setactor[THISACTOR].z z
}

ifl burning 0
ifactor PIGCOPDIVE
{
	cactor PIGCOP
	ai AIPIGHIT
}

setvar peractor1 0
setvar mtype 0
ifvare fear 0
{
	ifcansee setvar peractor1 1
	ifcanshoottarget setvar mtype 1
	ifvarn target -1 { setvar peractor1 1 setvar mtype 1 }
}
ifvarand sprite[THISACTOR].cstat 8 nullop else
{
	addvar z 4096
	setactor[THISACTOR].z z
}

  ifvarand sprite[THISACTOR].cstat 8
  {
	  getceilzofslope sprite[THISACTOR].sectnum sprite[THISACTOR].x sprite[THISACTOR].y mz
	  ifspritepal 10 addvar mz 4096
	  addvar mz 12288
	  ifvarvarg sprite[THISACTOR].z mz
	  {
	  	getactor[THISACTOR].z z
	  	subvar z 512
	  	setactor[THISACTOR].z z
  	  }
  	  else
  	  setactor[THISACTOR].z mz
  }
  else
  fall
  state checksquished
  ifaction APIGSTAND
  {
	  setvar gold GOLD4
      ai AIPIGSEEKENEMY
      sizeat 32 27 // 48 40
      ifspritepal 10
	  {
		  addstrength PIGCOPSTRENGTH
		  sizeat 36 31
		  addvar gold GOLD2
		  ifg SKILL 3 state randomaura
	  }
	  ifspritepal 14
	  {
		  strength 46
		  sizeat 27 23
		  subvar gold GOLD2
	  }
	  
  }
  state monsterai
  
  ifaction APIGDEAD
  {
    
      strength 0
      ifhitweapon
      {
        ifwasweapon RADIUSEXPLOSION
	    {
	        state randsquishsound
	        state standard_jibs
	        killit
	    }
	    ifwasweapon RPG
	    {
	        state randsquishsound
	        state standard_jibs
	        killit
	    }
      }
      break
  }

  ifai AIPIGDYING
  {
    state pigdyingstate
    break
  }

    ifai AIPIGHIT
    {
	  ifvarl fear 0 resetactioncount
      ifactioncount 3
      ai AIPIGSEEKENEMY
    }
    
    ifai AIPIGSEEKENEMY
      state pigseekenemystate
    else
      ifai AIPIGDODGE
        state pigseekenemystate
    else
      ifai AIPIGSHOOTENEMY
        state pigshootenemystate
    else
      ifai AIPIGGROW
        state genericgrowcode
    else
      ifai AIPIGFLEEENEMY
        state pigfleeenemystate
    else
      ifai AIPIGDIVING
        state pigdivestate
    else
      ifai AIPIGCHARGE
        state pigseekenemystate
    else
      ifai AIPIGWAIT
        state pigwaitstate
    ifhitweapon
      state checkpighitstate
    ifrnd 1 ifvare burning 0
    {
      ifrnd 32
        soundonce PIG_ROAM
      else
        ifrnd 64
          soundonce PIG_ROAM2
      else
        soundonce PIG_ROAM3
    }
ends

actor PIGCOPDIVE PIGCOPSTRENGTH
  ifai 0
  {
	sizeat 32 27
	ai AIPIGDIVING
	setvar gold GOLD4
	ifspritepal 10
	  {
		  addstrength PIGCOPSTRENGTH
		  sizeat 36 31
		  addvar gold GOLD2
	  }
	  ifspritepal 14
	  {
		  strength 46
		 sizeat 27 23
		  subvar gold GOLD2
	  }
  }
  state pigcopcode
enda

actor PIGCOPSTAYPUT PIGCOPSTRENGTH
  ai AIPIGWAIT
  setvar gold GOLD4
  cactor PIGCOP
  ifspritepal 17 action APIGSTAND
  sizeat 32 27
  ifspritepal 10
  {
	  addstrength PIGCOPSTRENGTH
	  sizeat 36 31
	  addvar gold GOLD2
	  ifg SKILL 3 state randomaura
  }
  ifspritepal 14
  {
	  strength 46
	 sizeat 27 23
	  subvar gold GOLD2
  }
enda

useractor enemy 2025 PIGCOPSTRENGTH
ifspritepal 17
{
	action STATUEFRAME
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength PIGCOPSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		ifvare SKILL 5 ifrnd 64 spritepal 23
		ifvare peractor1 1 
		{
			setvar peractor1 0
			getactor[THISACTOR].sectnum mysector
			setactor[THISACTOR].htactorstayput mysector
			cactor PIGCOPSTAYPUT 
		}
		else
		cactor PIGCOP
		action APIGSTAND
	}
	else break
}
enda

actor PIGCOP PIGCOPSTRENGTH APIGSTAND

ifspritepal 17
{
	action APIGSTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	setvar command 0
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength PIGCOPSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
		spritepal 0
	else break
}

state pigcopcode
  
enda

action GOODHEADFRAMES -10  3  5  1  4
action DRONEFRAMES   -5   3   5   1   4
action DRONESCREAM   -5   3   5   1   4
move DRONERUNVELS 128 64
move DRONECHARGVEL 320 64
move DRONEFIREDVEL 480 64
move DRONEFIREDVEL2 644 64
move DRONEFIREDUPVEL 480 -64
move DRONERUNUPVELS 128 -64
move DRONEBULLVELS 252 -64
move DRONEBACKWARDS -64 -64
move DRONERISE 32 -32
// move DRONESTOPPED -16

ai AIDRONEFIRED DRONEFRAMES DRONEFIREDVEL geth
ai AIDRONEGETE DRONESCREAM DRONERUNVELS faceplayerslow getv
ai AIDRONEWAIT DRONEFRAMES DRONESTOPPED faceplayerslow
ai AIDRONEGETUP DRONESCREAM DRONERUNUPVELS faceplayer getv
ai AIDRONEPULLBACK DRONEFRAMES DRONEBACKWARDS faceplayerslow
ai AIDRONEHIT DRONESCREAM DRONEBACKWARDS faceplayer
ai AIDRONEDODGE DRONEFRAMES DRONEBULLVELS dodgebullet geth
ai AIDRONEDODGEUP DRONEFRAMES DRONERISE getv geth

state dronefiredstate

ifvare mtype 2 ifmove DRONEFIREDVEL move DRONEFIREDVEL2 faceplayerslow
ifvarn target -1
{
	getactor[THISACTOR].z z
	getactor[target].z mz
	subvarvar mz z
	shiftvarr mz 3
	movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
}
		
ifcount 130 ai AIDRONEGETE

ends


state dronedead

 ifactor GOODHEAD
 {
	  ifvarn target -1
	  {
		  ifvarvarg sprite[target].htextra sprite[target].extra break
		  getactor[target].htextra temp
		  addvar temp 79
		  setactor[target].htextra temp
		  setactor[target].htpicnum RADIUSEXPLOSION
		  setactor[target].htowner player[THISACTOR].i
		  setactor[target].htang sprite[THISACTOR].ang
		  espawn EXPLOSION2
  		  setsprite RETURN sprite[target].x sprite[target].y sprite[target].z
	  }
	  ifrnd 128 espawn BURNING else espawn BURNING2
	  setactor[RETURN].pal sprite[THISACTOR].pal
	  state aimjib
	  ezshoot zdist JIBSPROJ2
	  state setjib
	  state aimjib
	  ezshoot zdist JIBSPROJ2
	  state setjib
	  state aimjib
	  ezshoot zdist JIBSPROJ3
	  state setjib
	  state aimjib
	  ezshoot zdist JIBSPROJ3
	  state setjib
	  state aimjib
	  ezshoot zdist JIBSPROJ3
	  state setjib
  
	  spawn BLUDSPLASH
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  espawn SOUNDMAKER
  	  setactorvar[RETURN].peractor1 EXPL6
	  killit
	  break
  }
  addkills 1 add globalkills 1
  state specialdeath
  
  ifrnd 128 espawn BURNING else espawn BURNING2
  ifspritepal 0 spritepal 20
  ifspritepal 16 spritepal 20
  ifspritepal 19 spritepal 20
  getactor[THISACTOR].pal pal
  setactor[RETURN].pal pal
  ifspritepal 10 spritepal 2
  ifspritepal 11 spritepal 8

 ifvarl livemon 100
 {
  guts JIBS6 4
  state aimjib
  ezshoot zdist JIBSPROJ2
  state setjib
  state aimjib
  ezshoot zdist JIBSPROJ2
  state setjib
  state aimjib
  ezshoot zdist JIBSPROJ3
  state setjib
  state aimjib
  ezshoot zdist JIBSPROJ3
  state setjib
  state aimjib
  ezshoot zdist JIBSPROJ3
  state setjib
}
  
  
  spawn BLUDSPLASH
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  spawn BLOODCLOUD
  ifspritepal 21 
  {
	  hitradius 3072 25 35 45 55 
	  spawn MININUKE
	  spawn MININUKE
	  spawn MININUKE
	  spawn MININUKE
	  spawn MININUKE
	  spawn MININUKE
	  spawn MININUKE
  }
 
  else
  hitradius 2048 15 20 25 30
  espawn SOUNDMAKER
  setactorvar[RETURN].peractor1 EXPL6
  killit
ends

state checkdronehitstate
  ifdead state dronedead
  else ifsquished state dronedead
  else
  {
    sound SKELPAIN
    ifbulletnear
    {
      ifceilingdistl 64
        ifrnd 48
          ai AIDRONEDODGE
      else
      ai AIDRONEDODGEUP
    }
    else
      ai AIDRONEGETE
  }
ends

state checkdronenearplayer

ifvarn endlevel 0 break
ifvarl burning 0 break

ifspritepal 21
{
  ifpdistl 4096
  ifrnd 8 soundonce DRON_RECOG
  
   ifvare cursed 0
   ifp palive
	ifpdistl 2048
      ifcount 8
   {
	   ifcansee
   		state dronedead
   	   else ifpdistl 1408
   	    state dronedead
   }
   else
   ifvarn target -1
   ifvarl xydist2 1596
   state dronedead
   
   break
}

  ifvare cursed 0
  ifp palive
    ifpdistl 1596
      ifcount 8
   state dronedead
   
   else
   ifcount 8
   ifvarn target -1
   {
   	  ifvarl xydist2 1024
      state dronedead
   }
   
ends

state dronegetstate

  ifrnd 192
  {
	setvar temp 0
    ifcansee setvar temp 1
    else ifvarn target -1 setvar temp 1
    ifvare temp 1
    {
      ifbulletnear
      {
        ai AIDRONEDODGE
        break
      }
      ifmove DRONECHARGVEL
      {
	      ifrnd 64
	      {
		      espawn BIGSMOKE
		      setactor[RETURN].pal 2
	      }
	    ifcount 48
          ai AIDRONEPULLBACK
        else
          ifnotmoving
            ifcount 16
              ai AIDRONEPULLBACK
      }
      else
      ifmove DRONEBULLVELS
      {
        ifcount 64
          ai AIDRONEPULLBACK
        else
          ifnotmoving
            ifcount 16
              ai AIDRONEPULLBACK
      }
      else
        ifcount 32
      {
	    sound DRON_ATTACK1
	    ifvarn target -1
	    {
		     ifvarvarl sprite[target].z sprite[THISACTOR].z
	          move DRONEBULLVELS geth getv
	        else
	        {
		      ifspritepal 21 move DRONECHARGVEL geth else
	          move DRONEBULLVELS geth
	        }
	    }
	    else
	    {
	        ifp phigher
	          move DRONEBULLVELS geth getv
	        else
	        {
		      ifspritepal 21 move DRONECHARGVEL geth else
	          move DRONEBULLVELS geth
	        }
        }
      }
    }
    else
    {
	    ifcount 78 
      	ai AIDRONEWAIT
    }
  }
ends

state dronedodgestate
  ifai AIDRONEDODGEUP
  {
     ifcount 8
       ai AIDRONEGETE
     else
       ifnotmoving
         ai AIDRONEGETE
  }
  else
  {
    ifcount 8
      ai AIDRONEGETE
    else
      ifnotmoving
        ai AIDRONEGETE
  }
ends

eventloadactor DRONE
ifvare SKILL 5 ifrnd 48 spritepal 21
ifspritepal 1 spritepal 20
ifspritepal 21 sizeat 48 48
else
sizeat 26 26
enda

state dronecode

 state checkdronenearplayer
 
 ifaction GOODHEADFRAMES nullop else ifactor GOODHEAD action GOODHEADFRAMES
  
ifspritepal 21 soundonce FIRE_CRACKLE

  ifrnd 2
    fall
  // else
  //  soundonce DRON_JETSND

  ifaction 0
  {
	sound DRON_ATTACK1
	geta[].htflags temp, orvar temp 4 seta[].htflags temp
	seta[].shade -64
	ifn sprite[].pal 21 ifg sprite[].xrepeat 26 sizeat 26 26
	ifactor GOODHEAD { cstat 256 sizeat 42 36 } else
	{
		ifn gold -1
		setvar gold GOLD1
		ifspritepal 21 sizeat 48 48 else
		sizeat 32 32
	}
    ifspawnedby DETHNITE
    {
	    ai AIDRONEFIRED
	    setvar gold 0
	    break
    }
	ifspawnedby LUGLORD
	{
		ai AIDRONEFIRED
		move DRONEFIREDVEL2 faceplayerslow
		setvar gold 0
		break
	}
    ifvare mtype 2
    {
	    ai AIDRONEFIRED
	    sizeat 48 48
	    spritepal 21
	    addstrength DRONESTRENGTH
		addstrength DRONESTRENGTH
		addstrength DRONESTRENGTH
	    setvar gold 0
	    break
    }
    ai AIDRONEGETE
    ifspritepal 21
	{
		clipdist 48
		addstrength DRONESTRENGTH
		addstrength DRONESTRENGTH
		addstrength DRONESTRENGTH
		sizeat 48 48
		addvar gold GOLD1
	}
	ifmultiplayer nullop else
	{
		setvar tempd 2
		getactor[THISACTOR].pal pal
		ifvare pal 0 setvar pal 1
		// setvar intensity 5120
		setvar tempc 1 // moving
		setvar peractor6 35
		// state spawnpointlight
	}
  }

  else
    ifai AIDRONEWAIT
  {
    ifactioncount 4
      ifrnd 16
    //    ifcansee
    {
      ifvarn target -1
      {
      	ifvarvarg sprite[THISACTOR].z sprite[target].z
        	ai AIDRONEGETUP
       	else ai AIDRONEGETE
      }
      else
      {
      	ifp phigher
        	ai AIDRONEGETUP
      	else ai AIDRONEGETE
      }
    }
  }
  else
    ifai AIDRONEGETE
      state dronegetstate
  else
    ifai AIDRONEGETUP
      state dronegetstate
  else
    ifai AIDRONEPULLBACK
  {
    ifcount 32
    ai AIDRONEWAIT
  }
  else
    ifai AIDRONEHIT
  {
    ifcount 8
      ai AIDRONEWAIT
  }
  else
    ifai AIDRONEDODGE
      state dronedodgestate
  else
    ifai AIDRONEDODGEUP
      state dronedodgestate
  else
    ifai AIDRONEFIRED
      state dronefiredstate
  
  state monsterai

  ifhitweapon
    state checkdronehitstate

  ifrnd 1 ifvare burning 0
    soundonce DRON_ROAM
    
    getceilzofslope sprite[THISACTOR].sectnum sprite[THISACTOR].x sprite[THISACTOR].y mz
    getactor[THISACTOR].z z
    subvarvar z mz
    ifvarl z 3072
    {
	    ifvarl sprite[THISACTOR].zvel 0 setactor[THISACTOR].zvel 512
	    getactor[THISACTOR].z z
	    addvar z 512
	    setactor[THISACTOR].z z
    }
ends

spritenoshade DRONE
spritenopal DRONE
actor DRONE DRONESTRENGTH

state dronecode

enda

spritenopal GOODHEAD
useractor notenemy GOODHEAD 80

ifaction 0
{
	ifvare myspawner -1 killit
	action GOODHEADFRAMES
	cstat 2 
	sizeat 36 36
	move INIT spin
	ifmultiplayer nullop else
	{
		setvar tempd 2
		getactor[THISACTOR].pal pal
		ifvare pal 0 setvar pal 1
		// setvar intensity 5120
		setvar tempc 1 // moving
		setvar peractor6 35
		// state spawnpointlight
	}
}

setvar cursed -1


ifvare mtype 0
{
	cstat 2
	ifn endlevel 0 killit
	getplayer[myspawner].posx x
	getplayer[myspawner].posy y
	getplayer[myspawner].posz z
	
	setvar xydist 1024

	setvarvar mx x
	addvarvar mx xydist
	rotatepoint x y mx y peractor1 mx my
	setsprite THISACTOR mx my z
	
	ifn player[].cursectnum -1
	{
		ifn sector[player[].cursectnum].floorbunch -1
			changespritesect THISACTOR player[].cursectnum
		else
		ifn sector[player[].cursectnum].ceilingbunch -1
			changespritesect THISACTOR player[].cursectnum
	}
	
	addvar peractor1 32
	ifvarg peractor1 2047 subvar peractor1 2048
	
	setvarvar temp timer
	addvarvar temp peractor1
	shiftvarl temp 6
	sin tempb temp
	shiftvarr tempb 7
	
	
	getactor[THISACTOR].z z
	subvarvar z tempb
	setactor[THISACTOR].z z
	
	ifp palive nullop else
	{
		cstat 256
		ai AIDRONEFIRED
		action GOODHEADFRAMES
		setvar mtype 1
	}
	
	ifrnd 32
	state targetmon
	ifvarn target -1 // activated
	{
		cstat 256
		ai AIDRONEFIRED
		action GOODHEADFRAMES
		setvar mtype 1
	}
	break
}


state dronecode

ifp palive
ifvare target -1
ifpdistl 1024
{
	setvar mtype 0
	randvar peractor1 2047
	cstat 2
}

enda

action AWIZFLOAT 0 2 5 1 30
action AWIZSPELL 5 3 5 1 30
action AWIZSPELL2 15 1 5 1 20
action AWIZSPIN 10 1 5 1 2
action AWIZFLINCH 20 1 5 1 50
action AWIZDIE 25 8 1 1 16
action AWIZDEAD 32 1 1

move WIZSLOW 72 -38
move WIZMEDIUM 128 -38
move WIZFAST 384 -38
move WIZSTOPPED 0 -38
move WIZLEVUP 0 -128
move WIZLEV 0 -56
move WIZLEVDOWN 0 -24

ai AIWIZFLOAT AWIZFLOAT WIZSLOW seekplayer
ai AIWIZSPELL AWIZSPELL WIZSTOPPED faceplayerslow
ai AIWIZSPIN AWIZSPIN WIZLEVUP geth
ai AIWIZTELEPORT AWIZFLOAT WIZSTOPPED faceplayer
ai AIWIZLIFT AWIZSPELL2 WIZLEVUP geth
ai AIWIZHAIL AWIZSPELL2 WIZSTOPPED faceplayer

eventloadactor WIZARD
sizeat 29 27
ifspritepal 17 
{
	action AWIZFLOAT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
ifspritepal 25
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	sizeat 33 30
}
enda

action WIZSCRIPTFLOAT -5 2 5 1 30
action WIZSCRIPTSPELL1 5 1 5 1 30
action WIZSCRIPTSPELL2 10 1 5 1 30

eventloadactor WIZSCRIPT
getactor[THISACTOR].lotag lotag
setactor[THISACTOR].lotag 0
sizeat 38 35 // 56 52
cstator 128
enda

spritenoshade WIZSCRIPT
useractor notenemy WIZSCRIPT 0

addvar countvar 1
ifvarl countvar 182
{
	fall
	setp[].horiz 110
}
ifmove 0
{
	globalsound WIZWAKE
	lockplayer 180
	setp[].horiz 110
	geta[].x mx
	geta[].y my
	sub mx player[].posx
	sub my player[].posy
	getangle angvar mx my
	setp[].ang angvar
	
	setp[].horiz 115
	move WIZLEV faceplayerslow
	action WIZSCRIPTFLOAT
	sizeat 38 35 // 64 58
	cstat 128
	getactor[THISACTOR].ang targetlock
}

ifvare countvar 78 action WIZSCRIPTSPELL1
ifvarg countvar 104 ifvarl countvar 182
{
	ifvare countvar 105
	{
		 globalsound WIZROAM1
		 move WIZLEV geth
		 action WIZSCRIPTSPELL2
	}
	spawn COLDGUST
	spawn COLDGUST
	spawn COLDGUST
	spawn COLDGUST
	spawn COLDGUST
	spawn COLDGUST
	soundonce CLDWND
	getactor[THISACTOR].ang angvar
	subvar angvar 117
	setactor[THISACTOR].ang angvar
}

ifvarl countvar 96
{
	espawn GLINT
	setactor[RETURN].htpicnum GOLEM
}

ifvare countvar 182 
{
	globalsound WIZROAM2
	move STOPPED
	setactor[THISACTOR].ang targetlock
	action WIZSCRIPTSPELL1
	globalsound PICKUPSND
	palfrom 63 55 55 63
	globalsound FREEZEOVER
}

ifvarg countvar 182 ifvarl countvar 338
{
	soundonce FREEZEOVER
	getactor[THISACTOR].ang angvar
	addvar angvar 84
	setactor[THISACTOR].ang angvar
	ifspritepal 6 spritepal 25 else spritepal 6
	
	setvar weap 0
	setvar countvarb 6

	
	getplayer[THISACTOR].posx x
	getplayer[THISACTOR].posy y
	whilevarvarn weap countvarb
	{
		addvar weap 1
		setvarvar xydist x
		randvar temp 16384 // the random component of the distance from player
		addvar temp 512
		addvarvar xydist temp
		randvar angvar 2047
		
		rotatepoint x y xydist y angvar tempb y
		// tempb and y are the new x and y coords
		updatesector tempb y tempd
		ifvarn tempd -1
		{
		
			ifvarand sector[tempd].ceilingstat 1
			{
				getceilzofslope tempd tempb y z // z is the return var

				updatesectorz tempb y z tempd
				ifvarn tempd -1
				{
					espawn ICEMETEOR
					setactor[RETURN].x tempb
					setactor[RETURN].y y
					setactor[RETURN].z z
					// setactor[RETURN].sectnum tempd
					changespritesect RETURN tempd
				}
			}
		}
	}
}

ifvare countvar 338
{
	spritepal 25
	setactor[THISACTOR].ang targetlock
	action WIZSCRIPTFLOAT
	ifvarn lotag 0
	{
		operateactivators lotag 0
		operatemasterswitches lotag
		operaterespawns lotag
		setvar lotag 0
	}
	globalsound PICKUPSND
	palfrom 63 55 55 63
	globalsound FREEZEOVER
	setvar raining -5
	setvar snowceiling 65536
	setvar rainradius 16384
	setvar raincstat 0
	setvar rainpal 0

}

ifvarg countvar 338
{
	sizeto 0 132 // 255
	getactor[THISACTOR].z z
	subvar z 1024
	setactor[THISACTOR].z z
}

enda

state wizbosstargetport

sound WIZPORT
espawn BIGSMOKE
setactor[RETURN].pal 1

getactor[target2].x mx
getactor[target2].y my
getactor[THISACTOR].x x
getactor[THISACTOR].y y
subvarvar mx x
subvarvar my y
getangle angvar mx my
setactor[THISACTOR].ang angvar

getactor[target2].x mx
getactor[target2].y my
getactor[target2].z mz

seta[].x mx
seta[].y my
seta[].z mz
seta[].sectnum sprite[target2].sectnum

setvarvar temp mx
addvar temp 768
addvar angvar 1024
rotatepoint mx my temp my angvar x y
setsprite THISACTOR x y mz
sound WIZPORT
espawn BIGSMOKE
setactor[RETURN].pal 1

ifcansee nullop else
{
	setvar bigmsg 417
	setvar bigmsgcount 104
}

ends

state wizliftstate

getactor[THISACTOR].x x
getactor[THISACTOR].y y
getactor[THISACTOR].z z
setactor[target2].z z
updatesectorz x y z temp
ifvare temp -1 
{ 
	setactorvar[target2].lotag -65
	ai AIWIZFLOAT
	setvar target2 -1
	break
}
getceilzofslope temp x y mz
addvar mz 8192
ifvarvarl z mz
{
	setactorvar[target2].lotag -65
	ai AIWIZFLOAT
	setvar target2 -1
	break
}
ifcount 78
{
	setactorvar[target2].lotag -65
	ai AIWIZFLOAT
	setvar target2 -1
	break
}

ends

state wizhailstate

getactor[THISACTOR].htextra temp
ifvarg temp 0
{
	shiftvarr temp 4
	ifvarl temp 1 setvar temp 1
	getactor[THISACTOR].yrepeat y
	subvarvar y temp
	ifvarl y 32 // 52 
	{ 
		ai AIWIZSPIN
		setvar countvarb 0
		sizeat 33 31 // 56 52 
		cstat 257
		// setvar raining -5
		// setvar snowceiling 65536
		// setvar rainradius 16384
		// setvar raincstat 0
		// setvar rainpal 0
		break
	}
	setvarvar x y
	addvar x 2 // 4
	setactor[THISACTOR].xrepeat x
	setactor[THISACTOR].yrepeat y
}

setactor[THISACTOR].xvel 0

ifcansee
{
	setvar weap 0
	setvar countvarb 3
	
	getplayer[THISACTOR].posx x
	getplayer[THISACTOR].posy y
	whilevarvarn weap countvarb
	{
		addvar weap 1
		setvarvar xydist x
		randvar temp 3072 // the random component of the distance from player
		addvarvar xydist temp
		randvar angvar 2047	
		rotatepoint x y xydist y angvar tempb y
		// tempb and y are the new x and y coords
		updatesector tempb y tempd
		ifvarn tempd -1
		ifvarand sector[tempd].ceilingstat 1
		{
			getceilzofslope tempd tempb y z // z is the return var
			updatesectorz tempb y z tempd
			ifvarn tempd -1
			{
				espawn ICEMETEOR
				setactor[RETURN].x tempb
				setactor[RETURN].y y
				setactor[RETURN].z z
				changespritesect RETURN tempd
				setactorvar[RETURN].weap 1 // makes it deadly
				resetcount
			}
			
		}
	}
	ifcount 104
	{
		ai AIWIZSPIN
		setvar countvarb 0
		sizeat 33 31 // 56 52  
		cstat 257
		// setvar raining -5
		// setvar snowceiling 65536
		// setvar rainradius 16384
		// setvar raincstat 0
		// setvar rainpal 0
	}
}


ends

state wizteleportstate

setactor[THISACTOR].htextra -1

action AWIZSPIN
ifcount 11 nullop else
{
	// sizeto 4 46
	// sizeto 4 46
	// sizeto 4 46
	sizeto 2 28
	sizeto 2 28
}

ifcount 2 nullop else cstat 2

ifcount 7 nullop else
ifcount 6 cstator 512

ifcount 11 nullop else
ifcount 10 
{
	cstat 32768
	sound TELEPORTER
	move WIZFAST randomangle
	count 11
}

ifcount 27 
{ 
	// sizeto 48 46 sizeto 48 46 
	sizeto 29 27 
} 
else
ifcount 26
{
	move WIZSTOPPED faceplayer
	count 27
	cstat 514
	sound TELEPORTER
}

ifcount 33 nullop else
ifcount 32 cstat 2

ifcount 36
{
	cstat 257
	sizeat 29 27 // 48 46
	ifrnd 128 ai AIWIZFLOAT else
		ai AIWIZSPELL

}

ends

state spawnshields

findnearactor WIZARDSHIELD 3072 spriteid
ifn spriteid -1 ife actorvar[spriteid].myspawner THISACTOR break

ifn sprite[].pal 25 subvar ammo 100

setvar countvar 0
whilevarn countvar 24 // 32
{
	espawn WIZARDSHIELD
	setactorvar[RETURN].myspawner THISACTOR
	setvarvar temp countvar
	// shiftvarl temp 6
	mulvar temp 85
	setactorvar[RETURN].peractor1 temp
	addvar countvar 1
}
ends

state wizspinstate


ifmove WIZLEVUP
ifcount 10
{
	move WIZLEV spin
	sound WIZHEAL
	ifspritepal 24 nullop else
	ifg ammo 99
	ifg mtype 300
	{
		set mtype 0
		ifspritepal 10 cactor WIZARDRED
		state spawnshields
		ifspritepal 10 cactor WIZARD
	}
}

ifmove WIZLEV 
{
	
	ifcount 26 move WIZLEV spin
	ifactioncount 2
	{
		resetactioncount	
		ifspritepal 24 spawn LIGHTNINGBOLT 	
	}
	ifspritepal 6 getlastpal else spritepal 6
	addstrength 1
	ifspritepal 25 addstrength 2
	ifvarg burning 2 subvar burning 1
	subvar ammo 1
	ifvarvarl sprite[THISACTOR].extra peractor1 nullop else
	{
		move WIZLEVDOWN faceplayerslow
		ifspritepal 6 getlastpal
	}
	ifcount 78 
	{
		move WIZLEVDOWN faceplayerslow
		ifspritepal 6 getlastpal
	}
	ifvarl ammo 1
	{
		move WIZLEVDOWN faceplayerslow
		ifspritepal 6 getlastpal
	}
}

ifmove WIZLEVDOWN
{
	iffloordistl 16 ai AIWIZFLOAT else
	ifcount 52 ai AIWIZFLOAT
	ifspritepal 6 getlastpal
}

ends

state encaseplayer

getplayer[THISACTOR].posx saveit
getplayer[THISACTOR].posy saveit2
getplayer[THISACTOR].posz mz
getplayer[THISACTOR].ang angvar
addvar mz 8192
setvar countvar 8
whilevarn countvar 0
{
	espawn WIZARDSHIELD
	setactor[RETURN].ang angvar
	
	setvarvar temp saveit
	addvar temp 844
	rotatepoint saveit saveit2 temp saveit2 angvar x y
	updatesector x y sect
	ifn sect -1
		setsprite RETURN x y mz
	// setactor[RETURN].x x
	// setactor[RETURN].y y
	// setactor[RETURN].z mz
	setactor[RETURN].htpicnum APLAYER
	setactor[RETURN].ang angvar
	subvar countvar 1
	addvar angvar 256
}
lockplayer 10

ends

state icespikes

setvar countvar 16
setvar angvar 0
whilevarn countvar 0
{
	setvarvar temp player[].posx
	addvar temp 1536
	rotatepoint player[].posx player[].posy temp player[].posy angvar saveit saveit2
	updatesector saveit saveit2 sect
	ifn sect -1
	{
		espawn ICESPIKE 
		setactorvar[RETURN].myspawner THISACTOR
		
		setsprite RETURN saveit saveit2 sprite[player[].i].z

		addvar angvar 128
		updatesectorz saveit saveit2 sprite[player[].i].z temp
		ifvarn temp -1
			getflorzofslope temp saveit saveit2 z	
		else set z sprite[player[].i].z 
		setactor[RETURN].z z
	}
	subvar countvar 1
}

ends


state wizfloatstate

ifpdistg 6144 ifmove WIZSLOW move WIZMEDIUM seekplayer faceplayerslow

ifrnd 1 ifvare burning 0
{
	ifrnd 128 sound WIZROAM1
	else sound WIZROAM2
}

setvar temp 0
ifvarn target -1 setvar temp 1
else
ifvare cursed 0 ifcansee setvar temp 1

ifactioncount 3
ifp palive
ifvare temp 1
ifrnd 32
{
	ifvarg ammo 50
	{
		ifstrength 99 ai AIWIZSPIN else
		ifrnd 160
		ifvarvarl sprite[THISACTOR].extra initz ai AIWIZSPIN else
		ifrnd 64
		ifvarvarl sprite[THISACTOR].extra peractor1 ai AIWIZSPIN
		else
			ai AIWIZSPELL
			
		ifai AIWIZSPIN
		{
			findnearactor WIZARDSHIELD 3072 spriteid
			ifn spriteid -1 ife actorvar[spriteid].myspawner THISACTOR ai AIWIZSPELL
		}
			
	}
	else
		ai AIWIZSPELL
		
}

ifcount 26
ifp palive
{
	ifp pfacing ifcansee
	ifrnd 4 ifvare cursed 0 ai AIWIZTELEPORT
	else
	ifbulletnear ifrnd 16 ai AIWIZTELEPORT
}

ends

state wizspellstate

ifvarg fear 0 { ai AIWIZFLOAT break }
ifvare target -1 ifvarn cursed 0 { ai AIWIZFLOAT break }

ifcount 2 nullop else 
{
	sound WIZATK
	ifspritepal 24 sound BOS4_ATTACK
	ifspritepal 0 ifrnd 32
	ifvare cursed 0
	ifpdistl 10240 ifcansee
	{
		wackplayer
		wackplayer
		ife invulntime 0 addphealth -3
		palfrom 36 0 0 40
		state dukepainsounds
		state encaseplayer
		ai AIWIZFLOAT
		break
	}
	ifspritepal 25
	{
		setvar temp 0
		ifrnd 32 setvar temp 1
		else ifstrength 600 ifg ammo 200 setvar temp 1
		ifvare temp 1
		{
			setvar target2 -1
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				setvar temp 0
				ifvare sprite[spriteid].picnum GOLEM setvar temp 1
				ifvare sprite[spriteid].picnum WRAITH setvar temp 1
				ifvare temp 1
				ifvare sprite[spriteid].pal 17
				ife actorvar[spriteid].lotag 100
				{
					setvarvar target2 spriteid
					setvar spriteid -1
				}
				else nextspritestat spriteid spriteid
			}
			ifvarn target2 -1
			{
				state wizbosstargetport
				ai AIWIZLIFT
				ifstrength 600 { sub ammo 200 strength 1000 }
				break
			}
			
		}
		ifrnd 48
		ifpdistl 16384 // ifcansee
		{
			state icespikes
			ai AIWIZFLOAT
		}
	}
	
}
ifactioncount 3 
{
	ifspritepal 10
	{
		ifrnd 128 ifpdistl 10240
		{
			sound WIZFLAME
			
			ifvare target -1 { setvarvar target player[THISACTOR].i }
			state calczdist
			ldist xydist THISACTOR target
			ifvare xydist 0 setvar xydist 1
			mul zdist 768
			divvarvar zdist xydist
			
			setvarvar x xydist
			div x 10
			subvarvar zdist x
			
			ezshoot zdist FTPROJ	
			ezshoot zdist FTPROJ
			setactorvar[RETURN].peractor1 1
			ifvarvare target player[THISACTOR].i setvar target -1
		}
		else
		{
			geta[].z initz
			sub initz 8192
			seta[].z initz
			
			espawn RPG_GUIDED
			setactorvar[RETURN].myspawner THISACTOR
			ifvarn target -1 setactorvar[RETURN].target target
			else ife cursed 0 setav[RETURN].target player[].i
			sound RPG_SHOOT
			
			add initz 8192
			seta[].z initz
		}
	}
	else ifspritepal 24
	{
		soundonce LGTNSND
		spawn LIGHTNINGBOLT
		ifrnd 192 break
	}
	else ifspritepal 25
	{
		// iffloordistl 16
		ifp ponground
		ifrnd 128 ifpdistl 10240
		{
			sound WIZFLAME
			spawn TIDALWAVE
			spawn TIDALWAVE
			spawn TIDALWAVE
		}
		else
		{
			geta[].z initz
			sub initz 8192
			seta[].z initz
			
			espawn RPG_GUIDED
			setactor[RETURN].pal 1
			setactorvar[RETURN].myspawner THISACTOR
			ifvarn target -1 setactorvar[RETURN].target target
			else ife cursed 0 setav[RETURN].target player[].i
			sound RPG_SHOOT
			
			add initz 8192
			seta[].z initz
		}
	}
	else 
	{
		ifrnd 160
		{
			resetactioncount
			sound CAT_FIRE
			shoot FREEZEBLAST
			action AWIZSPELL2
			ifrnd 192 break
		}
		else ifrnd 96
		iffloordistl 16
		ifpdistl 10240
		ifvare target -1
		{
			sound WIZFLAME
			spawn TIDALWAVE
			spawn TIDALWAVE
		}
		else ifrnd 64
		{
			sound LGTNSND
			espawn LIGHTNINGBOLT
			ifvarn target -1 setactorvar[RETURN].target target
			espawn LIGHTNINGBOLT
			ifvarn target -1 setactorvar[RETURN].target target
		}
		else
		{
			geta[].z initz
			sub initz 8192
			seta[].z initz
			
			espawn RPG_GUIDED
			setactorvar[RETURN].myspawner THISACTOR
			ifvarn target -1 setactorvar[RETURN].target target
			else ife cursed 0 setav[RETURN].target player[].i
			ifrnd 128 setactor[RETURN].pal 1
			sound RPG_SHOOT
			
			add initz 8192
			seta[].z initz
		}
	}
	ai AIWIZFLOAT
}

ends

state teleportcheck

ifvarl fear 0 action AWIZFLINCH else
ifrnd 64
action AWIZFLINCH
else
ifcount 10
{
	ifvarg bleeding 80
	ifrnd 96
		ai AIWIZTELEPORT
	else
	ifrnd 16
		ai AIWIZTELEPORT
}

		
ends

useractor enemy WIZARD WIZARDSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action AWIZFLOAT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength WIZARDSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
		setvar mtype 300
	}
	else break
}

ifaction 0
{
	ifg SKILL 3
	{
		ifspritepal 25 nullop else
		ifspritepal 10 setvar aura -64 else
		ifspritepal 24 setvar aura -8 else
		state randomaura
	}

	setvar ammo WIZARDSTRENGTH
	setvar mtype 300
	sound WIZWAKE
	cstat 257
	ifspritepal 25 
	{
		strength 5000 
		setvar ammo 1300
		ai AIWIZHAIL
		sizeat 115 110 // 192 188
		// cstator 2
		ifvare lotag 0 setvar lotag 100
	}
	else
	{
		sizeat 29 27 // 48 46
		ai AIWIZSPIN
	}
	setvar gold 75
	ifspritepal 25 addvar gold GOLD6
	ifvarand monstflags 2 addvar gold GOLD3
	ifvarand monstflags 4 addvar gold GOLD4
	getactor[THISACTOR].extra peractor1 // original health
	mulvar peractor1 3 divvar peractor1 4 // 75%
	setvarvar initz peractor1
	mulvar initz 4 divvar initz 6 // 50%
}

ifrnd 8 ifvarl ammo 300 addvar ammo 1
state monsterai
ifai AIWIZSPELL nullop else
ifaction AWIZDIE nullop else
ifonwater iffloordistl 64 nullop else
fall
add mtype 1 // counter for shield spawn

ifaction AWIZDEAD
{
	ifspritepal 6 getlastpal 
	move WIZSTOPPED
	fall
	ifhitweapon
	{
		state standard_jibs
		state randsquishsound
		killit
	}
	break
}
ifaction AWIZDIE
{
	ifspritepal 6 getlastpal else spritepal 6
	//ifvarl burning 0 spritepal 1
	ifactioncount 8
	{
		action AWIZDEAD
		espawn FLIES
		// setvar command 65536
        // setactor[THISACTOR].mdflags 16
		setactorvar[RETURN].myspawner THISACTOR
	}
	break
}

ifp phigher 
{
	ifai AIWIZFLOAT
	ifcansee
	{
		ifmove WIZLEVUP nullop
		else move WIZLEVUP getv
	}
} 
else
{
	ifai AIWIZFLOAT ifmove WIZLEVUP move WIZSLOW seekplayer faceplayerslow
	getplayer[THISACTOR].posz mz
	getactor[THISACTOR].z z
	ifvarvarl z mz movesprite THISACTOR 0 0 1024 CLIPMASK0 RETURN
}

ifspritepal 25
{
	ifvarg sprite[THISACTOR].extra 0
	
	ifp pfacing ifpdistl 16384 ifvarvarn bossid THISACTOR
	{
		set monid THISACTOR
		set monidcount 0
		setvarvar bossid THISACTOR
		starttrackslot 1 1
		set music2 1
	}
	
	ifspritepal 25
	ifrnd 16
	ife character 0
	ife player[].curr_weapon 0
	ifp pfacing
	{
		ifpdistl 1408
		{
			espawn GLARESPRITE
			geta[RETURN].z z
			sub z 8192
			seta[RETURN].z z
			sound PUFFSND
			getp[].posx mx
			getp[].posy my
			sub mx sprite[].x
			sub my sprite[].y
			getangle angvar mx my
			cos xvel angvar
			sin yvel angvar
			shiftvarl xvel 11
			shiftvarl yvel 11
			setplayer[].posxv xvel
			setplayer[].posyv yvel
			
			seta[player[].i].htextra 15
			seta[player[].i].htpicnum RPG
			seta[player[].i].htang angvar
			seta[player[].i].htowner THISACTOR
		}
	}
	
	
}

ifaction AWIZFLINCH
{
	ifspritepal 25 ifl fear 0 
	ifrnd 32
	{
		ifpdistl 1408
		{
			sound PUFFSND
			getp[].posx mx
			getp[].posy my
			sub mx sprite[].x
			sub my sprite[].y
			getangle angvar mx my
			cos xvel angvar
			sin yvel angvar
			shiftvarl xvel 9
			shiftvarl yvel 9
			setplayer[].posxv xvel
			setplayer[].posyv yvel
		}
		set fear 0
		
	}
	ifvarl fear 0 resetactioncount
	ifactioncount 2
	{
		ifrnd 128 ai AIWIZFLOAT
		else 
			ai AIWIZSPELL
		
	}
}
else ifai AIWIZFLOAT state wizfloatstate
else ifai AIWIZSPELL state wizspellstate
else ifai AIWIZSPIN state wizspinstate
else ifai AIWIZTELEPORT state wizteleportstate
else ifai AIWIZLIFT state wizliftstate
else ifai AIWIZHAIL state wizhailstate


ifspritepal 25
	ifvarg bleeding 0 ifp ponsteroids
	{
		getactor[THISACTOR].htowner spriteid
		ifvare sprite[spriteid].picnum APLAYER
		{
			shiftvarr bleeding 1
			setactor[THISACTOR].htextra bleeding
		}
	}
ifhitweapon
{
	ifwasweapon FREEZEBLAST 
	{
		ifspritepal 0 addstrength 10
		ifdead spawn BIGSMOKE
	}
	ifspritepal 6 getlastpal
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifspritepal 25 ifvarg ammo 300
		{
			subvar ammo 300
			setvar target2 -1
			headspritestat spriteid 1
			whilevarn spriteid -1
			{
				set temp NO
				ifvare sprite[spriteid].pal 17
				ife actorvar[spriteid].lotag 100
				{
					ifvare sprite[spriteid].picnum GOLEM set temp YES
					ifvare sprite[spriteid].picnum WRAITH set temp YES
				}
				ife temp YES
				{
					setvarvar target2 spriteid
					setvar spriteid -1
				}
				else nextspritestat spriteid spriteid
			}
			ifvarn target2 -1
			{
				strength 1000
				state wizbosstargetport
				ai AIWIZLIFT
				break
			}
		}
		sound WIZDIE
		addkills 1 add globalkills 1
		state specialdeath
		// ifspritepal 25 spawn RETURNSCROLL
		// ifspritepal 25
		// ifvarl raining 0 setvar raining 0
		
		action AWIZDIE
		move WIZSTOPPED
		cstat 0
		strength 0
		ifvarn target2 -1 setactorvar[target2].lotag 65
		break
	}
	else
	{
		ifmove WIZLEV 
			ai AIWIZSPELL	
		ifmove WIZLEVDOWN ai AIWIZFLOAT
		soundonce WIZPAIN
		ifai AIWIZFLOAT 
		{
			ifrnd 64 ifvare cursed 0 ai AIWIZSPELL else
			state teleportcheck 
		}
		else
		ifai AIWIZSPELL state teleportcheck
	}
}

enda

action SPIDER2STAND 5 1 5 1 10
action SPIDER2WALK 5 4 5 1 6
action SPIDER2SHOOT 0 1 5 1 20
action SPIDER2FLIP 6 6 1 1 10
action SPIDER2DIE 30 6 1 1 20
action SPIDER2JUMP 25 1 5 1 10
action SPIDER2DEAD 35 1 1 1 1

move SPIDER2VELS 128
move SPIDER2CEILVELS 128 -15
move SPIDER2VELSUP 128 -64
move SPIDER2CHARGEVELS 192
move SPIDER2SHRUNKVELS 24
move SPIDER2UP 0 -160


ai AISPIDER2WAIT SPIDER2STAND STOPPED geth
ai AISPIDER2SEEK SPIDER2WALK SPIDER2VELS seekplayer faceplayerslow
ai AISPIDER2SHOOT SPIDER2STAND STOPPED faceplayersmart
ai AISPIDER2CHARGE SPIDER2WALK SPIDER2CHARGEVELS faceplayer
ai AISPIDER2SUFFER SPIDER2FLIP STOPPED geth
ai AISPIDER2DYING SPIDER2DIE STOPPED geth
ai AISPIDER2FLIPUP SPIDER2FLIP SPIDER2UP getv
ai AISPIDER2FLIPDOWN SPIDER2FLIP STOPPED geth
ai AISPIDER2FROZEN SPIDER2STAND STOPPED geth
ai AISPIDER2SHRUNK SPIDER2WALK SPIDER2SHRUNKVELS fleeenemy
ai AISPIDER2JUMP SPIDER2JUMP SPIDER2CHARGEVELS jumptoplayer

eventloadactor SPIDER2

ifspritepal 7 sizeat 48 42
else sizeat 36 36
enda

actor SPIDER2 SPIDERSTRENGTH
//useractor enemy SPIDER2 SPIDERSTRENGTH

ifai 0
{
	ife SKILL 5 ifrnd 64 spritepal 7
	ai AISPIDER2WAIT
	ifspritepal 7 
	{ 
		sizeat 48 42 addstrength SPIDERSTRENGTH addstrength SPIDERSTRENGTH 
		ifn gold -1
		setvar gold GOLD3 
	} 
	else
	{
		ifspawnedby EGGSACK sizeat 20 20 else
		{ 
			sizeat 36 36 
			ifn gold -1
			setvar gold GOLD1 
		}
	}
	getp[].max_actors_killed temp
	add temp 1
	setp[].max_actors_killed temp
}
ifl cursed 0 ifspritepal 0 spritepal 7
state monsterai

ifvare peractor1 0 fall // don't fall if on the ceiling

ifaction SPIDER2DEAD
{
	setvar monstatus 2
	cstat 0
	strength 0
	ifhitweapon  { guts JIBS6 5 killit }
	break
}
ifai AISPIDER2DYING
{
	//ifvarl burning 0 spritepal 1
	soundonce SQUISHED
	ifactioncount 6 
	{
		action SPIDER2DEAD
		// setvar command 65536
    	// setactor[THISACTOR].mdflags 16
	}
	break
}

ifai AISPIDER2SUFFER cstat 265 else
{
	cstat 257
	ifvare peractor1 3 cstator 8
}

ifvare peractor1 3
{
	ifceilingdistl 4
	{
		ifpdistl 8192 ifrnd 1
		{
			setvar peractor1 0
			ai AISPIDER2FLIPDOWN
		}
		ifbulletnear
		{
			setvar peractor1 0
			ai AISPIDER2FLIPDOWN
		}
	}
}

ifai AISPIDER2SUFFER
{
	strength 0
	ifaction SPIDER2FLIP
	{
		ifactioncount 6
		{
			action SPIDER2WALK
			setvar monstatus 1
			resetcount
		}
		break
	}
	
	ifhitweapon
	{
		addkills 1 add globalkills 1	
		state specialdeath
		ai AISPIDER2DYING
		cstat 0
		break
    }
	ifcount 208
	  ai AISPIDER2SEEK
	break
}

ifvarg burning -1
ifaction SPIDER2WALK ifactioncount 2 { resetactioncount sound SPIDWALK }

getactor[THISACTOR].htextra temp
ifhitweapon
{
	// sound SPIDER2PAIN
	ifvare peractor1 0 nullop else
	{
		setvar peractor1 0
		ai AISPIDER2FLIPDOWN
	}
	ifai AISPIDER2WAIT ai AISPIDER2SEEK
	ifdead
	{
		ifvare character 0
		ifrnd 32 globalsound KILLBUGS
		setvar monstatus 2
		ifvarg temp SPIDERSTRENGTH 
		{
		   ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
			addkills 1 add globalkills 1	
			state specialdeath
			ai AISPIDER2DYING
			cstat 0
			break
    	}
		ai AISPIDER2SUFFER
		strength 0
		ifwasweapon RPG 
		{
			addkills 1 add globalkills 1	
			state specialdeath
			ai AISPIDER2DYING
			cstat 0
			break
    	}
		ifwasweapon RADIUSEXPLOSION 
		{
			addkills 1 add globalkills 1	
			state specialdeath
			ai AISPIDER2DYING
			cstat 0
			break
    	}
		break
	}
}

ifai AISPIDER2SHOOT
{
	ifcount 18 action SPIDER2SHOOT
	
	ifcount 21
	{
		resetcount
		action SPIDER2STAND
		ifcansee
		{
			ifpdistg 8192 ai AISPIDER2SEEK else
			ifrnd 64 ai AISPIDER2WAIT
		}
		else ai AISPIDER2SEEK
	}
	ifcount 20
	{
		ifspritepal 7
		ifpdistg 5120
		{
			ifvare target -1 { ifvarn cursed 0 { ai AISPIDER2WAIT break } getplayer[THISACTOR].i target }
			state calczdist
			ldist xydist THISACTOR target
			ifvare xydist 0 setvar xydist 1
			mulvar zdist 644
			divvarvar zdist xydist
			sound DEMSPITSND
			ezshoot zdist DEMONSPIT
			setthisprojectile[RETURN].pal 6
			ifvarvare target player[THISACTOR].i setvar target -1
		}
		else
		{
			sound DEMSPITSND
			eshoot WEBPROJ
			ifpdistg 4096
			{
				getactor[THISACTOR].ang angvar
				setactor[RETURN].ang angvar
			}
		}
	}
	ifvarg playerstuck 0 ai AISPIDER2CHARGE
	
	ifvarg fear 0 ai AISPIDER2CHARGE
}

ifai AISPIDER2CHARGE
{
	
	ifvare peractor1 0 nullop else
	{
		getplayer[THISACTOR].i spriteid
		ldist xydist THISACTOR spriteid
		ifvarl xydist 2048
		{
			setvar peractor1 0
			ai AISPIDER2FLIPDOWN
			break
		}
		ifnotmoving
		{
			setvar peractor1 0
			ai AISPIDER2FLIPDOWN
			break
		}
	}
	ifpdistl 1280 
	{
		ifvare cursed 0
		ifcount 13 ifrnd 64
		{
			sound BITEME
			soundonce DUKE_LONGTERM_PAIN
			ifvarl sprite[THISACTOR].xrepeat 30 addvar ppoison 40 else
			addvar ppoison 80
			// resetcount
			move STOPPED faceplayer
		}
		
	}
	else ifmove STOPPED move SPIDER2CHARGEVELS faceplayer
	ifnotmoving ifcount 14 iffloordistl 4 ai AISPIDER2JUMP else
	ifcount 52 ai AISPIDER2WAIT
}

ifai AISPIDER2JUMP
	ifcount 8 ai AISPIDER2SEEK


ifai AISPIDER2WAIT
{
	 ifoutside 
	 {
		 ifvare peractor1 0 nullop else
		 {
			 setvar peractor1 0
			 ai AISPIDER2FLIPDOWN
			 break
		 }
	 }
	 else ifvare peractor1 0
	    iffloordistl 8
	       	 ifrnd 6
     {
	     ifgapzl 512
	     {
		     ifgapzl 48 nullop else
		     {
			     setvar peractor1 3
			     ai AISPIDER2FLIPUP
			     espawn WEBLINE
			     ifvarl sprite[THISACTOR].xrepeat 30 setactor[RETURN].pal 3
	     		 break
     		 }
 		 }
	 }
    
	ifcount 32 ifrnd 8
	{
		ifcansee
		    move SPIDER2VELS faceplayer
		ai AISPIDER2SEEK
	}
	
}


ifai AISPIDER2FLIPUP
{
	ifaction SPIDER2FLIP
	{
		ifactioncount 6
			action SPIDER2STAND
	}
	
	ifceilingdistl 48
		ai AISPIDER2WAIT
}

ifai AISPIDER2FLIPDOWN
	ifactioncount 6
		ai AISPIDER2SEEK
	
ifai AISPIDER2SEEK
{
    ifp palive
      	ifcansee
        	ifcount 20
        	{

        		ifpdistl 3072 ifrnd 8
        		{
	        		ai AISPIDER2CHARGE
	        		break
        		}

    			ifpdistl 10240 ifvarg sprite[THISACTOR].xrepeat 24
    			{
	    	      ifvare target -1 ifvarn cursed 0 break
				  
				  
				  ifn playerstuck 0 ife target -1 nullop else
				  {
					  ai AISPIDER2SHOOT
					  break
				  }
			    }
    		}

    ifvare peractor1 0 ifnotmoving ifcount 24 iffloordistl 4
	{
		ai AISPIDER2JUMP
		break
	}
	ifvare peractor1 3 
	{
		ifmove SPIDER2VELS move SPIDER2CEILVELS seekplayer faceplayerslow getv
		fall
		
		ifnotmoving ifcount 6 ifrnd 16
		{
			setvar peractor1 0
			ai AISPIDER2FLIPDOWN
			break
		}
		ifceilingdistl 48 nullop else 
		{
			ai AISPIDER2FLIPDOWN
			setvar peractor1 0
		}
		ifoutside
		{
			ai AISPIDER2FLIPDOWN
			setvar peractor1 0
		}
	}
	
	ifcount 52
     	ifrnd 16
     		ai AISPIDER2WAIT
}

enda

eventloadactor HORNET sizeat 40 40 enda

action HORNETFLY 0 2 5 1 2
action HORNETDYING 10 1 1 1 1
action HORNETDEAD 11 1 1 1 1
move HORNETMOVE1 96 -16

move HORNETMOVE2 96 16
move HORNETATTACK 192 32


state hornetdeadstate

fall
strength 0

ifaction HORNETDYING iffloordistl 2 
{
	action HORNETDEAD
	ifrnd 128 cstator 4
	// setvar command 65536
    // setactor[THISACTOR].mdflags 16
}

ifhitweapon { sound SQUISHED spritepal 6 guts JIBS3 3 killit }


ends

state hornetattackstate

ifvarn target -1
{
	getactor[target].z mz
	subvar mz 8192
	ifvarvarl mz sprite[THISACTOR].z movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN else
	iffloordistl 32 movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
	
	ifvarl xydist2 1024
	{
		getactor[target].htextra temp
		addvar temp 5
		setactor[target].htextra temp
		ifvarn sprite[target].htpicnum RPG setactor[target].htpicnum SHOTSPARK1
		ifvarn cursed 0
		setactor[target].htowner player[THISACTOR].i else setactor[target].htowner THISACTOR
		setactor[target].htang sprite[THISACTOR].ang
		move HORNETMOVE1 geth getv randomangle
	}
}
else
{
	ifp phigher movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN else
	iffloordistl 32 movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
	
	ifpdistl 1024 ifcanshoottarget
	ifvarvarn swordtarg THISACTOR 
	{
		state dukepainsounds
		addvar ppoison 52
		setvar tempd -1
		state directdamage
		// addphealth -3
		move HORNETMOVE1 geth getv randomangle
	}
}
ifcount 260 ifrnd 2 move HORNETMOVE1 geth getv randomangle

ends

useractor enemy HORNET 10 HORNETFLY

ifaction HORNETDYING { state hornetdeadstate break }
ifaction HORNETDEAD { state hornetdeadstate break }

ifmove 0
{
	cstat 256
	sizeat 40 40
	move HORNETMOVE1 geth getv
	
	randvar peractor1 1560 // random timer offsset and get off the ground
	addvar peractor1 1
}
state monsterai

setvarvar temp timer
addvarvar temp peractor1
shiftvarl temp 6
sin tempb temp
shiftvarr tempb 5

getactor[THISACTOR].z z
subvarvar z tempb

getactor[THISACTOR].sectnum mysector
ifvarn mysector -1
{
	getactor[THISACTOR].x x
	getactor[THISACTOR].y y
	getflorzofslope mysector x y mz
	ifvarvarg z mz { setvarvar z mz subvar z 1024 }
}
setactor[THISACTOR].z z


ifhitweapon
{
	ifdead
	{
		ifvare character 0
		ifrnd 32 globalsound KILLBUGS
		addkills 1 add globalkills 1
		// state specialdeath
		setvar monstatus 2
		action HORNETDYING
		move STOPPED
		cstat 0
		 stopactorsound THISACTOR WASPFLY
		
		ifwasweapon RPG { sound SQUISHED spritepal 6 guts JIBS3 3 killit }
		ifwasweapon RADIUSEXPLOSION { sound SQUISHED spritepal 6 guts JIBS3 3 killit }
		break
	}
}
soundonce WASPFLY

ifbulletnear { move HORNETATTACK faceplayer getv break }
ifmove HORNETATTACK { state hornetattackstate break }
ifrnd 1
ifpdistl 4096
ifcansee ifcanshoottarget
{
	move HORNETATTACK faceplayer getv
	resetcount
	break
}
ifcount 52 ifrnd 64
{
	ifpdistl 1560 move HORNETMOVE1 geth getv furthestdir
	else
	iffloordistl 8 move HORNETMOVE1 geth getv randomangle else
	ifceilingdistl 16 move HORNETMOVE2 geth getv randomangle
	else
	ifrnd 128 move HORNETMOVE1 geth getv randomangle 
	else
	move HORNETMOVE2 geth getv randomangle
	ifrnd 128 setvar mtype 1 else setvar mtype 0
}

getactor[THISACTOR].ang angvar
ifvare mtype 0 addvar angvar 16 else subvar angvar 16
setactor[THISACTOR].ang angvar


enda

action EGGSACKOPEN 1 1 1 1 1


useractor notenemy EGGSACK 10

ifmove 0
{
	move INIT
	cstator 257
	setvar gold GOLD2
}
state monsterai

ifaction EGGSACKOPEN
{
	strength 0
	ifhitweapon
	// ifwasweapon RADIUSEXPLOSION
	{
		spritepal 6
		guts JIBS3 2
		guts JIBS4 2
		guts JIBS5 2
		guts JIBS6 3
		killit
	}
	break
}

ifhitweapon
  ifdead
{
	sound SLIM_HATCH
	spritepal 6
	guts JIBS3 2
	guts JIBS4 2
	guts JIBS6 3
	getlastpal
	state specialdeath
	action EGGSACKOPEN
	spawn SPIDER2
	ifrnd 128 spawn SPIDER2
	ifrnd 128 spawn SPIDER2
} else
ifcansee
{
	addvar countvar 1
	ifvarg countvar 52
	ifrnd 1
	{
		sound SLIM_HATCH
		spritepal 6
		guts JIBS3 2
		guts JIBS4 2
		guts JIBS6 3
		getlastpal
		state specialdeath
		action EGGSACKOPEN
		spawn SPIDER2
		spawn SPIDER2
		ifrnd 128 spawn SPIDER2
		ifrnd 128 spawn SPIDER2
		ifrnd 128 spawn SPIDER2
	}
}
else ifrnd 4 setvar countvar 0


enda

action ACYBERSTAND 48 1 8 1 20
action ACYBERWALK 0 4 8 1 16
action ACYBERRUN 0 4 8 1 10
action ACYBERSHOOT 32 3 8 1 24
action ACYBERSHOOTFAST 32 2 8 1 4
action ACYBERBEAM 40 1 8 1 24
action ACYBERPAIN 56 1 1 1 16
action ACYBERDIE 56 8 1 1 20
action ACYBERDEAD 64 1 1

move CYBERWALKVEL 128
move CYBERRUNVEL 192
move CYBERKNOCKBACK

ai AICYBERSEEKENEMY ACYBERWALK CYBERWALKVEL seekplayer faceplayerslow
ai AICYBERSHOOTENEMY ACYBERSHOOT STOPPED faceplayer
ai AICYBERPAIN ACYBERPAIN STOPPED faceplayer

action ACYBERFLOAT 0 3 8 1 8
action ACYBERFLOATSTATUE 0 1 8 1 8
action ACYBERFLOATSHOOT 170 2 8 1 45
action ACYBERFLOATPAIN 24 1 1 1 16
action ACYBERFLOATDIE 24 7 1 1 20

move CYBERFLOATVEL 160
move CYBERFLOATUP 160 -72
move CYBERFLOATDOWN 160 72

ai AICYBERFLOATSEEK ACYBERFLOAT CYBERFLOATVEL seekplayer faceplayerslow
ai AICYBERFLOATSHOOT ACYBERFLOATSHOOT STOPPED faceplayer
ai AICYBERFLOATPAIN ACYBERFLOATPAIN STOPPED faceplayer

// BATGUNNER
action ABATGUNNER 0 3 8 1 14
action ABATGUNNERB 16 3 8 -1 14
action ABATGUNNERSHOOT 32 2 8 -1 45
action ABATGUNNERSHOOTFAST 32 2 8 -1 5
action ABATGUNNERPAIN 40 1 1 1 16
action ABATGUNNERDIE 40 7 1 1 20
action ABATGUNNERDIE2 51 6 1 1 18
action ABATGUNNERDEAD 56 1 1

move BATGUNNERVEL 160
move BATGUNNERUP 160 -72
move BATGUNNERDOWN 160 60

ai AIBATGUNNERSEEK ABATGUNNER BATGUNNERVEL seekplayer faceplayerslow
ai AIBATGUNNERSHOOT ABATGUNNERSHOOT STOPPED faceplayer
ai AIBATGUNNERPAIN ABATGUNNERPAIN STOPPED faceplayer

state cyberpainstate

ifvarl fear 0 resetactioncount
ifactioncount 3 ai AICYBERSEEKENEMY

ends

state cyberseekstate

ifactioncount 4 { sound CYBERSTEP resetactioncount } else
ifactioncount 2 { soundonce CYBERSTOMP ifvarand monstflags 4 ifpdistl 16384  quake 6 }

ifvarand monstflags 4 ifg sprite[].xvel 0 ifl sprite[].xvel 384 seta[].xvel 384

ifvarn target -1 setvar peractor1 1
else ife sprite[].extra inithp ifg xydist MAXVISRANGE setvar peractor1 0

ifcount 52 ifvare peractor1 1 // ifcansee
{
	ifvarn cursed 0 ifvare target -1
	{
		ai AICYBERSEEKENEMY 
		break
	}
	ai AICYBERSHOOTENEMY
	ifspritepal 16 
	{
		action ACYBERSHOOTFAST
		move STOPPED faceplayersmart
	}
	ifspritepal 0
	{
		action ACYBERSHOOTFAST
		move STOPPED faceplayersmart
	}
	ifspritepal 11
	{
		action ACYBERBEAM
		move STOPPED faceplayerslow
	}
	ifspritepal 13 ifrnd 128 move STOPPED faceplayersmart
}

ends

defstate cyberocket

// sound RPG_SHOOT

ifactor BATGUNNER ifspritepal 92
{
	
	// fire different thing depending on ammo: 0, 1, or 2
	ife target -1 set target player[].i
	
	geta[].x saveit
	geta[].y saveit2
	geta[].z zveloverride
	set z zveloverride, add z 10240, rand mz 4096 add z mz seta[].z z
	getactor[THISACTOR].x mx
	addvar mx 1024
	set angvar sprite[].ang
	add angvar 416
	rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y angvar x y
	setsprite THISACTOR x y sprite[THISACTOR].z
	
	ife ammo 0
	{
		globalsound RPG_SHOOT
		eshoot RPG
		setactorvar[RETURN].mtype 2 // drunken missile
		eshoot RPG
		setactorvar[RETURN].mtype 2 // drunken missile
		eshoot RPG
		setactorvar[RETURN].mtype 2 // drunken missile
		eshoot RPG
		setactorvar[RETURN].mtype 2 // drunken missile
		eshoot RPG
		setactorvar[RETURN].mtype 2 // drunken missile
	}
	else ife ammo 1
	{
		globalsound DB_FIRE
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
	}
	else
	{
		globalsound CAT_FIRE
		ifcount 40 // final shot only
		{
			eshoot ARCHONPROJ 
			state calczdist
			ldist temp THISACTOR target
			ifvare temp 0 setvar temp 1
			mulvar zdist 768
			divvarvar zdist temp
			setactor[RETURN].zvel zdist
			setactor[RETURN].ang sprite[THISACTOR].ang
			
			seta[RETURN].pal sprite[].pal
			
			setav[RETURN].target target
			state projzveltotarg
		}
		else
		{
			set countvar 0
			set countvarb sprite[].ang
			sub countvarb 128
			whilevarn countvar 32
			{
				eshoot PLASMAPROJ
				seta[RETURN].ang countvarb
				seta[RETURN].pal 23
				add countvarb 8
				add countvar 1
			}
		}
	}


	
	ife target player[].i set target -1
	
	seta[].x saveit
	seta[].y saveit2
	seta[].z zveloverride
	break
}

ifvarand monstflags 4 // giant
{
	eshoot RPG
	setactorvar[RETURN].mtype 2 // drunken missile
	eshoot RPG
	setactorvar[RETURN].mtype 2 // drunken missile
	eshoot RPG
	setactorvar[RETURN].mtype 2 // drunken missile
	eshoot RPG
	setactorvar[RETURN].mtype 2 // drunken missile
	eshoot RPG
	setactorvar[RETURN].mtype 2 // drunken missile
	break
}

// ifactor BATGUNNER
// {
	geta[].x saveit
	geta[].y saveit2
	getactor[THISACTOR].x mx
	addvar mx 512
	rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y sprite[THISACTOR].ang x y
	setsprite THISACTOR x y sprite[THISACTOR].z
// }

eshoot RPG
ifvarn target -1
{
	state calczdist
	ldist xydist THISACTOR target
	ifvare xydist 0 setvar xydist 1
	mulvar zdist 644
	divvarvar zdist xydist
	setactor[RETURN].zvel zdist
	setactor[RETURN].ang sprite[THISACTOR].ang
}
ifactor CYBERFLOAT setactor[RETURN].extra 40 else
ifactor BATGUNNER setactor[RETURN].extra 40 else
setactor[RETURN].extra 60

// ifactor BATGUNNER
// {
	seta[].x saveit
	seta[].y saveit2
// }


ends

state cybershootstate

ifvarg fear 0 { ai AICYBERSEEKENEMY break }

geta[].x initx
geta[].y inity

set mx initx
geta[].yrepeat y
mul y 10
add mx y
geta[].ang angvar
add angvar 256 // 316
rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y angvar x y
setsprite THISACTOR x y sprite[THISACTOR].z

ifspritepal 10
{
	ifcount 6 nullop else ifcount 5
	state cyberocket
	ifcount 26 nullop else ifcount 25
	state cyberocket
	ifcount 46 nullop else ifcount 45
	state cyberocket		
}
else
ifspritepal 21
{
	ifcount 6 nullop else ifcount 5
	state cyberocket
	ifcount 26 nullop else ifcount 25
	state cyberocket
	ifcount 46 nullop else ifcount 45
	state cyberocket		
}
else
ifspritepal 13
{
	ifcount 6 nullop else ifcount 5
	{
		sound SHOTGUN_FIRE
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
	}
	ifcount 26 nullop else ifcount 25
	{
		sound SHOTGUN_FIRE
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
	}
	ifcount 46 nullop else ifcount 45
	{
		sound SHOTGUN_FIRE
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
	}
}
else
ifspritepal 16
{
	ifcount 6 nullop else ifcount 5
	{
		sound CAT_FIRE
		ifvare target -1 eshoot FREEZEBLAST else
		{
			state calczdist
			ldist xydist THISACTOR target
			ifvare xydist 0 setvar xydist 1
			mulvar zdist 644
			divvarvar zdist xydist
			ezshoot zdist FREEZEBLAST
			setactor[RETURN].ang sprite[THISACTOR].ang
		}
		resetcount
	}
}
else
ifspritepal 0
{
	ifcount 6 nullop else ifcount 5
	{
		sound CAT_FIRE
		ifvare target -1
		shoot FREEZEBLAST
		else
		{
			state calczdist
			ldist xydist THISACTOR target
			ifvare xydist 0 setvar xydist 1
			mulvar zdist 644
			divvarvar zdist xydist
			ezshoot zdist FREEZEBLAST
			setactor[RETURN].ang sprite[THISACTOR].ang
		}
		resetcount
	}
}
else
ifspritepal 11
{
	// beam weapon
	state gradualturn // because faceplayerslow is too slow by itself
	soundonce BEAMLOOP
	ifvare target -1 { ifvarn cursed 0 { ai AICYBERSEEKENEMY break } setvarvar target player[THISACTOR].i }
	state calczdist
	ldist xydist THISACTOR target
	ifvare xydist 0 setvar xydist 1
	mulvar zdist 768
	divvarvar zdist xydist
	ezshoot zdist BFGBEAMLITE
	ifvarvare target player[THISACTOR].i setvar target -1
}
else
{
	ifcount 6 nullop else ifcount 5
	state cyberocket
	ifcount 26 nullop else ifcount 25
	state cyberocket
	ifcount 46 nullop else ifcount 45
	state cyberocket	
}

ifvarn cursed 0 ifvare target -1
{
	ai AICYBERSEEKENEMY
	 stopactorsound THISACTOR BEAMLOOP
}

ifspritepal 16 { ifactioncount 18 ai AICYBERSEEKENEMY } else
ifspritepal 0 { ifactioncount 18 ai AICYBERSEEKENEMY } else
ifactioncount 9 
{
	ai AICYBERSEEKENEMY
	ifspritepal 11  stopactorsound THISACTOR BEAMLOOP
}

seta[].x initx
seta[].y inity

ends

eventloadactor CYBERDEMON
sizeat 32 30 // 44 44
ifspritepal 17 
{
	action ACYBERSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
ifspritepal 0 spritepal 16
enda

eventloadactor CYBERDEMONSTAYPUT
sizeat 32 30 // 44 44
ifspritepal 17 
{
	action ACYBERSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
ifspritepal 0 spritepal 16
enda

useractor enemystayput CYBERDEMONSTAYPUT CYBERDEMONSTRENGTH

    cactor CYBERDEMON
	setvar gold GOLD6
	sizeat 32 30
	ifvarand monstflags 4 
	{
		sizeat 132 128 // 255 255 
		strength 32000 addvar gold 2000 
		spritepal 10
		clipdist 254
		starttrackslot 1 1
		set music2 1
	}
	cstator 257
	ifspritepal 17 action ACYBERSTAND else ai AICYBERSEEKENEMY
	ifspritepal 0 spritepal 16
enda

useractor enemy CYBERDEMON CYBERDEMONSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ACYBERSTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength CYBERDEMONSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 10
		action 0
	}
	else break
}

getactor[THISACTOR].z z
subvar z 4096
setactor[THISACTOR].z z
setvar peractor1 0
ifvare fear 0
{
	ifcansee setvar peractor1 1 else 
	ifvarn target -1 setvar peractor1 1
}
addvar z 4096
setactor[THISACTOR].z z

fall

ifvarand monstflags 4
ifpdistl 20480 setvarvar bossid THISACTOR

ifvarand monstflags 4
ifp palive ifpdistl 2048 ifvarvarn swordtarg THISACTOR { setvar tempd -1000 state directdamage palfrom 63 63 }

ifmove 0
{
	ifg SKILL 3 state randomaura
	ai AICYBERSEEKENEMY
	sizeat 32 30 // 44 44
	setvar gold GOLD6
	ifspritepal 0 spritepal 16
	ifvarand monstflags 4
	{
		 sizeat 130 128 // sizeat 255 255 
		 strength 32000 addvar gold 2000
		 clipdist 254 spritepal 10
		 starttrackslot 1 1
		 set music2 1
	 }
	cstator 257
	sound CYBROAM
}

state monsterai
ifvarand monstflags 4 nullop else
ifactornotstayput state avoidcliffs

ifmove CYBERKNOCKBACK ifcount 2
{
	ifai AICYBERSEEKENEMY move CYBERWALKVEL seekplayer faceplayerslow else
	move STOPPED
}

ifaction ACYBERDEAD
{
	strength 0
	ifhitweapon
	{
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
	}
	break
}
ifaction ACYBERDIE
{
	strength 0
	ifhitweapon { debris SCRAP1 1 guts JIBS6 2 }
	
	ifrnd 85
	{
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
	} else
	ifrnd 128
	{
		state aimjib
		ezshoot zdist JIBSPROJ4
		state setjib
	}
	else
	{
		state aimjib
		ezshoot zdist JIBSPROJ5
		state setjib
	}
	
	ifvarand monstflags 4
	{
		ifactioncount 7
		{
			espawn CYBERFLOAT
			setactorvar[RETURN].monstflags monstflags
			setactorvar[RETURN].activator activator
			setactor[RETURN].pal sprite[THISACTOR].pal
			set activator 0
			killit
		}
		else
		break
	}
	ifactioncount 8
	{
		ifrnd 128
		ifvare character 0
		globalsound BIGBLEED
		action ACYBERDEAD
		espawn FLIES
		setactorvar[RETURN].myspawner THISACTOR
		cstat 0
		// setactor[THISACTOR].mdflags 16
		// setvar command 65536
	}
	break
}
ifrnd 1 ifvare burning 0 soundonce CYBROAM
ifai AICYBERSEEKENEMY state cyberseekstate else
ifai AICYBERSHOOTENEMY state cybershootstate else
ifai AICYBERPAIN state cyberpainstate

ifvarg sprite[THISACTOR].htextra 5
ifvarg burning -1
{
	debris SCRAP1 1 
	guts JIBS6 2
}

ifvarand monstflags 4 nullop else
ifpdistg 16384 ifvarvare bossid THISACTOR setvar bossid -1


ifhitweapon
{
	ifwasweapon RPG 
	{
		ifai AICYBERSEEKENEMY ifrnd 128 ifcansee ai AICYBERSHOOTENEMY
		// ifmove CYBERKNOCKBACK nullop else
		// move CYBERKNOCKBACK geth
		setactor[THISACTOR].xvel 0
	}
	soundonce CYBPAIN
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		addkills 1 add globalkills 1
		ifvarand monstflags 4 { set monstatus 2 set aura 0 } else
		state specialdeath

		move STOPPED
		action ACYBERDIE
		sound CYBDTH
	}
	else
	ifvarl fear 0 ai AICYBERPAIN else
	ifai AICYBERPAIN nullop else
	ifvarg bleeding 100 { ifrnd 96 ai AICYBERPAIN } else
	ifg bleeding 30 ifrnd 4 ai AICYBERPAIN
	else ifvarand monstflags 4 ifai AICYBERSEEKENEMY ai AICYBERSHOOTENEMY
}
	

enda

state cyberfloatpainstate

ifvarl fear 0 resetactioncount
ifactioncount 3 ai AICYBERFLOATSEEK

ends

state cyberfloatseekstate

ifvarn target -1 setvar peractor1 1
	else ife sprite[].extra inithp ifg xydist MAXVISRANGE setvar peractor1 0

ifcount 52 ifvare peractor1 1 // ifcansee
{
	ifvarn cursed 0 ifvare target -1
	{
		ai AICYBERFLOATSEEK 
		break
	}
	ai AICYBERFLOATSHOOT
	break
}

ifmove CYBERFLOATUP nullop else
ifp phigher
move CYBERFLOATUP seekplayer getv


ifmove CYBERFLOATDOWN nullop else
ifvarvarg player[THISACTOR].posz sprite[THISACTOR].z
move CYBERFLOATDOWN seekplayer getv

ifp phigher nullop
else ifmove CYBERFLOATVEL nullop else
ifvarvarl player[THISACTOR].posz sprite[THISACTOR].z
move CYBERFLOATVEL seekplayer getv

ends

state cyberfloatshootstate

	ifcount 16 nullop else ifcount 15
	state cyberocket
	ifcount 36 nullop else ifcount 35
	state cyberocket
	ifcount 56 nullop else ifcount 55
	state cyberocket	

ifvarn cursed 0 ifvare target -1
	ai AICYBERSEEKENEMY
	
ifactioncount 6 
	ai AICYBERFLOATSEEK

ends

eventloadactor CYBERFLOAT 
ifspritepal 17 
{
	action ACYBERFLOATSTATUE
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
sizeat 32 30 ifspritepal 0 spritepal 21 
enda
eventloadactor CYBERFLOATPUT 
ifspritepal 17 
{
	action ACYBERFLOATSTATUE
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
sizeat 32 30 ifspritepal 0 spritepal 21 
enda

useractor enemy CYBERFLOAT BOSS1PALSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ACYBERFLOATSTATUE
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength BOSS1PALSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
		set mtype -1
	}
	else break
}

ifmove 0
{
	ifg SKILL 3 state randomaura
	setvar gold GOLD5
	ifvarand monstflags 4 { add gold 2000 strength 32000 sizeat 130 128 clipdist 254 setvar aura -64 } else
	sizeat 32 30
	ai AICYBERFLOATSEEK
	cstator 257
	
	sound CYBROAM
	setactor[THISACTOR].mdflags 16
    setvar command 32768 // float flag
    set mtype -1
}

state monsterai

// ife mtype -1
// {
	// espawn FLOATHITBOX
	// setactorvar[RETURN].myspawner THISACTOR
	// set mtype RETURN
// }

// ifn mtype -1 
// {
	// getactor[].z z
	// set temp sprite[].yrepeat
	// mul temp 144
	// sub z temp
	// setsprite mtype sprite[].x sprite[].y z
// }

getactor[THISACTOR].z z
subvar z 4096
setactor[THISACTOR].z z
setvar peractor1 0
ifvare fear 0
{
	ifcansee setvar peractor1 1 else 
	ifvarn target -1 setvar peractor1 1
}
addvar z 4096
setactor[THISACTOR].z z

ifaction ACYBERFLOATDIE
{
	ifvarand monstflags 4
	{
		ifactioncount 7
		{
			state specialdeath
			cstat 32768
			ifcount 201 nullop else
			ifcount 200
			{
				// KILL EVERY MONSTER IN THE MAP
				// setvar spriteid 0
				// whilevarn spriteid 16384
				// {
					// ifvarn sprite[spriteid].statnum 1024
					// ifvarg actorvar[spriteid].monstatus 0
					// ifvarl actorvar[spriteid].monstatus 3
					// ifvarvarn spriteid THISACTOR
					// {
						// setactor[spriteid].picnum SMALLSMOKE
						// changespritestat spriteid 1
					// }
					// addvar spriteid 1
				// }
				getactor[THISACTOR].z initz
				subvar initz 32768
				setactor[THISACTOR].z initz
				espawn REDRIFT
				setactorvar[RETURN].countvarb 200
				addvar initz 32768
				setactor[THISACTOR].z initz
				globalsound BFGBOOM
			}
			ifcount 600 	
			{
				// setvar testnodes YES
				killit
			}
			break
		}
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ4
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ5
		state setjib
		
		break
	}
	ifactioncount 7
		killit
	break
}

ifvarand monstflags 4
ife monstatus 1
{
	ifpdistl 20480 setvarvar bossid THISACTOR
	ifp palive ifpdistl 2048 ifvarvarn swordtarg THISACTOR { setvar tempd -1000 state directdamage palfrom 63 63 }
	
	ifvarl livemon 100
	ifrnd 2 { espawn EGG setactor[RETURN].pal 21 setactor[RETURN].xrepeat 64 setactor[RETURN].yrepeat 64 }
}

ifrnd 1 ifvare burning 0 soundonce CYBROAM
ifai AICYBERFLOATSEEK state cyberfloatseekstate else
ifai AICYBERFLOATSHOOT state cyberfloatshootstate else
ifai AICYBERFLOATPAIN state cyberfloatpainstate

ifvarg sprite[THISACTOR].htextra 5
ifvarg burning -1
{
	debris SCRAP1 1 
	guts JIBS6 2
}

ifhitweapon
{
	ifwasweapon RPG 
	{
		ifai AICYBERFLOATSEEK ifrnd 128 ifcansee ai AICYBERFLOATSHOOT
		setactor[THISACTOR].xvel 0
	}
	soundonce CYBPAIN
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		addkills 1 add globalkills 1
		move STOPPED
		action ACYBERFLOATDIE
		sound CYBDTH
		ifvarand monstflags 4 quake 90
		state specialdeath
	}
	else
	ifvarl fear 0 ai AICYBERFLOATPAIN else
	ifai AICYBERFLOATPAIN nullop else
	ifvarg bleeding 100 { ifrnd 96 ai AICYBERFLOATPAIN } else
	ifg bleeding 30 ifrnd 4 ai AICYBERFLOATPAIN
}

enda

useractor enemystayput CYBERFLOATPUT BOSS1PALSTRENGTH cactor CYBERFLOAT enda

eventloadactor BATGUNNER 
ifspritepal 17 
{
	action ACYBERFLOATSTATUE
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
sizeat 32 32 
ifspritepal 0 spritepal 21 
enda
eventloadactor BATGUNNERSTAYPUT 
ifspritepal 17 
{
	action ACYBERFLOATSTATUE
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
sizeat 32 32 
ifspritepal 0 spritepal 21 
enda

state batgunnerpainstate

ifvarl fear 0 resetactioncount
ifactioncount 3 ai AIBATGUNNERSEEK

ends

state batgunnerdodge
	ifspritepal 21 set peractor4 0
	ifg peractor4 0
	{
		// state motionblur
		seta[].xvel 0
		cos xvel dodgeang
		sin yvel dodgeang

		ifspritepal 92 { shiftr xvel 6 shiftr yvel 6 }
		else
		{
			div xvel 96
			div yvel 96
		}
		setvar zvel 0
		movesprite THISACTOR xvel yvel zvel CLIPMASK0 RETURN
		ifn RETURN 0 set peractor4 0
	}
ends

state batgunnerup

	sub initz 1 ifl initz 0 set initz 0
	ifceilingdistl 192 set initz 0
	
	ifg initz 0
	{
		movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
		ifn RETURN 0 set initz 0
	}

ends

state batgunnerseekstate

	ifvarn target -1 setvar peractor1 1
	else ife sprite[].extra inithp ifg xydist MAXVISRANGE setvar peractor1 0

	ifcount 48 ifrnd 32 ifvare peractor1 1 // ifcansee
	{
		ifvarn cursed 0 ifvare target -1
		{
			ai AIBATGUNNERSEEK 
			break
		}
		ai AIBATGUNNERSHOOT
		add ammo 1
		ifg ammo 2 set ammo 0
		break
	}
	state dodgecheck
	state batgunnerdodge
	ifceilingdistl 192 nullop else
	ife initz 0
	{
		ifspritepal 92
		{
			iffloordistl 512
			ifrnd 16 { rand initz 80 add initz 80 }
		}
		else
		{
			iffloordistl 128
			ifrnd 16 { rand initz 20 add initz 20 }
		}
	}

	ife initz 0
	{
		ifmove BATGUNNERUP nullop else
		ifp phigher
		move BATGUNNERUP seekplayer getv


		ifmove BATGUNNERDOWN nullop else
		ifvarvarg player[THISACTOR].posz sprite[THISACTOR].z
		move BATGUNNERDOWN seekplayer getv

		ifp phigher nullop
		else ifmove BATGUNNERVEL nullop else
		ifvarvarl player[THISACTOR].posz sprite[THISACTOR].z
		move BATGUNNERVEL seekplayer getv
	}
	else state batgunnerup

ends

state batplasma

	sound CAT_FIRE
	
	geta[].x saveit
	geta[].y saveit2
	getactor[THISACTOR].x mx
	addvar mx 512
	rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y sprite[THISACTOR].ang x y
	setsprite THISACTOR x y sprite[THISACTOR].z
	
	eshoot PLASMAPROJ
	
	eshoot PLASMAPROJ
	geta[RETURN].ang angvar
	sub angvar 32 rand temp 64 sub angvar temp
	seta[RETURN].ang angvar
	
	eshoot PLASMAPROJ
	geta[RETURN].ang angvar
	add angvar 32 rand temp 64 add angvar temp
	seta[RETURN].ang angvar
	
	seta[].x saveit
	seta[].y saveit2

ends

state batplasmafast

	sound CAT_FIRE
	setprojectile[PLASMAPROJ].vel 1024
	setprojectile[PLASMAPROJ].pal 11
	setprojectile[PLASMAPROJ].extra 16
	setprojectile[PLASMAPROJ].xrepeat 24
	setprojectile[PLASMAPROJ].yrepeat 24
	geta[].x saveit
	geta[].y saveit2
	getactor[THISACTOR].x mx
	addvar mx 512
	rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y sprite[THISACTOR].ang x y
	setsprite THISACTOR x y sprite[THISACTOR].z
	
	eshoot PLASMAPROJ
	setprojectile[PLASMAPROJ].vel 768
	setprojectile[PLASMAPROJ].pal 0
	setprojectile[PLASMAPROJ].extra 20
	setprojectile[PLASMAPROJ].xrepeat 32
	setprojectile[PLASMAPROJ].yrepeat 32
	
	geta[RETURN].ang angvar
	rand temp 96 sub temp 48 add angvar temp
	seta[RETURN].ang angvar
	
	seta[].x saveit
	seta[].y saveit2

ends

state batgunnershootstate

	ifspritepal 3
	{
		ifcount 16 nullop else ifcount 15
		state batplasma
		ifcount 36 nullop else ifcount 35
		state batplasma
		ifcount 56 nullop else ifcount 55
		state batplasma
	}
	else
	ifspritepal 11
	{
		ifaction ABATGUNNERSHOOTFAST nullop else action ABATGUNNERSHOOTFAST
		ifcount 3 { resetcount state batplasmafast }
	}
	else
	{
		ifcount 16 nullop else ifcount 15
		state cyberocket
		ifcount 36 nullop else ifcount 35
		state cyberocket
		ifcount 56 nullop else ifcount 55
		state cyberocket
	}
	state batgunnerdodge
	ifg initz 0 state batgunnerup

ifvarn cursed 0 ifvare target -1
	ai AIBATGUNNERSEEK
	
ifspritepal 11
{
	ifactioncount 10
		ai AIBATGUNNERSEEK
	break
}
ifactioncount 6 
	ai AIBATGUNNERSEEK

ends

useractor enemy BATGUNNER 1000

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ACYBERFLOATSTATUE
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength 1000
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
		set mtype -1
	}
	else break
}

ifmove 0
{
	set initz 0 // used to control upward dodges
	ifg SKILL 3 state randomaura
	sizeat 32 32
	ifspritepal 0 spritepal 21 
	ai AIBATGUNNERSEEK
	cstator 257
	setvar gold GOLD5
	sound BATGUNALERT
	setactor[THISACTOR].mdflags 16
    setvar command 32768 // float flag
    set mtype -1
	ifspritepal 21 orvar aura -64
	ifspritepal 92 // boss
	{
		clipdist 192
		sizeat 144 128
		orvar aura -8192
		set gold GOLD8
		globalsound BATGUNALERT
		strength 26000
		set ammo 0 // to be used as weapon type counter
	}
	
}

ifaction ABATGUNNER { ifactioncount 3 { action ABATGUNNERB sound WINGSND ifspritepal 192 globalsound WINGSND } } else
ifaction ABATGUNNERB { ifactioncount 3 action ABATGUNNER }

seta[].mdflags 16
state monsterai

// ife mtype -1
// {
	// espawn FLOATHITBOX
	// setactorvar[RETURN].myspawner THISACTOR
// }

ifn mtype -1 
{
	getactor[].z z
	set temp sprite[].yrepeat
	mul temp 144
	sub z temp
	setsprite mtype sprite[].x sprite[].y z
}

getactor[THISACTOR].z z
subvar z 4096
setactor[THISACTOR].z z
setvar peractor1 0
ifvare fear 0
{
	ifcansee setvar peractor1 1 else 
	ifvarn target -1 setvar peractor1 1
}
addvar z 4096
setactor[THISACTOR].z z

ifaction ABATGUNNERDIE
{
	set initz 0
	set command 0
	seta[].mdflags 0
	ifactioncount 7
		killit
	ifrnd 128 guts JIBS6 1 else
	ifrnd 64 guts JIBS4 1 else
	ifrnd 64 guts JIBS5 1
	break
}
ifaction ABATGUNNERDIE2
{
	ifspritepal 92
	{
		ifrnd 192 guts JIBS6 1 else
		ifrnd 96 guts JIBS4 1 else
		ifrnd 96 guts JIBS5 1
	}
	set initz 0
	set command 0
	seta[].mdflags 0
	ifvarand player[].player_par 1 fall
	ifactioncount 5
	{
		action ABATGUNNERDEAD
		iffloordistl 16 
			sound THUD
	}
	strength 0
	ifhitweapon { }
	break
}
ifaction ABATGUNNERDEAD
{
	fall
	ifhitweapon 
	{
		ifwasweapon RPG
		{ state standard_jibs killit }
		ifwasweapon RADIUSEXPLOSION
		{ state standard_jibs killit }
	}
	strength 0
	break
}
ifspritepal 92
{
	ifpdistg 20480 orvar aura -1 else
	ifvarand aura -1 xorvar aura -1
}

ifrnd 1 ifvare burning 0 soundonce BATGUNROAM
ifai AIBATGUNNERSEEK state batgunnerseekstate else
ifai AIBATGUNNERSHOOT state batgunnershootstate else
ifai AIBATGUNNERPAIN state batgunnerpainstate

ifvarg sprite[THISACTOR].htextra 5
ifvarg burning -1
{
	debris SCRAP1 1 
	guts JIBS6 2
}

ifhitweapon
{
	ifwasweapon RPG 
	{
		ifg sprite[].extra 0
		ifai AIBATGUNNERSEEK ifrnd 128 
		{
			set temp NO
			ifn target -1 set temp YES
			else
			ifcansee set temp YES
			ife temp YES
			{
				ai AIBATGUNNERSHOOT
				add ammo 1
				ifg ammo 2 set ammo 0
			}
		}
		setactor[THISACTOR].xvel 0
	}
	ifg bleeding 30 soundonce BATGUNBIGPAIN else soundonce BATGUNPAIN
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		addkills 1 add globalkills 1
		move STOPPED
		ifspritepal 92 action ABATGUNNERDIE2 else
		ifwasweapon RADIUSEXPLOSION
			action ABATGUNNERDIE
		else
		ifwasweapon RPG
			action ABATGUNNERDIE
		else
			action ABATGUNNERDIE2
		cstat 0
		ifrnd 128 cstator 4
		stopsound BATGUNBIGPAIN stopsound BATGUNPAIN
		sound BATGUNDIE
		ifspritepal 92 globalsound BATGUNDIE
		state specialdeath
	}
	else
	{
		ifspritepal 92 shiftr bleeding 1
		
		ifvarl fear 0 ai AIBATGUNNERPAIN else
		ifai AIBATGUNNERPAIN nullop else
		ifvarg bleeding 100 { ifrnd 128 ai AIBATGUNNERPAIN } else
		ifg bleeding 20 ifrnd 4 ai AIBATGUNNERPAIN
	}
}

enda

useractor enemystayput BATGUNNERSTAYPUT 1000 cactor BATGUNNER enda

// useractor notenemy FLOATHITBOX 0

// ifmove 0
// {
	// move INIT
	// getactor[myspawner].picnum mtype
	// getactor[myspawner].xrepeat countvar
	// setactor[THISACTOR].xrepeat countvar
	// getactor[myspawner].yrepeat countvarb
	// setactor[THISACTOR].yrepeat countvarb
	// cstat 33024
	// setvar monstatus 1
// }

// setactor[THISACTOR].clipdist countvar

// ifvare sprite[myspawner].statnum 1024 killit
// ifvarvarn sprite[myspawner].picnum mtype killit
// ifn actorvar[myspawner].monstatus 1 killit

// getactor[myspawner].z z
// setvarvar temp countvarb
// mulvar temp 144
// subvarvar z temp

// setsprite THISACTOR sprite[myspawner].x sprite[myspawner].y z


// ifvarg sprite[THISACTOR].htextra 0
// {
	// setactor[myspawner].htextra sprite[THISACTOR].htextra
	// setactor[myspawner].htowner sprite[THISACTOR].htowner
	// ife sprite[].htpicnum SHOTSPARK1 ife sprite[].htowner player[].i
	// ife character 0
	// ife CURRWEAP 7
	// {
		// ife WEAP7MODE 0 seta[].htpicnum FIREBEAM
		// ife WEAP7MODE 1 seta[].htpicnum ICEBEAM
	// }
	// ifn burning 0
	// {
		// getav[myspawner].burning temp
		// add temp burning
		// setav[myspawner].burning temp
	// }
	// setactor[myspawner].htpicnum sprite[THISACTOR].htpicnum
	// setactor[myspawner].htang sprite[THISACTOR].htang
// }
// ifhitweapon strength 0
// ifn target2 -1 { setav[myspawner].target2 target2 set target2 -1 }
// set burning 0

// enda

state spawnshield

espawn SHIELD
getactor[THISACTOR].pal pal
ifvare pal 0 setvar pal 13
ifvare pal 19 setvar pal 10
ifvare pal 20 setvar pal 16
ifvare pal 24 setvar pal 10
setactor[RETURN].pal pal
setvar temp 5
ifactor SKELKNIGHT
{
	ifspritepal 19 addvar temp 3
	ifspritepal 20 addvar temp 5
}
ifactor SKELKNIGHTBONES
{
	ifspritepal 19 addvar temp 3
	ifspritepal 20 addvar temp 5
}
ifactor DETHNITE
{
	addvar temp 15
	ifspritepal 20 setvar temp 50
}
	
setactorvar[RETURN].peractor1 temp

ends


action ASKELWALK 0 4 8 1 20
action AKELRUN 0 4 8 1 15
action ASKELWAIT 0 1 8 1 20
action ASKELSTRIKE 32 3 8 1 8
action ASKELSTRUCK 48 1 8 1 5
action ASKELRETRACT 48 3 8 -1 10
action ASKELFLINTCH 56 1 8 1 20
action ASKELFALL 64 7 1 1 14
action ASKELDEAD 70 1 1 1 20
action ASKELRISE 70 7 1 -1 20

move SKELWALKVELS 72
move SKELCHARGEVELS 108

move DARKSKEL1 144
move DARKSKEL2 208

ai AIS_SEEK ASKELWALK SKELWALKVELS seekplayer faceplayerslow
ai AIS_CHARGE AKELRUN SKELCHARGEVELS faceplayer
ai AIS_WANDER ASKELWALK SKELWALKVELS geth
ai AIS_STRIKE ASKELSTRIKE STOPPED geth // STOPPED
ai AIS_THROW ASKELSTRIKE STOPPED geth
ai AIS_FLINTCH ASKELFLINTCH STOPPED geth
ai AIS_DYING ASKELFLINTCH STOPPED geth
ai AIS_DODGE AKELRUN SKELCHARGEVELS dodgebullet

state randsize

	setvar mtype 1
	setvar x 30
	randvar temp 5
	addvarvar x temp
	setactor[THISACTOR].xrepeat x
	setvar y 30
	randvar temp 5
	addvarvar y temp
	setactor[THISACTOR].yrepeat y

ends

state initskelloadactor


ifactor SKELKNIGHTBONES nullop else cstator 257

ifspritepal 19 // medium strength
	sizeat 34 32 // 46 42
else
ifspritepal 20 // strong
	sizeat 38 36 // 50 44
else
	sizeat 32 30 // 40 36

ends

state initskel

move INIT
setvar mtype 1
ifactor SKELKNIGHTBONES nullop else cstator 257
strength SKELSTRENGTH
ifvarn gold -1 setvar gold GOLD1

ifspritepal 19 // medium strength
{
	sizeat 34 32 // 46 42
	ifvarn gold -1 addvar gold GOLD1
	ifvarn monstatus 2
	addstrength SKELSTRENGTH
}
else
ifspritepal 20 // strong
{
	sizeat 38 36 // 50 44
	ifvarn gold -1 addvar gold GOLD2
	ifvarn monstatus 2
	{
		addstrength SKELSTRENGTH
		addstrength SKELSTRENGTH
		ifg SKILL 3 state randomaura
	}
}
else
{
	sizeat 32 30 // 40 36
}
ifspritepal 24 setvar aura -64

ends

eventloadactor SKELKNIGHT
// state randsize
state initskelloadactor
ifspritepal 17 
{
	action ASKELWAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

eventloadactor SKELKNIGHTSTAYPUT
// state randsize
state initskelloadactor
ifspritepal 17 
{
	action ASKELWAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

state skelshootstate

ifvare target -1 { ifvarn cursed 0 { ai AIS_SEEK break } setvarvar target player[THISACTOR].i }
state calczdist
ldist tempe THISACTOR target
ifvare tempe 0 setvar temp 1
mul zdist 644
divvarvar zdist tempe
ezshoot zdist SKELAXE

getactor[RETURN].zvel zvel
randvar tempb 384
subvarvar zvel tempb

shiftvarr tempe 3
subvarvar zvel tempe
setactor[RETURN].zvel zvel

ifvarvare target player[THISACTOR].i setvar target -1

ends

state skeldodgestate

ifcount 18 ai AIS_SEEK

ends

state skeldyingstate 

strength 0

ifaction ASKELFALL ifactioncount 7
{
	action 0
	cactor SKELKNIGHTBONES
	set monstatus 2
	break
}

ifaction ASKELFLINTCH ifcount 3
	action ASKELFALL


ifaction ASKELRISE
ifactioncount 7
{
	state initskel
	setvar monstatus 1
	state facetarget
	ai AIS_FLINTCH
}

ends

state skelseekstate

ifcount 12 ifcansee
{
	ifspritepal 20 ifcount 78
	{
		setvar temp 0
		ifvarn target -1 setvar temp 1 else
		ifcanshoottarget ifpdistg 3072 ifpdistl 20480 setvar temp 1
		ifvarn cursed 0 ifvare target -1 setvar temp 0
		ifvare temp 1
		{
			state facetarget
			ai AIS_THROW
			break
			
		}
	}
	ifbulletnear ifrnd 64
	{
		ai AIS_DODGE
		setvar peractor4 18
		break
	}
	ifpdistl 5120
	{
		ai AIS_CHARGE
		break
	}
	ifvarl xydist2 5120
	{
		ai AIS_CHARGE
		break
	}
	ifrnd 2
	{
		ai AIS_CHARGE
		break
	}
	ifpdistg 8192 ifcount 180 ifrnd 2
	{
		ai AIS_WANDER
		break
	}
}
else
	ifcount 130 ifrnd 2
		ai AIS_WANDER	
ends

state skelchargestate 

ifmove SKELCHARGEVELS
{
	ifspritepal 19 move DARKSKEL1 faceplayer else
	ifspritepal 20 move DARKSKEL2 faceplayer
}
ifcount 52
ifpdistg 6144 { ai AIS_SEEK break }

ifpdistl 1152
ifcanshoottarget
ifvare cursed 0
{
	state facetarget
	ai AIS_STRIKE
	sound SWORDSWING 
}

ifvarn target -1
ifvarl xydist2 1152
{
	state facetarget
	ai AIS_STRIKE
	sound SWORDSWING 
}

ifai AIS_CHARGE
ifcount 156 ai AIS_SEEK

	ifcount 40
	ifrnd 4
	{
		setvar temp 0
		ifvarn target -1 setvar temp 1 else
		ifcanshoottarget ifpdistg 3072 ifpdistl 20480 setvar temp 1
		ifvarn cursed 0 ifvare target -1 setvar temp 0
		ifvare temp 1
		{
			state facetarget
			ai AIS_THROW
			break
			
		}
	}
	ifbulletnear ifrnd 32
	{
		ai AIS_DODGE
		setvar peractor4 12
	}

ends

state skelwanderstate

ifvarn target -1
{
	ai AIS_CHARGE
	break
}

ifaction ASKELWAIT
{
	ifactioncount 2
	{
		action ASKELWALK
		move SKELWALKVELS randomangle
		resetcount
	}
	break
} else
ifnotmoving ifcount 20
{
	resetcount
	action ASKELWAIT
	move STOPPED
	break
}
ifpdistg 8192
{
	ifcansee ifrnd 2
	{
		ifrnd 160
			ai AIS_SEEK
		else
		{
			state facetarget
			ai AIS_THROW	
			sound SWORDSWING
		}
	}
}
else ifcount 26
{
	ifpdistl 5120
	{
		ai AIS_CHARGE
		break
	}
	ifcansee ifrnd 8
	{
		ifrnd 96
		{
			state facetarget
			ai AIS_THROW
			sound SWORDSWING
		}
		else
			ai AIS_SEEK
	}
}

	
ends

state skelwaitstate 

ifactioncount 4 ai AIS_SEEK

ends

state skelstrikestate

state motionblur

ifaction ASKELSTRIKE 
{
	ifactioncount 3
	{
		// shoot stuff
		state monsterbite
		ifspritepal 19 state monsterbite
		ifspritepal 20 { state monsterbite state monsterbite }
		
		action ASKELSTRUCK
		
	}
} else
ifaction ASKELSTRUCK
{
	ifactioncount 2
	action ASKELRETRACT
} else
ifaction ASKELRETRACT
{
	ifactioncount 3
	{
		ifvarn target -1
		ifvarl xydist2 1280
		{
			action ASKELSTRIKE 
			move STOPPED faceplayerslow
			sound SWORDSWING
			break
		}
		
		ifcansee ifcanshoottarget
		ifp palive
		ifpdistl 1280 
		ifvare cursed 0
		{
			setvar target -1
			action ASKELSTRIKE 
			move STOPPED faceplayerslow
			sound SWORDSWING
		}
		else
		ai AIS_CHARGE
	}
}

ends

state skelthrowstate 

state motionblur
ifaction ASKELSTRIKE 
{
	ifactioncount 3
	action ASKELSTRUCK else
	ifactioncount 2 ifcount 6
	{
		state skelshootstate
		resetcount
	}
} else
ifaction ASKELSTRUCK
{
	ifactioncount 2
	action ASKELRETRACT
} else
ifaction ASKELRETRACT
{
	ifactioncount 3
	ai AIS_SEEK
}

ends

state skelflintchstate 
ifvarl fear 0 resetactioncount
ifactioncount 2
{
	ifpdistl 5120 ifcansee ai AIS_CHARGE else
	ai AIS_SEEK
}
ends


useractor enemy SKELKNIGHT SKELSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ASKELWAIT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength SKELSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
		ifvare SKILL 5 ifrnd 64
		{
			ifrnd 128 spritepal 19 else spritepal 20
		}
	}
	else break
}
fall
ifmove 0
{
	state initskel
	ai AIS_SEEK
}
ifmove INIT
	ai AIS_SEEK


ifspritepal 4
{
	ifmove SKELWALKVELS move DARKSKEL1 seekplayer faceplayerslow
    ifmove SKELCHARGEVELS move DARKSKEL2 faceplayerslow
    ifrnd 96 cstat 2 else ifrnd 128 cstat 514 else cstat 32768
	espawn SMALLSMOKE
	setactor[RETURN].pal 4
	setactor[RETURN].cstat 130
	randvar angvar 2047
	getactor[THISACTOR].x x
	getactor[THISACTOR].y y
	setvarvar mx x
	randvar temp 1400
	addvarvar mx temp
	rotatepoint x y mx y angvar mx my
	updatesector mx my sect
	ifn sect -1
		setsprite RETURN mx my sprite[RETURN].z
	// setactor[RETURN].x mx
	// setactor[RETURN].y my
}

state monsterai
ifactornotstayput state avoidcliffs
	  
ifvarg dodge 0
{
	ifai AIS_DYING nullop else // setvar dodge 0 else
	ifai AIS_FLINTCH nullop else ai AIS_FLINTCH
}

ifai AIS_DYING { state skeldyingstate break }
ifai AIS_SEEK state skelseekstate else
ifai AIS_CHARGE state skelchargestate else
ifai AIS_WANDER state skelwanderstate else
ifai AIS_STRIKE state skelstrikestate else
ifai AIS_THROW state skelthrowstate else
ifai AIS_FLINTCH state skelflintchstate else
ifai AIS_DODGE state skeldodgestate

ifhitweapon
{
	//ifwasweapon BURNING nullop else
	//guts JIBS6 1
	ifdead
	{
		setvar monstatus 2
		sound SKELDETH
		cstat 0
		guts JIBS6 3
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		
		ai AIS_DYING
		setvar temp 0
		ifwasweapon RADIUSEXPLOSION setvar temp 1
		ifwasweapon RPG setvar temp 1
		ifinwater setvar temp 1
		ifvare temp 1
		{
			addkills 1 add globalkills 1
			state specialdeath
			ifvare initcstat 666 // no gore
		    {
				spawn BIGSMOKE
				spawn SKELGHOST
				state spawnshield
    			killit
    			break
		    }
			ifvarg framerate 30
			{
				state aimjib
		  		ezshoot zdist JIBSPROJ2
		  		state setjib
	  		}
	  		
	  		ifvarg framerate 22
	  		{
		  		state aimjib
				ezshoot zdist JIBSPROJ3
				state setjib
				
				state aimjib
			    ezshoot zdist JIBSPROJ3
			    state setjib 
		    }
		    
		    ifvarg framerate 15
		    {
			    state aimjib
		  		ezshoot zdist JIBSPROJ4
		  		state setjib
		  		
				state aimjib
		  		ezshoot zdist JIBSPROJ5
		  		state setjib
	  		}
			
	  		ifvarg framerate 10
	  		{
				debris SCRAP3 3
				debris SCRAP4 3
			}
			spawn SKELGHOST
			state spawnshield
    		killit
		}
		
	}
	else
	ifvarn sprite[THISACTOR].htpicnum BURNING
	{
		ai AIS_FLINTCH
		soundonce SKELPAIN
	}
}
else ifsquished
{
	sound SKELDETH
	addkills 1 add globalkills 1
	state specialdeath
	guts JIBS5 2
	debris SCRAP3 3
	debris SCRAP4 3
	spawn SKELGHOST
	state spawnshield
	sound SQUISHED
    state random_ooz
	killit
}

set fear 0

enda

useractor enemystayput SKELKNIGHTSTAYPUT SKELSTRENGTH
cactor SKELKNIGHT
enda


eventloadactor SKELKNIGHTBONES
// state randsize
state initskelloadactor
state eventloadactorstate
enda

useractor enemy SKELKNIGHTBONES SKELSTRENGTH

ifmove 0
{
	move INIT
	state initskel
	count 260
}
fall

// state monsttarget

ifp phigher nullop else
ifrnd 4
  ifcount 260
  ifvare peractor1 0 // set to 1 to make dormant
{
	getactor[THISACTOR].z z
	subvar z 8192
	setactor[THISACTOR].z z
	ifcansee setvar temp 1 else setvar temp 0
	addvar z 8192
	setactor[THISACTOR].z z
	ifvare temp 1
//		ifpdistl 6144
	{
		sound RESURRECT
		cactor SKELKNIGHT
		ai AIS_DYING
		action ASKELRISE
	}
}

ifvare peractor1 2
{
	sound RESURRECT
	cactor SKELKNIGHT
	ai AIS_DYING
	action ASKELRISE
}

ifhitweapon
  ifdead
  {
	addkills 1 add globalkills 1
	debris SCRAP3 3
	debris SCRAP4 3
	spawn SKELGHOST
	state spawnshield
	state specialdeath
    killit
  }
		

enda

action SKELGHOSTFRAME 0 1 8 1 3
move GHOSTRISE 256 -128
useractor notenemy SKELGHOST 0

ifmove 0
{
	action SKELGHOSTFRAME
	move GHOSTRISE geth getv
	sizeat 34 34 // 40 40
	cstat 642
	spritepal 6
	setvar peractor1 32
	randvar temp 32
	addvarvar peractor1 temp
	ifrnd 128 mulvar peractor1 -1
	sound GHOSTHOWL
}

getactor[THISACTOR].ang angvar
addvarvar angvar peractor1
setactor[THISACTOR].ang angvar
ifcount 64 killit
ifceilingdistl 32 killit
ifvarg peractor1 0
addvar peractor1 2 else subvar peractor1 2
enda

action ADETHSTAND 0 1 8 1 20
action ADETHWALK 0 4 8 1 20
action ADETHRUN 0 4 8 1 12
action ADETHWAIT 32 1 8 1 30
action ADETHSTRIKE 32 3 8 1 5
action ADETHSTRUCK 48 1 8 1 5
action ADETHRETRACT 48 3 8 -1 12
action ADETHFLINTCH 56 1 8 1 20
action ADETHFALL 64 6 1 1 16
action ADETHFALL2 70 6 1 1 16
action ADETHDEAD 69 1 1 1 20
action ADETHDEAD2 75 1 1 1 20
action ADETHRISE 69 6 1 -1 20
action ADETHRISE2 75 6 1 -1 20
action ADETHBLOCK 76 1 8 1 20

move DETHWALKVELS 128
move DETHCHARGEVELS 192
move DETHSPRINT 256


ai AIDETH_SEEK ADETHWALK DETHWALKVELS seekplayer faceplayerslow
ai AIDETH_CHARGE ADETHRUN DETHCHARGEVELS faceplayer
ai AIDETH_WANDER ADETHWALK DETHWALKVELS geth
ai AIDETH_STRIKE ADETHSTRIKE DETHSPRINT geth
ai AIDETH_THROW ADETHSTRIKE STOPPED geth
ai AIDETH_FLINTCH ADETHFLINTCH STOPPED geth
ai AIDETH_DYING ADETHFLINTCH STOPPED geth
ai AIDETH_SHIELD ADETHBLOCK STOPPED faceplayerslow //geth
ai AIDETH_WAIT ADETHWAIT STOPPED geth

state dethspawnskel
			
espawn SKELKNIGHT
setactor[RETURN].pal 20
setav[RETURN].gold -1
setvar countvar 0
ifvarn target -1
{
	getactor[target].x x
	getactor[target].y y
	getactor[target].z z
}
else
{
	getplayer[THISACTOR].posx x
	getplayer[THISACTOR].posy y
	getplayer[THISACTOR].posz z
	addvar z 8192
}
setsprite RETURN x y z
sound TELEPORTER
espawn TRANSPORTERSTAR
setsprite RETURN x y z

ends

state initDETHloadactor

// sizeat 46 42
sizeat 35 32
cstator 257
ifspritepal 20 
{
	ifvare mtype 0
	{
		geta[].x initx
		geta[].y inity
		geta[].z initz
		ifvarn gold -1 addvar gold GOLD6
		espawn REVERSERIFT
		setactorvar[RETURN].myspawner THISACTOR
		getactor[RETURN].z z
		subvar z 12288
		setactor[RETURN].z z
		spritepal 4
		spriteflags 8289
		cstat 32768
		setvar mtype -1
	}

	setactor[THISACTOR].mdflags 16

	setvar aura -2 // will cycle
	setvar weap -1	
}
ifspritepal 17 
{
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
ifspritepal 24 // guardian
	sizeat 42 40 // 56 50

ends

state initDETH

move INIT
sizeat 35 32 // 46 42
cstator 257
strength DETHSTRENGTH
ifvarn gold -1 setvar gold GOLD5

ifspritepal 0 setvar aura -1 // speed
ifspritepal 1 setvar aura -32 // freezing
ifspritepal 2 setvar aura -2 // might
ifspritepal 4 { cstat 514 setvar aura -4 } // invisibility
ifspritepal 8 setvar aura -16 // poison
ifspritepal 20 // boss
{
	ifvare mtype 0
	{
		geta[].x initx
		geta[].y inity
		geta[].z initz
		ifvarn gold -1 addvar gold GOLD6
		espawn REVERSERIFT
		setactorvar[RETURN].myspawner THISACTOR
		getactor[RETURN].z z
		subvar z 12288
		setactor[RETURN].z z
		spritepal 4
		spriteflags 8289
		cstat 32768
		setvar mtype -1
	}
	else
	setvar mtype 1
	
	setactor[THISACTOR].mdflags 16
	
	ifaction ADETHRISE nullop else
	strength 3000
	setvar aura -2 // will cycle
	setvar weap -1
	
}
ifspritepal 17 
{
	action ADETHWAIT
}
ifspritepal 24 // guardian
{
	sizeat 42 40 // 56 50
	strength 1200
	setvar aura -64
}

ends

eventloadactor DETHNITE
state initDETHloadactor
enda

eventloadactor DETHNITESTAYPUT
state initDETHloadactor
enda

state DETHshootstate

ifspritepal 20 ifvare aura -2 // might aura
{
	setvar countvar 210
	getactor[THISACTOR].ang angvar
	espawn DRONE
	setactor[RETURN].ang angvar
	setactor[RETURN].pal 20
	setav[RETURN].gold -1
	subvar angvar 48
	espawn DRONE
	setactor[RETURN].ang angvar
	setactor[RETURN].pal 20
	setav[RETURN].gold -1
	addvar angvar 96
	espawn DRONE
	setactor[RETURN].ang angvar
	setactor[RETURN].pal 20
	setav[RETURN].gold -1
	break
}

ifvare target -1 
{
	ifvarn cursed 0 { ai AIDETH_SEEK break } 
	setvarvar target player[THISACTOR].i 
}
state calczdist
ldist temp THISACTOR target
ifvare temp 0 setvar temp 1
shiftvarl zdist 9
divvarvar zdist temp
sound DEMSPITSND
zshoot zdist PUSHPROJ

getactor[THISACTOR].ang angvar
subvar angvar 64
setactor[THISACTOR].ang angvar
zshoot zdist PUSHPROJ
addvar angvar 32
setactor[THISACTOR].ang angvar
zshoot zdist PUSHPROJ
addvar angvar 64
setactor[THISACTOR].ang angvar
zshoot zdist PUSHPROJ
addvar angvar 32
setactor[THISACTOR].ang angvar
zshoot zdist PUSHPROJ
subvar angvar 64
setactor[THISACTOR].ang angvar

ife actorvar[target].monstatus 3
{
	sound PUFFSND
	setactor[target].htextra 10
	setactor[target].htang sprite[THISACTOR].ang
	setactor[target].htpicnum RADIUSEXPLOSION
	setactor[target].htowner THISACTOR
	set angvar sprite[].ang
	
	cos xvel angvar
	sin yvel angvar
	mulvarvar xvel 512
	mulvarvar yvel 512
	shiftvarr xvel 13
	shiftvarr yvel 13
	movesprite target xvel yvel 0 CLIPMASK0 RETURN 
}
	
ifvarvare target player[THISACTOR].i setvar target -1

ends

state DETHshieldstate

setvar peractor4 2
ifvarg sprite[THISACTOR].htextra 0
{
	getactor[THISACTOR].htang tempb
	getactor[THISACTOR].ang angvar
	getincangle tempc angvar tempb
	ifvarl tempc -768 mulvar tempc -1
	ifvare mtype 1 ifvare sprite[THISACTOR].htpicnum RADIUSEXPLOSION setvar tempc 1024
	ifvarg tempc 768
	{
		setactor[THISACTOR].htextra -1
		setactor[THISACTOR].htpicnum -1
		ifvare SHOWDAMAGE YES
		{
			setvar temp 0
			state spawn_number
			state moveitright
		}

		getactor[THISACTOR].z z
		subvar z 8192
		setactor[THISACTOR].z z
		spawn SHIELDRING
		sound PUFFSND
		addvar z 8192
		setactor[THISACTOR].z z
	}
	ifvare aura -8
	{
		espawn LIGHTNINGBOLT
		randvar angvar 2047
		setactor[RETURN].ang angvar
		espawn LIGHTNINGBOLT
		randvar angvar 2047
		setactor[RETURN].ang angvar
	}
}
ifcount 18
{
	ifrnd 128 
	{
		ai AIDETH_THROW
		state facetarget
		sound SWORDSWING
		ifspritepal 20 ifvare aura -4
		{
			ifrnd 128
			state dethspawnskel
			else
			{
				setvar countvar 0
				setvar aura -8	 
				setvarvar weap aura
			}
		}
	}
	else
	{
		ai AIDETH_CHARGE
		count 53
	}
	break
}

getactor[THISACTOR].ang angvar
ifbulletnear resetcount
setactor[THISACTOR].ang angvar
ifp pfacing ifvarn KICKBACKPIC 0
ifvarn CURRWEAP 0
resetcount


ends

action ADETHBONES2 -6  1  1  1 10

state DETHdyingstate 

strength 0

ifaction ADETHFALL ifactioncount 5
{
	action 0
	cactor DETHNITEBONES
	set monstatus 2
	action ADETHBONES2
	break
}

ifaction ADETHFALL2 ifactioncount 5
{
	action 0
	cactor DETHNITEBONES
	set monstatus 2
	break
}

ifaction ADETHFLINTCH ifcount 3
{
	ifrnd 128
	action ADETHFALL
	else
	action ADETHFALL2
}

ifaction ADETHDEAD
{
	ifhitweapon
	spawn SKELGHOST
	state spawnshield
	killit
}

ifaction ADETHRISE
	ifactioncount 5
	{
		state initDETH
		state facetarget
		ai AIDETH_FLINTCH
		setvar monstatus 1
		strength 100
	}



ends

state DETHseekstate

ifcount 12 ifcansee
{
	ifspritepal 24 nullop else
	ifcount 78
	{
		setvar temp 0
		ifvarn target -1 setvar temp 1 else
		ifcanshoottarget ifpdistg 3072 ifpdistl 20480 ifvare cursed 0 setvar temp 1
		ifvare temp 1
		{
			state facetarget
			ai AIDETH_THROW
			sound SWORDSWING
			ifspritepal 20 ifvare aura -4 state dethspawnskel
			break
			
		}
	}
	ifbulletnear ifrnd 96
	{
		ai AIDETH_SHIELD
		break
	}

	ifp pfacing ifvarn KICKBACKPIC 0 ifrnd 64
	ifcanshoottarget
	ifvarn CURRWEAP 0
	{
		ai AIDETH_SHIELD
		break
	}
	ifpdistl 5120
	{
		ai AIDETH_CHARGE
		break
	}
	ifrnd 2
	{
		ai AIDETH_CHARGE
		break
	}
	ifpdistg 8192 ifcount 130 ifrnd 2
	{
		ai AIDETH_WANDER
		break
	}
}
else
	ifcount 260 ifrnd 2
		ai AIDETH_WANDER	
ends

state DETHchargestate

ifcount 52
{
	ifspritepal 24 nullop else
	{
		setvar temp 0
		ifvarn target -1 { ifvarg xydist2 1280 setvar temp 1 } else
		ifcanshoottarget ifpdistg 1280 ifpdistl 20480 ifvare cursed 0 setvar temp 1
		ifvare temp 1
		{
			state facetarget
			ai AIDETH_THROW
			sound SWORDSWING
			ifspritepal 20 ifvare aura -4	
			{
				espawn SKELKNIGHT
				setactor[RETURN].pal 20
				setvar countvar 0
				ifvarn target -1
				{
					getactor[target].x x
					getactor[target].y y
					getactor[target].z z
				}
				else
				{
					getplayer[THISACTOR].posx x
					getplayer[THISACTOR].posy y
					getplayer[THISACTOR].posz z
					addvar z 8192
				}
				setsprite RETURN x y z
				sound TELEPORTER
				espawn TRANSPORTERSTAR
				setsprite RETURN x y z
			}
			break
			
		}
	}
	ifvarn target -1 nullop else
	ifpdistg 5120 { ai AIDETH_SEEK break }
	
}
ifpdistl 1280
ifcanshoottarget
ifvare cursed 0
{
	setvar target -1
	state facetarget
	ai AIDETH_STRIKE
	sound SWORDSWING
	break 
}
ifvarn target -1
ifvarl xydist2 1280
{
	state facetarget
	ai AIDETH_STRIKE
	sound SWORDSWING
	break 
}
ifbulletnear ifrnd 96
{
	ai AIDETH_SHIELD
	break
}
ifp pfacing ifvarn KICKBACKPIC 0 ifrnd 64
ifcanshoottarget
ifvarn CURRWEAP 0
{
	ai AIDETH_SHIELD
	break
}
ifcount 156 ai AIDETH_SEEK

ends

state DETHwanderstate 

ifvarn target -1
{
	ai AIDETH_CHARGE
	break
}

ifaction ADETHSTAND
{
	ifactioncount 2
	{
		action ADETHWALK
		move DETHWALKVELS randomangle
		resetcount
	}
	break
} else
ifnotmoving ifcount 20
{
	resetcount
	action ADETHSTAND
	move STOPPED
	break
}
ifpdistg 8192
{
	ifspritepal 24
	{
		ai AIDETH_SEEK
		break
	}
	ifcansee ifrnd 1
	{
		ifrnd 160
			ai AIDETH_SEEK
		else ifvare cursed 0
		{
			state facetarget
			ai AIDETH_THROW	
			sound SWORDSWING
		}
	}
}
else ifcount 26
{
	ifpdistl 5120
	{
		ai AIDETH_CHARGE
		break
	}
	ifcansee ifrnd 8
	{
		ifspritepal 24 ai AIDETH_SEEK else
		ifrnd 96 ifpdistg 4096
		ifvare cursed 0
		{
			state facetarget
			ai AIDETH_THROW
			sound SWORDSWING
			ifspritepal 20 ifvare aura -4	
			{
				sound TELEPORTER
				espawn TRANSPORTERSTAR
				setvar countvar 0
				getplayer[THISACTOR].posx x
				getplayer[THISACTOR].posy y
				getplayer[THISACTOR].posz z
				addvar z 8192
				setsprite THISACTOR x y z
				changespritesect THISACTOR temp
				sound TELEPORTER
				espawn TRANSPORTERSTAR
			}
		}
		else
			ai AIDETH_SEEK
	}
}
ends

state DETHwaitstate

ifspritepal 6 getlastpal else spritepal 6
ifactioncount 4 
{
	ifspritepal 6 getlastpal
	ai AIDETH_SEEK
}

ifvarn target -1
ifvarl xydist2 1280
ifactioncount 2
{
	ifspritepal 6 getlastpal
	state facetarget
	ai AIDETH_STRIKE
	sound SWORDSWING
	break 
}

ends

state DETHstrikestate

state motionblur
ifaction ADETHSTRIKE 
{
	ifactioncount 3
	{
		ifmove STOPPED nullop else move STOPPED geth
		ifspritepal 20 ifvare aura -4
		{
			setvar countvar 210
			move STOPPED faceplayer
			action ADETHSTRUCK
			
			ifpdistl 1280
			ifvare target -1
			{
				getplayer[THISACTOR].i playersprite
				setactor[playersprite].htextra 13
				setactor[playersprite].htpicnum SHOTSPARK1
				setactor[playersprite].htowner THISACTOR
				getactor[THISACTOR].ang angvar
				setactor[playersprite].htang angvar
				sound AXEHIT
			}
			ifvarn target -1
			ifvarl xydist2 1280
			{
				setactor[target].htextra 50
				setactor[target].htpicnum SHOTSPARK1
				setactor[target].htowner THISACTOR
				sound AXEHIT
			}
			break
		}
		// shoot stuff
		state monsterbite
		state monsterbite
		ifspritepal 24 nullop else
		state DETHshootstate
		action ADETHSTRUCK
		
	}
	else
	{
		ife target -1
		{
			ifpdistl 1280
			{
				ifmove STOPPED nullop else
				move STOPPED faceplayer
			}
		}
		else
		{
			ifl xydist 1280
			{
				ifmove STOPPED nullop else
				move STOPPED geth
			}
		}
	}
}
else
ifaction ADETHSTRUCK
{
	ifactioncount 2
	action ADETHRETRACT
} else
ifaction ADETHRETRACT
{
	ifactioncount 3
	{
		ifvarn target -1
		ifvarl xydist2 1280
		{
			action ADETHSTRIKE 
			move STOPPED faceplayerslow
			sound SWORDSWING
		}
		else
		ifcansee ifcanshoottarget
		ifp palive
		ifpdistl 1280 
		{
			setvar target -1
			action ADETHSTRIKE 
			move STOPPED faceplayerslow
			sound SWORDSWING
		} else
		{
			ifspritepal 24
			{
				ifvarn cursed 0 { ai AIDETH_SEEK break }
				ai AIDETH_WAIT
				sound SKELPAIN
			}
			else
			ifrnd 64
			ai AIDETH_CHARGE
			else
			{
				ifvarn cursed 0 { ai AIDETH_SEEK break }
				ai AIDETH_WAIT
				sound SKELPAIN
			}
		}
	}
}

ends

state DETHthrowstate

state motionblur
ifaction ADETHSTRIKE 
{
	ifactioncount 3
	action ADETHSTRUCK else
	ifactioncount 2
	ifcount 3
	{
		ifspritepal 20 ifvare aura -4 
		{
			setvar countvar 210
			move STOPPED faceplayer
			ifpdistl 1280
			ifvare target -1
			{
				getplayer[THISACTOR].i playersprite
				setactor[playersprite].htextra 13
				setactor[playersprite].htpicnum SHOTSPARK1
				setactor[playersprite].htowner THISACTOR
				getactor[THISACTOR].ang angvar
				setactor[playersprite].htang angvar
				sound AXEHIT
			}
			else
			ifvarn target -1
			ifvarl xydist2 1280
			{
				setactor[target].htextra 50
				setactor[target].htpicnum SHOTSPARK1
				setactor[target].htowner THISACTOR
				sound AXEHIT
			}
				
		}
		else
		state DETHshootstate
		resetcount
	}
} else
ifaction ADETHSTRUCK
{
	ifactioncount 2
	action ADETHRETRACT
} else
ifaction ADETHRETRACT
{
	ifactioncount 3
	{
		ifvarn cursed 0 { ai AIDETH_SEEK break }
		ai AIDETH_WAIT
		sound SKELPAIN
	}
}

ends

state DETHflintchstate 
ifvarl fear 0 resetactioncount
ifactioncount 2
{
	ifvarg burning 0 ai AIDETH_SHIELD else
	ifpdistl 5120 ifcansee
	{
		ifrnd 64 ai AIDETH_SHIELD else
		ai AIDETH_CHARGE 
	}
	else
	ai AIDETH_SEEK
}
ends

state spawndethss

espawn SHIELDFALL
getactor[THISACTOR].pal pal
setactor[RETURN].pal pal
getactor[THISACTOR].xrepeat x
setactor[RETURN].xrepeat x
getactor[THISACTOR].yrepeat y
setactor[RETURN].yrepeat y

espawn SWORDFALL
setactor[RETURN].pal pal
setactor[RETURN].xrepeat x
setactor[RETURN].yrepeat y

ends

defstate DETHfatalblow

	ifwasweapon FREEZEBLAST
	spawn BIGSMOKE
	sound SKELDETH
	cstat 0
	
	guts JIBS6 3
	  
	ai AIDETH_DYING
	state spawndethss
	ifspritepal 20
	{
		addkills 1 add globalkills 1
		state randsquishsound
		state specialdeath
		guts JIBS5 4
		debris SCRAP3 4
		debris SCRAP4 4
		spawn SKELGHOST
		state spawnshield
		getactor[THISACTOR].z z
		subvar z 12288
		setactor[THISACTOR].z z
		espawn REDRIFT
		setactorvar[RETURN].countvarb 130
		globalsound BFGBOOM
		killit
	}

	setvar temp 0
	ifwasweapon RADIUSEXPLOSION setvar temp 1
	ifwasweapon RPG setvar temp 1
	ifvare temp 1
	{
		addkills 1 add globalkills 1
		state specialdeath
		state aimjib
		ezshoot zdist JIBSPROJ2
		state setjib
		
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib 
		
		state aimjib
		ezshoot zdist JIBSPROJ4
		state setjib
		
		state aimjib
		ezshoot zdist JIBSPROJ5
		state setjib
		
		debris SCRAP3 3
		debris SCRAP4 3
		spawn SKELGHOST
		state spawnshield
		killit
	}
ends

useractor enemy DETHNITE DETHSTRENGTH

ifvare mtype -1
{
	setsprite THISACTOR initx inity initz
	break
}

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ADETHWAIT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength DETHSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		setvar aura -1
		ai AIDETH_SEEK
	}
	else break
}
fall
ifmove 0
{
	state initDETH
	ai AIDETH_SEEK	
} 
  else
ifmove INIT
ai AIDETH_SEEK

ifvare mtype 1
{
	addvar countvar 1
	ifspritepal 4 // initial phase form
	{
		spriteflags 8289
		strength 4000
		ai AIDETH_WAIT
		setsprite THISACTOR initx inity initz
		ifrnd 128
		{
			ifrnd 128 cstat 0 else
			cstat 2
		}
		ifvarg countvar 260
		{
			globalsound LIGHTNING_SLAP
			palfrom 55 63 63 63
			ai AIDETH_SEEK
			spritepal 20
			set inithp 4000
			strength 4000
			cstat 257
			setvar countvar 0
		}
		break
	}
	ifvare aura 0 ifvarg countvar 10
	{
		setvar countvar 0
		ifvare weap -1 { setvar aura -2 cstat 257 } else 
		ifvare weap -2 { setvar aura -4 cstat 514 } else
		ifvare weap -4 { setvar aura -8 cstat 257 } else
		{ setvar aura -1 cstat 257 }
		 
		setvarvar weap aura
	}
	ifvarg countvar 207
	{
		setvarvar weap aura
		setvar countvar 0
		setvar aura 0
	}
}
	  
ifvarg dodge 0
{
	ifai AIDETH_DYING nullop else // setvar dodge 0 else
	ifai AIDETH_FLINTCH nullop else ai AIDETH_FLINTCH
}

state monsterai
ifactornotstayput state avoidcliffs

ifai AIDETH_DYING { state DETHdyingstate break }
ifai AIDETH_SEEK state DETHseekstate else
ifai AIDETH_CHARGE state DETHchargestate else
ifai AIDETH_WANDER state DETHwanderstate else
ifai AIDETH_STRIKE state DETHstrikestate else
ifai AIDETH_THROW state DETHthrowstate else
ifai AIDETH_FLINTCH state DETHflintchstate else
ifai AIDETH_SHIELD state DETHshieldstate else
ifai AIDETH_WAIT state DETHwaitstate

ifl sprite[].extra 0
ifg sprite[].htextra 0
	state DETHfatalblow
else
ifhitweapon
{
	ifspritepal 20
	ifn sprite[].htowner -1
	ife sprite[sprite[].htowner].picnum DEADLYEYE
	{
		geta[].htowner spriteid
		espawn RPG_GUIDED
		seta[RETURN].z sprite[spriteid].z
		seta[RETURN].htbposz sprite[spriteid].z
		setav[RETURN].target spriteid
		addstrength 20
	}
	ifspritepal 6 getlastpal
	ifwasweapon BURNING nullop else
		state random_wall_jibs
		
	ifdead
		state DETHfatalblow
	else 
	{
		ifai AIDETH_WAIT nullop else
		ifai AIDETH_FLINTCH nullop else
		ifvarl fear 0 ai AIDETH_FLINTCH else
		ifrnd 32 ai AIDETH_FLINTCH
		soundonce DETHPAIN
	}
}
else ifspritepal 20
{
	ifmove DETHWALKVELS move DETHCHARGEVELS seekplayer else
	ifmove DETHCHARGEVELS move DETHSPRINT faceplayerslow
	
	ifvarg sprite[THISACTOR].extra 0
	ifp pfacing ifpdistl 16384 ifvarvarn bossid THISACTOR
	{
		setvarvar bossid THISACTOR
		starttrackslot 1 1
		set music2 1
	}
	getactor[THISACTOR].htflags temp
	orvar temp 4
	setactor[THISACTOR].htflags temp
	setactor[THISACTOR].shade 0
}
else ifsquished
{
	ifspritepal 6 getlastpal
	sound SKELDETH
	addkills 1 add globalkills 1
	state specialdeath
	guts JIBS5 2
	debris SCRAP3 3
	debris SCRAP4 3
	spawn SKELGHOST
	state spawnshield
	sound SQUISHED
    state random_ooz
    state spawndethss
	killit
}

set fear 0

enda

useractor enemystayput DETHNITESTAYPUT DETHSTRENGTH
cactor DETHNITE
enda

eventloadactor 5326 // formerly DETHNITEBONES
cactor 5327
state initDETHloadactor
state eventloadactorstate
enda

eventloadactor DETHNITEBONES
state initDETHloadactor
state eventloadactorstate
enda

useractor enemy DETHNITEBONES DETHSTRENGTH

ifmove 0
{
	state initDETH
	count 260
}
fall

// state monsttarget

ifp phigher nullop else
// ifrnd 4
  ifcount 260
  ifvare peractor1 0 // set to 1 to make dormant
{
	ifcansee
//		ifpdistl 6144
	{
		sound RESURRECT
		cactor DETHNITE
		ai AIDETH_DYING
		action ADETHRISE
	}
}

ifvare peractor1 2
{
	sound RESURRECT
	set monstatus 1
	cactor DETHNITE
	ai AIDETH_DYING
	action ADETHRISE
}

ifhitweapon
  ifdead
  {
	debris SCRAP3 3
	debris SCRAP4 3
	spawn SKELGHOST
	state spawnshield
	set monstatus 1
	state specialdeath
    killit
  }
		

enda

move SHIELDFLY1 96 -64
move SHIELDFLY2 96
action SHIELDFALLFRAMES 0 8 1 1 18
action SWORDFALLFRAMES 0 8 1 1 14
action SHIELDREST 7 1 1 1 1
action SWORDREST 7 1 1 1 1
useractor notenemy SHIELDFALL 0 SHIELDFALLFRAMES
fall
sizeat 32 32
ifmove 0
move SHIELDFLY1 getv randomangle
ifmove SHIELDFLY1
ifcount 10 move SHIELDFLY2 geth

ifaction SHIELDFALLFRAMES
ifactioncount 7
{
	action SHIELDREST
	move INIT
}
ifcount 208
{
	spawn SMALLSMOKE
	killit
}
enda

useractor notenemy SWORDFALL 0 SWORDFALLFRAMES
fall
sizeat 32 32
ifmove 0
move SHIELDFLY1 getv randomangle
ifmove SHIELDFLY1
ifcount 10 move SHIELDFLY2 geth

ifaction SWORDFALLFRAMES
ifactioncount 7
{
	action SWORDREST
	move INIT
}
ifcount 208
{
	spawn SMALLSMOKE
	killit
}
enda

action PUSHTRAILFRAME 0 1 5 1 1
useractor notenemy PUSHTRAIL 0 PUSHTRAILFRAME

ifpdistl 1536 ifcansee
{
	ifvarg shieldup 0
	{
		soundonce PUFFSND
		killit
	}
	getplayer[THISACTOR].i playersprite
	getplayer[THISACTOR].posx mx
	getplayer[THISACTOR].posy my
	getactor[THISACTOR].x x
	getactor[THISACTOR].y y
	subvarvar mx x
	subvarvar my y
	getangle angvar mx my
	
	getactor[THISACTOR].ang temp
	
	getincangle tempb angvar temp
	ifvarl tempb 0 mulvar tempb -1
	ifvarle tempb 128
	{
		sound PUFFSND
		getactorvar[playersprite].dodge temp
		addvar temp 4096
		setactorvar[playersprite].dodge temp
		setactorvar[playersprite].dodgeang angvar
		getactor[playersprite].htextra tempb
		addvar tempb 1
		setactor[playersprite].htextra tempb
		setactor[playersprite].htang dodgeang
		setactor[playersprite].htpicnum PUSHPROJ
		setactor[playersprite].htowner THISACTOR
	}
}

headspritestat spriteid 1
whilevarn spriteid -1
{
	ife actorvar[spriteid].monstatus 3
	ifn sprite[spriteid].picnum AXON
	{
		dist xydist THISACTOR spriteid
		ifl xydist 1560
		{
			sound PUFFSND
			setactor[spriteid].htextra 10
			setactor[spriteid].htang sprite[THISACTOR].ang
			setactor[spriteid].htpicnum RADIUSEXPLOSION
			setactor[spriteid].htowner THISACTOR
			set angvar sprite[].ang
			
			cos xvel angvar
			sin yvel angvar
			mulvarvar xvel 512
			mulvarvar yvel 512
			shiftvarr xvel 13
			shiftvarr yvel 13
			movesprite spriteid xvel yvel 0 CLIPMASK0 RETURN 
		}
	}
	nextspritestat spriteid spriteid
}


getactor[THISACTOR].htowner spriteid
ifvarn spriteid -1
{
	ifvare sprite[spriteid].pal 1 spritepal 1
	else spritepal 2
}
else
spritepal 2
sizeat 128 128
cstat 642

ifcount 1 killit
enda

action D_WALK 0 4 5 1 24
action D_RUN 0 4 5 1 12
action D_SPIT 30 1 5 1 52
action D_BITE 25 2 5 1 20
action D_DYING 35 7 1 1 14
action D_FLINTCH 35 1 1 1 18
action D_DEAD 41 1 1 1 20
action D_FROZEN 44 1 1 1 24
action D_STAND 25 1 5 1 1

move DWALKVELS 160
move DRUNVELS 256
move DWALKVELS2 196
move DRUNVELS2 320

ai AID_SEEK D_WALK DWALKVELS seekplayer faceplayerslow
ai AID_WANDER D_WALK DWALKVELS geth
ai AID_CHARGE D_RUN DRUNVELS faceplayer
ai AID_DODGE D_RUN DRUNVELS geth
ai AID_SPIT D_SPIT STOPPED faceplayer // change to geth for bot targetting
ai AID_BITE D_BITE DRUNVELS2 faceplayer // change to geth for bot targetting
ai AID_DYING D_DYING STOPPED geth
ai AID_FLINTCH D_FLINTCH STOPPED faceplayer

state Dseekstate

getactor[THISACTOR].ang angvar
ifbulletnear ifrnd 32 
{ 
	ifrnd 128 addvar angvar 320 else subvar angvar 320
	setactor[THISACTOR].ang angvar
	ai AID_DODGE 
	setvar peractor4 12
	break 
}

ifvarn target -1
{
	soundonce DEMROAR
	ifvarl xydist2 4096 ai AID_CHARGE else
	ai AID_SPIT
	break
}

ifcansee
{
	ifrnd 8 ifp pfacing
	ifactornotstayput
	{
		ifrnd 128 addvar angvar 256 else subvar angvar 256
		setactor[THISACTOR].ang angvar
		ai AID_DODGE 
		setvar peractor4 12
		break 
	}
	ifpdistl 4096 ifrnd 16 { ai AID_CHARGE soundonce DEMROAR }
	else ifpdistl 2048 { ai AID_CHARGE soundonce DEMROAR }
	else
	ifactornotstayput ifvare cursed 0
	{
		ifrnd 16 ifcount 26
		ai AID_SPIT
	}
	else
	ifawayfromwall nullop else
	ifcount 26 ifvare cursed 0
	ai AID_SPIT
}
else ifcount 130 ifrnd 2 ai AID_WANDER
	
ends

state Dwanderstate

ifcansee ai AID_SEEK
else
	ifnotmoving
		ifcount 8
			move DWALKVELS randomangle

ends

state Dchargestate

getactor[THISACTOR].ang angvar
ifwasweapon SHOTSPARK1 nullop else
ifbulletnear 
{
	ifrnd 128 addvar angvar 320 else subvar angvar 320
	setactor[THISACTOR].ang angvar
	ai AID_DODGE
	setvar peractor4 12
	break 
}
ifvare target -1
{ 
	ifpdistg 4608 { ai AID_SEEK break }
}
else ifvarg xydist2 4608 { ai AID_SEEK break }
ifpdistl 1280
ifcanshoottarget
ifvare cursed 0
{ 
	setvar target -1
	ai AID_BITE 
	break 
}

ifvarn target -1
ifvarl xydist2 1024
{
	ai AID_BITE
	break
}
ifcount 78 ai AID_SEEK
ends

state Ddodgestate
ifcount 12
{
	ifrnd 128 ai AID_SEEK
	else ai AID_CHARGE
}
ends

state DSPITstate

ifvarg fear 0 { ai AID_SEEK break }

ifcount 18 nullop else ifcount 17
{
	ifvare target -1 { ifvarn cursed 0 { ai AID_SEEK break } setvarvar target player[THISACTOR].i }
	state calczdist
	ldist temp THISACTOR target
	ifvare temp 0 setvar temp 1
	mulvar zdist 644
	divvarvar zdist temp
	sound DEMSPITSND
	zshoot zdist DEMONSPIT
	ifactor DEMON
	ifspritepal 19
	{
		getactor[THISACTOR].ang angvar
		subvar angvar 64
		setactor[THISACTOR].ang angvar
		zshoot zdist DEMONSPIT
		addvar angvar 32
		setactor[THISACTOR].ang angvar
		zshoot zdist DEMONSPIT
		addvar angvar 64
		setactor[THISACTOR].ang angvar
		zshoot zdist DEMONSPIT
		addvar angvar 32
		setactor[THISACTOR].ang angvar
		zshoot zdist DEMONSPIT
		subvar angvar 64
		setactor[THISACTOR].ang angvar
	}
		
	ifvarvare target player[THISACTOR].i setvar target -1
}

ifactor DEMON
ifactioncount 2 ai AID_SEEK
ends

state DBITEstate
	
ifvare target -1
{
	ifpdistl 844
	{
		state facetarget
		ifmove DRUNVELS2 move STOPPED faceplayer
	}
	else ifpdistg 1024 ifmove STOPPED move DRUNVELS2 faceplayer
}
	
ifactioncount 2
{
	sound BITEME
	ifn sprite[].pal 25 shoot MONSTERBITE
    shoot MONSTERBITE
    ifspritepal 19 shoot MONSTERBITE
    
    ifvarn target -1 ifvarl xydist 1596
    {
	    resetactioncount
	    resetcount
    } else
    ifvarn cursed 0 ifvare target -1
    {
	    ai AID_SEEK
	    break
    }
    ifpdistg 1596
    {
	  // sound for missing the bite
      ai AID_SEEK
    }
    else ifp palive ifcansee
    ifvare cursed 0
    {
      resetactioncount
      resetcount
    }
}
			
ends

state Dflintchstate
ifvarl fear 0 resetactioncount
ifactioncount 2 
{
	ifcansee ifcanshoottarget
	ai AID_CHARGE
	else
	ai AID_WANDER
}
ends

state Ddyingstate
strength 0

ifhitweapon
{
	ifwasweapon RADIUSEXPLOSION
	{
		guts JIBS6 4
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state randsquishsound
		killit
	}
	ifwasweapon RPG
	{
		guts JIBS6 4
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state randsquishsound
		killit
	}
}

ifaction D_DEAD
nullop
else ifactioncount 7
{
	action D_DEAD 
	// setvar command 65536
    // setactor[THISACTOR].mdflags 16
	espawn FLIES
	setactorvar[RETURN].myspawner THISACTOR
}
ends

eventloadactor DEMON
sizeat 30 28 // 40 36
ifspritepal 7 spritepal 19
ifspritepal 19 sizeat 37 34 // 50 44
ifspritepal 17 
{
	action D_STAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

eventloadactor DEMONSTAYPUT
sizeat 30 28 // 40 36
ifspritepal 7 spritepal 19
ifspritepal 19 sizeat 37 34 // 50 44
ifspritepal 17 
{
	action D_STAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
ifspritepal 25 sizeat 18 17
enda

useractor enemystayput DEMONSTAYPUT DEMONSTRENGTH
cactor DEMON
enda

useractor enemy DEMON DEMONSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action D_STAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength DEMONSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
	}
	else break
}
ifspritepal 19
{
	ifmove DWALKVELS
	{
		ifai AID_SEEK move DWALKVELS2 seekplayer faceplayerslow else
		ifai AID_WANDER move DWALKVELS2 geth
	} else
	ifmove DRUNVELS
	{
		ifai AID_CHARGE move DRUNVELS2 faceplayer else
		ifai AID_DODGE move DRUNVELS2 geth
	}
}

ifai 0
{
	cstator 257
	sizeat 30 28 // 40 36
	ifspritepal 19 
	{
		ifg SKILL 3 state randomaura
		sizeat 37 34 // 50 44
		addstrength TROOPSTRENGTH
		ifvarn gold -1
		setvar gold GOLD4
	}
	else
	ifspritepal 25
	{
		sizeat 18 17
		strength 100
		ifvarn gold -1
		setvar gold GOLD2
	}
	else ifvarn gold -1 
	{
		setvar gold GOLD3
		ife SKILL 5 ifrnd 64
		{
			sizeat 37 34
			addstrength TROOPSTRENGTH
			state randomaura
		}
	}
	ai AID_SEEK
	sound DEMROAR
}

state monsterai
ifactornotstayput state avoidcliffs
  ifvarand sprite[THISACTOR].cstat 8
  {
	  getceilzofslope sprite[THISACTOR].sectnum sprite[THISACTOR].x sprite[THISACTOR].y mz
	  addvar mz 16384
	  ifvarvarg sprite[THISACTOR].z mz
	  {
	  	getactor[THISACTOR].z z
	  	subvar z 512
	  	setactor[THISACTOR].z z
  	  }
  	  else
  	  setactor[THISACTOR].z mz
  }
  else
  fall
ifvarg dodge 0
{
	ifai AID_DYING nullop else // setvar dodge 0 else
	ifai AID_FLINTCH nullop else ai AID_FLINTCH
}



ifactioncount 2
{
	setvar temp 0
	ifaction D_WALK setvar temp 1
	ifaction D_RUN setvar temp 1
	ifvare temp 1
	{
		resetactioncount
		sound DMWALK
	}
}

ifai AID_DYING { state Ddyingstate break }


ifai AID_SEEK state Dseekstate else
ifai AID_WANDER state Dwanderstate else
ifai AID_CHARGE state Dchargestate else
ifai AID_DODGE state Ddodgestate else
ifai AID_SPIT state DSPITstate else
ifai AID_BITE state DBITEstate else
ifai AID_FLINTCH state Dflintchstate

ifhitweapon
{
	spawn BLOOD
	ifwasweapon SHOTSPARK1 
	state random_wall_jibs
	//sound DEMONPAIN
	ifai AID_WANDER ai AID_SEEK
	ifdead 
	{ 
		// ifspritepal 1 nullop else { spawn BURNING guts JIBS3 4 }
		sound NEWBEAST_DYING
		ifwasweapon FREEZEBLAST spawn BIGSMOKE else
		ifwasweapon BURNING nullop else
		{
			state standard_jibs
			state randsquishsound
			setvar command 1
		}
		addkills 1 add globalkills 1
		state specialdeath
		ai AID_DYING  
		cstat 0
		// ifrnd 128 cstator 4
	}
	else
	{
		ifrnd 32 ai AID_FLINTCH
		ifvarl fear 0 ai AID_FLINTCH
		soundonce NEWBEAST_PAIN
	}
}
else ifsquished
{
	sound SQUISHED
	sound NEWBEAST_DYING
	state standard_jibs	
	addkills 1 add globalkills 1
	state specialdeath
	state random_ooz
	killit
}
else
ifrnd 1 ifrnd 128 ifvare burning 0 soundonce BOS4_RECOG
	

enda

move EWALKVELS 128
move ERUNVELS 192
move EWALKMAD 160
move ERUNMAD  272

action ETTIN_WALK 0 4 8 1 20
action ETTIN_RUN 0 4 8 1 14
action ETTIN_STRIKE 32 3 8 1 12
action ETTIN_THROW 32 4 8 1 20
action ETTIN_SWUNG 48 1 8 1 16
action ETTIN_WAIT 56 1 8 1 14
action ETTIN_JIBBED 71 8 1 1 14
action ETTIN_JIBS 78 1 1 1 1
action ETTIN_FALL 83 7 1 1 14
action ETTIN_DEAD 89 1 1 1 1
action ETTIN_FLINTCH 56 1 8 1 20

ai AIE_CHARGE ETTIN_RUN ERUNVELS faceplayer
ai AIE_SEEK ETTIN_WALK EWALKVELS seekplayer faceplayerslow
ai AIE_WANDER ETTIN_WALK EWALKVELS geth
ai AIE_WAIT ETTIN_WAIT STOPPED geth
ai AIE_FLINTCH ETTIN_FLINTCH STOPPED faceplayer
ai AIE_STRIKE ETTIN_STRIKE ERUNVELS geth
ai AIE_THROW ETTIN_THROW STOPPED faceplayersmart
ai AIE_DYINGJIB ETTIN_JIBBED STOPPED faceplayer
ai AIE_DYINGFALL ETTIN_FALL STOPPED faceplayer

state Edyingjibstate

strength 0
ifaction ETTIN_JIBS
{
	ifhitweapon
	{
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
	}
}
else
ifactioncount 8
{
	// setvar command 65536
    // setactor[THISACTOR].mdflags 16
	action ETTIN_JIBS
	espawn FLIES
	setactorvar[RETURN].myspawner THISACTOR
}
else ifhitweapon { }

ends

state Edyingfallstate

strength 0

ifaction ETTIN_DEAD
{
	ifhitweapon
	{
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
	}
}
else
ifactioncount 7
{
	action ETTIN_DEAD
	espawn FLIES
	// setvar command 65536
    // setactor[THISACTOR].mdflags 16
	setactorvar[RETURN].myspawner THISACTOR
}



ends

state Eseekstate

ifvarn target -1
{
	ifrnd 128 ai AIE_THROW else
	ai AIE_CHARGE
	break
}
ifvarn cursed 0
{
	ifcount 130 ai AIE_CHARGE
	break
}

ifp palive
ifcansee
{
	ifcanshoottarget ifpdistl 16384 
	ifvare fear 0
	{
		ifrnd 2
		{
			ai AIE_THROW
			break
		}
		ifstrength 150 ifrnd 2
		{
			ai AIE_THROW
			break
		}
	}
	ifactornotstayput nullop else
	ifawayfromwall nullop else
	ifvare fear 0
	ifcanshoottarget
	ifcount 20
	{
		ai AIE_THROW
		break
	}
	ifpdistl 6144
	{
		ai AIE_CHARGE
		break
	}
	ifrnd 1
	{
		ai AIE_CHARGE
		break
	}
	ifpdistg 8192 ifcount 130 ifrnd 2
	{
		ai AIE_WANDER
		move EWALKVELS randomangle
		break
	}
}
else
{
	ifcount 260 ifrnd 2
	{
		ai AIE_WANDER
		move EWALKVELS randomangle
	}
}
ends

state Ewanderstate 

ifvarn target -1
{
	ifrnd 128 ai AIE_THROW else
	ai AIE_CHARGE
	break
}

ifaction ETTIN_WAIT
{
	ifactioncount 2
	{
		action ETTIN_WALK
		move EWALKVELS randomangle
	}
	break
} else
ifnotmoving ifcount 12
{
	action ETTIN_WAIT
	move STOPPED
	break
}
ifp palive nullop else break

ifpdistl 6144
	ai AIE_CHARGE
	
ifpdistg 10240
{
	ifcansee ifrnd 2
	{
		ifrnd 160
			ai AIE_SEEK
		else ifvare fear 0
			ai AIE_THROW	
	}
}
else
{
	ifcansee ifrnd 8
	{
		ifrnd 96 ifpdistg 4096
		ifvare fear 0
			ai AIE_THROW
		else
			ai AIE_SEEK
	}
}


ends

state Echargestate

ifvare target -1 ifpdistg 7168 { ai AIE_SEEK break }

ifvare fear 0
ifvare cursed 0
ifpdistl 1560
ifcanshoottarget
ifp palive
{
	setvar target -1
	state facetarget
	ai AIE_STRIKE
	break 
}

ifvarn target -1
ifvarl xydist2 1560
{
	state facetarget
	ai AIE_STRIKE
	break 
}

ifp palive
ifrnd 2
ifvare fear 0
{
	ifvare cursed 0
	ai AIE_THROW
	else
	ifvarn target -1
	ai AIE_THROW
}


ifcount 78 ai AIE_SEEK
ends

state Ethrowstate

ifvarg fear 0 { ai AIE_SEEK break }

ifactioncount 3 nullop else
ifactioncount 2
{
	ifcount 9
	{
		resetcount
		
		ifvare target -1 { ifvarn cursed 0 { ai AIE_SEEK break } setvarvar target player[THISACTOR].i }
		state calczdist
		ldist tempe THISACTOR target
		ifvare tempe 0 setvar tempe 1
		shiftvarl zdist 9
		divvarvar zdist tempe
		ezshoot zdist ETTINCLUB
		ifspritepal 24
		{
			setthisprojectile[RETURN].pal 24
		}
		
		getactor[RETURN].zvel zvel
		randvar tempb 1024
		ifrnd 128 mulvar tempb -1
		addvarvar zvel tempb
		
		shiftvarr tempe 3
		subvarvar zvel tempe
		setactor[RETURN].zvel zvel
		ifvarvare target player[THISACTOR].i setvar target -1
	}
}
ifactioncount 4
	ai AIE_SEEK

ends

state Estrikestate

state motionblur

ifpdistl 1024
ifvare target -1
{
	move STOPPED
	state facetarget
}

ifaction ETTIN_SWUNG
{
	ifactioncount 2
	ai AIE_WAIT
	break
}
ifactioncount 2 
{
	sound THUD
	state monsterbite
    state monsterbite
    state monsterbite
    ifstrength 150 state monsterbite
    action ETTIN_SWUNG
} 
else
ifactioncount 1 
{
	soundonce SWORDSWING
}


ends

state Ewaitstate

ifcount 6
{
	ifvare target -1 ifpdistg 4608 { ai AIE_SEEK break }
	
	ifvarn target -1
	{
		ifvarl xydist2 1024
		{
			state facetarget
			ai AIE_STRIKE 
		}
		else
		ai AIE_CHARGE
		break
	}
	ifvare fear 0
	ifvare cursed 0
	ifpdistl 1280
	ifcanshoottarget 
	ifp palive
	{
		state facetarget
		ai AIE_STRIKE 
		break 
	}
	ifpdistg 1280 ifpdistl 6144
	{
		ai AIE_CHARGE
		break
	}
	ifcount 26 ai AIE_SEEK
}



ends

state Eflintchstate
ifvarl fear 0 resetcount
ifstrength 150 ifcount 8 ai AIE_SEEK
ifcount 12 ai AIE_SEEK

ends

eventloadactor ETTIN
sizeat 34 32
ifspritepal 17 
{
	action ETTIN_WAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

eventloadactor ETTINSTAYPUT
sizeat 34 32
ifspritepal 17 
{
	action ETTIN_WAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

useractor enemystayput ETTINSTAYPUT ETTINSTRENGTH
cactor ETTIN
enda

useractor enemy ETTIN ETTINSTRENGTH
ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ETTIN_WAIT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength ETTINSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
	}
	else break
}

  ifvarand sprite[THISACTOR].cstat 8
  {
	  getceilzofslope sprite[THISACTOR].sectnum sprite[THISACTOR].x sprite[THISACTOR].y mz
	  addvar mz 13000
	  ifvarvarg sprite[THISACTOR].z mz
	  {
	  	getactor[THISACTOR].z z
	  	subvar z 512
	  	setactor[THISACTOR].z z
  	  }
  	  else
  	  setactor[THISACTOR].z mz
  }
  else
  fall
state checksquished

ifaction 0
{
	ifg SKILL 3 state randomaura
	sizeat 34 32
	cstator 257
	ai AIE_SEEK
	ifvarn gold -1
	setvar gold GOLD4
	sound ETTINWAKE
}


state monsterai
ifactornotstayput state avoidcliffs

ifai AIE_DYINGJIB { state Edyingjibstate break }
ifai AIE_DYINGFALL { state Edyingfallstate break }

ifstrength 150
{
	ifai AIE_CHARGE ifmove ERUNVELS move ERUNMAD faceplayer else
	ifai AIE_SEEK ifmove EWALKVELS move EWALKMAD seekplayer faceplayerslow
}

ifvarg dodge 0
{
	ifai AIE_DYINGJIB nullop else // setvar dodge 0 else
	ifai AIE_DYINGFALL nullop else // setvar dodge 0 else
	ifai AIE_FLINTCH nullop else ai AIE_FLINTCH
}

ifactioncount 2
{
	setvar temp 0
	ifaction ETTIN_WALK setvar temp 1
	ifaction ETTIN_RUN setvar temp 1
	ifvare temp 1
	{
		resetactioncount
		sound DMWALK
	}
}




ifai AIE_SEEK state Eseekstate else
ifai AIE_WANDER state Ewanderstate else
ifai AIE_CHARGE state Echargestate else
ifai AIE_THROW state Ethrowstate else
ifai AIE_STRIKE state Estrikestate else
ifai AIE_FLINTCH state Eflintchstate else
ifai AIE_WAIT state Ewaitstate


ifhitweapon
{
	ifspritepal 24 nullop else ifstrength 150 spritepal 24
	soundonce ETTINPAIN
	ifai AIE_WANDER ai AIE_SEEK
	ifdead 
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		getlastpal
		sound ETTINDYING
		cstat 0
		addkills 1 add globalkills 1
		state specialdeath
		ifwasweapon RADIUSEXPLOSION 
		ai AIE_DYINGJIB else
		ifwasweapon RPG
		ai AIE_DYINGJIB else
		ai AIE_DYINGFALL 
		ifai AIE_DYINGJIB
		{
			state standard_jibs 
			state randsquishsound
			setvar command 1
		}
		else
		{
			guts JIBS6 3
					    
		    state aimjib
		    ezshoot zdist JIBSPROJ3
		    state setjib
		    
		    state aimjib
		    ezshoot zdist JIBSPROJ4
		    state setjib
		}
	}
	else 
	{
		ifrnd 24 ai AIE_FLINTCH
		ifvarl fear 0 ai AIE_FLINTCH
	}
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	
}
else ifvare burning 0 ifrnd 1 ifrnd 128 soundonce ETTINWAKE
	

enda

useractor notenemy ETTINCLUBGROUND
fall
ifmove 0
{
	move INIT
	sizeat 24 24
	ifrnd 128 cstator 4
}
// ifcount 104 killit
ifcount 26
{
	sound PIPEBOMB_EXPLODE
	spawn EXPLOSION2
	debris SCRAP2 3
	debris SCRAP3 3
	debris SCRAP4 3
	hitradius 2048 20 30 40 50
	killit
}
enda

state facesimple

getactor[target].x mx
getactor[target].y my
subvarvar mx sprite[THISACTOR].x
subvarvar my sprite[THISACTOR].y
getangle angvar mx my
setactor[THISACTOR].ang angvar

ends

action EVILSTAND 0 1 5 1 1
action EVILRUN 5 4 5 1 10
action EVILJUMP1 25 1 5 1 20
action EVILJUMP2 30 1 5 1 20
action EVILFALL 35 1 5 1 30
action EVILRUNBACK 25 4 5 -1 10
action EVILCROUCH 0 1 5 1 1 // THE STANDING FRAME OF EVILDUKECROUCH
action EVILCRAWL 0 3 5 1 18
action EVILDYING 55 5 1 1 18
action EVILDYING2 202 5 1 1 36
action EVILDEAD 60 1 1
action EVILDEAD2 207 1 1
action EVILSHOOTING 61 1 5 1 20
action EVILCRAWLSHOOT 26 1 5 1 20

move EVILRUNVEL 192
move EVILRUNBACKVEL -160
move EVILJUMPVEL 192 -256
move EVILJUMPVEL2 192 -32
move EVILFALLVEL 192
move EVILCRAWLVEL 128

ai AIEVILDYING EVILDYING STOPPED geth
ai AIEVILSEEK EVILRUN EVILRUNVEL seekplayer
ai AIEVILCHARGE EVILRUN EVILRUNVEL faceplayer
ai AIEVILSHOOT EVILSTAND STOPPED faceplayerslow // faceplayersmart
ai AIEVILSTAND EVILSTAND STOPPED geth
ai AIEVILJUMP EVILJUMP1 EVILJUMPVEL getv geth
ai AIEVILSTRAFE EVILRUN STOPPED faceplayersmart

state stayputcheck
ifactornotstayput nullop else
ifawayfromwall nullop else
{
	ai AIEVILSHOOT
	ifrnd 128
	{
		cactor EVILDUKECROUCH
		action EVILCROUCH
	}
}
ends

state evilstrafestate

ifp palive
ifvare seevar YES ifvare cursed 0 
{
	ifpdistg 10240 ifrnd 192 ifvarg weap 0 subvar weap 1 else
	ifvarl weap 52 addvar weap 1 
}
else ifvarn cursed 0 ifvarn target -1 { ifvarl weap 52 addvar weap 1 state facesimple }
else ifvarg weap 0 subvar weap 1

cos xvel dodgeang
sin yvel dodgeang

shiftvarr xvel 6
shiftvarr yvel 6
setvar zvel 0
movesprite THISACTOR xvel yvel zvel CLIPMASK0 RETURN
ifvarn RETURN 0 { cactor EVILDUKE ai AIEVILSEEK }
else ifvarn cursed 0 ifvarn target -1 ifvarl xydist2 3072
{
	ai AIEVILCHARGE
	action EVILRUNBACK
	move EVILRUNBACKVEL geth
}
else
ifvare seevar YES ifpdistl 3072
ifvare target2 -1
{
	cactor EVILDUKE
	ai AIEVILCHARGE
	action EVILRUNBACK
	move EVILRUNBACKVEL faceplayer
}
else
ifcount 13 ifrnd 10 { cactor EVILDUKE ai AIEVILSEEK }
else
ifcount 20 { cactor EVILDUKE ai AIEVILSEEK }

state stayputcheck

ends

state checkevildodge

ifvarn sprite[THISACTOR].pal 10
ifvarn sprite[THISACTOR].pal 14
ifvare cursed 0
ifp pfacing
ifvare seevar YES
ifvarl CURRWEAP 8
ifvarn CURRWEAP 0
ifvarn CURRWEAP 4
ifrnd 32
{
	ifvarg peractor1 -128 ifvarl peractor1 128
	{
		findplayer xydist
		addvar xydist 1024
		randvar temp 16384
		ifvarvarg temp xydist
		{
			// start dodge
			randvar tempb 128
			addvar tempb 256
			ifvarl peractor1 0 mulvar tempb -1
			getactor[THISACTOR].ang dodgeang
			addvarvar dodgeang tempb
			cactor EVILDUKE
			ai AIEVILSTRAFE
			break
		}
	}
}

ifvarn sprite[THISACTOR].pal 10
ifvarn sprite[THISACTOR].pal 14
// ifvarn cursed 0 
ifvarn target -1
ifrnd 16
{
	ifvarg peractor1 -128 ifvarl peractor1 128
	{
			// start dodge
			randvar tempb 128
			addvar tempb 256
			ifvarl peractor1 0 mulvar tempb -1
			getactor[THISACTOR].ang dodgeang
			addvarvar dodgeang tempb
			cactor EVILDUKE
			ai AIEVILSTRAFE
			break
	}
}

ifvarg peractor1 -160 ifvarl peractor1 160 nullop else
ifai AIEVILSHOOT nullop else
ifonwater nullop else
ifvare seevar YES
{
	ai AIEVILSHOOT
	ifrnd 128
	{
		cactor EVILDUKECROUCH
		action EVILCROUCH
	}
}

ends

state evilshootstate

// he can shoot any time, but this state is for when he is shooting while not moving

setvar dodgeang 6666
ifp palive ifvare cursed 0
ifvare seevar YES 
{
	ifpdistg 10240 ifrnd 192 ifvarg weap 0 subvar weap 1 else
	ifvarl weap 52 addvar weap 1 
}
else
ifvarn target -1 ifvarn cursed 0 { ifvarl weap 52 addvar weap 1 state facesimple }
else ifvarg weap 0 subvar weap 1

ifvarg weap MINIFIRECOUNT
{
	ifactor EVILDUKE action EVILSHOOTING else
	action EVILCRAWLSHOOT
}
// else
// {
	// move STOPPED
	// ifactor EVILDUKE action EVILSTAND
	// else action EVILCROUCH
// }

ifcount 45 
{
	ifvare cursed 0
	ifvare seevar NO { cactor EVILDUKE ai AIEVILSEEK }
	else
	state checkevildodge
	ifvarl weap 1 { cactor EVILDUKE ai AIEVILSEEK }
}

ifactornotstayput ifcount 90
ifrnd 16
{
	cactor EVILDUKE ai AIEVILSEEK
	set weap 0
}

ends


state evilseekstate

	setvar dodgeang 6666
	subvar weap 2
	ifvarl weap 0 setvar weap 0

	state checkevildodge
	ifai AIEVILSTRAFE break

	ifcount 78 ai AIEVILCHARGE else
	ifvare cursed 0
	{
		ifcount 32
		ifpdistl 6144 ifvare seevar YES ai AIEVILCHARGE else
		ifcount 10
		ifvarvare player[THISACTOR].cursectnum sprite[THISACTOR].sectnum ai AIEVILCHARGE
	}
	else ifvarn target -1 ifvarl xydist2 6144 ai AIEVILCHARGE

	ifcount 6 state stayputcheck

ends

state evilchargestate

setvar dodgeang 6666

ifp palive ifvare cursed 0
ifvare seevar YES { ifvarl weap 52 addvar weap 1 }
else ifvarn cursed 0 ifvarn target -1 { ifvarl weap 52 addvar weap 1 state facesimple }
else subvar weap 1

ifaction EVILRUN
{
	ifvare cursed 0
	ifvare seevar YES ifpdistl 3072
	{
		ifrnd 128 ai AIEVILSHOOT else
		{
			action EVILRUNBACK
			move EVILRUNBACKVEL faceplayer
		}
	}
	else ifvarn cursed 0 ifvarn target -1 ifvarl xydist2 3072
	{
		action EVILRUNBACK
		move EVILRUNBACKVEL geth
	}
}
else
{
	ifvare cursed 0
	{
		ifpdistg 6144
		{
			action EVILRUN
			move EVILRUNVEL faceplayer
		}
		else
		ifvare seevar NO
		{
			action EVILRUNBACK
			move EVILRUNBACKVEL faceplayer
		}
	}
	else
	{
		ifvarn target -1
		{
			ifvarg xydist2 6144
			{
				action EVILRUN
				move EVILRUNVEL geth
			}
		}
		else
		{
			ifpdistg 6144
			{
				action EVILRUN
				move EVILRUNVEL faceplayer
			}
		}
	}
}


state checkevildodge
ifai AIEVILSTRAFE break

ifcount 20
{
	ifnotmoving ai AIEVILSEEK else
	ifvare seevar NO ai AIEVILSEEK
}

ifcount 6 state stayputcheck

ends

state eviljumpstate

setvar dodgeang 6666

ifp palive
ifvare seevar YES { ifvarl weap 52 addvar weap 1 }
else ifvarn cursed 0 ifvarn target -1 { ifvarl weap 52 addvar weap 1 }
else subvar weap 1

ifaction EVILJUMP1
{
	ifcount 6 
	{ 
		action EVILJUMP2 
		move EVILJUMPVEL2 getv getv 
	}
}

ifaction EVILJUMP2
{
	ifcount 5
	{
		action EVILFALL
		move EVILFALLVEL geth
	}
	// getceilzofslope blah blah
}
ifaction EVILFALL
{
	iffloordistl 4
	ai AIEVILSEEK
}

ends

state evildyingstate

strength 0

ifhitweapon
{
	ifwasweapon RADIUSEXPLOSION
	{
		state standard_jibs
		killit
	}
	ifwasweapon RPG
	{
		state standard_jibs
		killit
	}
}

ifaction EVILDYING
{
	setvar weap 0
	ifactioncount 5
	{
		action EVILDEAD
		cstat 0
		// setvar command 65536
   		// setactor[THISACTOR].mdflags 16
	}
}

ifaction EVILDYING2
{
	setvar weap 0
	ifactioncount 5
	{
		action EVILDEAD2
		cstat 0
		// setvar command 65536
   		// setactor[THISACTOR].mdflags 16
	}
}
ends

state evilfirebullet

setvar countvar 0
sound MINIGUN_FIRE
state calczdist
ldist xydist THISACTOR target
ifvare xydist 0 setvar xydist 1
shiftvarl zdist 10
divvarvar zdist xydist		
ezshoot zdist EVILBULLET
	
ends

state evilbumpcheck
	
	ifvarn sprite[THISACTOR].pal 10
	ifvarn sprite[THISACTOR].pal 14
	{
		cactor EVILDUKE
		getactor[THISACTOR].htmovflag spriteid
		ifvarn spriteid 0
		{
			// hit something
			addvar spriteid 16384
			ifvarl spriteid 16384 ifvarg spriteid -1
			ife actorvar[spriteid].monstatus 1
			{
				ai AIEVILJUMP
				move EVILJUMPVEL getv faceplayer
				break
			}
		}
			
		ifmove EVILRUNBACKVEL ai AIEVILSEEK
		else
		ifrnd 16 
			ai AIEVILJUMP
		else
		ifvarl weap MINIFIRECOUNT
		{
			ifrnd 128
			{
				ai AIEVILJUMP
				move EVILJUMPVEL getv randomangle
			}
			else
				ai AIEVILSEEK
			
		}
		else
		{
			ifai AIEVILSHOOT nullop else
			{
				move EVILRUNBACKVEL faceplayersmart
				action EVILRUNBACK
			}
		}
	}
	else
	{
		ifcount 20
		{
			ifactor EVILDUKECROUCH nullop else
			ifai AIEVILSEEK nullop else
			ai AIEVILSEEK
		}
	}
ends

state evildukecode
fall

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action EVILSTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
	}
	else break
}

ifai 0
{
	sizeat 22 20 // 44 40
	ife SKILL 5 ifrnd 64 spritepal 10
	cstat 257
	clipdist 32
	ai AIEVILSEEK
	ifspritepal 14 
	{ 
	  strength 70 setvar gold GOLD2 
	  sizeat 18 16 // 36 32 
	  clipdist 28 
	}
	else ifvarl sprite[THISACTOR].pal 10 setvar gold GOLD3
	else setvar gold GOLD4
	ifvarl sprite[THISACTOR].pal 10 setvar ammo 0
	else ifspritepal 14 setvar ammo 0 else
	setvar ammo 20 // grenades
	ifrnd 128 globalsound EPREPDIE else
	globalsound EPAYBACK
	ifspritepal 10 
	{ 
		strength 400 
		sizeat 30 24 // 60 48 
		clipdist 48 
		ifg SKILL 3 state randomaura
	}
	// pal 10 fat, extra health
	// pal 14 small and weak
}

ifspritepal 10 // fat speed adjustment
{
	ifvarg sprite[THISACTOR].xvel 160 setactor[THISACTOR].xvel 160
	ifvarl sprite[THISACTOR].xvel -128 setactor[THISACTOR].xvel -128
}

getactor[THISACTOR].z z
subvar z 8192
setactor[THISACTOR].z z
ifcansee setvar seevar YES else setvar seevar NO // seevar == cansee
addvar z 8192
setactor[THISACTOR].z z

ifvare seevar YES addvar initz 1 else subvar initz 1
ifvarg initz 10 setvar initz 10
ifvarl initz 0 setvar initz 0

ifvarg initz 0 setvar seevar YES // treat the player as visible until he has been out of view for a while


ifvare cursed 0 getplayer[THISACTOR].i target

ifvarn target -1
{
	getactor[target].x mx
	getactor[target].y my
	getactor[THISACTOR].x x
	getactor[THISACTOR].y y
	subvarvar mx x
	subvarvar my y
	getangle angvar mx my
	getactor[THISACTOR].ang tempb
	getincangle peractor1 angvar tempb
}
else setvar peractor1 1024

// ifai AIEVILSTRAFE setvar weap 0
ifp palive nullop else setvar weap 0
ifvarg weap 0 
{
	ifvarg peractor1 -160 ifvarl peractor1 160
	nullop
	else subvar weap 2
	ifvarl weap 0 setvar weap 0

	addvar countvar 1
	ifvarl weap MINISPEEDCOUNT 
	{
		soundonce MINISPINSLOW 
		stopactorsound THISACTOR MINISPINFAST
	}
	else
	{
		soundonce MINISPINFAST
		stopactorsound THISACTOR MINISPINSLOW
	}
	
	ifvarl burning 0 setvar weap 0 else
	{
		ifvarg weap MINISPEEDCOUNT
		{
			ifvarn target -1
			ifvarg countvar 1
				state evilfirebullet
		}
		else
		ifvarg weap MINIFIRECOUNT
		{
			ifvarn target -1
			ifvarg countvar 2
				state evilfirebullet
		}
	}
	
	ifvarg weap 52 setvar weap 52
}
// else
// {
	 // stopactorsound THISACTOR MINISPINSLOW
	 // stopactorsound THISACTOR MINISPINFAST
// }

ifvarg mtype 0 subvar mtype 1


state monsterai
ifactornotstayput state avoidcliffs

ifai AIEVILDYING { state evildyingstate break } else
ifai AIEVILCHARGE state evilchargestate else
ifai AIEVILSEEK state evilseekstate else
ifai AIEVILJUMP state eviljumpstate else
ifai AIEVILSTRAFE state evilstrafestate else
ifai AIEVILSHOOT state evilshootstate

addvar initx 1 // used as a counter
ifp palive
ifvare cursed 0
ifvarg initx 40
ifrnd 16
ifvare seevar YES
ifvarg peractor1 -64 ifvarl peractor1 64
ifvarg ammo 0
ifai AIEVILSTRAFE
{
	setvar initx 0
	eshoot GRENADEPROJ
	setthisprojectile[RETURN].trail SMALLSMOKE
	setthisprojectile[RETURN].extra 30
	sound GRENSHOOT
	subvar ammo 1
// 	ifai AIEVILSHOOT
// 	{
// 		ifactor EVILDUKE action EVILSHOOTING
// 		else action EVILCRAWLSHOOT
// 	}
}
else
ifvarg initx 40
ifrnd 16
ifvarg ammo 0
ifai AIEVILSTRAFE
ifvarn cursed 0
ifvarn target -1
{
	setvar initx 0
	eshoot GRENADEPROJ
	setthisprojectile[RETURN].trail SMALLSMOKE
	setthisprojectile[RETURN].extra 30
	sound GRENSHOOT
	subvar ammo 1
}

// the following checks an incoming projectile and initiates a dodge strafe if appropriate
ifai AIEVILJUMP nullop else
ifvarn target2 -1
ifvarn sprite[THISACTOR].pal 10
ifvarn sprite[THISACTOR].pal 14
{
	ifvarn sprite[target2].statnum 4 setvar target2 -1
	ifvarn target2 -1
	{
		ldist xydist2 THISACTOR target2
		ifvarl xydist2 6144
		{
			getactor[THISACTOR].x mx
			getactor[THISACTOR].y my
			getactor[target2].x x
			getactor[target2].y y
			subvarvar mx x
			subvarvar my y
			getangle angvar mx my
			getactor[target2].ang tempb
			getincangle tempc angvar tempb
			ifvarg tempc -256 ifvarl tempc 256 // the amount the projectile has to turn to face THISACTOR
			{
				setvar mtype 10
				getactor[target2].x mx
				getactor[target2].y my
				getactor[THISACTOR].x x
				getactor[THISACTOR].y y
				subvarvar mx x
				subvarvar my y
				getangle dodgeang mx my
				ifvarl tempc 0
				subvar dodgeang 512
				else 
				addvar dodgeang 512
				cactor EVILDUKE
				ifai AIEVILSTRAFE nullop else 
					ai AIEVILSTRAFE
			}
		}
	}
	
}

ifvarn sprite[THISACTOR].pal 10
ifvarn sprite[THISACTOR].pal 14
ifvare mtype 0
{
	setvar tempe 0
	headspritestat spriteid 4
	whilevarn spriteid -1
	{
		ifvarn sprite[spriteid].picnum EVILBULLET
		//ifvarvare sprite[spriteid].owner player[THISACTOR].i
		{
			dist xydist THISACTOR spriteid
			ifvarl xydist 5120
			{
				getactor[THISACTOR].x mx
				getactor[THISACTOR].y my
				getactor[spriteid].x x
				getactor[spriteid].y y
				subvarvar mx x
				subvarvar my y
				getangle angvar mx my
				getactor[spriteid].ang tempb
				getincangle tempc angvar tempb
				ifvarg tempc -128 ifvarl tempc 128
				{	
					getactor[spriteid].x mx
					getactor[spriteid].y my
					getactor[THISACTOR].x x
					getactor[THISACTOR].y y
					subvarvar mx x
					subvarvar my y
					getangle dodgeang mx my
					ifvarl tempc 0
					subvar dodgeang 512
					else 
					addvar dodgeang 512
					cactor EVILDUKE
					ifai AIEVILSTRAFE nullop else 
						ai AIEVILSTRAFE
					setvar tempe 1	
				}
			}
		}
		ifvare tempe 1 setvar spriteid -1 else
		nextspritestat spriteid spriteid
	}
}

ifai AIEVILJUMP nullop else
ifnotmoving
ifcount 8
	state evilbumpcheck




ifhitweapon
{
	ifvare seevar YES addvar weap 100
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	sound EPAIN
	ifdead
	{
		stopactorsound THISACTOR MINISPINSLOW
	    stopactorsound THISACTOR MINISPINFAST
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		sound EDYING
		addkills 1 add globalkills 1
		state specialdeath
		ifvarn droptile -1
		{
			ifspritepal 14 spawn BATTERYAMMO else
			ifspritepal 10 { spawn GRENADEAMMO spawn CHAINGUNSPRITE } else
			{ ifrnd 128 spawn GRENADEAMMO else spawn CHAINGUNSPRITE }
		}
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		cactor EVILDUKE
		ai AIEVILDYING
		
	   ifvare command 1
	   {
			action EVILDYING2
			ifmultiplayer nullop else ifvare powcount 0 ifvare powmode 0 
			ifl combo combomax
			{
				addvar combo 2
				ifg combo combomax set combo combomax
			}
			state aimjib
			ezshoot zdist EDHEADJIBPROJ
			state setjib
	   }
	}
}

ifmove STOPPED nullop else 
ifactor EVILDUKE state stepsounds

ifrnd 1 ifrnd 32
{
	ifrnd 128 soundonce EDIE
	else ifrnd 128
	soundonce EUGLY
	else
	soundonce EHURTS2BU
}

ifvarvare target player[THISACTOR].i setvar target -1


ends

eventloadactor EVILDUKE 
sizeat 22 20 // 44 40 
ifspritepal 10 sizeat 30 24 // 60 44
ifspritepal 14 sizeat 18 16 // 38 35

ifspritepal 17 
{
	action EVILSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}

enda

eventloadactor EVILDUKESTAYPUT 
sizeat 22 20 // 44 40 
ifspritepal 10 sizeat 30 24 // 60 44
ifspritepal 14 sizeat 18 16 // 38 35

ifspritepal 17 
{
	action EVILSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

useractor enemystayput EVILDUKESTAYPUT EVILDUKESTRENGTH
cactor EVILDUKE
enda

useractor enemy EVILDUKE EVILDUKESTRENGTH 

state evildukecode

enda

useractor enemy EVILDUKECROUCH EVILDUKESTRENGTH

state evildukecode

enda

move GWALKVELS 144
move GRUNVELS 224

action GOLEM_WALK 0 4 5 1 20
action GOLEM_RUN 0 4 5 1 14
action GOLEM_STRIKE 20 2 5 1 12
action GOLEM_STRIKING 25 1 5 1 12
action GOLEM_SHOOT 20 3 5 1 20
action GOLEM_SHOOTING 30 1 5 1 16
action GOLEM_STAND 35 1 5 1 20
action GOLEM_WAIT 20 1 5 1 14

ai AIG_CHARGE GOLEM_RUN GRUNVELS faceplayer
ai AIG_SEEK GOLEM_WALK GWALKVELS seekplayer faceplayerslow
ai AIG_WANDER GOLEM_WALK GWALKVELS geth
ai AIG_WAIT GOLEM_WAIT STOPPED geth
ai AIG_STRIKE GOLEM_STRIKE GRUNVELS geth
ai AIG_SHOOT GOLEM_SHOOT STOPPED faceplayersmart
ai AIG_RISE GOLEM_WAIT STOPPED geth
ai AIG_STAND GOLEM_STAND STOPPED geth

state Gstandstate

ifvarl fear 0 resetactioncount
ifactioncount 2 ai AIG_SEEK

ends

state Grisestate

setactor[THISACTOR].htextra -1
ifvare peractor1 0
{
	resetcount
	ifcansee
	ifpdistl 6144 setvar peractor1 1
	break
}
sizeto 36 36 // 44 44
strength GOLEMSTRENGTH

ifcount 40 
{ 
	ai AIG_SEEK 
	sizeat 36 36 // 44 44 
}

ends

state Gseekstate

ifvarn target -1
{
	ai AIG_CHARGE
	break
}

ifcansee ifvare cursed 0
{
	ifcanshoottarget ifpdistg 2048 ifpdistl 10240
	ifrnd 8 ifvarg ammo 50
	{
		ai AIG_SHOOT
		break
	}
	ifpdistl 4096
	{
		ai AIG_CHARGE
		break
	}
	ifrnd 1
	{
		ai AIG_CHARGE
		break
	}
	ifpdistg 8192 ifcount 130 ifrnd 2
	{
		ai AIG_WANDER
		move GWALKVELS randomangle
		break
	}
}
else
{
	ifcount 260 ifrnd 2
	{
		ai AIG_WANDER
		move GWALKVELS randomangle
	}
}
ends

state Gwanderstate 

ifaction GOLEM_WAIT
{
	ifactioncount 2
	{
		action GOLEM_WALK
		move GWALKVELS randomangle
	}
	break
} else
ifnotmoving ifcount 12
{
	action GOLEM_WAIT
	move STOPPED
	break
}

ifvarn cursed 0 { ai AIG_CHARGE break }

ifpdistg 10240
{
	ifcansee ifrnd 4
		ai AIG_SEEK	
}
else
{
	ifcansee ifrnd 8
	{
		ifrnd 96 ifpdistg 2048
		ifvarg ammo 50
			ai AIG_SHOOT
		else
			ai AIG_SEEK
		break
	}
}
ifpdistl 4096
	ai AIG_CHARGE

ends

state Gchargestate

ifvarn target -1
{
	ifvarl xydist2 1024 { ai AIG_STRIKE break }
	ifvarg actorvar[target].burning -1
	ifvarl xydist2 6144
	ifvarg ammo 50 { ai AIG_SHOOT break }
	ifnotmoving ifcount 8
	{
		ai AIG_WAIT
		action GOLEM_WAIT
		move STOPPED
	}
	break
}

ifpdistg 4608 { ai AIG_SEEK break }
ifp palive
ifcanshoottarget
ifvare cursed 0
{
	ifpdistl 1536
	{
		state facetarget
		ai AIG_STRIKE
		break 
	}
	else ifrnd 2 ifvarg ammo 50
	{
		ai AIG_SHOOT
		break
	}
}
ifcount 78 ai AIG_SEEK
ends

state GSHOOTstate

ifvarg fear 0 { ai AIG_WAIT break }

ifaction GOLEM_SHOOTING
{
	ifspritepal 10
	{
		ifcount 10
		{
			ifvare target -1 { ifvarn cursed 0 { ai AIG_SEEK break } setvarvar target player[THISACTOR].i }
			state calczdist
			ldist temp THISACTOR target
			ifvare temp 0 setvar temp 1
			mulvar zdist 644
			divvarvar zdist temp
			subvar zdist 1024
			ezshoot zdist LAVAROCK
			ifvarvare target player[THISACTOR].i setvar target -1
			setactor[RETURN].xvel 644
			setactor[RETURN].extra 25
			ai AIG_WAIT
		}
		break
	}
	ifspritepal 15
	{
			ifvare target -1 { ifvarn cursed 0 { ai AIG_SEEK break } setvarvar target player[THISACTOR].i }
			state calczdist
			ldist temp THISACTOR target
			ifvare temp 0 setvar temp 1
			mulvar zdist 644
			divvarvar zdist temp
			subvar zdist 1024
			ezshoot zdist ROCKPROJ
			ifvarvare target player[THISACTOR].i setvar target -1
			soundonce CLDWND
			subvar ammo 1
			ifrnd 32 { stopactorsound THISACTOR CLDWND ai AIG_WAIT }
			ifvarl ammo 1 {  stopactorsound THISACTOR CLDWND ai AIG_WAIT }
			break
	}
	ifactioncount 6
	{
		ai AIG_WAIT
		 stopactorsound THISACTOR CLDWND
		break
	}
	
	ifspritepal 4
	{
		espawn VACUUMPROJ
		setav[RETURN].myspawner THISACTOR
		soundonce RIFTATTACK
	}
	else
	{
		espawn COLDGUST
		setactorvar[RETURN].target target
		setav[RETURN].myspawner THISACTOR
		soundonce CLDWND
	}
	subvar ammo 1
	ifvarl ammo 1 {  stopactorsound THISACTOR CLDWND ai AIG_WAIT }
	break
}

ifactioncount 2
	action GOLEM_SHOOTING

ends

state Gstrikestate

ifpdistl 1024
{
	move STOPPED
	state facetarget
}

ifvare target -1
ifvarn cursed 0
{
	ai AIG_WAIT
	break
}

ifspritepal 14
{
	ifactioncount 1
	{
		ifaction GOLEM_STRIKING nullop else action GOLEM_STRIKING
		spawn POISONAURA
		ifvare target -1 // attacking player
		{
			ifpdistg 1280 ifcount 10
			{
			   ai AIG_WAIT
			   break
		    }
		    ifvare mtype 2 
		    {
			    addvar ppoison 78 
			    setactor[THISACTOR].htextra 50 setactor[THISACTOR].htpicnum BFGIMPACT
			    setactor[THISACTOR].htowner player[THISACTOR].i
			    break
		    }
			ifvare mtype 0 setvar xydist 16 else
			ifvare mtype 1 setvar xydist 8 else
			setvar xydist 4
			setplayer[THISACTOR].poszv 0
			setplayer[THISACTOR].posxv 0
			setplayer[THISACTOR].posyv 0
			getplayer[THISACTOR].posx mx
			getplayer[THISACTOR].posy my
			getactor[THISACTOR].x x
			getactor[THISACTOR].y y
			getplayer[THISACTOR].i spriteid
			subvarvar x mx
			ifvarg x 256 addvarvar mx xydist else
			ifvarl x -256 subvarvar mx xydist
			subvarvar y my
			ifvarg y 256 addvarvar my xydist else
			ifvarl y -256 subvarvar my xydist
			setactor[spriteid].x mx
			setactor[spriteid].y my
			setplayer[THISACTOR].posx mx
			setplayer[THISACTOR].posy my
			// setvar playerstuck 26
			soundonce SLIMEHOLD
			ifvare mtype 1 addvar ppoison 2 else
			addvar ppoison 3
		}
		else
		{
			ifvarg xydist2 1024 ifcount 10
			{
				ai AIG_WAIT
				break
			}
			ifvare mtype 2 
		    {
			    setactor[THISACTOR].htextra 50 setactor[THISACTOR].htpicnum BFGIMPACT
			    setactor[THISACTOR].htowner target
			    setactor[target].htextra 5
				setactor[target].htowner THISACTOR
				setactor[target].htpicnum KNEE
				setactor[target].htang sprite[THISACTOR].ang
			    break
		    }
			setactor[target].xvel 0
			soundonce SLIMEHOLD
			ifvarl sprite[target].htextra 1 
			{
				setactor[target].htextra 1
				setactor[target].htowner THISACTOR
				setactor[target].htpicnum KNEE
				setactor[target].htang sprite[THISACTOR].ang
				ifrnd 32
				{
					getactorvar[target].fear tempb
					subvar tempb 4
					addvarvar tempb mtype
					setactorvar[target].fear tempb
					espawn BIGSMOKE
					setactor[RETURN].pal 8
					setsprite RETURN sprite[target].x sprite[target].y sprite[RETURN].z
					// setactor[RETURN].x sprite[target].x
					// setactor[RETURN].y sprite[target].y
				}
			}
		}
	}
}
else
ifactioncount 2 
    ai AIG_WAIT
else
ifactioncount 1
{
	ifvare target -1
	ifcansee ifpdistl 1536 state knockplayer
	else
		state monsterbite
}
else
ifactioncount 0 
{
	soundonce SWORDSWING
}
ends

state Gwaitstate

ifcount 6
{
	ifvarn target -1
	{
		ifvarl xydist2 1024
		ai AIG_STRIKE
		else
		ai AIG_CHARGE
	}
	ifvarn cursed 0
	{
		ai AIG_SEEK
		break
	}
	ifpdistg 4608 { ai AIG_SEEK break }
	ifp palive
	ifpdistl 1536
	ifcanshoottarget 
	{ 
		state facetarget
		ai AIG_STRIKE 
		break 
	}
	ifpdistg 1536 ifpdistl 4096
	{
		ai AIG_CHARGE
		break
	}
	ifcount 26 ai AIG_SEEK
}
ends

eventloadactor GOLEM
sizeat 36 36 // 44 44
cstator 257
ifspritepal 10 cstat 32768
ifspritepal 17 
{
	action GOLEM_WAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

eventloadactor GOLEMSTAYPUT

sizeat 36 36 // 44 44
cstator 257
ifspritepal 10 cstat 32768
ifspritepal 17 
{
	action GOLEM_WAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}

enda

useractor enemystayput GOLEMSTAYPUT GOLEMSTRENGTH
cactor GOLEM
enda

useractor enemy GOLEM GOLEMSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action GOLEM_WAIT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
		ifvare lotag -66 break // held by wizard
		ifvare lotag -65 // dropped by wizard
		{
			ifvarg initz 0 iffloordistl 2
			ifvare sprite[THISACTOR].zvel 0
			{
				quake 52
				globalsound BIGTHUD
				debris SCRAP2 10
				debris SCRAP3 10
				debris SCRAP4 10
				debris SCRAP5 10
				strength GOLEMSTRENGTH
				setactor[THISACTOR].htextra -1
				spritepal 0
				action 0
				setvar lotag 0
			}
			getactor[THISACTOR].zvel initz
		}
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength GOLEMSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
	}
	else break
}

fall

ifaction 0
{
	// ifspritepal 10 setvar aura -64
	ifspritepal 14 
	{
		// cstator 2
		seta[].alpha 30
		seta[].blend 1
		ifvare mtype 2 
		{
			setvar gold GOLD2
			strength 40
			sizeat 16 16 // 20 20
		}
		else
		ifvare mtype 1 
		{
			strength 60 
			sizeat 24 24 // 32 32
		}
		else
		{
			strength 80
			sizeat 36 36 // 44 44
		}
		setvar ammo 0 
	}
	else
		sizeat 36 36 // 44 44
	
	cstat 257
	ifspritepal 10
	{
		// cstator 2
		seta[].blend 1
		seta[].alpha 20
		geta[].htflags temp orvar temp 4 seta[].htflags temp
		seta[].shade -100
		ifspawnedby KORAXPROJ ai AIG_SEEK
		else
		{
			sizeat 36 2 // 44 2
			ai AIG_RISE
		}
	} else
	ifspritepal 4
	{
		cstat 258
		ai AIG_SEEK
	}
	else
	ai AIG_SEEK
	ifspawnedby WIZARD nullop else
	ifvarn gold -1
	setvar gold GOLD4
	// sound GOLEMWAKE
}
ifvarn sprite[THISACTOR].pal 14
ifvarl ammo 200 ifrnd 8 addvar ammo 1

ifai AIG_RISE { state Grisestate break }

state monsterai
ifactornotstayput state avoidcliffs

ifspritepal 10
ifge burning 0
{
	espawn STICKYFIRE
	setactorvar[RETURN].monstatus GOLEM
	setactorvar[RETURN].myspawner THISACTOR
}

ifspritepal 4
	spawn GLINT


ifactioncount 2
{
	setvar temp 0
	ifaction GOLEM_WALK setvar temp 1
	ifaction GOLEM_RUN setvar temp 1
	ifvare temp 1
	{
		resetactioncount
		ifspritepal 14 sound JIBLAND2 else
		sound DMWALK
	}
}


ifai AIG_SEEK state Gseekstate else
ifai AIG_WANDER state Gwanderstate else
ifai AIG_CHARGE state Gchargestate else
ifai AIG_SHOOT state GSHOOTstate else
ifai AIG_STRIKE state Gstrikestate else
ifai AIG_WAIT state Gwaitstate else
ifai AIG_STAND state Gstandstate


ifhitweapon
{
	ifspritepal 14 state randsquishsound else
	soundonce GOLEMPAIN
	ifai AIG_WANDER ai AIG_SEEK
	ifdead 
	{
		ifvarn sprite[THISACTOR].pal 14
		{
			espawn SOUNDMAKER
			setactorvar[RETURN].peractor1 GOLEMDEATH
		}
		cstat 0
		addkills 1 add globalkills 1
		state specialdeath
		ifspritepal 0
		{
			espawn SOUNDMAKER
			randvar temp 2
			addvar temp 584
			setactorvar[RETURN].peractor1 temp
			spritepal 1
			lotsofglass 100
			espawn BIGSMOKE
			setactor[RETURN].pal 1
			cactor WATERFALL
			sizeat 160 160
			spawn BLUDSPLASH
			killit
		}
		ifspritepal 10
		{
			setvar countvar 0
			whilevarn countvar 12
			{
				randvar tempb 2047
				setactor[THISACTOR].ang tempb
				randvar temp 2048
				subvar temp 3072
				zshoot temp LAVAROCK
				addvar countvar 1
			}
			spritepal 2
			spawn EXPLOSION2
			spawn FIRESTARTER
			espawn SOUNDMAKER
			setactorvar[RETURN].peractor1 RPG_EXPLODE2
			killit
		}
		else
		ifspritepal 15
		{
			setvar countvar 0
			whilevarn countvar 9
			{
				randvar tempb 2047
				setactor[THISACTOR].ang tempb
				randvar temp 2048
				subvar temp 3072
				ezshoot temp ROCKPROJ
				randvar tempb 192
				setthisprojectile[RETURN].vel tempb
				setthisprojectile[RETURN].extra 1
				setactor[RETURN].xvel tempb
				setactor[RETURN].xrepeat 32
				setactor[RETURN].yrepeat 32
				addvar countvar 1
			}
			spritepal 2
			lotsofglass 40
			espawn BIGSMOKE
			setactor[RETURN].pal 15
			killit
		}
		else
		{
			ifspritepal 14
			{
				state randsquishsound
				state randsquishsound
				spritepal 6
				ife sector[].lotag 0
				spawn BLOODPOOL
				state standard_jibs
				ifvare character 0
				ifvare mtype 0 globalsound BAK2SWAMP
				addvar mtype 1
				ifvarl mtype 3
				{
					espawn BIGSMOKE
					setactor[RETURN].pal 8
					
					getactor[THISACTOR].x x
					getactor[THISACTOR].y y
					getactor[THISACTOR].z z
					setvarvar mx x
					ifvare mtype 0
					addvar mx 384 else
					ifvare mtype 1 addvar mx 256
					else addvar mx 192
					getactor[THISACTOR].ang angvar
					subvar angvar 512
					rotatepoint x y mx y angvar tempb my
					updatesectorz tempb my z mysector
					
					espawn GOLEM
					setactor[RETURN].pal 14
					setactorvar[RETURN].mtype mtype
					ifvarn mysector -1
						setsprite RETURN tempb my z
					espawn GOLEM
					setactor[RETURN].pal 14
					setactorvar[RETURN].mtype mtype
					addvar angvar 1024
					rotatepoint x y mx y angvar tempb my
					updatesectorz tempb my z mysector
					ifvarn mysector -1
						setsprite RETURN tempb my z
					ifvarvare sprite[THISACTOR].sectnum
					sprite[RETURN].sectnum
					{
						getactor[THISACTOR].htactorstayput temp
						setactor[RETURN].htactorstayput temp
					}
					
				}
				killit
				
			}
			state randsquishsound
			state standard_jibs
			killit
		}
	}
	else ifvarl fear 0 ai AIG_STAND
	
	ifspritepal 0 lotsofglass 2
// 	else
// 	ifwasweapon BURNING nullop else
// 	guts JIBS6 1

}
// else ifrnd 1 ifrnd 128 soundonce GOLEMWAKE
	

enda

action DRAGON_IDLE 0 1 5 1 1
action DRAGON_FLAPDOWN 0 4 5 1 9
action DRAGON_FLAPUP 15 4 5 -1 12
action DRAGON_SHOOT 20 1 5 1 12
action DRAGON_THINK 25 1 5 1 20
action DRAGON_FLINTCH 30 2 1 1 20
action DRAGON_DIE 30 6 1 1 16
action DRAGON_DEAD 35 1 1 1 10
action DRAGON_SWOOP 10 1 5 1 10

move DGFLYVELS 256 0
move DGFLYVELSEEK 192 0
move DGSWOOPVELS 480 0
move DGPRESWOOPVELS -84 0

ai DRAG_SEEK DRAGON_FLAPDOWN DGFLYVELS seekplayer faceplayerslow
ai DRAG_DYING DRAGON_DIE STOPPED faceplayer
ai DRAG_FLINTCH DRAGON_FLINTCH STOPPED geth
ai DRAG_SWOOP DRAGON_SWOOP DGSWOOPVELS geth
ai DRAG_PRESWOOP DRAGON_FLAPDOWN DGPRESWOOPVELS geth
ai DRAG_GRABBING DRAGON_FLAPDOWN DGFLYVELS geth

state dragondyingstate

ifcount 1
{
	resetcount
	fall
}

ifaction DRAGON_DIE ifactioncount 6
{
	setvar peractor1 0
	action DRAGON_DEAD
	cstat 0
}

ifaction DRAGON_DEAD
{
	strength 0
	ifhitweapon ifwasweapon RADIUSEXPLOSION
	{
		state standard_jibs
		state randsquishsound
		ifspritepal 2 state standard_jibs
		killit
	}
}

ends

state dragonflintchstate

setvar peractor1 0
setvar ammo 0
ifvarl fear 0 resetactioncount
ifactioncount 2 ai DRAG_SEEK

ends

state moveztotarget

ifvare target -1 getplayer[THISACTOR].i target

getactor[target].z mz
subvar mz 8192
getactor[THISACTOR].z z
subvarvar mz z
shiftvarr mz 4
movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
ifvarg mz 0
{
	getactor[THISACTOR].z digz
	ifvarvare z digz // failure to move down
	fall
	
	ifai DRAG_SWOOP ifvare peractor1 0 ifvarg mz 2048
	{
		ldist temp THISACTOR target
		ifvarl temp 2048 movesprite THISACTOR 0 0 1536 CLIPMASK0 RETURN
	}
}
else ifai DRAG_SWOOP ifaction DRAGON_SWOOP action DRAGON_FLAPUP

ifvarvare target player[THISACTOR].i setvar target -1

ends

state dragongrabbingstate

ifaction DRAGON_THINK 
{
	ifactioncount 3
	{
		ai DRAG_SEEK
		setvar ammo 0
		break
	}
} else
{
	ifvare target -1
	{
		setplayer[THISACTOR].transporter_hold 6
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		getactor[THISACTOR].z z
		addvar z 4096
		updatesectorz x y z temp
		ifvarn temp -1 
		{
			getplayer[THISACTOR].i spriteid
			changespritesect spriteid temp
			setplayer[THISACTOR].cursectnum temp
			setsprite spriteid x y z
			setplayer[THISACTOR].posx x
			setplayer[THISACTOR].posy y
			setplayer[THISACTOR].posz z
			setp[].falling_counter 0
		}
		else action DRAGON_THINK
	}
	else
	{
		getactorvar[target].monstatus temp
		ifvare temp 2
		{
			ai DRAG_SEEK
			break
		}
		ifrnd 16
		{
			setactor[target].htextra 1
			setactor[target].htpicnum SHOTSPARK1
			setactor[target].htowner THISACTOR
		}
		setactor[target].xvel 0
		setactor[target].zvel -256
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		getactor[THISACTOR].z z
		addvar z 12280
		setsprite target x y z
	}
	
	movesprite THISACTOR 0 0 -2048 CLIPMASK0 RETURN
	ifvarn RETURN 0 action DRAGON_THINK 
	else ifcount 260 action DRAGON_THINK 
}

ends

state dragonfirestate

geta[].z initz
set z initz
geta[].yrepeat y
mul y 128
sub z y
seta[].z z

ifvare target -1 { ifvarn cursed 0 { ai DRAG_SEEK break } getplayer[THISACTOR].i target }
state calczdist
ldist xydist THISACTOR target
ifvare xydist 0 setvar xydist 1
//shiftvarl zdist 9
mulvar zdist 768
divvarvar zdist xydist
sound DEMSPITSND


randvar tempb 512
ifrnd 128 addvarvar zdist tempb else subvarvar zdist tempb
setvarvar x xydist
subvar x 2048
ifvarl x 1 setvar x 1
shiftvarr x 4
subvarvar zdist x

ezshoot zdist FTPROJ
//ifspritepal 2 
//{
	randvar tempb 16
	getactor[RETURN].ang angvar
	ifrnd 128 addvarvar angvar tempb else subvarvar angvar tempb
	setactor[RETURN].ang angvar
//}

ezshoot zdist FTPROJ
setactorvar[RETURN].peractor1 1
//ifspritepal 2
	setactor[RETURN].ang angvar

ifvarvare target player[THISACTOR].i setvar target -1

seta[].z initz
	
ends

state dragonseekstate

ifmove DGFLYVELS state gradualturn

ifnotmoving movesprite THISACTOR 0 0 -2560 CLIPMASK0 RETURN else
state moveztotarget

getactor[THISACTOR].ang angvar
ifbulletnear setvar countvar 8
ifvarg countvar 0
{
	subvar countvar 1
	setvarvar tempb angvar
	ifvare weap 0 addvar tempb 512 else subvar tempb 512
	cos xvel tempb
    sin yvel tempb
    shiftvarr xvel 7
    shiftvarr yvel 7
	movesprite THISACTOR xvel yvel -2560 CLIPMASK0 RETURN
	
	ifvare countvar 0 ifrnd 128 
	{
		setvar temp 0
		ifcansee ifcanshoottarget ifvare cursed 0 setvar temp 1
		ifvarn target -1 setvar temp 1
		ifvare temp 1
		{
			ifspritepal 20
			{
				soundonce CLDWND
				espawn COLDGUST
				setav[RETURN].myspawner THISACTOR
				setactorvar[RETURN].target target
				espawn COLDGUST
				setactorvar[RETURN].target target
				setav[RETURN].myspawner THISACTOR
			}
			else
			ifspritepal 1
			{
				soundonce CLDWND
				espawn COLDGUST
				setactorvar[RETURN].target target
				setav[RETURN].myspawner THISACTOR
				espawn COLDGUST
				setactorvar[RETURN].target target
				setav[RETURN].myspawner THISACTOR
			}
			else
			ifspritepal 7 
			{
				ifvarn target -1 state wraithprojathero else
				shoot WRAITHPROJ
			}
			else
			{
				state dragonfirestate
				sound DRAGSHOOT
			}
		}
	}
}
else { ifrnd 128 setvar weap 0 else setvar weap 1 }
setactor[THISACTOR].ang angvar

ifcount 6
{
	ifcansee 
	{
		ifcanshoottarget ifpdistl 8192 ifvare cursed 0 { ai DRAG_PRESWOOP break }
		ifvarn target -1 ifvarl xydist2 8192 { ai DRAG_PRESWOOP break }
		ifmove DGFLYVELSEEK move DGFLYVELS geth
	} else
	{
		ifmove DGFLYVELS move DGFLYVELSEEK seekplayer faceplayerslow
	}
}

ends

state dragonshootstate

	ife target -1 set spriteid player[].i else set spriteid target
	geta[spriteid].x mx
	geta[spriteid].y my
	sub mx sprite[].x
	sub my sprite[].y
	getangle angvar mx my
	setactor[].ang angvar

	ifspritepal 1
	{
		soundonce CLDWND
		espawn COLDGUST
		setactorvar[RETURN].target target
		setav[RETURN].myspawner THISACTOR
	}
	else
	ifspritepal 20
	{
		soundonce CLDWND
		espawn COLDGUST
		setactorvar[RETURN].target target
		setav[RETURN].myspawner THISACTOR
	}
	else
	ifspritepal 7 
	{
		ifvarn target -1 state wraithprojathero else
		shoot WRAITHPROJ
		sound DRAGSHOOT
	}
	else
	{
		state dragonfirestate
		sound DRAGSHOOT
	}
	// action DRAGON_SHOOT 
	ifspritepal 2
	{
		state dragonfirestate
//		state dragonfirestate
//		state dragonfirestate
	}

ends

state preswoopstate

state gradualturn

// ifaction DRAGON_THINK 
// {
// 	ifactioncount 3 ai DRAG_SWOOP
// 	break
// }

setvar temp 0
ifcanshoottarget
ifrnd 16
setvar temp 1
ifspritepal 1 setvar temp 1
ifspritepal 20 setvar temp 1
ifvare temp 1
{
	ifaction DRAGON_SHOOT nullop else action DRAGON_SHOOT
	state dragonshootstate
}


ifaction DRAGON_SHOOT 
	ifactioncount 3 action DRAGON_FLAPDOWN

getactor[THISACTOR].sectnum mysector
ifvare mysector -1
{
	ifcansee ai DRAG_SWOOP //action DRAGON_THINK
	else ai DRAG_SEEK
	break
}

getactor[THISACTOR].x x
getactor[THISACTOR].y y
getactor[THISACTOR].z z
getceilzofslope mysector x y tempb
subvarvar z tempb

ifvarl z 16384
{
		ifcansee ai DRAG_SWOOP //action DRAGON_THINK
		else ai DRAG_SEEK
		ifcanshoottarget
		state dragonshootstate
}
else
{
	ifvare target -1 getplayer[THISACTOR].i target
	getactor[target].z mz
	ifvarvare target player[THISACTOR].i setvar target -1
	subvar mz 8192
	getactor[THISACTOR].z z
	subvarvar mz z
	ifvarg mz -65536
	{
		getactor[THISACTOR].z z
		movesprite THISACTOR 0 0 -1536 CLIPMASK0 RETURN
		getactor[THISACTOR].z digz
		ifvarvare z digz // failure to move up
		{
			ifceilingdistl 32 nullop else
			{
				subvar z 512
				setactor[THISACTOR].z z
			}
		}
	}
	else 
	{
		ifcansee ai DRAG_SWOOP //action DRAGON_THINK
		else
		ifvarn target -1 ai DRAG_SWOOP
		else ai DRAG_SEEK
		ifcanshoottarget
		state dragonshootstate
	}
}

ifcount 104
{
	ifcansee ai DRAG_SWOOP //action DRAGON_THINK
	else ai DRAG_SEEK
	ifcanshoottarget
	state dragonshootstate
}

ends

state swoopstate

ifvare peractor1 0
state gradualturn

setvar temp 0
ifpdistl 2048 ifcansee setvar temp 1
ifvarn target -1 ifvarl xydist2 2048 setvar temp 1
ifvare temp 1
{
	ifvare peractor1 0 resetcount
	setvar peractor1 1
	// state monsterbite
	ifvare target -1
	{
		ifvarvarn swordtarg THISACTOR 
		{
		ifvarn cursed 0 { ai DRAG_SEEK break }
		soundonce AXEHIT
		getplayer[THISACTOR].i spriteid
		getactorvar[spriteid].bleeding tempb
		addvar tempb 1
		setactorvar[spriteid].bleeding tempb
		setvar tempd -2
		state directdamage
		// addphealth -2
		palfrom 36 48
		state dukepainsounds
		}
	}
	else
	{
		setactor[target].htextra 2
		setactor[target].htpicnum SHOTSPARK1
		setactor[target].htowner THISACTOR
	}
	
	
	ifspritepal 2 
	{
		setvar temp 0
		ifvarn target -1 ifvarl xydist2 1792 setvar temp 1
		ifpdistl 1792 setvar temp 1
		ifvare temp 1
		{
			setvar ammo 3 // ignore visibility to target
			ifvare target -1 getplayer[THISACTOR].transporter_hold temp 
			ifvare character 1 sound BARBPAIN2 else
			sound DUKE_GRUNT
			ifvare target -1
			{
				ifvarvarn swordtarg THISACTOR 
				{
					sound HITFLESH2
					palfrom 40 48
					setvar tempd -10
					state directdamage
					// addphealth -10
					ifvarn swordattk 0 break // can't grab
					ife invulntime 0
					ifvarn temp 0 { addphealth -20 palfrom 48 48 }
				}
			} else
			{
				setactor[target].htextra 20
				setactor[target].htpicnum RADIUSEXPLOSION
				setactor[target].htowner THISACTOR
			}
			ai DRAG_GRABBING
			setvar peractor1 0
			break
		}
	}
	movesprite THISACTOR 0 0 -1536 CLIPMASK0 RETURN
	ifaction DRAGON_SWOOP action DRAGON_FLAPUP
} else
state moveztotarget

ifvare peractor1 1 ifcount 78 ai DRAG_SEEK
else 	
	ifcount 130 ai DRAG_SEEK

ifai DRAG_SEEK setvar peractor1 0

ends

eventloadactor DRAGON
ifspritepal 7 sizeat 36 30 else
sizeat 48 48
ifspritepal 2 { sizeat 96 96 }
ifspritepal 17 
{
	action DRAGON_IDLE
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

useractor enemy DRAGON DRAGONSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action DRAGON_IDLE
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength DRAGONSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
	}
	else break
}

ifaction 0
{
	set ammo 0
	sizeat 48 48 // 64 64
	getactor[THISACTOR].htflags temp
	orvar temp 136331264
	setactor[THISACTOR].htflags temp
	
	ifspritepal 2 
	{
		sizeat 96 96 
		addstrength DRAGONSTRENGTH
		addstrength DRAGONSTRENGTH
		ifvarn gold -1
		setvar gold GOLD7
		ifvarand monstflags 2
		{
			addstrength DRAGONSTRENGTH
			addstrength DRAGONSTRENGTH
			addstrength DRAGONSTRENGTH
			sizeat 96 96
		}
	}
	else ifvarn gold -1 setvar gold GOLD5
	ifspritepal 7
	{
		sizeat 36 30
		strength ETTINSTRENGTH
		setvar gold GOLD4
	}
	cstator 257
	ai DRAG_SEEK
}

state monsterai


ifn sprite[].sectnum -1
{
	ifvarand sector[].ceilingstat 1 nullop else
	{
		getceilzofslope sprite[].sectnum sprite[].x sprite[].y tempb
		geta[].z z
		add z 4096
		ifle z tempb
		{
			add tempb 8192
			seta[].z tempb
		}
	}
}
else al THISACTOR

ifai DRAG_DYING { state dragondyingstate break }
ifai DRAG_FLINTCH state dragonflintchstate else
ifai DRAG_SEEK state dragonseekstate else
ifai DRAG_PRESWOOP state preswoopstate else
ifai DRAG_SWOOP state swoopstate else
ifai DRAG_GRABBING state dragongrabbingstate
ifactioncount 3
{
	ifaction DRAGON_FLAPDOWN action DRAGON_FLAPUP else
	ifaction DRAGON_FLAPUP 
	{
		sound WINGSND
		action DRAGON_FLAPDOWN
	}
}
ifaction DRAGON_FLAPUP state motionblur else
ifaction DRAGON_FLAPDOWN state motionblur

ifhitweapon
{
	ifwasweapon SHOTSPARK1 
	state random_wall_jibs
	//sound DEMONPAIN
	ifdead 
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		sound DRAGDYING
		//guts JIBS2 1
		//guts JIBS3 2
		//guts JIBS4 1
		guts JIBS6 3
		
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ3
		state setjib
		state aimjib
		ezshoot zdist JIBSPROJ4
		state setjib
		
		addkills 1 add globalkills 1
		state specialdeath
		ai DRAG_DYING
		cstat 0
	}
	else 
	{
		ifvarl fear 0 ai DRAG_FLINTCH else
		ifvarg bleeding 100 ai DRAG_FLINTCH else
		ifrnd 24 ifvarn sprite[THISACTOR].pal 2 ai DRAG_FLINTCH
		else ifai DRAG_SEEK ai DRAG_PRESWOOP
		sound DRAGPAIN
	}
}
enda

eventloadactor DRACARRY
getactor[THISACTOR].lotag lotag // activation tag that makes it appear and start flying
setactor[THISACTOR].lotag 0
getactor[THISACTOR].hitag hitag // hitag of the path it will follow
setactor[THISACTOR].hitag 0
getactor[THISACTOR].extra monstflags
getactor[THISACTOR].yvel activator
getactor[THISACTOR].zvel group

ifvare monstflags -1 setvar monstflags 0

ifspritepal 7 sizeat 36 30 else
sizeat 48 48
ifspritepal 2 { sizeat 96 96 }

cstat 32768
// it will grab the first monster it finds in its sector
// the best way is to make a respawn with the same tag as the DRACARRY and put it in the same spot
enda

action DRAC_FLAPDOWN -1 4 5 1 9
action DRAC_FLAPUP 14 4 5 -1 12
move DRACVELS 320

ai AIDRAC DRAC_FLAPDOWN DRACVELS geth

state cactordragon
	cactor DRAGON
	setvar monstatus 0
	setvar monstflags 0
	setvar myspawner -1
	setvar hitag 0
	setvar gold GOLD5
	ifhitweapon { }
	
	strength DRAGONSTRENGTH
	ifspritepal 2 
	{
		sizeat 96 96 
		addstrength DRAGONSTRENGTH
		addstrength DRAGONSTRENGTH
		ifvarn gold -1
		setvar gold GOLD7
	}
	else ifvarn gold -1 setvar gold GOLD5
	ifspritepal 7
	{
		sizeat 36 30
		strength ETTINSTRENGTH
		setvar gold GOLD4
	}
	ai DRAG_SEEK
ends

state nextmonpath

	headspritestat spriteid 1
	whilevarn spriteid -1
	{
		ife sprite[spriteid].picnum MONPATH
		ifvarvare actorvar[spriteid].hitag hitag
		ifvarvare actorvar[spriteid].lotag peractor1
			set target spriteid
			
		nextspritestat spriteid spriteid
		ifn target -1 set spriteid -1
	}
ends

useractor enemy DRACARRY DRAGONSTRENGTH

ifvarn lotag 0 break
ifai 0
{
	cstat 257
	ai AIDRAC
	setvar monstatus 1
	geta[].htflags temp
	orvar temp 136331264
	seta[].htflags temp
}

ifvarn myspawner -1
{
	setactor[myspawner].xvel 0
	setactor[myspawner].zvel 0
	getactor[THISACTOR].z z
	addvar z 8192
	setsprite myspawner sprite[THISACTOR].x sprite[THISACTOR].y z
	setactor[myspawner].ang sprite[THISACTOR].ang
	
	ifvare sprite[myspawner].statnum 1024 setvar myspawner -1
	ifvarvarn sprite[myspawner].picnum mtype setvar myspawner -1
	ifvarn actorvar[myspawner].monstatus 1 setvar myspawner -1
}

ifactioncount 3
{
	ifaction DRAC_FLAPDOWN action DRAC_FLAPUP else
	ifaction DRAC_FLAPUP 
	{
		sound WINGSND
		action DRAC_FLAPDOWN
	}
}

state playertargetting

ifvare target -1 // looking for first waypoint
{
	
	state nextmonpath
	ifvare target -1
	{
		state cactordragon
		break
	}
}
else
{
	getactor[target].x mx
	getactor[target].y my
	sub mx sprite[].x
	sub my sprite[].y
	getangle angvar mx my
	setactor[THISACTOR].ang angvar
	getactor[target].z mz // add mz 8192
	getactor[THISACTOR].z z
	subvarvar mz z
	shiftvarl mz 8
	ldist xydist2 THISACTOR target
	divvarvar mz xydist2
	mulvar mz 5
	shiftvarr mz 2
	addvarvar z mz
	setactor[THISACTOR].z z
	
	updatesectorz sprite[THISACTOR].x sprite[THISACTOR].y sprite[THISACTOR].z mysector
	ifvarn mysector -1 changespritesect THISACTOR mysector
	
	dist xydist THISACTOR target
	ifvarl xydist 384
	{
		ifvare sprite[target].pal 6
		{
			ifvare weap 1 ifvarn myspawner -1 setactor[myspawner].htactorstayput sprite[myspawner].sectnum
			setvar myspawner -1 // drop off point
		}
		else
		ifvare sprite[target].pal 2
		{
			setvar target -1
			state cactordragon
			break
		}
		else
		ifvare sprite[target].pal 14 // acquire new cargo
		ifvare myspawner -1
		{
			setvar monstatus 0
			headspritesect spriteid sprite[THISACTOR].sectnum
			whilevarn spriteid -1
			{
				ifvare actorvar[spriteid].monstatus 1
				{
					setvarvar myspawner spriteid
					getactor[myspawner].picnum mtype
					ifvarn sprite[myspawner].htactorstayput -1 
					{
						setvar weap 1
						setactor[myspawner].htactorstayput -1
					}
				}
				nextspritesect spriteid spriteid
				ifvarn myspawner -1 setvar spriteid -1
			}
			setvar monstatus 1
		}
		else
		ifvare sprite[target].pal 4
		killit
		// search for next waypoint
		ifvare sprite[target].pal 23 // loop
		setvar peractor1 0 else addvar peractor1 1
		setvar target -1
		state nextmonpath
		
	}
}

addvar countvar 1
ifvarg countvar 25
{
	espawn SHOOTME
	setactorvar[RETURN].myspawner THISACTOR
	setvar countvar 0
}

ifhitweapon
{
	ifstrength 100
	{
		setvar target -1
		state cactordragon
	}
}


state motionblur

enda

action AWRAITHWALK    0   4   5   1   15
action AWRAITHSTAND     0   1   5   1   15
action AWRAITHHIT    35   1   5   1   10
action AWRAITHSHOOT    20   3   5   1   16
action AWRAITHDYING    40   10   1   1   17
action AWRAITHDEAD     49   1   1   1  1
action AWRAITHFROZEN    0   1   5
action AWRAITHWAIT		0	1	5	1	1

move WRAITHWALKVELS 128 -30
move WRAITHUPVELS 96 -70
// move WRAITHSTOPPED 0 -30
move WRAITHINWATER 96 24

move WRAITHFAST 320 -30

ai AIWRAITHGETENEMY AWRAITHWALK WRAITHWALKVELS seekplayer faceplayerslow
ai AIWRAITHSHOOTENEMY AWRAITHSHOOT WRAITHSTOPPED faceplayer
ai AIWRAITHHIT AWRAITHHIT WRAITHSTOPPED faceplayer
ai AIWRAITHDYING AWRAITHDYING WRAITHSTOPPED faceplayer

state spawnwraiths

getactor[THISACTOR].ang angvar
espawn WRAITH
setactorvar[RETURN].target2 THISACTOR
setav[RETURN].gold -1
setactorvar[RETURN].peractor1 512
addvar angvar 512
setactorvar[RETURN].mtype angvar
espawn WRAITH
setactorvar[RETURN].target2 THISACTOR
setav[RETURN].gold -1
setactorvar[RETURN].peractor1 -512
subvar angvar 1024
setactorvar[RETURN].mtype angvar

ends

state spawnpups

espawn HELLHOUND
seta[RETURN].pal 120
setav[RETURN].gold -1

getactor[].ang saveit
sub saveit 512
geta[].x x, add x 1024
rotatepoint sprite[].x sprite[].y x sprite[].y saveit mx my

setsprite RETURN mx my sprite[].z
espawn TRANSPORTERSTAR
setsprite RETURN mx my sprite[].z

espawn HELLHOUND
seta[RETURN].pal 120
addvar saveit 1024

rotatepoint sprite[].x sprite[].y x sprite[].y saveit mx my

setsprite RETURN mx my sprite[].z
espawn TRANSPORTERSTAR
setsprite RETURN mx my sprite[].z

ends

state wraithgetenemystate

ifspritepal 20
{
	ifmove WRAITHWALKVELS
	move WRAITHFAST seekplayer faceplayerslow
	spawn FRAMEEFFECT1
}

  // ifvare fear 0
  // ifvarn target -1
  // {
	  // ai AIWRAITHSHOOTENEMY
	  // break
  // }

  ifcansee set seevar YES
  else set seevar NO
  ife sprite[].extra inithp ifg xydist MAXVISRANGE setvar seevar NO
  
  ifvare fear 0
  {
	  ifvarn target -1
	  {
		    ifspritepal 24
			{
				ifactioncount 8
				{
				   ifrnd 48
				   {
					  ai AIWRAITHSHOOTENEMY
			          break
			       } 
		       }
		       else
		         ifvarl xydist2 2048
		           ai AIWRAITHSHOOTENEMY
				break
			}
		    ifactioncount 16
		    {
		      ifrnd 48
		      {
		        ai AIWRAITHSHOOTENEMY
		        break
		      }
		    }
		     else
		       ifvarl xydist2 2048
		         ai AIWRAITHSHOOTENEMY
	  }
	  else
	  ife seevar YES // ifcansee
	  ifp palive
	  {
		ifspritepal 24
		{
			ifactioncount 16
			{
				ifrnd 48
				  ifcanshoottarget
			   {
				  ai AIWRAITHSHOOTENEMY
		          break
		       } 
	       }
	       else
	         ifpdistl 2048
	           ai AIWRAITHSHOOTENEMY
			break
		}
	    ifactioncount 32
	    {
		  ifvare cursed 0
	      ifrnd 48
	        ifcanshoottarget
	      {
	        ai AIWRAITHSHOOTENEMY
	        break
	      }
	    }
	     else
	       ifpdistl 2048
	       ifvare cursed 0
	         ai AIWRAITHSHOOTENEMY
	  }
  }
  
  ifp phigher nullop else
  ifmove WRAITHUPVELS ifcount 78
  move WRAITHWALKVELS seekplayer faceplayerslow
  
  ifvarn target -1
  {
	getactor[THISACTOR].z z
	getactor[target].z mz
	subvarvar mz z
	shiftvarr mz 3
	movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
  }
  
ends

state wraithshootenemystate

ifvarg fear 0 { ai AIWRAITHGETENEMY break }
ifvarn cursed 0 ifvare target -1 { ai AIWRAITHGETENEMY break }

  ifactioncount 3
  {
    ai AIWRAITHGETENEMY
    ifvare countvar 0 setvar countvar 1
    else setvar countvar 0
  }
  else
    ifactioncount 2
    {
	  ifmultiplayer 
      	setvar countvar 1
      	
	  ifspritepal 24 ifvare countvar 0
	  {
		  getactor[THISACTOR].z z
		  subvar z 8192
		  setactor[THISACTOR].z z
		  espawn LIGHTNINGBOLT
		  addvar z 8192
		  setactor[THISACTOR].z z
		  randvar temp 64
		  ifrnd 128 mulvar temp -1
		  getactor[THISACTOR].ang angvar
		  addvarvar angvar temp
		  setactor[RETURN].ang angvar
		  break
	  }
	  ifcount 10
	  {
		  resetcount
		  soundonce WRTHATTK
		  ifspritepal 24	
		  {
			  eshoot NOVAPROJ
			  setactorvar[RETURN].mtype 1
		  }
		  else
		  // ifvare weap 1
		  ifspritepal 20
		  {
			  geta[].z initz
			  sub initz 8192
			  seta[].z initz
			  espawn RPG_GUIDED
			  setactorvar[RETURN].myspawner THISACTOR
			  setactor[RETURN].pal 1
			  ifvarn target -1 setactorvar[RETURN].target target
			  else ife cursed 0 setav[RETURN].target player[].i
			  sound RPG_SHOOT
			  add initz 8192
			  seta[].z initz
		  }
		  else
		  {
			ifvarn target -1 state wraithprojathero else
	      	shoot WRAITHPROJ
      	  }
      }
    }
    
      
ends

state checkwraithhitstate

    ifdead
    {
	  cstat 0
	  setvar command 0
	  seta[].mdflags 0
	  setvar monstatus 2
	  ifvarn target2 -1
	  setvar target2 -1
      ifwasweapon FREEZEBLAST
		   ifdead spawn BIGSMOKE
		   
      addkills 1 add globalkills 1
	  state specialdeath
      
        state rf
        ai AIWRAITHDYING
      
      sound WRTHDEATH
    }
    else
    {
      soundonce WRTHPAIN
      ifspritepal 24
      {
	      ifwasweapon SHOTSPARK1 nullop else
	      ifwasweapon ARROWPROJ nullop else
	      ifrnd 32 ai AIWRAITHHIT
	      ifstrength 1200 { spritepal 20 setvar weap 1 }
      }
      else
      ifwasweapon ARROWPROJ nullop else
      ifrnd 96
        ai AIWRAITHHIT
      ifvarl fear 0 ai AIWRAITHHIT
    }
  
  ifwasweapon BURNING nullop else
		state random_wall_jibs
ends


state wraithdyingstate
  ifaction AWRAITHDEAD
  {
	  fall
	  strength 0
	  ifhitweapon
	  {
		  ifwasweapon RADIUSEXPLOSION
		  {
			  spawn BIGSMOKE
			  killit
		  }
		  ifwasweapon RPG
		  {
			  spawn BIGSMOKE
			  killit
		  }
	  }
	  break
  }
  ifactioncount 10
  {
    move WRAITHSTOPPED
    action AWRAITHDEAD
    // setvar command 65536
    // setactor[THISACTOR].mdflags 16
    espawn FLIES
	setactorvar[RETURN].myspawner THISACTOR
    break
  }
ends

eventloadactor WRAITH
ifspritepal 24 sizeat 68 68 else
sizeat 40 40
ifspritepal 17 
{
	action AWRAITHWAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

eventloadactor WRAITHSTAYPUT
ifspritepal 24 sizeat 68 68 else
sizeat 40 40
ifspritepal 17 
{
	action AWRAITHWAIT
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
enda

useractor enemystayput WRAITHSTAYPUT WRAITHSTRENGTH
  setvar gold 35
  sizeat 40 40
  cactor WRAITH
enda

useractor enemy WRAITH WRAITHSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action AWRAITHWAIT
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
		ifvare lotag -66 break // held by wizard
		ifvare lotag -65 // dropped by wizard
		{
			ifvarg initz 0 iffloordistl 2
			ifvare sprite[THISACTOR].zvel 0
			{
				quake 52
				globalsound BIGTHUD
				debris SCRAP2 10
				debris SCRAP3 10
				debris SCRAP4 10
				debris SCRAP5 10
				strength WRAITHSTRENGTH
				setactor[THISACTOR].htextra -1
				spritepal 0
				action 0
				setvar lotag 0
			}
			getactor[THISACTOR].zvel initz
		}
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength WRAITHSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
		action 0
	}
	else break
}

  ifai 0
  {
	ifvarn target2 -1
	{
		sound WIZPORT
		espawn BIGSMOKE
		setactor[RETURN].pal 2
	}
	else ifg SKILL 3 state randomaura
	ifrnd 128 soundonce WRTHROAM1 else soundonce WRTHROAM2
	sizeat 40 40
	cstator 257
	setactor[THISACTOR].mdflags 16
    setvar command 32768 // float flag
    ai AIWRAITHGETENEMY
    ifvare myspawner -1
	ifvarn gold -1
    setvar gold 35
	
    ifspritepal 24
    {
	    ai AIWRAITHGETENEMY
	    addstrength 3500
	    ifn gold -1 addvar gold GOLD6
	    sizeat 68 68
	    state spawnwraiths
    }
  }

state monsterai

ifai AIWRAITHDYING
{
	state wraithdyingstate
	break
}
      
  ifspritepal 24
  {
	  ifvarg sprite[THISACTOR].extra 0
	  ifp pfacing ifpdistl 16384 ifvarvarn bossid THISACTOR
	  {
		   setvarvar bossid THISACTOR
		   starttrackslot 1 1
		   set music2 1
	  }
  } else
  ifsquished
  {
	  setvar target2 -1
	  addkills 1 add globalkills 1
	  state specialdeath
      
      state rf
      ai AIWRAITHDYING
      sound WRTHDEATH
  }
  ifvarn target2 -1
  {
	  cstat 256
	  geta[].htflags temp orvar temp 2048 seta[].htflags temp // no clip
	  setvar temp 0
	  ifvarn sprite[target2].statnum 1 setvar temp 1
	  // ifvarn sprite[target2].picnum WRAITH setvar temp 1
	  ifvarn actorvar[target2].monstatus 1 setvar temp 1
	  ifvare temp 1
	  {
		  setvar target2 -1
		  ai AIWRAITHDYING
		  sound WRTHDEATH
		  addkills 1 add globalkills 1
		  state specialdeath
      	  
		  break
	  }
	  getactor[target2].x mx
	  getactor[target2].y my
	  getactor[target2].z mz
	  subvar mz 5120
	  getactor[target2].ang angvar
	  setactor[THISACTOR].ang angvar
	  setvarvar temp mx
	  addvar temp 1536
	  addvarvar angvar peractor1
	  getincangle tempc angvar mtype
	  ifvarg tempc 16 setvar tempc 16
	  ifvarl tempc -16 setvar tempc -16

	  addvarvar mtype tempc
	  rotatepoint mx my temp my mtype x y
	  updatesectorz x y mz tempb
	  ifvarn tempb -1
	  {
		  changespritesect THISACTOR tempb
		  setsprite THISACTOR x y mz
	  }
	  setactorvar[target2].mtype 200
	  ifmove STOPPED nullop else move STOPPED faceplayersmart
	  
  }
  else fall


  
  ifspritepal 24
  ifvarg mtype 0
  {
	  subvar mtype 1
	  ifvare mtype 0 state spawnwraiths
  }
  
  ifvare weap 1 // deflects RPGs
  // ifspritepal 20
  {
	  findnearspritez RPG 3072 24576 spriteid
	  ifvarn spriteid -1
	  {
		  getactor[spriteid].htpicnum picnum
		  ifvare picnum APLAYER
		  {
			  sound TELEPORTER
			  espawn TRANSPORTERSTAR
			  getactor[spriteid].x x
			  getactor[spriteid].y y
			  getactor[spriteid].z z
			  setsprite RETURN x y z
			  setactor[spriteid].cstat 32768
			  setactorvar[spriteid].mtype -1
// 			setactorvar[spriteid].mtype 0
// 			getactor[spriteid].ang angvar
// 			ifvarg sprite[THISACTOR].ang 1024 setvar tempb 48 else setvar tempb -48
// 			addvarvar angvar tempb
// 			setactor[spriteid].ang angvar
		}
	}
  }
  
     ifrnd 1 ifvare burning 0
     {
		ifrnd 128 soundonce WRTHROAM1 else soundonce WRTHROAM2
	 }

    ifai AIWRAITHGETENEMY
      state wraithgetenemystate
    else
      ifai AIWRAITHHIT
      {
	    ifvarl fear 0 resetcount
        ifcount 13
          ai AIWRAITHSHOOTENEMY
      }   
    else
      ifai AIWRAITHSHOOTENEMY
        state wraithshootenemystate


    ifmove WRAITHUPVELS nullop
    else
      ifp phigher
        move WRAITHUPVELS seekplayer faceplayerslow
    else
      ifmove WRAITHINWATER nullop
      else
        ifinwater
          move WRAITHINWATER seekplayer faceplayerslow

    ifhitweapon
      state checkwraithhitstate
  
enda

action GHOSTFLY 0 2 5 1 20
action GHOSTDIE 10 6 1 1 14
move GHOSTVEL 116 -15

move STUNNED

spritenoshade GHOST
useractor enemy GHOST 10

set burning 0
ifmove 0
{
	clipdist 0
	move GHOSTVEL randomangle getv
	action GHOSTFLY
	sizeat 40 40
	cstat 2
	getactor[THISACTOR].htflags temp
	ifvarand temp 32 xorvar temp 32
	orvar temp 2048
	orvar temp 8192
	setactor[THISACTOR].htflags temp
	// setactor[THISACTOR].mdflags 16
	spriteflags 1
	rand countvar 19
}
ifmove GHOSTVEL fall
ifaction GHOSTDIE
{
	ifactioncount 6 killit
	break
}
iffloordistl 32 movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN

ifvarn myspawner -1
{
	ifvare player[THISACTOR].heat_on YES // ifvarand timer 1 spawn STARUP
	strength 10
	sizeat 96 96
	cstat 2 // 514
	ifvarn actorvar[myspawner].monstatus 1 
	{ 
		sound GHOSTHOWL spawn BIGSMOKE 
		setvar myspawner -1
		move STUNNED
		espawn SMALLPOTION
		setactor[RETURN].pal 23
		ife player[].heat_on YES set monstatus 2
	} else
	ifvare sprite[myspawner].statnum 1024 
	{ 
		sound GHOSTHOWL spawn BIGSMOKE 
		setvar myspawner -1 
		move STUNNED
		espawn SMALLPOTION
		setactor[RETURN].pal 23
		ife player[].heat_on YES set monstatus 2
	}
	ifvare myspawner -1 break
	getactor[myspawner].x x
	getactor[myspawner].y y
	getactor[myspawner].z z
	subvar z 8192
	setsprite THISACTOR x y z
	getactor[myspawner].ang angvar
	setactor[THISACTOR].ang angvar
	break
}
else sizeto 40 40

addvar countvar 1
ifvarg countvar 20
{
	setvar countvar 0
	ifrnd 85 findnearactor SHOOTME 16384 temp else
	ifrnd 128 findnearactor SHOOTME 8192 temp else
	findnearactor SHOOTME 4096 temp
	ifvarn temp -1
	{
		getactorvar[temp].myspawner spriteid
		ifvarl sprite[spriteid].yrepeat 10 setvar tempb 0 else
		ifvare sprite[spriteid].picnum HORNET setvar tempb 0 else
		canseespr THISACTOR temp tempb
		ifvare tempb 1
		{
			ifvarn target -1
			{
				dist xydist target THISACTOR
				dist tempc temp THISACTOR
				ifvarvarl tempc xydist setvarvar target temp
			}
			else setvarvar target temp
		}
		else ifvarvare temp target setvar target -1
	}
	else setvar target -1
}

ifmove STUNNED
{
	setvar countvar 0
	setvar target -1
	ife monstatus 2 ifcount 20 { seta[].htextra 10 seta[].htpicnum RPG } else
	ifcount 78 move GHOSTVEL geth getv
}

ifvarn target -1
{
	getactorvar[target].myspawner spriteid
	ifvare spriteid -1 { setvar target -1 break }
	ifvarn actorvar[spriteid].monstatus 1 setvar target -1 else
	ifvarn sprite[spriteid].statnum 1 setvar target -1 
	ifvarn actorvar[spriteid].myspawner -1 setvar target -1
	ifvarn target -1
	{
		dist xydist target THISACTOR
		ifvarl xydist 512 
		{
			sound RESURRECT
			setvarvar myspawner spriteid
			getactor[myspawner].extra temp
			setvarvar tempb temp
			ifvarl temp 50 setvar temp 50
			ifvarg temp 1000 setvar temp 1000
			addvarvar tempb temp
			setactor[myspawner].extra tempb
			setactorvar[myspawner].myspawner THISACTOR
			ifvare sprite[myspawner].picnum DRONE
			{
				setactor[myspawner].pal 21
				setactor[myspawner].clipdist 48
				setactor[myspawner].extra 120
				setactor[myspawner].xrepeat 80
				setactor[myspawner].yrepeat 80
				setactorvar[myspawner].gold GOLD2
			}
			ifvare sprite[myspawner].picnum SPIDER2
			{
				setactor[myspawner].pal 7
				setactor[myspawner].extra 120
				setactor[myspawner].xrepeat 48
				setactor[myspawner].yrepeat 42
			}
				
			break
		}
		getactor[target].x mx
		getactor[target].y my
		getactor[target].z mz
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		getactor[THISACTOR].z z
		ifnotmoving // phase through obstacles
		{
			setvarvar xvel x
			setvarvar yvel y
			setvarvar zvel z
			ifvarvarg xvel mx subvar xvel 64 else addvar xvel 64
			ifvarvarg yvel my subvar yvel 64 else addvar yvel 64
			ifvarvarg zvel mz subvar zvel 512 else addvar zvel 512
			setsprite THISACTOR xvel yvel zvel
		}
		subvarvar mx x
		subvarvar my y
		getangle angvar mx my
		setactor[THISACTOR].ang angvar
		
		subvarvar mz z
		ifvarg mz 2048 // target is below
		{
			addvar z 256
			setactor[THISACTOR].z z
		}
		shiftvarr mz 3
		movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
	}
}
else
{
	ifnotmoving ifcount 21 move GHOSTVEL randomangle getv
	iffloordistl 32
	movesprite THISACTOR 0 0 -1024 CLIPMASK0 RETURN
}	

ifvare player[THISACTOR].heat_on YES 
{
	cstat 256
	seta[].shade -127
	spritepal 6
	
	ifmove STOPPED move GHOSTVEL randomangle getv
	ifvarg barbquake 0 { findplayer xydist state barbquakecheck }
	// findnearspritez ARROWPROJ 1024 8192 spriteid
	// ifvarn spriteid -1
	// {
		// setactor[THISACTOR].htextra 20
		// setactor[THISACTOR].htpicnum ARROWPROJ
		// setactor[THISACTOR].htowner player[THISACTOR].i
		// setactor[THISACTOR].htang player[THISACTOR].ang
		// move STOPPED
	// }
}
else
{
	spritepal 0
	ifvarn myspawner -1 cstat 514 else
	cstat 2 
	strength 10
	seta[].shade 10
	setactor[THISACTOR].htextra -1
	ifpdistl 1280 { move STOPPED cstat 32768 }
	else ifmove STOPPED move GHOSTVEL randomangle getv
	break 
}

ifhitweapon
  ifdead
{
	move STOPPED
    action GHOSTDIE
    
    addkills 1 add globalkills 1
    sound GHOSTHOWL
    // espawn SMALLPOTION
    // setactor[RETURN].pal 23
}

enda

action AKORSTAND 0 1 5 1 20
action AKORWALK 5 4 5 1 40
action AKORRUN 5 4 5 1 25
action AKORHIT 35 1 5 1 30
action AKORATTK1 25 1 5 1 60 // 20
action AKORATTK2 30 1 5 1 100 // 25
action AKORATTK3 30 1 5 1 120 // 25
action AKORDIE1 45 9 1 1 25
action AKORDIE2 47 7 1 1 25
action AKORDEAD 54 1 1 1 1

move KORVEL 160
move KORRUNVEL 256
move KORFASTVEL 320

ai AIKORWAIT AKORSTAND STOPPED geth
ai AIKORRISE AKORSTAND STOPPED geth
ai AIKORSEEKPLAYER AKORWALK KORVEL faceplayerslow
ai AIKORATTK AKORATTK1 STOPPED geth
ai AIKORHIT AKORHIT STOPPED geth
ai AIKORDYING AKORDIE1 STOPPED faceplayer

spritenopal KORAX

state koraxfire

	rotatepoint x y mx y angvar xvel yvel

	ifvare countvar 0 
	{
		ifvare zveloverride 1 subvar z 20480 else
		subvar z 40960
		setsprite THISACTOR xvel yvel z
		espawn SOUNDMAKER
		setactorvar[RETURN].peractor1 ARCHONSHOOT2
		
		state calczdist
		ldist temp THISACTOR target
		ifvare temp 0 setvar temp 1
		mulvar zdist 480
		divvarvar zdist temp
		
		ezshoot zdist DARKPROJ
		setav[RETURN].target target
		state projzveltotarg
		
		// espawn LIGHTNINGBOLT 
		// setactorvar[RETURN].mtype 1
		// sound LGTNSND
		ifvare zveloverride 1 addvar z 20480 else
		addvar z 40960
	}
	else
	{
		
		setsprite THISACTOR xvel yvel z
		ezshoot zdist KORAXPROJ
		
		randvar temp 6
		switch temp
		case 0 findnearactor GOLEM 32768 spriteid ifvare spriteid -1 setvar pal 10 else setvar pal 18 break // spawn fire golem
		
		case 1 findnearactor WRAITH 32768 spriteid ifvare spriteid -1 setvar pal 23 else setvar pal 18 break // spawn wraith
		
		case 2 findnearactor DRONE 32768 spriteid ifvare spriteid -1 setvar pal 21 else setvar pal 18 break // spawn drone
		
		case 3 case 4 case 5 case 6 setvar pal 18 break // regular tracking
		
	// 	case 4 setvar pal 16 break // cold
	// 	case 5 setvar pal 6 break // curse
	// 	case 6 setvar pal 14 break // poison
		
		endswitch
		
		ifvare zveloverride 1 
		setvar pal 18
		
		setthisprojectile[RETURN].pal pal
		setactor[RETURN].pal pal
		setav[RETURN].target target
		ife pal 18 seta[RETURN].xvel 644
		
	}
ends

state korattkstate

ifaction AKORATTK1
{
	ifvare zveloverride 1
	{
		ifactioncount 1 { action AKORATTK2 resetcount break } 
	}
	
	ifstrength 14000 // ifstrength 16000 
	{
		// ifactioncount 4 { action AKORATTK3 resetcount }
		ifactioncount 3 { action AKORATTK3 resetcount }
	}
	else
	{ 
		ifactioncount 1 { action AKORATTK2 resetcount } 
	}
}
else
{
	ifactioncount 1 ai AIKORSEEKPLAYER else
	ifcount 20
	{
		globalsound FLYBY 
		ifvare target -1 setvarvar target player[THISACTOR].i
		state calczdist
		ldist temp THISACTOR target
		ifvare temp 0 setvar temp 1
		mulvar zdist 480
		divvarvar zdist temp
		
		// KORAX must be moved to 6 different positions for his different arms
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		getactor[THISACTOR].z z
		setvarvar saveit x
		setvarvar saveit2 y
		getactor[THISACTOR].ang angvar
		// lowest arm shots
		addvar angvar 512
		setvarvar mx x
		ifvare zveloverride 1 addvar mx 1280 else
		addvar mx 2560
		state koraxfire
		
		subvar angvar 1024
		state koraxfire
		
		// middle arm shots
		ifvare zveloverride 1 subvar z 10240 else
		subvar z 20480
		state koraxfire
		addvar angvar 1024
		state koraxfire
		
		// high arm shots
		ifvare zveloverride 1 { subvar z 10240 subvar mx 384 } else
		{
			subvar z 20480
			subvar mx 768
		}
		state koraxfire
		subvar angvar 1024
		state koraxfire
		
		ifvare zveloverride 1 addvar z 20480 else
		addvar z 40960
		setsprite THISACTOR saveit saveit2 z // reset to original position
		
		ifvarvare target player[THISACTOR].i setvar target -1
		resetcount
	}
}

ends

state kordyingstate

// ifaction AKORDEAD 
// {
	// ifvare zveloverride 0
	// {
		// ifcount 210
		// ifvare testnodes NO
		// {
			// setvar testnodes YES
		// }
	// }
	// break
// }

ifaction AKORDIE1
ifactioncount 9
{
	globalsound THUD
	quake 20
	action AKORDEAD
	cstat 0
	ifvare character 0 globalsound KILLBUGS
	al monstflags
	setvarvar activator monstflags
	ifvarn activator 0 state activatorstate

}

ends

state korseekstate

ifvarn lotag 0 ifmove KORVEL { move KORFASTVEL geth action AKORRUN } else
ifvare zveloverride 1 ifmove KORVEL move KORRUNVEL faceplayersmart else
ifstrength 14000 ifmove KORVEL move KORRUNVEL faceplayer

ifvarn lotag 0
{
	setvarvar temp aura
	mulvar temp -1
	ifvarand temp 64
	{
		ifrnd 128 espawn BURNING else espawn BURNING2
		setactor[RETURN].xrepeat 64
		setactor[RETURN].yrepeat 64
	}
	ifvarn target2 -1 // currently have a waypoint
	{

		getactor[target2].x mx
		getactor[target2].y my
		subvarvar mx sprite[THISACTOR].x
		subvarvar my sprite[THISACTOR].y
		getangle angvar mx my
		setactor[THISACTOR].ang angvar

		iffloordistl 8 nullop else
		ifvarvarl sprite[target2].z sprite[THISACTOR].z
		{
			getactor[target2].z mz
			getactor[THISACTOR].z z
			subvarvar mz z
			shiftvarr mz 3
			movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
		}
		
		ldist xydist THISACTOR target2
		ifvarl xydist 384 
		{
			// setactorvar[target2].target THISACTOR // we have now been to this waypoint
			// setvarvar tempd activator
			// getactorvar[target2].lotag activator
			// ifvarn activator 0 state activatorstate
			// setvarvar activator tempd
			// ifvare actorvar[target2].hitag 1 { setvar lotag 0 break }
			add peractor1 1
			ife sprite[target2].pal 23 set peractor1 0
			setvar target2 -1
		}
	}
	else // find a waypoint
	{
		headspritestat spriteid 1
		whilevarn spriteid -1
		{
			ife sprite[spriteid].picnum MONPATH
			ifvarvare actorvar[spriteid].hitag hitag
			ifvarvare actorvar[spriteid].lotag peractor1
				set target2 spriteid
				
			nextspritestat spriteid spriteid
			ifn target2 -1 set spriteid -1
		}
/*
		setvar tempd 0 // end of loop flag
		setvar spriteid 0 // place in array
		setvar xydist 65535 // shortest distance
		setvar tempb -1 // ID of closest trail node
		whilevarn tempd 1
		{
			setvarvar temp towntrails[spriteid]
			ifvare temp -1 setvar tempd 1 // not an actual waypoint
			addvar spriteid 1
			ifvare spriteid 2048 setvar tempd 1 // not an actual waypoint
			
			ifvare tempd 0 // it is an actual waypoint
			ifvarvarn actorvar[temp].target THISACTOR // it is not one we have been to yet
			{
				dist tempc THISACTOR temp
				ifvarvarl tempc xydist
				{
					setvarvar xydist tempc
					setvarvar tempb temp
				}
			}
		}
		ifvarn tempb -1 setvarvar target2 tempb else
		// failed to find any waypoints
		{
			// let's clear our ID from the array, since that was most likely why
			setvar spriteid 0
			whilevarn spriteid 2048
			{
				setactorvar[spriteid].target -1
				addvar spriteid 1
			}
		}
*/
		
	}
	ifnotmoving hitradius 2048 50 50 50 50
	
	ifg bleeding 0 ifl bleeding 150
	ifrnd 40
	{
		ai AIKORATTK 
		ife target -1 state facetarget
		ifrnd 128
		globalsound KORACT
		addvar countvar 1
		ifvarg countvar 2 setvar countvar 0
	}
}
else
ifactioncount 1 ifcount 210
ife camerasprite -1
{
	ai AIKORATTK 
	ife target -1 state facetarget
	ifrnd 128
	globalsound KORACT
	addvar countvar 1
	ifvarg countvar 2 setvar countvar 0
}

ends

state korrisestate

ifvarn myspawner -1
{
	ifvare sprite[myspawner].statnum 1024 setvar myspawner -1 else
	ifvare actorvar[myspawner].monstatus 2 setvar myspawner -1 else
	ifvarn sprite[myspawner].picnum DRAGON setvar myspawner -1
	break
}

ifvarn monid -1 ifvarvarn monid THISACTOR break

ifvarvarn bossid THISACTOR 
{
	setvarvar bossid THISACTOR
	set monid THISACTOR
	set monidcount 0
	starttrackslot 1 1
	set music2 1
}


getactor[THISACTOR].x weap
getactor[THISACTOR].y dodge

setvarvar x weap
randvar temp 4096
subvar temp 2048
addvarvar x temp

setvarvar y dodge
randvar temp 4096
subvar temp 2048
addvarvar y temp

setactor[THISACTOR].x x
setactor[THISACTOR].y y
setactor[THISACTOR].z initz

randvar zdist 2048
subvar zdist 5120
cactor ROTATEGUN
ezshoot zdist LAVAROCK
cactor KORAX
randvar angvar 2047
setactor[RETURN].ang angvar
ifrnd 64
{
	espawn BIGSMOKE
	setactor[RETURN].pal 2
}

setactor[THISACTOR].x weap
setactor[THISACTOR].y dodge
setactor[THISACTOR].z mtype

cstat 257

subvar mtype 512
ifvarvarl mtype initz 
{
	setactor[THISACTOR].z initz
	ai AIKORSEEKPLAYER
	globalsound KORACT
	setvar weap 0
	count 150
	ifg SKILL 3
		setvar aura -64
	

}


ends

state korwaitstate

ifcount 240
{
	getactor[THISACTOR].z initz	
	getsector[THISACTOR].ceilingz z
	addvar z 16384	
	setactor[THISACTOR].z z
	espawn DRAGON
	setactor[RETURN].pal 2
	setactorvar[RETURN].monstflags 3
	setvarvar myspawner RETURN
	espawn DRAGON
	setactor[RETURN].pal 2
	espawn DRAGON
	setactor[RETURN].pal 2
	
	setvarvar mtype initz
	addvar mtype 98304
	setactor[THISACTOR].z mtype
	
	setactor[THISACTOR].mdflags 16
	orvar monstflags 4
	clipdist 254
	ai AIKORRISE
	quake 90
}

ends

eventloadactor KORAX
getactor[THISACTOR].xvel droptile
setactor[THISACTOR].xvel 0
getactor[THISACTOR].yvel activator
setactor[THISACTOR].yvel 0
getactor[THISACTOR].extra monstflags
ifvarl monstflags 1 
	setvar monstflags 0
	
setactor[THISACTOR].extra 0
cstat 32768
enda

eventloadactor MINIKORAX
geta[].hitag hitag
seta[].hitag 0
getactor[THISACTOR].lotag lotag
setactor[THISACTOR].lotag 0
getactor[THISACTOR].xvel droptile
setactor[THISACTOR].xvel 0
getactor[THISACTOR].yvel activator
setactor[THISACTOR].yvel 0
getactor[THISACTOR].extra monstflags
ifvarl monstflags 1 
	setvar monstflags 0
getactor[THISACTOR].owner aura
ifvare aura -1 setvar aura 0 else
{
	mulvar aura -1
	setactor[THISACTOR].owner -1
}
setactor[THISACTOR].extra 0
sizeat 64 64
enda

spritenopal KORSTATUE
action KORSTATFRAME 0 1 5 1 1
useractor notenemy KORSTATUE 0 KORSTATFRAME

ifmove INIT
{
	ifcount 91 nullop else
	ifcount 90 setvar countvar 1
	ifvarg countvar 0
	{
		soundonce CRUMBLE
		debris SCRAP2 1
		debris SCRAP3 1
		debris SCRAP4 1
		debris SCRAP5 1
		addvar countvar 1
		getactor[THISACTOR].z z
		addvar z 192
		setactor[THISACTOR].z z
		ifvarg countvar 150 
		{
			spawn BIGSMOKE
			killit
		}
	}
}
ifmove 0
{
	ifvarvare player[THISACTOR].cursectnum sprite[THISACTOR].sectnum
	move INIT
}

enda

useractor enemy MINIKORAX 10000

ifai 0
{
	sizeat 64 64
	cstat 257
	setvar zveloverride 1 // mini
	ifg SKILL 3 state randomaura
	setvar gold GOLD8
	ifvare lotag 0
	{
		cactor KORAX
		ai AIKORSEEKPLAYER
		globalsound KOROAM
	}
	else
	ifcansee ifvarg character -1
	{
		cactor KORAX
		ai AIKORSEEKPLAYER
		globalsound KOROAM
	}
}
	

enda

useractor enemy KORAX 32000

	ifai 0
	{
		globalsound KORAXSPCH
		sizeat 132 132
		cstat 32768
		ai AIKORWAIT
		break
	}
	else
	ifai AIKORRISE
	{
		state korrisestate
		break
	}
	else
	ifai AIKORWAIT
	{
		state korwaitstate
		break
	}
	
	fall
	
	ifai AIKORDYING
	{
		state kordyingstate
		break
	}
	

	ifvarvarn bossid THISACTOR 
	{
		set monid THISACTOR
		set monidcount 0
		setvarvar bossid THISACTOR
		starttrackslot 1 1
		set music2 1
	}
	set temp NO
	ifaction AKORRUN set temp YES
	ifaction AKORWALK set temp YES
	ife temp YES
	{
		ifactioncount 2 
		{ 
			soundonce CYBERSTOMP
			 ifpdistl 16384 quake 6 
			 resetactioncount 
			 ifpdistl 2048 
			 ifvarvarn swordtarg THISACTOR 
			 {
				 setvar tempd -50
				 state directdamage
				 // addphealth -50 
				 state randsquishsound
				 wackplayer wackplayer
				 ifp palive
				 state dukepainsounds
				 palfrom 42 48
				 lockplayer 8
			 }
		}
	}
	
	state monsterai
	ifspritepal 2 ifl burning -2 set burning -2
	
	ifvarg bleeding 150 ifrnd 64
	ifai AIKORSEEKPLAYER
	ifn sprite[].pal 2
	{
		ai AIKORHIT
		globalsound KORPAIN
	}
	
	ifai AIKORHIT
	{
		ifcount 25
		{
			// ifvarn lotag 0 ai AIKORSEEKPLAYER else
			 ai AIKORATTK 
			 globalsound KORACT
			 addvar countvar 1
			ifvarg countvar 2 setvar countvar 0
		}
	}
	else
	ifai AIKORATTK state korattkstate else
	ifai AIKORSEEKPLAYER state korseekstate
	
	ifvarg bleeding 0 ifp ponsteroids
	{
		getactor[THISACTOR].htowner spriteid
		ifvare sprite[spriteid].picnum APLAYER
		{
			shiftvarr bleeding 1
			setactor[THISACTOR].htextra bleeding
		}
	}
	ifhitweapon
	{
		ifdead
		{
			state specialdeath
			ai AIKORDYING
			spritepal 0
			cstat 257
			ifvare character 0 globalsound DUKE_TALKTOBOSSFALL
			break
		}
		ifvarn activator 0 ifstrength 20000
		ifvare zveloverride 0
		{
			al activator
			state activatorstate
			setvar activator 0
			ai AIKORHIT
		}
		
		ifvare zveloverride 0
		ifstrength 14000
		ifn sprite[].pal 2
		{
			spritepal 2
			set aura -4160
			
			seta[].blend 1
			ai AIKORHIT
			quake 30
		}
			
	}
	
	ifvare zveloverride 0
	ifn target -1
	  ifl targetdist 6144
	  {
		ldist temp THISACTOR target
		ifl temp 2048
		{
			setactor[target].htextra 100
			setactor[target].htpicnum SHOTSPARK1
			setactor[target].htowner THISACTOR
		}
	  }
	
	ifsound KOROAM nullop else
	ifrnd 1 ifrnd 128 globalsound KOROAM
	
enda

action KAZEDELAY 20  1  5  1  1
action KAZERUN 0 4 5 1 8
action KAZESTAND 20 1 5 1 1
action KAZEDIE 20 1 5 1 16

move KAZERUNVEL1 192
move KAZERUNVEL2 208
move KAZERUNVEL3 224

move KAZECURSEVEL 224

eventloadactor KAMIKAZE 

ifspritepal 17
{	
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
action KAZESTAND
sizeat 29 27
ifspritepal 23 { set mtype 1 sizeat 40 32 }
else
ifspritepal 0 spritepal 21 

enda

state kamexplode

	stopactorsound THISACTOR ZROAM1
	stopactorsound THISACTOR ZROAM2
	setvar command 1
	cstat 0
	addkills 1 add globalkills 1
	ifspritepal 23 spritepal 113
	state specialdeath
	state standard_jibs
	move STOPPED
	action KAZEDIE
	spawn EXPLOSION2
	
	  spawn BLUDSPLASH
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD
	  spawn BLOODCLOUD

	ife mtype 1 
	{
		state standard_jibs
		hitradius 3072 30 50 70 90
		 spawn MININUKE
		 spawn MININUKE
		 spawn MININUKE
		 spawn MININUKE
		 spawn MININUKE
	}
	else
	hitradius 1536 15 25 35 45
	ifvarn target -1
	{
		ife mtype 1 setactor[target].htextra 90
		else
		setactor[target].htextra 45
		setactor[target].htowner THISACTOR
	}
	ifrnd 92 spawn BLOODPOOL
	killit

ends

// pal variants 18=purple, 11=green, 23=yellow
// start as 21 becomes fat and powerful (contolled by var)

useractor enemy KAMIKAZE 50 KAZESTAND

ifspritepal 17
{
	action KAZESTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength 50
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		spritepal 21
		setvar command 0
		move 0
	}
	else break
}


ifmove 0
{
	move KAZERUNVEL1 seekplayer
	action KAZERUN
	cstat 257
	setvar gold GOLD1
	ife mtype 1 
	{ sizeat 40 32 addstrength 100 
	  ifg SKILL 3 state randomaura
	}
	else sizeat 29 27 
	ifspritepal 0 ifspawnedby RESPAWN spritepal 21
}

fall
state monsterai

ifaction KAZEDELAY
{
	ifl fear -30 set fear -30
	ifl fear 0 break
	ifcount 3 state kamexplode
	break
}

ifmove KAZERUNVEL1
{
	ifcansee ifpdistl 3072
	{
		move KAZERUNVEL3 faceplayer
		resetcount
	}
	else
	ifcount 30 ifrnd 16
	{
		move KAZERUNVEL2 faceplayerslow
		resetcount
	}
}
else
ifmove KAZERUNVEL2
{
	ifcansee ifpdistl 3072
	{
		move KAZERUNVEL3 faceplayer
		resetcount
	}
	else ifcount 30 ifrnd 16
	{
		move KAZERUNVEL1 seekplayer
		resetcount
	}
}
else
ifmove KAZERUNVEL3
{
	ifpdistg 4096
	{
		move KAZERUNVEL2 faceplayerslow
		resetcount
	}
}

ifn cursed 0
{
	ifmove KAZECURSEVEL nullop else
	move KAZECURSEVEL furthestdir
}

ifactioncount 2
ife mtype 1
{
	resetactioncount
	sound DMWALK
}

ifactorsound THISACTOR ZROAM1 nullop else
ifactorsound THISACTOR ZROAM2 nullop else
{
	ifrnd 128 sound ZROAM1 else
	sound ZROAM2
}

ifl sprite[].extra 0
ifg sprite[].htextra 0
	state kamexplode
else
ifhitweapon
{
	ifdead
	{
		ifwasweapon BURNING nullop else
		state random_wall_jibs
		
		ifl fear 0 { strength 0 action KAZEDELAY move STOPPED geth }
		else
		state kamexplode
		break
	}
}

ifcansee ifpdistl 1024
ife cursed 0
{
	// state kamexplode
	action KAZEDELAY move STOPPED geth
}

else ifvarn target -1 
	ifvarl xydist2 1024
{
	// state kamexplode
	action KAZEDELAY move STOPPED geth
}

enda

action AHOUNDRUN 5 6 5 1 4
action AHOUNDTROT 5 6 5 1 8
action AHOUNDBITE 30 4 5 1 10
action AHOUNDSTAND 0 1 5 1 10
action AHOUNDJUMP 5 4 5 1 15
action AHOUNDFALL 20 1 5 1 15
action AHOUNDLAND 30 1 5 1 15
action AHOUNDSTUN 45 1 5 1 10
action AHOUNDDIE 50 7 1 1 18
action AHOUNDDEAD 56 1 1 1 1
move HOUNDVELS 416
move HOUNDCLOSEVELS 416
move HOUNDSEARCHVEL 192

move HOUNDBITE 128
move HOUNDJUMPVEL 288 -24
move HOUNDFALLVEL 288

ai AIHOUNDCHARGE AHOUNDRUN HOUNDVELS faceplayerslow
ai AIHOUNDBITE AHOUNDBITE HOUNDBITE faceplayer
ai AIHOUNDSEEK AHOUNDTROT HOUNDSEARCHVEL seekplayer
ai AIHOUNDWAIT AHOUNDSTAND STOPPED geth
ai AIHOUNDJUMP AHOUNDJUMP HOUNDJUMPVEL jumptoplayer
ai AIHOUNDDYING AHOUNDDIE STOPPED geth
ai AIHOUNDSTUN AHOUNDSTUN STOPPED geth

state houndstunstate

	ifl fear 0 resetcount
	ifcount 8
	ai AIHOUNDSEEK

ends

state hounddyingstate 

	strength 0
	ifaction AHOUNDDEAD
	{
		ifhitweapon
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		break
	}
	ifaction AHOUNDDIE
	{
		ifactioncount 7 
		{
			action AHOUNDDEAD
			ifspritepal 50 sound THUD
		}
	}

ends

state houndjumpstate

	ifaction AHOUNDJUMP
	{
		ifactioncount 3
		{
			action AHOUNDFALL
			move HOUNDFALLVEL geth
		}
	}
	else
	ifaction AHOUNDFALL
	{
		iffloordistl 8
		{
			action AHOUNDLAND
			move STOPPED geth
		}
	}
	else
	ifaction AHOUNDLAND
	{
		ifactioncount 2 { ai AIHOUNDCHARGE ifrnd 128 sound HOUNDALRT }
	}

ends

state houndchargestate 

	ifspritepal 50 set saveit 2048
	else set saveit 1280

	ifvarn target -1
	ifl xydist2 saveit
	{
		set target2 -1
		ai AIHOUNDBITE
		ifspritepal 50 move STOPPED faceplayer
		break
	}
	state dodgecheck
	ifn peractor4 0 seta[].ang dodgeang
	
	ifcansee
	{
		ifvare cursed 0
		ifl xydist saveit
		{
			ai AIHOUNDBITE
			ifspritepal 50 move STOPPED faceplayer
			break
		}
		ifmove HOUNDVELS
		ife cursed 0
		ifpdistl 3072 move HOUNDCLOSEVELS faceplayer
	}
	else ifcount 30
	ifvare target -1 ai AIHOUNDSEEK
	
	ifpdistg 8192 ifrnd 1 ai AIHOUNDSEEK
	else
	ifcount 10
	ifnotmoving
	{
		ifrnd 128 ai AIHOUNDJUMP
		else
		ai AIHOUNDWAIT 
	}
	
	ifrnd 1 sound HOUNDROAM

ends

state houndseekstate

	ifspritepal 50 ifmove HOUNDSEARCHVEL move HOUNDVELS seekplayer
	
	ifspritepal 50 set saveit 2048
	else set saveit 1280
	
	ifvarn target -1
	ifl xydist2 saveit
	{
		set target2 -1
		ai AIHOUNDBITE
		ifspritepal 50 move STOPPED faceplayer
		break
	}
	state dodgecheck
	ifn peractor4 0 seta[].ang dodgeang
	ifcansee
	{
		ifvare cursed 0
		ifl xydist saveit
		{
			ai AIHOUNDBITE
			ifspritepal 50 move STOPPED faceplayer
			break
		}
	}
	ifp phigher ifrnd 2
	{
		ai AIHOUNDJUMP
		break
	}
	ifcount 60 
	{
		ifcansee ai AIHOUNDCHARGE
		ifvarn target -1 ai AIHOUNDCHARGE
		
		ifai AIHOUNDCHARGE ifn cursed 0
			move HOUNDVELS randomangle
	}
	
	ifcount 10
	ifnotmoving
	{
		ifrnd 128 ai AIHOUNDJUMP
		else
		ai AIHOUNDWAIT 
	}
	
	ifrnd 1 sound HOUNDROAM

ends

state houndwaitstate

	set target2 -1
	ifcount 10
	{
		ai AIHOUNDSEEK
	}

ends

state houndbitestate

	set peractor4 0
	set target2 -1
	
	ifn sprite[].pal 50
	{
		ifvarn target -1
		{
			ifvarg xydist2 1024 { ifmove STOPPED move HOUNDSEARCHVEL geth } else
			move STOPPED geth
		}
		else
		ifvarn cursed 0 // ally
		{
			ifpdistl 1280 { action AHOUNDSTAND move STOPPED } else
			ai AIHOUNDSEEK
			break
		}
		else
		{
			ifpdistg 1024 { ifmove STOPPED move HOUNDSEARCHVEL faceplayer } else
			move STOPPED faceplayer
		}
	}
	
	ifactioncount 4
	{
		ifrnd 64 sound HOUNDATTK
		sound BITEME
		resetactioncount
		ifspritepal 50
		{
			sound WRTHATTK
			ifvarn target -1 state wraithprojathero else
			shoot WRAITHPROJ
			
			ifn target -1 { ifg xydist2 2048 ai AIHOUNDCHARGE break }
			ifcansee ifl xydist 2048 nullop else
			ai AIHOUNDCHARGE
		}
		else
		{
			ifvarn target -1 
			{
				ifvarg xydist2 1280 ai AIHOUNDCHARGE
				else state monsterbite
			} 
			else
			{
				ifpdistg 1280 ai AIHOUNDCHARGE
				else 
				{
					state monsterbite
					ifcansee { setp[].posxv 0 setp[].posyv 0 setp[].poszv 0 setp[].jumping_counter 0 }
				}
			}
		}
	}


ends

eventloadactor HELLHOUND 

ifspritepal 17
{
	action AHOUNDSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}
sizeat 44 38 
ife SKILL 5 ifrnd 64 spritepal 50
ifspritepal 50 sizeat 60 56

enda

useractor enemy HELLHOUND HOUNDSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action AHOUNDSTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 0
	}
	else break
}

fall
state monsterai
ifai 0
{
	sound HOUNDALRT
	ai AIHOUNDCHARGE
	sizeat 44 38
	cstat 257
	setvar gold GOLD1
	ifspritepal 50 
	{ 
		sizeat 60 56 setvar gold GOLD3 
		addstrength 300 
		ifg SKILL 3 state randomaura 
	}
	ifspritepal 120 
	{
		set gold 0
		cstator 2
		strength 80
	}
}

ifai AIHOUNDDYING { state hounddyingstate break }

ifai AIHOUNDCHARGE state houndchargestate else
ifai AIHOUNDSEEK state houndseekstate else
ifai AIHOUNDWAIT state houndwaitstate else
ifai AIHOUNDBITE state houndbitestate else
ifai AIHOUNDJUMP state houndjumpstate else
ifai AIHOUNDSTUN state houndstunstate

ifhitweapon
{
	sound HOUNDPAIN
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		sound HOUNDIESND
		addkills 1 add globalkills 1
		state specialdeath
		ifrnd 128 cstat 0 else cstat 4
		ifspritepal 120
		{
			// spritepal 4
			state standard_jibs
			espawn BIGSMOKE
			seta[RETURN].pal 4
			seta[RETURN].cstat 2
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ai AIHOUNDDYING
	}
	else
	ifl fear 0 ai AIHOUNDSTUN else
	ifrnd 16
	ai AIHOUNDSTUN
}

enda

action ASCUBAGLIDE  5  1  5  1  20
action ASCUBASWIM   0  3  5  1  12
action ASCUBASHOOT  15 2  5  1  30
action ASCUBADYING  25 6  1  1  18
action ASCUBADEAD   30 1  1  1  10
action ASCUBAFROZEN 15  1  5  1  10
action ASCUBAGROW   15  1  5  1  10
move SCUBAGLIDEVELS 160
move SCUBASWIMVELS 128

ai AISCUBASEEK ASCUBASWIM SCUBASWIMVELS seekplayer
ai AISCUBASHOOT ASCUBASHOOT STOPPED faceplayer
ai AISCUBAGROW ASCUBAGROW STOPPED geth
ai AISCUBAFROZEN ASCUBAFROZEN STOPPED geth
ai AISCUBADYING ASCUBADYING STOPPED geth

state scubashootstate

ifcount 8 nullop else
ifcount 7
{
	geta[].z saveit
	add saveit 3072
	seta[].z saveit
	sound SCUBATTK
	ifspritepal 21
	{
		ifvare target -1 setvarvar target player[THISACTOR].i
		state calczdist
		ldist temp THISACTOR target
		
		setprojectile[KORAXPROJ].vel 644
		ifvare temp 0 setvar temp 1
		mulvar zdist 644
		divvarvar zdist temp
		
		ezshoot zdist KORAXPROJ
		setthisprojectile[RETURN].pal 97
		seta[RETURN].pal 97
		setprojectile[KORAXPROJ].vel 480
		seta[RETURN].xrepeat 32
		seta[RETURN].yrepeat 32
		setav[RETURN].target target
	}
	else
	{
		ifvarn target -1
		{
			state calczdist
			ldist temp THISACTOR target
			ifvare temp 0 setvar temp 1
			mulvar zdist 768
			divvarvar zdist temp
			ezshoot zdist FIRELASER
		}
		else
		shoot FIRELASER
	}
	
	sub saveit 3072
	seta[].z saveit
	add mtype 1
	move STOPPED geth
	ifg mtype 2
	{
		set mtype 0
		ai AISCUBASEEK
		ifpdistl 4096
			move SCUBASWIMVELS randomangle
	}
}

ends

state scubadyingstate

	strength 0
	
	ifaction ASCUBADYING
	{
		ifhitweapon state random_wall_jibs
		ifactioncount 5
		{
			cstator 256
			seta[].mdflags 16
			setvar command 32768
			iffloordistl 512 nullop else
			spriteflags 4096
			action ASCUBADEAD
			iffloordistl 8 sound THUD
			break
		}
	}
	ifaction ASCUBADEAD
	{
		ifhitweapon
		{
			ifwasweapon RPG
			{
				state randsquishsound
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				state randsquishsound
				spawn BLOODPOOL
				state standard_jibs
				killit
			}
		}
	}
	
ends

state scubaseekstate

	ifrnd 1 soundonce SCUBROAM
	add ammo 1
	getp[].posz zdist
	add zdist 8192
	
	sub zdist sprite[].z
	shiftr zdist 1
	ifl ammo 20 set zdist 0
	ifaction ASCUBAGLIDE set zdist 0
	
	ifl zdist -1560 set zdist -1560
	ifg zdist 1560 set zdist 1560
	
	getceilzofslope sprite[].sectnum sprite[].x sprite[].y z
	geta[].z temp
	sub temp z

	ifl temp 6144 ifl zdist 0 set zdist 512
	
	getflorzofslope sprite[].sectnum sprite[].x sprite[].y z
	sub z sprite[].z
	ifl z 6144 ifg zdist 0 set zdist -512
	
	movesprite THISACTOR 0 0 zdist CLIPMASK0 RETURN
	ifg zdist 0 ife countvar -1 set ammo 0
	ifl zdist 0 ife countvar 1 set ammo 0
	ifg zdist 0 set countvar 1 else ifl zdist 0 set countvar -1
	
	ifcansee
	{
		ifaction ASCUBASWIM
		ifawayfromwall
		{
			ifpdistg 4096
			{
				ifpdistg 8192 ifrnd 8
				{
					action ASCUBAGLIDE
					move SCUBAGLIDEVELS geth
				}
				else ifrnd 2
				{
					action ASCUBAGLIDE
					move SCUBAGLIDEVELS geth
				}
			}
		}
		
		ifcount 24
		ifpdistl 16384
		ifcanshoottarget
		{
			ifaction ASCUBAGLIDE ifpdistl 5120 nullop else
			{
				ifrnd 3 ai AISCUBASHOOT
				else ifpdistl 5120 ifrnd 8 ai AISCUBASHOOT
				else ifpdistl 2560 ai AISCUBASHOOT
			}
		}
	}
	
	ifaction ASCUBAGLIDE
	{
		ifnotmoving count 100
		ifcount 90
		{
			action ASCUBASWIM
			move SCUBASWIMVELS seekplayer
		}
	}

ends

eventloadactor SCUBATROOP sizeat 42 40 cstator 257 enda

useractor enemy SCUBATROOP 250

	ifai 0
	{
		ai AISCUBASEEK
		sizeat 42 40
		cstator 257
		sound SCUBALERT
		ifspritepal 0 ife SKILL 5 ifrnd 48 spritepal 21
		ifspritepal 21 
		{ 
			addstrength 150 sizeat 44 42 
			ife SKILL 5 state randomaura
		}
		set ammo 0
	}
	ifai AISCUBADYING { state scubadyingstate break }
	ifinwater ifrnd 28 spawn WATERBUBBLE
	state monsterai
	ifai AISCUBASEEK state scubaseekstate else
	ifai AISCUBASHOOT state scubashootstate

	ifhitweapon
	{
		ifwasweapon BURNING nullop else
		state random_wall_jibs
		ifdead
		{
			sound SCUBDEATH
			addkills 1 add globalkills 1
			state specialdeath
			ai AISCUBADYING
			ifrnd 128 cstator 4
			sound SCUBMON_DYING
			
			guts JIBS6 4
			
			ifwasweapon RPG
			{
				state randsquishsound
				state standard_jibs
				killit
			}
			ifwasweapon RADIUSEXPLOSION
			{
				state randsquishsound
				state standard_jibs
				killit
			}
			break
		}
		else soundonce SCUBPAIN
	}
	else state checksquished

enda

action ABARONSTAND 0 1 5 1 20
action ABARONWALK 5 4 5 1 16
action ABARONRUN 5 4 5 1 10
action ABARONMELEE 25 3 5 1 12
action ABARONTHROW 25 3 5 1 18
action ABARONTHROWN 35 1 5 1 30
action ABARONCAST 40 3 5 1 20
action ABARONCASTED 50 1 5 1 40
action ABARONPAIN 55 1 1 1 16
action ABARONDIE 55 7 1 1 18
action ABARONDEAD 61 1 1

move BARONWALKVEL 112
move BARONRUNVEL 180
move BARONKNOCKBACK

ai AIBARONSEEKENEMY ABARONWALK BARONWALKVEL seekplayer faceplayerslow
ai AIBARONSHOOTENEMY ABARONTHROW STOPPED faceplayer
ai AIBARONCAST ABARONCAST STOPPED faceplayerslow
ai AIBARONPAIN ABARONPAIN STOPPED faceplayer
ai AIBARONMELEE ABARONMELEE STOPPED faceplayer

state baronpainstate

ifvarl fear 0 resetactioncount
ifactioncount 3 
{ 
	ai AIBARONSEEKENEMY 
	move BARONRUNVEL seekplayer
	action ABARONRUN
}

ends

state baronmeleecheck

ifn target -1 
ifl xydist2 1280 
{ 
	ai AIBARONMELEE
	sound BARONATTK1
} 
else
ifpdistl 1280 ifcansee 
ifcanshoottarget
ifp palive
{
	ai AIBARONMELEE
	sound BARONATTK1
}
ends

state baronseekstate

ifactioncount 2 { sound MONSTEP resetactioncount }

ifvarn target -1 setvar peractor1 1

ifcount 40 ifvare peractor1 1 // ifcansee
ifrnd 16
{
	ifvarn cursed 0 ifvare target -1
	{
		ai AIBARONSEEKENEMY 
		break
	}
	ifrnd 84
	{
		set temp YES
		ife target -1 ifp phigher set temp NO
		ifn target -1 ifl sprite[target].z sprite[].z set temp NO
		ife temp YES
		{
			sound BARONATTK2
			ai AIBARONCAST
			break
		}
	}
	sound BARONATTK1
	ai AIBARONSHOOTENEMY
	break
}

state baronmeleecheck

ends

state shootboulder
	espawn BOULDER
	seta[RETURN].ang sprite[].ang
	seta[RETURN].xrepeat 40
	seta[RETURN].yrepeat 40
	ifspritepal 0 set pal 1 else
	ifspritepal 24 set pal 1 else
	set pal 2
	seta[RETURN].pal pal
	seta[RETURN].mdflags 1
	setav[RETURN].weap pal
	
	getactor[].x mx
	addvar mx 512
	rotatepoint sprite[THISACTOR].x sprite[THISACTOR].y mx sprite[THISACTOR].y sprite[THISACTOR].ang x y
	setsprite RETURN x y sprite[THISACTOR].z
	
	cos xvel sprite[].ang
	sin yvel sprite[].ang
	
	shiftvarr xvel 3
	shiftvarr yvel 3

	setav[RETURN].countvar xvel
	setav[RETURN].countvarb yvel
	ifn target -1 setav[RETURN].target target else
	setav[RETURN].target player[].i
	setav[RETURN].myspawner THISACTOR
ends

state baronfiring

	// sound RPG_SHOOT
	ife target -1 set target player[].i
	ifspritepal 23 eshoot ARCHONPROJ else
	eshoot BARONBALL
	
	state calczdist
	ldist temp THISACTOR target
	ifvare temp 0 setvar temp 1
	mulvar zdist 768
	divvarvar zdist temp
	setactor[RETURN].zvel zdist
	setactor[RETURN].ang sprite[THISACTOR].ang
	ifspritepal 24 seta[RETURN].pal 12 else
	seta[RETURN].pal sprite[].pal
	ifspritepal 23
	{
		setav[RETURN].target target
		state projzveltotarg
	}
	ife target player[].i set target -1

ends

state baronspell

	ifspritepal 21
	{
		espawn TIDALWAVE
		seta[RETURN].pal 2
		espawn TIDALWAVE
		seta[RETURN].pal 2
	}
	else
	ifspritepal 24
	{
		spawn TIDALWAVE
		spawn TIDALWAVE
	}
	else
	ifspritepal 23
	{
		espawn FIRERING
		seta[RETURN].pal 83
		hitradius 2048 30 35 40 50
	}
	else
	{
		ifrnd 128
		state shootboulder
		else
		{
			ifvarn target -1
			{
				state calczdist
				ldist temp THISACTOR target
				ifvare temp 0 setvar temp 1
				mulvar zdist 644
				divvarvar zdist temp
				ezshoot zdist NOVAPROJ
				
				ezshoot zdist NOVAPROJ
				geta[RETURN].ang angvar
				sub angvar 96
				seta[RETURN].ang angvar
				
				ezshoot zdist NOVAPROJ
				geta[RETURN].ang angvar
				add angvar 96
				seta[RETURN].ang angvar
			}
			else
			{
				eshoot NOVAPROJ
				eshoot NOVAPROJ
				geta[RETURN].ang angvar
				sub angvar 96
				seta[RETURN].ang angvar
				eshoot NOVAPROJ
				geta[RETURN].ang angvar
				add angvar 96
				seta[RETURN].ang angvar
				
			}
		}
		
	}

ends

state baronshootstate

	ifvarg fear 0 { ai AIBARONSEEKENEMY break }
	ifvarn cursed 0 ifvare target -1
	{
		ai AIBARONSEEKENEMY
		break
	}

	ifaction ABARONTHROW ifactioncount 2
	{
		state baronfiring
		action ABARONTHROWN
	}

		 
	ifaction ABARONTHROWN ifactioncount 1
	{
		state baronmeleecheck
		ifai AIBARONMELEE break
		
		ifrnd 96
		ifcansee ifcanshoottarget 
		{
			action ABARONTHROW
			sound BARONATTK1
			break
		}
		ai AIBARONSEEKENEMY
	}

ends

state baroncaststate

	ifvarg fear 0 { ai AIBARONSEEKENEMY break }

	ifaction ABARONCAST ifactioncount 2
	{
		state baronspell
		action ABARONCASTED
		move STOPPED geth
		sound BIGTHUD
		ifspritepal 23 sound GIANTSTEP
		ifpdistl 10240 quake 30
	}

	ifvarn cursed 0 ifvare target -1
		ai AIBARONSEEKENEMY
		 
	
	ifaction ABARONCASTED ifactioncount 1
	{
		state baronmeleecheck
		ifai AIBARONMELEE break

		ai AIBARONSEEKENEMY
		move BARONRUNVEL seekplayer
		action ABARONRUN
	}

ends

state baronmeleestate

ifactioncount 2
{
	ifvare target -1
		ifcansee ifpdistl 1536 state knockplayer
		else
		{
			state monsterbite
			state monsterbite
			ifspritepal 23
				state monsterbite
			
		}
}
ifactioncount 3 ai AIBARONSEEKENEMY

ends

eventloadactor BARON
sizeat 32 30 // 44 44
ifspritepal 23 sizeat 64 60
ifspritepal 17 
{
	action ABARONSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}

enda

eventloadactor BARONSTAYPUT
sizeat 32 30 // 44 44
ifspritepal 23 sizeat 64 60
ifspritepal 17 
{
	action ABARONSTAND
	getactor[THISACTOR].lotag lotag
	setactor[THISACTOR].lotag 0
	getactor[THISACTOR].ang command
}

enda

useractor enemystayput BARONSTAYPUT BARONSTRENGTH ABARONSTAND

    cactor BARON
	setvar gold GOLD5
	sizeat 32 30

	cstator 257
	ifspritepal 17 action ABARONSTAND else ai AIBARONSEEKENEMY
	
	cstator 257
	sound BARONALERT
	ifspritepal 24 // ice variant
	{
		mul aura -1
		ifvarand aura 32 nullop else orvar aura 32
		mul aura -1
	}
	ifspritepal 10 // fire variant
	{
		mul aura -1
		ifvarand aura 64 nullop else orvar aura 64
		mul aura -1
	}
	ifspritepal 23 // boss variant
	{
		strength 10000
		sizeat 64 60
	}
	ifg SKILL 3 state randomaura
	
enda

useractor enemy BARON BARONSTRENGTH

ifspritepal 17
{
	setactor[THISACTOR].ang command
	action ABARONSTAND
	cstator 257
	ifvarn lotag 0 
	{
		resetcount 
		checkactivatormotion lotag
		ifvare RETURN 1 setvar lotag 0
	    fall 
		break
	}
	fall
	soundonce CRUMBLE
	debris SCRAP2 1
	debris SCRAP3 1
	debris SCRAP4 1
	debris SCRAP5 1
	strength BARONSTRENGTH
	setactor[THISACTOR].htextra -1
	ifcount 26
	{
		setvar command 0
		spritepal 10
		action 0
	}
	else break
}

getactor[THISACTOR].z z
subvar z 4096
setactor[THISACTOR].z z
setvar peractor1 0
ifvare fear 0
{
	ifcansee setvar peractor1 1 else 
	ifvarn target -1 setvar peractor1 1
}
addvar z 4096
setactor[THISACTOR].z z

fall


ifmove 0
{
	ai AIBARONSEEKENEMY
	sizeat 32 30 // 44 44
	setvar gold GOLD5
	cstator 257
	sound BARONALERT
	ifspritepal 24 // ice variant
	{
		mul aura -1
		ifvarand aura 32 nullop else orvar aura 32
		mul aura -1
	}
	ifspritepal 10 // fire variant
	{
		mul aura -1
		ifvarand aura 64 nullop else orvar aura 64
		mul aura -1
	}
	ifspritepal 23 // boss variant
	{
		strength 10000
		sizeat 64 60
	}
	ifg SKILL 3 state randomaura
}

state monsterai
ifactornotstayput state avoidcliffs

ifmove BARONKNOCKBACK ifcount 2
{
	ifai AIBARONSEEKENEMY move BARONWALKVEL seekplayer faceplayerslow else
	move STOPPED
}

ifaction ABARONDEAD
{
	strength 0
	ifhitweapon
	{
		ifwasweapon RADIUSEXPLOSION
		{
			state standard_jibs
			state randsquishsound
			killit
		}
		ifwasweapon RPG
		{
			state standard_jibs
			state randsquishsound
			killit
		}
	}
	break
}
ifaction ABARONDIE
{
	strength 0
	ifhitweapon guts JIBS6 2
	
	ifvarand player[].player_par 1
	{
		ifrnd 85
		{
			state aimjib
			ezshoot zdist JIBSPROJ3
			state setjib
		} else
		ifrnd 128
		{
			state aimjib
			ezshoot zdist JIBSPROJ4
			state setjib
		}
		else
		{
			state aimjib
			ezshoot zdist JIBSPROJ5
			state setjib
		}
	}
	
	ifactioncount 6
	{
		
		action ABARONDEAD
		espawn FLIES
		setactorvar[RETURN].myspawner THISACTOR
		cstat 0
		// setactor[THISACTOR].mdflags 16
		// setvar command 65536
	}
	break
}
ifrnd 1 ifvare burning 0 soundonce BARONROAM
ifai AIBARONSEEKENEMY state baronseekstate else
ifai AIBARONSHOOTENEMY state baronshootstate else
ifai AIBARONCAST state baroncaststate else
ifai AIBARONMELEE state baronmeleestate else
ifai AIBARONPAIN state baronpainstate

ifhitweapon
{
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifdead
	{
		ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE
		addkills 1 add globalkills 1
		state specialdeath
		
		move STOPPED
		action ABARONDIE
		sound BARONDEATH
		//ifvarl burning 0 spritepal 1
	}
	else
	{
		ifrnd 128 soundonce BARONPAIN1 else soundonce BARONPAIN2
		
		ifvarl fear 0 ai AIBARONPAIN else
		ifai AIBARONPAIN nullop else
		ifvarg bleeding 100 
		{ 
			ifrnd 128 ai AIBARONPAIN else
			ifai AIBARONSEEKENEMY ife peractor1 1 ai AIBARONSHOOTENEMY
		} 
		else
		ifrnd 4 ai AIBARONPAIN
		else ifrnd 32 ifai AIBARONSEEKENEMY ife peractor1 1 ai AIBARONSHOOTENEMY
	}

}
	
enda

// LUGLORD CODE
// his actor tile is the spearless tile 7522 to give him the correct hitbox


// state lugteststate
// ifai AILUGTESTAI
// {
	// ifcount 10
	// ifhitspace
	// {
		// resetcount
		// ifaction ALUGSTAND action ALUGRUN else
		// ifaction ALUGRUN action ALUGSTAB else
		// ifaction ALUGSTAB action ALUGSTARTSHOOT else
		// ifaction ALUGSTARTSHOOT action ALUGSHOOTING else
		// ifaction ALUGSHOOTING action ALUGSMASH else
		// ifaction ALUGSMASH action ALUGSMASHDONE else
		// ifaction ALUGSMASHDONE action ALUGLEAP else
		// ifaction ALUGLEAP action ALUGHURT else
		// ifaction ALUGHURT action ALUGSPINPREP else
		// ifaction ALUGSPINPREP action ALUGSPIN else
		// ifaction ALUGSPIN action ALUGDOWN else
		// ifaction ALUGDOWN action ALUGDYING else
		// ifaction ALUGDYING action ALUGDEAD else
		// ifaction ALUGDEAD action ALUGSTAND
	// }
// }
// ends

action ALUGSTAND -98 1 5 1 10
action ALUGRUN -93 4 5 1 10
action ALUGSTAB -73 2 5 1 20
action ALUGSTARTSHOOT -73 3 5 1 20
action ALUGSHOOTING -58 1 5 1 8
action ALUGSHOOTDONE -63 1 5 1 10
action ALUGSMASH -53 3 5 1 16
action ALUGSMASHDONE -43 1 5 1 20
action ALUGLEAP -38 1 5 1 20
action ALUGFALL -38 1 5 1 20
action ALUGHURT -33 1 5 1 18
action ALUGSPINPREP -28 1 5 1 40
action ALUGSPIN -23 1 5 1 10
action ALUGDOWN -18 2 5 1 40
action ALUGDYING -8 5 1 1 30
action ALUGDEAD -3 1 1 1 10

move LUGLEAPVEL 700 -208
move LUGFALLVEL 600
move LUGRUNVEL 360
move LUGRUNPALVEL 300

ai AILUGRUN ALUGRUN LUGRUNVEL seekplayer
ai AILUGLEAP ALUGLEAP LUGLEAPVEL geth getv
ai AILUGSMASH ALUGSMASH STOPPED geth
ai AILUGSPIN ALUGSPINPREP STOPPED geth
ai AILUGTESTAI ALUGSTAND STOPPED geth
ai AILUGWAIT ALUGSTAND STOPPED geth
ai AILUGDYING ALUGDYING STOPPED faceplayer
ai AILUGDOWN ALUGDOWN STOPPED geth
ai AILUGHURT ALUGHURT STOPPED geth
ai AILUGSHOOT ALUGSTARTSHOOT STOPPED geth

eventloadactor LUGLORD
ifspritepal 0 sizeat 80 80 // 64 64
else sizeat 40 40
cstator 257
action ALUGSTAND
enda

state lugmeleehit

	set tempb NO
	
	ifspritepal 0 { ifl xydist 2816 set tempb YES else ifn target -1 ifl xydist2 2816 set tempb YES }
	else { ifl xydist 2048 set tempb YES else ifn target -1 ifl xydist2 2048 set tempb YES }

	ife tempb YES
	{
		cos xvel angvar
		sin yvel angvar
		ifai AILUGSMASH
		{
			ifspritepal 0 set temp 60
			else set temp 40
		}
		else
		{
			ifspritepal 0 set temp 40
			else set temp 20
		}
		
		ife target -1
		{
			ifangdiffl 768 nullop else
			ifcansee
			{
				sound THUMPIN
				wackplayer
				seta[player[].i].htextra temp
				seta[player[].i].htpicnum RPG
				seta[player[].i].htang angvar
				seta[player[].i].htowner THISACTOR
				shiftvarl xvel 10
				shiftvarl yvel 10
				setplayer[THISACTOR].posxv xvel
				setplayer[THISACTOR].posyv yvel
				// push player backwards
				ifaction ALUGSPIN { sub ammo 1 ifle ammo 0 ai AILUGWAIT }
			}
		}
		else
		{
			sound THUMPIN
			shiftr xvel 1 shiftr yvel 1
			movesprite target xvel yvel 0 CLIPMASK0 RETURN
			seta[target].htextra temp
			seta[target].htpicnum RPG
			seta[target].htang angvar
			seta[target].htowner THISACTOR
			ifaction ALUGSPIN { sub ammo 1 ifle ammo 0 ai AILUGWAIT }
		}
	}
ends

state lugleapstate
	
	seta[].ang weap
	ifaction ALUGLEAP
	{
		set x initx
		set y inity
		sub x sprite[].x
		sub y sprite[].y
		getangle angvar x y
		seta[].ang angvar
		
		set x sprite[].x
		sub x initx
		mul x x
		
		set y sprite[].y
		sub y inity
		mul y y
		
		add x y
		sqrt x x
		
		set temp countvar
		div temp 2
		ifl x temp // more than halfway to destination
		{
			action ALUGFALL
			// globalsound CYBOSS_SWOOP
			move LUGFALLVEL geth
		}
		else
		ifcount 90 
		{
			ifceilingdistl 64 nullop else
			ifp phigher count 70
			
			ifcount 90
			{
				action ALUGFALL
				move LUGFALLVEL geth
			}
		}
	}
	else
	ifaction ALUGFALL
	{
		iffloordistl 128 nullop else
		ifvarand timer 1 state gradualturn
		iffloordistl 8
		{
			action ALUGSMASHDONE
			move STOPPED geth
			globalsound BIGTHUD
			quake 20
			ifspritepal 0
			{
				debris SCRAP1 4
				debris SCRAP2 4
				debris SCRAP3 4
				debris SCRAP4 4
				debris SCRAP5 4
				hitradius 5120 35 60 100 200
				ifl xydist2 7168
				{
					ifn target -1
					{
						geta[target].z z
						sub z 2048
						seta[target].z z
						seta[target].zvel -3072
					}
				}
				ifpdistl 7168
				{
					setp[].poszv -4096
					getp[].posz z
					sub z 1024
					setp[].posz z
				}
			}
			else 
			{
				hitradius 4096 25 40 60 100
				debris SCRAP1 2
				debris SCRAP2 2
				debris SCRAP3 2
				debris SCRAP4 2
				debris SCRAP5 2
			}
			
			set saveit 0
			whilevarn saveit 48
			{
				add saveit 1
				espawn SMALLSMOKE
				rand x 12288
				sub x 6144
				add x sprite[].x
				seta[RETURN].x x
				rand x 12288
				sub x 6144
				add x sprite[].y
				seta[RETURN].y x
			}

		}
	}
	else
	ifaction ALUGSMASHDONE
	{
		ifcount 30 ai AILUGWAIT
	}
	geta[].ang weap

ends



state lugfire

	sound STAFF_FIRE
	
	geta[].x saveit
	geta[].y saveit2
	geta[].z initz
	
	set z initz
	ifspritepal 0 add z 4096 else add z 2048
	seta[].z z
	
	getactor[THISACTOR].x mx
	addvar mx 480
	geta[].ang tempb, sub tempb 256
	rotatepoint sprite[].x sprite[].y mx sprite[].y tempb x y
	setsprite THISACTOR x y sprite[].z

	ifvare target -1 setvarvar target player[THISACTOR].i
	set xvel 1024
	state calczdist
	ldist temp THISACTOR target
	
	setprojectile[KORAXPROJ].vel 1024
	ifvare temp 0 setvar temp 1
	mulvar zdist 1024
	divvarvar zdist temp

	ife peractor1 2
	{
		ezshoot zdist KORAXPROJ
		seta[RETURN].picnum DRONE
		setav[RETURN].gold -1
		changespritestat RETURN 1
		setactor[RETURN].ang sprite[].ang
		setactor[RETURN].pal 11
		setav[RETURN].droptile MEDIUMPOTION
	}
	else
	ife peractor1 1
	{
		ifaction ALUGSPIN setprojectile[EXPLOSIVEBULLET].workslike 36866
		
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		shoot EXPLOSIVEBULLET
		
		setprojectile[EXPLOSIVEBULLET].workslike 32770
	}
	else
	{
		ezshoot zdist KORAXPROJ
		state projzveltotarg
	}
	
	geta[RETURN].ang angvar
	rand temp 96 sub temp 48 add angvar temp
	seta[RETURN].ang angvar
	
	setthisprojectile[RETURN].pal 11
	seta[RETURN].pal 11
	setprojectile[KORAXPROJ].vel 480
	seta[RETURN].xrepeat 40
	seta[RETURN].yrepeat 40
	setav[RETURN].target target
	
	ife target player[].i set target -1
	
	seta[].x saveit
	seta[].y saveit2
	seta[].z initz

ends

state lugshootstate

	state gradualturn

	ifaction ALUGSTARTSHOOT
	{
		state gradualturn
		ifactioncount 3
		{
			action ALUGSHOOTING
			// sound LUGFIRING
			state lugfire
		}
	}
	ifaction ALUGSHOOTING
	{
		ifactioncount 1
			action ALUGSHOOTDONE
	}
	
	ifaction ALUGSHOOTDONE
	{
		ifactioncount 1
		{
			set temp NO
			ifcount 60 nullop else set temp YES
			ife peractor1 2 ifcount 50 set temp NO
			ifn target -1 ifrnd 160 set temp YES else
			ifcansee ifrnd 160 set temp YES
			ife temp YES
			{
				action ALUGSHOOTING
				// sound LUGFIRING
				state lugfire
			}
			else ai AILUGWAIT
		}
	}

ends

state lugdyingstate
	ifaction ALUGDYING
	ifactioncount 5
	{
		// big thud sound
		action ALUGDEAD
		set burning 0
		quake 30
	}

ends

state lugdownstate

	ifl burning -1 set burning -1
	ifg burning 1 set burning 1
	ifl mtype 0
	{
		set inithp 10000
		ifhitweapon { }
		
		ifcount 150
		{
			sub mtype 50
			set temp mtype, abs temp
			seta[].extra temp
			ifspritepal 0 spritepal 33 else getlastpal
			ifge temp inithp
			{
				ifspritepal 33 getlastpal
				ai AILUGWAIT
				sound LUGRECOG
				seta[].extra inithp
				set mtype 0
			}
		}
		
		break
		// starting position
	}
	ifhitweapon { }
	// {
		// ifstrength 3000
		// {
			// ifspritepal 33 getlastpal
			// ai AILUGWAIT
			// sound LUGRECOG
		// }
	// }
	ifcount 300
	{
		ifspritepal 33 getlastpal else spritepal 33
	}
	ifcount 400 
	{ 
		ifspritepal 33 getlastpal
		ai AILUGWAIT
		sound LUGRECOG
	}

ends

state lugspinstate

	ife target -1 ifn cursed 0 { ai AILUGWAIT break }

	ifaction ALUGSPINPREP ifactioncount 1 
	{
		action ALUGSPIN
		geta[].ang weap
		set dodgeang weap
	}
	ifaction ALUGSPIN
	{
	
		set temp player[].player_par
			  modvar temp 5
			  
		ife temp 0
		{
			ifn target -1
			state lugfire
			else ifp palive
			state lugfire
		}
			  
		ife countvar 0 set countvar 48 else
		add countvar 3 // amount of angle change per tic
		add weap countvar
		seta[].ang weap
		
		ifvare target -1 setvarvar target player[THISACTOR].i
		getactor[target].x mx
		getactor[target].y my
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		subvarvar mx x
		subvarvar my y
		getangle angvar mx my
		
		getincangle temp angvar dodgeang
		ifg temp 24 set temp 24
		ifl temp -24 set temp -24
		sub dodgeang temp
		
		ifvarvare target player[THISACTOR].i setvar target -1
		
		set dodge countvar
		
		ifcount 170 
		{
			ai AILUGWAIT
			set countvar 0
			set dodge 0
		}
		else
			state lugmeleehit
		
	}

ends

state lugsmashstate

	state gradualturn
	ifaction ALUGSMASH
	{
		ifactioncount 2
		{
			sound BIGTHUD
			state lugmeleehit
			action ALUGSMASHDONE
			resetcount
			ifpdistl 6144 quake 15
			ifl xydist2 4096
			{
				ifn target -1
				{
					geta[target].z z
					sub z 2048
					seta[target].z z
					seta[target].zvel -2560
				}
			}
			ifpdistl 4096
			{
				setp[].poszv -3072
				getp[].posz z
				sub z 1024
				setp[].posz z
			}
			set saveit 0
			whilevarn saveit 24
			{
				add saveit 1
				espawn SMALLSMOKE
				rand x 6144
				sub x 3072
				add x sprite[].x
				seta[RETURN].x x
				rand x 6144
				sub x 3072
				add x sprite[].y
				seta[RETURN].y x
			}
			
		}
	}
	ifaction ALUGSMASHDONE
	{
		ifcount 8 ai AILUGWAIT
	}

ends

state lugattackeval

	// goal is to come out of this state with an attack
	
	ife target -1 ifn cursed 0 break

	ifn target -1 set xydist xydist2 // consolidate into one distance var
	ife target -1
	ifcansee ifp palive set target player[].i
	
	ife target -1 
	{
		ifoutside
		iffloordistl 8
		ifnotmoving
		{
			ai AILUGLEAP
			sound LUGJUMP
			getp[].posx initx
			getp[].posy inity
			geta[].ang weap
			ldist countvar THISACTOR player[].i
		}
		break
	}
	// at this point there is a target and xydist is the distance to it
	
	ifl xydist 3072
	{
		ifrnd 192
		{
			ai AILUGSMASH
			sound LUGATTACK
		}
		else
		{
			ai AILUGSPIN
			sound LUGSTARTSPIN
			set countvar 0
			set ammo 3
		}
	} 
	else
	{
		geta[].z zdist
		sub zdist sprite[target].z
		ifg zdist 16384 // target is significantly higher than you
		{
			ifrnd 128 iffloordistl 8
			{
				ai AILUGLEAP
				sound LUGJUMP
				geta[].ang weap
				ifn target -1
				{
					geta[target].x initx
					geta[target].y inity
					ldist countvar THISACTOR target
				}
				else
				{
					getp[].posx initx
					getp[].posy inity
					ldist countvar THISACTOR player[].i
				}
			}
			else
			{
				ai AILUGSHOOT
				sound LUGSHOOT
			}
		}
		else
		{
			ifrnd 180
			{
				ai AILUGSHOOT
				sound LUGSHOOT
			}
			else
			ifrnd 108
			{
				ai AILUGLEAP
				sound LUGJUMP
				geta[].ang weap
				ifn target -1
				{
					geta[target].x initx
					geta[target].y inity
					ldist countvar THISACTOR target
				}
				else
				{
					getp[].posx initx
					getp[].posy inity
					ldist countvar THISACTOR player[].i
				}
			}
			else
			{
				ai AILUGSPIN
				sound LUGSTARTSPIN
				set countvar 0
				set ammo 3
			}
		}
	}

ends

state lughurtstate

	ifcount 14 
	{
		ai AILUGWAIT
		state lugattackeval
	}

ends

state lugrunstate

	ifspritepal 0
	{
		ifactioncount 2
		{
			resetactioncount
			sound MONSTEP
			ifpdistl 4096 quake 10
		}
	}
	
	ifcount 240 { ai AILUGWAIT break }
	
	ifcansee
	ifpdistl 2560 state lugattackeval else
	ifn target -1
	ifl xydist2 2560 state lugattackeval
	
	ifai AILUGRUN
	{
		ifnotmoving
		{
			ifrnd 128 state lugattackeval
			else ai AILUGWAIT
			break
		}
		// check for cliff ahead
		getactor[THISACTOR].x x
		getactor[THISACTOR].y y
		getactor[THISACTOR].ang angvar
		
		setvarvar mx x
		addvar mx 1024
		
		rotatepoint x y mx y angvar mx my
		updatesector mx my mysector
		ifvarn mysector -1
		ifvarvarn mysector sprite[THISACTOR].sectnum
		{
			getflorzofslope mysector mx my temp
			getflorzofslope sprite[THISACTOR].sectnum x y tempb
			subvarvar temp tempb
			ifvarg temp 32768 // cliff
			{
				ifrnd 128
				{
					ai AILUGLEAP
					sound LUGJUMP
					geta[].ang weap
					ifn target -1
					{
						geta[target].x initx
						geta[target].y inity
						ldist countvar THISACTOR target
					}
					else
					{
						getp[].posx initx
						getp[].posy inity
						ldist countvar THISACTOR player[].i
					}
				}
				else state lugattackeval
			}
		}
		ifai AILUGRUN
		{
			ifg bleeding 8
			ifrnd 16 state lugattackeval
			else
			ifcount 60 ifrnd 4 state lugattackeval
		}
	}
ends

state lugwaitstate

	ifcount 6
	{
		state gradualturn
		ifn target -1 state lugattackeval else
		ifcansee ifpdistl 6144 state lugattackeval else
		ifrnd 192 ai AILUGRUN
		else state lugattackeval
	}

ends

useractor enemy LUGLORD BOSS4STRENGTH ALUGSTAND

ifai 0 
{
	set gold GOLD5
	sound LUGRECOG
	ai AILUGWAIT
	
	cstator 257
	geta[].htflags temp
	orvar temp 1048576
	orvar temp 8192
	seta[].htflags temp
	ifspritepal 0 
	{
		sizeat 80 80 
		clipdist 180
		set mtype -1
		ai AILUGDOWN
		
		set gold GOLD6
		strength 100
		espawn REVERSERIFT
		setactorvar[RETURN].myspawner THISACTOR
		getactor[RETURN].z z
		subvar z 12288
		setactor[RETURN].z z
		set bossid THISACTOR
		set monid THISACTOR
		set monidcount 0
		starttrackslot 1 1
		set music2 1

	}
	else { sizeat 40 40 clipdist 120 }
}

fall
sleeptime -1

ifai AILUGDOWN { state lugdownstate break }

state monsterai

ifspritepal 0
ife bossid -1 
ifpdistl 16384
ife monstatus 1
{
	set bossid THISACTOR
	set monid THISACTOR
	set monidcount 0
	starttrackslot 1 1
	set music2 1
}

ifai AILUGDYING { state lugdyingstate break }


ifai AILUGSPIN state lugspinstate else
ifai AILUGHURT state lughurtstate else
ifai AILUGSHOOT state lugshootstate else
ifai AILUGLEAP state lugleapstate else
ifai AILUGSMASH state lugsmashstate else
ifai AILUGRUN state lugrunstate else
ifai AILUGWAIT state lugwaitstate

ifhitweapon 
{
	ifwasweapon BURNING nullop else
		state random_wall_jibs
	ifactorsound THISACTOR LUGPAIN nullop else
	ifactorsound THISACTOR LUGPAIN2 nullop else
	{
		ifrnd 128 sound LUGPAIN else
		sound LUGPAIN2
	}
	ifdead
	{
		ifn sprite[].pal 0 set peractor1 3
			
		set countvar 0
		add peractor1 1
		ifg peractor1 2
		{
			ifwasweapon FREEZEBLAST
				spawn BIGSMOKE
			addkills 1 add globalkills 1
			state specialdeath
			
			ifg burning 0 set burning 0
			ai AILUGDYING
			cstat 0
			globalsound LUGDYING
			espawn SPEARSPIN
			seta[RETURN].pal sprite[].pal
		}
		else
		{
			ai AILUGDOWN
			strength 100
			set mtype -1
		}
	}
	else 
	{
		ifg bleeding 150
		ifrnd 40
		ai AILUGHURT
		else
		ifg bleeding 8
		ifn sprite[].htpicnum BURNING
		ifn sprite[].htpicnum ARROWPROJ
		ifrnd 4
		ai AILUGHURT
	}
	
	
}

enda

// The slope of a sprite takes up the same position as X and Y offset, where Y offset is the multiple of 256 and X offset 0-255 is precision down // to single number, i.e. offset 40, 4 would be 40 + 1024 = 1064.
useractor notenemy SPEARSPIN 0

	ifmove 0
	{
		move INIT
		ifspritepal 0 sizeat 80 80 else sizeat 40 40
		// cstat 48
		cstator 32
		cstator 16
		cstator 128
		geta[].ang angvar
		add angvar 1024
		seta[].ang angvar
		set countvar 32
		geta[].z z sub z 16384 seta[].z z
	}
	
	ifmove INIT
	{

		geta[].xoffset x
		add x countvar
		geta[].yoffset y
		add y countvar
		
		ifg x 127 // 111 
		{
			geta[].ang angvar
			add angvar 1024
			seta[].ang angvar
			cstator 4
			set countvar -32
			set x 127
			set y 127
		}
		else
		ifl x -127 // -111
		{
			geta[].ang angvar
			add angvar 1024
			seta[].ang angvar
			cstat 128
			cstator 32
			cstator 16
			set countvar 32
			set x -127
			set y -127
		}	
		seta[].xoffset x
		seta[].yoffset y

	}
	iffloordistl 4
	{
		move STOPPED geth
		cactor 7520
	}
	else
	{
		geta[].z z
		add z 768
		seta[].z z
	}


enda

action MINICACFLY -5 4 5 1 8
action MINICACATTK 21  1  5  1  10
action EBATDIE1 32 5 1 1 14

move EBATFLY 256
move EBATFAST 512

state minicacodie
	addkills 1 add globalkills 1
		state specialdeath
	action EBATDIE1
	cstat 0
	move STOPPED
	spawn BLUDSPLASH
	spawn BLOODCLOUD
	spawn BLOODCLOUD
	spawn BLOODCLOUD
	spawn BLOODCLOUD
	hitradius 2048 15 20 25 30
	sound EXPL6

ends

spritenoshade MINICACO
useractor enemy MINICACO 20 MINICACFLY

ifaction EBATDIE1
{
	ifvarand timer 1 spawn BLOODCLOUD
	ifactioncount 5 killit
	break
}

ifmove 0
{
	cstat 256
	seta[].alpha 0
	seta[].shade -127
	seta[].zvel 0
	sizeat 18 18
	spritepal 2
	sound MINICACSND
	move EBATFAST faceplayerslow
}

ifcount 10
{
	ifn target -1 
	{ 
		ifl xydist2 4096 action MINICACATTK 
		ifl xydist2 1408 state minicacodie
	}
	else ifp palive ifpdistl 4096 
	{
		action MINICACATTK
		ifcansee
		ifpdistl 1408 state minicacodie
	}
	else action MINICACFLY
}

ife target -1 set target player[].i
getactor[].z z
getactor[target].z mz, sub mz 8192
subvarvar mz z
shiftr mz 3
movesprite THISACTOR 0 0 mz CLIPMASK0 RETURN
ife target player[].i set target -1

ifrnd 1 ifrnd 64 soundonce MINICACSND

state monsterai
ifn cursed 0 { set cursed 0 seta[].htextra 50 seta[].htpicnum RPG }
ifhitweapon 
{
	ifwasweapon FREEZEBLAST
		    spawn BIGSMOKE

	ifwasweapon RADIUSEXPLOSION strength 5
	ifdead
	{
		state minicacodie
		break
	}
}


ifrnd 128
{
	espawn STICKYFIRE
	setav[RETURN].monstatus sprite[].picnum
	setav[RETURN].myspawner THISACTOR
	changespritesect RETURN sprite[].sectnum
	
	ifrnd 1 soundonce BATROAM
}

enda

eventloadactor COFFIN
	geta[].lotag activator
	seta[].lotag 0
	geta[].hitag droptile
	seta[].hitag 0
	ifn sprite[].extra -1
	geta[].extra mtype
	set initx sprite[].x
	set inity sprite[].y
	cstator 257
	espawn COFFINLID
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	seta[RETURN].pal sprite[].pal
	seta[RETURN].ang sprite[].ang
	geta[].owner monstflags
	ife monstflags -1 set monstflags 0
	set myspawner RETURN
enda

useractor notenemy COFFIN 1000

	ifmove 0
	{
		move STOPPED
		set temp sprite[].xrepeat
		mul temp 2, ifg temp 255 set temp 255
		seta[].clipdist temp
	}
	ife myspawner -1 break
	
	ifn sprite[].pal 3
	ife activator 0
	ife peractor1 NO
	ifp pfacing
	ifp palive
	ifpdistl 1792
	ifhitspace
		set peractor1 YES
		
	ife peractor1 YES // just triggered
	{
		sound DUMPSTER_MOVE
		set peractor1 2
		
		seta[].clipdist sprite[].xrepeat
	}
	ifg peractor1 1
	ifl peractor1 33
	{
		// add angle to lid and move the lid to the right
		geta[myspawner].ang angvar
		add angvar 16
		seta[myspawner].ang angvar
		
		getactor[].ang angvar
		addvar angvar 256
		setvarvar mx sprite[myspawner].x
		set temp sprite[myspawner].xrepeat
		div temp 3, ifl temp 4 set temp 4
		add mx temp
		rotatepoint sprite[myspawner].x sprite[myspawner].y mx sprite[myspawner].y angvar mx my
		setactor[myspawner].x mx
		setactor[myspawner].y my
		add peractor1 1
		ife peractor1 12
		{
			espawn droptile
			ifn monstflags 0 setav[RETURN].aura monstflags
			seta[RETURN].pal sprite[].pal
			ifn mtype 0 setav[RETURN].activator mtype
			// setav[RETURN].mysignpost THISACTOR
			geta[].z z
			sub z 2048
			seta[RETURN].z z
			seta[RETURN].htbposz z
		}
		ife peractor1 33 seta[].clipdist sprite[].xrepeat
	}
	else
	ifl peractor1 0
	{
		ife peractor1 -1
		{
			seta[myspawner].ang sprite[].ang
			seta[myspawner].x sprite[].x
			seta[myspawner].y sprite[].y
			sound MONSTEP
		}
		else
		{
			geta[myspawner].ang angvar
			sub angvar 16
			seta[myspawner].ang angvar
			
			getactor[].ang angvar
			subvar angvar 768
			setvarvar mx sprite[myspawner].x
			set temp sprite[myspawner].xrepeat
			div temp 3, ifl temp 4 set temp 4
			add mx temp
			rotatepoint sprite[myspawner].x sprite[myspawner].y mx sprite[myspawner].y angvar mx my
			setactor[myspawner].x mx
			setactor[myspawner].y my
			add peractor1 1
		}
	}
	ifspritepal 3 
	{
		ife peractor1 33 
		{
			add countvar 1
			ifge countvar 300
			{
				set peractor1 -33
				set countvar 0
			}
		}
		else
		ife peractor1 -1
		{
			add countvar 1
			ifge countvar 900
			{
				set peractor1 0
				set countvar 0
			}
		}
	}
enda

state sidegundeath

	state randsquishsound
	guts JIBS6 3
	guts JIBS3 3
	spritepal 2
	spawn BLUDSPLASH
	spawn BLOODCLOUD
	spawn BLOODCLOUD
    killit
	
ends

action ASIDEGUNIDLE  0  1  5  1  16
action ASIDEGUNSHOOT 5  1  5  1  40

move SIDEGUNSHOOT

useractor enemy SIDEGUN 80 ASIDEGUNIDLE

ifmove 0
{
	orvar monstflags 1
	move STOPPED geth
	// ife myspawner -1 
	// {
		// set myspawner player[].i
		// findnearactor SIDEGUN 3072 spriteid
		// ifn spriteid -1 ife actorvar[spriteid].myspawner player[].i
		// set botclip 384 else set botclip -384
		// rand mtype 4
	// }
	
	getp[].max_actors_killed temp
	sub temp 1
	setp[].max_actors_killed temp
	
	set temp actorvar[myspawner].inithp
	div temp 4
	add temp 30
	seta[].extra temp
	cstat 256
	ife myspawner player[].i clipdist 8
	set lastang sprite[myspawner].ang
	set countvar 100
	
	geta[].mdflags temp
	orvar temp 16
	seta[].mdflags temp
	
	set countvarb 2000
}

seta[].ang lastang
getav[myspawner].cursed cursed
state monsterai
seta[].alpha sprite[myspawner].alpha

ifg sprite[].htextra 0
{
	set spriteid sprite[].htowner
	ifn spriteid -1
	{
		ife spriteid myspawner seta[].htextra -1
		ife spriteid THISACTOR seta[].htextra -1
		ife sprite[spriteid].picnum SIDEGUN seta[].htextra -1
	}
}
ifhitweapon
{
	ifdead state sidegundeath
}
ifn myspawner player[].i
{
	ifn actorvar[myspawner].monstatus 1 state sidegundeath else
	ife sprite[myspawner].statnum 1024 state sidegundeath
}
else
{
	ifl countvarb 0 state sidegundeath
	ifp palive nullop else state sidegundeath
}

set mx tiledata[sprite[myspawner].picnum].xsize
mul mx sprite[myspawner].xrepeat
shiftr mx 2 // distance from center
ifg mx 4096 set mx 4096

set x mx
shiftr x 5, add x 8, ifl x 8 set x 8
seta[].xrepeat x
mul x 3 div x 4
seta[].yrepeat x

// seta[].clipdist x

set my tiledata[sprite[myspawner].picnum].ysize
mul my sprite[myspawner].yrepeat
shiftl my 1 // distance from base of owner

// put it in z position now
geta[myspawner].z z
sub z my
ife myspawner player[].i sub z 1024
seta[].z z

// calculate position gun is supposed to be in

geta[myspawner].ang angvar
add angvar ammo

geta[myspawner].x initx
add initx mx

rotatepoint sprite[myspawner].x sprite[myspawner].y initx sprite[myspawner].y angvar x y

// setsprite THISACTOR x y z
set saveit x
set saveit2 y

// now calculate angle to make gun move towards that position

sub x sprite[].x
sub y sprite[].y
getangle angvar x y

// now calculate how far gun is from that position currently

set xydist3 sprite[].x
sub xydist3 saveit
mul xydist3 xydist3
set initx sprite[].y
sub initx saveit2
mul initx initx
add xydist3 initx
sqrt xydist3 xydist3

// now move towards the correct position with no clipping based on distance

ifg xydist3 3072 setsprite THISACTOR saveit saveit2 z
else
ifg xydist3 256
{
	cos xvel angvar
	sin tempb angvar
	
	// div by 128 for 1024 distance
	set tempe 3072 // maximum division
	sub tempe xydist3
	shiftr tempe 5
	ifl tempe 4 set tempe 4
	
	div xvel tempe
	div tempb tempe

	movesprite THISACTOR xvel tempb 0 0 RETURN
	
	ife myspawner player[].i
	ifn RETURN 0
	ifpdistl 1024
	{
		setsprite THISACTOR saveit saveit2 sprite[].z
	}
}
geta[].ang lastang


set topladder NO

getav[myspawner].target target

set xydist3 actorvar[myspawner].inithp
shiftl xydist3 1
add xydist3 20480


ifn target -1
	ifl xydist2 xydist3
	set topladder YES
else
	ifp palive
	ifl xydist xydist3
	ifcanshoottarget
	ife cursed 0
	{
		set topladder YES
		set target player[].i
	}
	
ife target -1 seta[].ang sprite[myspawner].ang

ifmove STOPPED
ifg countvar 100
ife topladder YES
	move SIDEGUNSHOOT geth

ifmove STOPPED
{
	ifaction ASIDEGUNSHOOT action ASIDEGUNIDLE
	ifle countvar 120 add countvar 1
}

ifmove SIDEGUNSHOOT
{
	ifaction ASIDEGUNIDLE
	{
		ife topladder YES
		ifg countvar 0
		nullop else
		{
			move STOPPED geth
			break
		}
	
		ifactioncount 1
		{
			geta[].x saveit
			geta[].y saveit2
			set x saveit
			add x 384
			
			rotatepoint sprite[].x sprite[].y x sprite[].y sprite[].ang mx my
			setsprite THISACTOR mx my sprite[].z
	
			
			action ASIDEGUNSHOOT
			
			sub countvar 60
			sub countvarb 60
			
			sound ARCHONSHOOT2
			ife target -1 set target player[].i
			
			state calczdist
			ldist temp THISACTOR target
			ifvare temp 0 setvar temp 1
			mulvar zdist 480
			divvarvar zdist temp
			
			
			setprojectile[DARKPROJ].offset -160
			ifl actorvar[myspawner].inithp 1000
			setprojectile[DARKPROJ].extra 10
			ezshoot zdist DARKPROJ
			seta[RETURN].ang sprite[].ang
			seta[RETURN].owner myspawner
			setprojectile[DARKPROJ].offset 448
			setprojectile[DARKPROJ].extra 20
			setav[RETURN].target target
			seta[RETURN].pal 10
			state projzveltotarg
				
			
			seta[].x saveit
			seta[].y saveit2
		}
	}
	ifaction ASIDEGUNSHOOT
	{
		ifactioncount 1 action ASIDEGUNIDLE
	}
}

enda
