/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

spriteflags PATCOS SFLAG_NODAMAGEPUSH

damageeventtile 19024
damageeventtile PATCOS

action A_PATCOS_STAND 0 1 5 1 0
action A_PATCOS_WALK 5 4 5 1 16
action A_PATCOS_ATTACK 25 1 5 1 12
action A_PATCOS_DYING 50 5 1 1 24
action A_PATCOS_DEAD 54 3 1 1 24

move PATCOS_RUN 160 0

ai AI_PATCOS_STAND A_PATCOS_STAND STOP faceplayer
ai AI_PATCOS_WALK A_PATCOS_WALK PATCOS_RUN seekplayer
ai AI_PATCOS_ATTACK A_PATCOS_ATTACK STOP faceplayer
ai AI_PATCOS_PUSH A_PATCOS_STAND STOP faceplayer
ai AI_PATCOS_ENDFIGHT A_PATCOS_STAND STOP faceplayer

spritenoshade 18157

useractor notenemy 18157
ifaction 0
	{
	ifg SKILL_LEVEL 2 killit
	seta[].shade -127
	cstat 403
	seta[].blend 255
	sizeat 4 4
	action MINUSONE
	set INTERNALCOUNT 9
	}

ifaction MINUSONE
	{
	sizeto 64 64
	set temp3 sprite[myowner].x
	add temp3 128

	rotatepoint sprite[myowner].x sprite[myowner].y temp3 sprite[myowner].y sprite[myowner].ang temp5 temp6

	updatesector temp5 temp6 temp7
	ife temp7 -1 nullop else
		{
		seta[].x temp5
		seta[].y temp6
		}
	geta[myowner].z temp
	sub temp 14000
	seta[].z temp
	add INTERNALCOUNT 1
	ifspritepal 8
		{
		ifg INTERNALCOUNT 30 killit
		}
	ifg INTERNALCOUNT 55 spritepal 21
	ifg INTERNALCOUNT 80 killit
	}

enda

useractor notenemy 19029
ifaction 0
	{
	sizeat 32 32
	cstat 272
	spritepal 6
	action ZERO
	}

ifaction ZERO
	{
	set temp3 sprite[myowner].x
	add temp3 512

	rotatepoint sprite[myowner].x sprite[myowner].y temp3 sprite[myowner].y sprite[myowner].ang temp5 temp6

	seta[].ang sprite[myowner].ang

	updatesector temp5 temp6 temp7

	geta[myowner].z temp
	sub temp 4096

	setsprite THISACTOR temp5 temp6 temp

	ifg PATCOS_CHARGE 0
	cstat 32768
	else
	cstat 272

	}
enda

spriteshadow 19024

useractor notenemy 19024

ifaction 0
	{
	sizeat 20 20
	cstat 257
	checkactivatormotion 827
	 ife RETURN 1
		{
		sound PATCOS_START
		action ZERO
		resetactioncount
		}
	}

ifaction ZERO
	{
	ifactioncount 30 { action ONE resetactioncount }
	}

ifaction ONE
	{
	ifactioncount 30 { action TWO resetactioncount }
	}

ifaction TWO
	{
	ifactioncount 30
		{
		state monst_glow
		sizeat 20 20
		cstat 257
		set PATCOS_CHARGE 0
		spawn 19029
		strength 5000
		cactor PATCOS
		ai AI_PATCOS_STAND
		}
	}

enda

useractor enemy PATCOS
fall

ifaction 0
{
	state monst_glow
	sizeat 20 20
	set actor_type TYPE_CASE_SPECIFIC
	cstat 257
	set PATCOS_CHARGE 0
	spawn 19029
	ifspawnedby RESPAWN { spawn TRANSPORTERSTAR sound SOMETHINGHITFORCE }
	ifspawnedby APLAYER ai AI_PATCOS_WALK else ai AI_PATCOS_STAND
	strength 5000
}

geta[].extra temp2

ifai AI_PATCOS_ENDFIGHT
{
	ifn HITAGSAVED 0
	{
		operateactivators HITAGSAVED THISACTOR
		operatemasterswitches HITAGSAVED
		operaterespawns HITAGSAVED
	}
	else
	{
		operateactivators 5108 THISACTOR
		operatemasterswitches 5108
		operaterespawns 5108
	}
	ife VOLUME 3 { ifand ELYSION_QUEST 4096 nullop else xor ELYSION_QUEST 4096 savegamevar ELYSION_QUEST }
	killit
}
else ifdead
	nullop
else ifai AI_PATCOS_STAND
{
	ifn LOTAGSAVED 0
		checkactivatormotion LOTAGSAVED
	else
		checkactivatormotion 10018

	// checkactivatormotion sets RETURN to 1 if that activator is in motion
	ife RETURN 1
	{
		globalsound PATCOS_1
		quake 26
		ife VOLUME 1 nullop else set CURBOSS THISACTOR
		ai AI_PATCOS_WALK
	}
}
else ifai AI_PATCOS_WALK
{

	ifge VOLUME 3
		{
		ifg INTERNALCOUNT_2 0 sub INTERNALCOUNT_2 1
		ifstrength 3000
		 ifrnd 16
		  ife INTERNALCOUNT_2 0
			{
			ifstrength 1000 shoot HOMING_MISSILE else shoot MICRO_MISSILE
			sound CY_FIGHTER_MISSILE
			set INTERNALCOUNT_2 90
			}
		}

	ifcansee
	{
		ifrnd 8 state enemyfindtarget
		add INTERNALCOUNT 1
			ifg INTERNALCOUNT 19
			{
				ifrnd 128 sound PATCOS_STEP else sound PATCOS_STEP
				ifpdistl 2048 quake 26
				set INTERNALCOUNT 0
			}

		ife PATCOS_CHARGE 0
			ifrnd 2
			{
				ifsound PATCOS_2 nullop
				else
				ifsound PATCOS_1 nullop
				else
				{
					globalsound PATCOS_1
					quake 26
				}
			}


		ifpdistl 1024
			ifrnd 32
				ifcansee
					ai AI_PATCOS_PUSH

		ifg PATCOS_CHARGE 0 sub PATCOS_CHARGE 1
			ife PATCOS_CHARGE 1 { globalsound PATCOS_2 quake 26 espawn 18157 seta[RETURN].pal 8 }

		ifrnd 4
		 ife PATCOS_CHARGE 0
			{
			ifcansee
			 ifcanseetarget
			  ifpdistl 16384
					{
					globalsound PATCOS_4
					spawn 18157
					ai AI_PATCOS_ATTACK
					resetactioncount
					quake 26
					}
			else
			ifn target -1
				ifl xydist 16384
					{
					globalsound PATCOS_4
					spawn 18157
					ai AI_PATCOS_ATTACK
					resetactioncount
					quake 26
					}
			}
	}
	else
	{

	}
}
else ifai AI_PATCOS_ATTACK
{
	ifactioncount 14
	{
		ifl ROLL_LEFT 13 nullop
		else ifl ROLL_RIGHT 13 nullop
		else ifl ROLL_FORWARDS 26 nullop
		else
		{
			ifl SKILL_LEVEL 3
			{
				ifcansee
				{
					getp[].posz temp
					sub temp 1024
					setp[].posz temp
					getp[].ang knockbackang
					set knockbackheight -4096
					set knockback2 16
					sound SPUNCHHIT
					addphealth -5
					wackplayer
				}
			}
			else
			{
				getp[].posz temp
				sub temp 1024
				setp[].posz temp
				getp[].ang knockbackang
				set knockbackheight -4096
				set knockback2 16
				sound SPUNCHHIT
				addphealth -10
				wackplayer
			}

		}
		set PATCOS_CHARGE 120
	ai AI_PATCOS_WALK
	}
	ife target -1
		{
		ifcansee ifcanseetarget nullop else
		ai AI_PATCOS_WALK
		}
	ifg xydist 16384 ai AI_PATCOS_WALK
}
else ifai AI_PATCOS_PUSH
{
	ifactioncount 1
	{
		getp[].posx mx
		getp[].posy my
		geta[].x x
		geta[].y y
		sub mx x
		sub my y
		getangle knockbackang mx my
		set knockback 6
		sound FORCE_BLAST
		ifpdistl 1024
			{
			wackplayer
			palfrom 20 30 0 0
			addphealth -15
			}
		ai AI_PATCOS_WALK
		resetactioncount
	}
}

	ifaction A_PATCOS_DYING
	{
		ifrnd 32 sound SHORT_CIRCUIT

		geta[].z z
		add z 4096
		seta[].z z
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		sub z 4096
		seta[].z z

		move STOP faceplayer
		strength 0

		add INTERNALCOUNT 1
		ife INTERNALCOUNT 30
		{
			set INTERNALCOUNT 0
			action A_PATCOS_DEAD
		}
	}
	else ifaction A_PATCOS_DEAD
	{
		ifrnd 32 sound SHORT_CIRCUIT

		geta[].z z
		add z 4096
		seta[].z z
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		sub z 4096
		seta[].z z

		move STOP faceplayer
		add INTERNALCOUNT 1
		ife INTERNALCOUNT 90
		{
			ifn HITAGSAVED 0
			{
				operateactivators HITAGSAVED THISACTOR
				operatemasterswitches HITAGSAVED
				operaterespawns HITAGSAVED
			}
			else
			{
				operateactivators 5108 THISACTOR
				operatemasterswitches 5108
				operaterespawns 5108
			}
			ife VOLUME 3 { ifand ELYSION_QUEST 4096 nullop else xor ELYSION_QUEST 4096 savegamevar ELYSION_QUEST }
			spawn TRANSPORTERSTAR
			sound SOMETHINGHITFORCE
			killit
		}
	}

	ifhitweapon sound 4 // does nothing but does not return an error like nullop

	ifdead
	{
		// No hitagsaved -> Episode 2 fight, don't mess with it
		ifn HITAGSAVED 0
		{
			ifai AI_PATCOS_ENDFIGHT nullop
			else ai AI_PATCOS_ENDFIGHT
		}
		// Else => episode 4 fight, have some animation
		else
		{
			ifaction A_PATCOS_DYING nullop
			else ifaction A_PATCOS_DEAD nullop
			else ifai AI_PATCOS_ENDFIGHT nullop
			else
			{
				set INTERNALCOUNT 0
				action A_PATCOS_DYING
			}
		}
	}


enda
