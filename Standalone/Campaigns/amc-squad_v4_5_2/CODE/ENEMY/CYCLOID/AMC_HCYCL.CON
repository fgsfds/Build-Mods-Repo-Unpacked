/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Based on the Duke3D CON files by 3DRealms, released under the GPL2 at:
https://github.com/videogamepreservation/dukenukem3d/blob/ef609159bd352985a0c63734a11241d86c873fe5/testdata/GAME.CON

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile BOSS5

action ABOSS5IDLE        48  1  5  1  20
action ABOSS5WALK        0  4  5  1  30
action ABOSS5FROZEN        0  1  5
action ABOSS5RUN         0  4  5  1  15
action ABOSS5SHOOT         20 2  5  1  15
action ABOSS5LOB         30 2  5  1  105
action ABOSS5DYING        40 7  1  1  35
action BOSS5FLINTCH       40 1  1  1  1
action ABOSS5DEAD         47

move PALBOSS5SHRUNKRUNVELS 32

ai AIBOSS5SEEKENEMY ABOSS5WALK BOSS2WALKVELS seekplayer
ai AIBOSS5RUNENEMY ABOSS5RUN BOSS2RUNVELS faceplayer
ai AIBOSS5SHOOTENEMY ABOSS5SHOOT STOP faceplayer
ai AIBOSS5LOBBED ABOSS5LOB STOP faceplayer
ai AIBOSS5DYING ABOSS5DYING STOP faceplayer
ai AIBOSS5PALSHRINK ABOSS5WALK PALBOSS5SHRUNKRUNVELS furthestdir

defstate BOSS5palshrunkstate
  ifcount SHRUNKDONECOUNT
  {
    cstat 257
    ai AITROOPSEEKENEMY
  }
  else
    ifcount SHRUNKCOUNT
	  {
	  sound ACTOR_UNSHRINK
      sizeto 40 40
	  }
  else
    state genericshrunkcode
ends

defstate checkBOSS5seekstate
  ai AIBOSS5SEEKENEMY
  ifspritepal 0 nullop
  else   // a fake way of doing a ifspritepal NOT.
    move PALBOSS2RUNVELS seekplayer
ends

defstate BOSS5runenemystate
  ifcansee
  {
    ifactioncount 3
    {
      ifcanshoottarget
      {
	   quake 6
        resetactioncount
        ifrnd 128 sound BIG_BOSS_WALK2 else sound BIG_BOSS_WALK1
      }
      else
        ai AIBOSS5SEEKENEMY
    }
    ifcount 48
      ifrnd 2
    {
      ifp palive
      {
        sound BOS2_ATTACK
        ai AIBOSS5SHOOTENEMY
      }
      break
    }
  }
  else
    ai AIBOSS5SEEKENEMY
ends

defstate BOSS5seekenemystate
  ifrnd 2
    soundonce BOS2_ROAM
  else
    ifactioncount 3
  {
      quake 6
    resetactioncount
    ifrnd 128 sound BIG_BOSS_WALK2 else sound BIG_BOSS_WALK1
  }

  ifcansee
    ifcount 32
      ifp palive
        ifrnd 48
          ifcanshoottarget
  {
    ifrnd 64
      ifpdistg 4096
    {
      ai AIBOSS5RUNENEMY
      ifspritepal 0 nullop
      else
        move PALBOSS2RUNVELS seekplayer
      break
    }

    ifpdistl 10240
    {
      ifrnd 128
      {
        sound BOS2_ATTACK
        ai AIBOSS5LOBBED
      }
    }
    else
    {
      sound BOS2_ATTACK
      ai AIBOSS5SHOOTENEMY
    }
  }
ends

defstate BOSS5dyingstate
  ifaction ABOSS5DEAD
  {
    ifspritepal 0
      break
    {
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        state squish_sounds
        guts JIBS2 5              // Instead of the normal "standard_jibs"
        guts JIBS3 5              // amount, I found that ,because these
        guts JIBS4 5              // bodies are so huge, they should spawn
        guts JIBS5 15             // a more realistic amount of gutter!
        guts JIBS6 15
        debris SCRAP1 5           // There are also spawned some metal
        debris SCRAP2 5           // scraps, since this boss has a lot
        debris SCRAP5 5           // of metalware with him.
        state standard_jibs
        killit
      }
      break
    }
  }
  ifactioncount 7
  {
  ifpdistl 8192 wackplayer
    iffloordistl 8
      sound BOSSFALL
	ifspritepal 0 quake 52
    action ABOSS5DEAD
    cstat 0
    ifspritepal 0
      {
	ifn VOLUME 5
		{
		state save_arrays
		state fatigue_system
		state budget_system
		}
	}
  }
ends

defstate BOSS5lobbedstate
  ifcansee
  {
    ifactioncount 2
      resetactioncount
    else
      ifactioncount 1
    {
      ifrnd 128
        shoot COOLEXPLOSION1
    }
    else
      ifcount 64
        ifrnd 16
          state checkBOSS5seekstate
  }
  else
    state checkBOSS5seekstate
ends

defstate BOSS5shootenemy
  ifcount 72
    state checkBOSS5seekstate
  else
    ifaction ABOSS5SHOOT
      ifactioncount 2
  {
    sound ERPG_SHOOT
    shoot RPG
    resetactioncount
  }
ends

defstate checkBOSS5hitstate
set CURBOSS THISACTOR
  ifrnd 2
    spawn BLOODPOOL
  ifdead
  {
      ifg ice_damage 0
      {
        sound SOMETHINGFROZE
        spritepal 1
        move 0
        action ABOSS5FROZEN
        strength 0
        break
      }
    sound BOS2_DYING
    addkills 1
    ai AIBOSS5DYING
  }
  else
  {
    ifrnd 144
    {
      ifrnd 32
	  ifstrength 2000
      {
        action BOSS5FLINTCH
        move 0
      }
      else
      {
        sound BOS2_ATTACK
        ai AIBOSS5SHOOTENEMY
      }
    }

    ifspritepal 0 nullop
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      ai AIBOSS5PALSHRINK
      break
    }

    soundonce BOS2_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends

defstate BOSS5code

  ifaction ABOSS5FROZEN
	  {
	  state frozen_code
	  break
	  }
  ifai 0
  {
	state monst_glow
  cstat 257
  globalsound BOS2_RECOG
  sizeat 75 75
  spawn SHOOTME
    ifspritepal 0
	  {
	  set CURBOSS THISACTOR
	  set BOSS_TYPE 1
      ai AIBOSS5RUNENEMY
	  }
    else
    {
      strength 1
      sound BOS2_ATTACK ai AIBOSS5SHOOTENEMY
    }
  }
  else
    ifaction BOSS5FLINTCH
  {
    ifactioncount 3
      ai AIBOSS5SEEKENEMY
  }
  else
    ifai AIBOSS5SEEKENEMY
      state BOSS5seekenemystate
  else
    ifai AIBOSS5RUNENEMY
      state BOSS5runenemystate
  else
    ifai AIBOSS5SHOOTENEMY
      state BOSS5shootenemy
  else
    ifai AIBOSS5LOBBED
      state BOSS5lobbedstate
  else
    ifai AIBOSS5PALSHRINK
      state BOSS5palshrunkstate

  ifai AIBOSS5DYING
    state BOSS5dyingstate
  else
  {
geta[].htextra temp6
ifhitweapon
	{
	seta[].xvel 0
	state checkBOSS5hitstate
	}
    else
      ifp palive
        ifspritepal 0
		ifpdistl 1280
		 ifcansee
    {
      addphealth -1000
      palfrom 63 63
    }
  }


ends

useractor enemy BOSS5 9000 ABOSS5IDLE fall state BOSS5code enda
