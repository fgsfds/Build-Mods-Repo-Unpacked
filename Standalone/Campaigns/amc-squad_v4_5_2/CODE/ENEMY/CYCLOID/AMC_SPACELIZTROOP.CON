/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile SPACE_LIZTROOP

action ALIZ_SPACE_FLY 5 1 5 1 20
action ALIZ_SPACE_BOOST 0 1 5 1 20
action ALIZ_SPACE_FIRE 5 2 5 1 20
action ALIZ_SPACE_PAIN 15
action ALIZ_SPACE_DIE 15 5 1 1 12
action ALIZ_SPACE_DEAD 19
action ALIZ_SPACE_DEAD2 19

move DRIFT 1 1
move SPACE_MOVE 70
move SPACE_BOOST 210 25

ai AI_SPACELIZ_FLY ALIZ_SPACE_FLY SPACE_MOVE seekplayer
ai AI_SPACELIZ_BOOST ALIZ_SPACE_BOOST SPACE_BOOST faceplayer
ai AI_SPACELIZ_FIRE ALIZ_SPACE_FIRE SPACE_MOVE faceplayerslow
ai AI_SPACELIZ_DIE ALIZ_SPACE_DIE STOP

useractor enemy SPACE_LIZTROOP

ifaction 0
{
cstat 257
state monst_glow
set actor_type TYPE_CASE_SPECIFIC
ifspritepal 0 spritepal 11
sizeat 26 26
sound PRED_RECOG
strength 100
spriteflags NORMAL_ENEMY
ai AI_SPACELIZ_FLY
}

state ENEMYKNOCKBACKS

ifaction ALIZ_SPACE_DEAD
{
ifpdistl 1536
 ifp pfacing
  ife SPACE_SUIT 1
  {
  set player_use 0
   ifhitspace
	{
	set hit_key 30
	randvar temp 450 // anywhere between 0 - 15 seconds
	add temp 450 // add 15 on so they always get something
	add LIFE_SUPPORT temp
	screensound SPACESUIT_OFF
	action ALIZ_SPACE_DEAD2
	}
  }
    ifhitweapon
	    {
	      ifwasweapon RADIUSEXPLOSION
	      {
	        state squish_sounds
			state standard_jibs
			state human_jibs
	        killit
	      }
	      break
	    }
}
else
ifaction ALIZ_SPACE_DEAD2
{
    ifhitweapon
	    {
	      ifwasweapon RADIUSEXPLOSION
	      {
	        state squish_sounds
			state standard_jibs
			state human_jibs
	        killit
	      }
	      break
	    }
}
else
ifai AI_SPACELIZ_DIE
{
move DRIFT spin
strength 0
	ifactioncount 5 { state rf action ALIZ_SPACE_DEAD }
}
else
ifaction ALIZ_SPACE_PAIN
{
	sub PAIN_AMOUNT 1
	ife PAIN_AMOUNT 0 ai AI_SPACELIZ_FLY
}
else
ifai AI_SPACELIZ_BOOST
{
state correctcansee

  ifp palive
   ife sightvar YES
    ifpdistl 8192
		{
		ai AI_SPACELIZ_FIRE
		}
		else
		ifrnd 2 ai AI_SPACELIZ_FLY
}
else
ifai AI_SPACELIZ_FLY
{

      ifp phigher ifrnd 128
			{
			geta[].z temp
			sub temp 1024
			seta[].z temp
			}
      else
	  ifp ponground nullop
	  else
	  ifrnd 128
			{
			geta[].z temp
			add temp 1024
			seta[].z temp
			}

  ifrnd 1
  {
	ifrnd 128 soundonce PRED_ROAM
	else soundonce PRED_ROAM2
  }

state correctcansee

  ifp palive
   ife sightvar YES
    ifrnd 32
		{
		ai AI_SPACELIZ_FIRE
		}
		else
		ifpdistg 8192 ifrnd 8
			{
			sound SPACESUIT_THRUSTER
			ai AI_SPACELIZ_BOOST
			}
		else
		ifcansee ifrnd 8 ai AI_SPACELIZ_FLY

}
else
ifai AI_SPACELIZ_FIRE
{
	ifactioncount 2
	{
		// Just use standard hardcoded firing code for this
		sound PRED_ATTACK
		shoot FIRELASER
		ai AI_SPACELIZ_FLY
	}
}

ifhitweapon
{
	state NEWGUNEFFECTS
//	spawn BLOOD
//	guts JIBS6 5
	state random_wall_jibs
	ifdead
		{
		ifwasweapon RPG
			{
			  state squish_sounds
			  shoot 4714 shoot 4714 shoot 4714 shoot 4714 shoot 4714
			  state standard_jibs
			  killit
			}
		else
		  ifwasweapon RADIUSEXPLOSION
			{
			  state squish_sounds
			  shoot 4714 shoot 4714 shoot 4714 shoot 4714 shoot 4714
			  state standard_jibs
			  killit
			}
		else
		  ifand PROJ_UDATA PROJ_GIB
			{
			  state squish_sounds
			  shoot 4714 shoot 4714 shoot 4714 shoot 4714 shoot 4714
			  state standard_jibs
			  killit
			}
		else
			{
			sound PRED_DYING
			state rf
			addkills 1

			ai AI_SPACELIZ_DIE
			}
		}
	else
	ifg PAIN_AMOUNT 0 action ALIZ_SPACE_PAIN
	else ifrnd 16
		{
		set playsound PRED_PAIN
		state PAIN_SKILL_LEVELADJUST
		action ALIZ_SPACE_PAIN
		}
}

enda
