/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Based on the Duke3D CON files by 3DRealms, released under the GPL2 at:
https://github.com/videogamepreservation/dukenukem3d/blob/ef609159bd352985a0c63734a11241d86c873fe5/testdata/GAME.CON

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile TANK
damageeventtile RECON // Here since it's a hardcoded actor

define TANKSTRENGTH       500      // 2 3 4 5 7 8
//           off-set, numofframes, numofrot, skipframes, delay
action ATANKSPIN   1210 3 5 1 12
action ATANKSHOOTING 0 2 5 1 10
action ATANKSHOOTING_UP 10 2 5 1 10
action ATANKWAIT 1210 1 5 1 1
action ATANKDESTRUCT 20 1 5 1 1
action ATANKDEAD 1210 1 5 1 1
// move TANKVEL horiz vert
move TANKFORWARD 100

useractor enemystayput TANKSTAYPUT TANKSTRENGTH fall cactor TANK enda

useractor enemy TANK TANKSTRENGTH fall

  ifstrength 200
   ifrnd 16
	{
	ifrnd 16 { sound SHORT_CIRCUIT shoot SPARK shoot SPARK }
	geta[].x temp
	geta[].y temp2

	set temp3 temp
	randvarvar temp4 512
	add temp3 temp4
	randvar temp4 2048

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	espawn GUNSMOKE

	seta[RETURN].x temp5
	seta[RETURN].y temp6

	geta[].z temp7
	sub temp7 6144
	randvar temp8 2048
	ifrnd 128 mulvar temp8 -1
	add temp7 temp8
	seta[RETURN].z temp7

	}

  ifaction 0
  {
	state monst_glow
	set actor_type TYPE_ARMOURED
    spawn SHOOTME
    sizeat 32 32
    action ATANKWAIT
    cstat 257
    clipdist 100
  }
  else
    ifaction ATANKSPIN
  {
  state checkfortarget
    soundonce TANK_ROAM
	ifcount 8
			{
			ifinwater nullop else
			ifonwater nullop else
			spawn 2085
			resetcount
			}

    ifactioncount 20
    {
      ifrnd 16
        ifn my_target -1
      {
        move STOP geth
        action ATANKSHOOTING
        stopsound TANK_ROAM
      }
    }

	ifnotmoving move TANKFORWARD geth else
    ifrnd 16
	 ifn my_target -1
	  {
	  state fronttowardstarget
      move TANKFORWARD geth
	  }
  }
  else
  ifaction ATANKSHOOTING_UP
  {
  state checkfortarget
  state fronttowardstarget
    ifactioncount 2
    {
      ifpdistg 8192
      {
	  ifspritepal 25
		{
		sound FIREBALL_FIRE
		shoot 7047
		}
		else
		{
        sound BOS1_ATTACK2
        shoot MORTER
		}
      }
      resetcount
      ifrnd 64 action ATANKWAIT
	  else ife my_target -1 action ATANKWAIT
    }
  }
  else
    ifaction ATANKSHOOTING
  {
  state checkfortarget
  state fronttowardstarget
  ife my_target -1 { move TANKFORWARD geth action ATANKSPIN }
	ldist temp7 THISACTOR my_target
    ifactioncount 22
      ifg temp7 16384
	   ifrnd 96
		  {
		  resetcount
		  move 0 action ATANKSHOOTING_UP
		  }
    else
      ifactioncount 2
    {
			ife temp8 0
			 ife PERFMODE 1
				{
					set gunsmoke_z 8244
					set gunsmoke_angle 256
					geta[].x temp
					geta[].y temp2
					set temp3 temp
					add temp3 512
					geta[].ang temp4
					add temp4 gunsmoke_angle
					randvar temp5 16
					ifrnd 128 add temp4 temp5 else sub temp4 temp5
					rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
					espawn 16115
					seta[RETURN].x temp5 seta[RETURN].y temp6
					geta[].z temp sub temp 8244 seta[RETURN].z temp
					espawn 16115
					seta[RETURN].x temp5 seta[RETURN].y temp6
					geta[].z temp sub temp 9644 seta[RETURN].z temp
					set temp8 1
				}
			ife temp8 1
			 ife PERFMODE 1
				{
					set gunsmoke_z 8244
					set gunsmoke_angle -256
					geta[].x temp
					geta[].y temp2
					set temp3 temp
					add temp3 512
					geta[].ang temp4
					add temp4 gunsmoke_angle
					randvar temp5 16
					ifrnd 128 add temp4 temp5 else sub temp4 temp5
					rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
					espawn 16115
					seta[RETURN].x temp5 seta[RETURN].y temp6
					geta[].z temp sub temp 8244 seta[RETURN].z temp
					espawn 16115
					seta[RETURN].x temp5 seta[RETURN].y temp6
					geta[].z temp sub temp 9644 seta[RETURN].z temp
					set temp8 0
				}
		ldist temp7 THISACTOR my_target
        ifg temp7 16384
        {
          ifrnd 128
          {
			  ifspritepal 25
				{
				set PROJECTILE_FIRING_SOUND NAILGUN_FIRE
				set PROJECTILE_TO_SHOOT 6826
				set PROJECTILE_STRENGTH 3
				state sang_enemyShootProjectile
				}
			else
				{
				set PROJECTILE_FIRING_SOUND PISTOL_FIRE
				set PROJECTILE_TO_SHOOT ENEMY_BULLET
				state sang_enemyShootProjectile
				}
          }
        }
        else
          ifrnd 128
        {
		 sound RECO_ATTACK
		 shoot FIRELASER
        }
    }

    ifrnd 16
    {
      stopsound TANK_ROAM
      move STOP faceplayerslow
    }
  }
  else
    ifaction ATANKWAIT
  {
    ifactioncount 32
    {
      move TANKFORWARD geth
      action ATANKSPIN
    }
  }
  else
    ifaction ATANKDESTRUCT
  {
  move STOP

  ifrnd 16
	{
	soundonce PIG_DYING
	setactorsoundpitch THISACTOR PIG_DYING 512
	}

    ifactioncount 64
      {
	  espawn PIGCOP
	  action ATANKDEAD

	  addkills 1

    hitradius 4096 WEAKEST WEAK MEDIUMSTRENGTH TOUGH
    sound LASERTRIP_EXPLODE
    debris SCRAP1 15
    spawn EXPLOSION2
    ifrnd 128
      {
        ifrnd 128
          espawn 2081
        else
          espawn 2082
      }
    else
      {
        ifrnd 128
          espawn 2083
        else
          espawn 2084
      }
	 seta[RETURN].pal sprite[].pal
	killit
	  }
    else
      ifactioncount 56
       sound LASERTRIP_ARMING
     else
       ifactioncount 48
         sound LASERTRIP_ARMING
     else
       ifactioncount 32
         sound LASERTRIP_ARMING
     else
       ifactioncount 16
         sound LASERTRIP_ARMING
     break
  }

  ifhitweapon
  {
    ifdead
		{
		state squish_sounds
		state standard_jibs
		addkills 1

		hitradius 6144 TOUGH TOUGH TOUGH REALLYTOUGH
		sound LASERTRIP_EXPLODE
		debris SCRAP1 15
		spawn EXPLOSION2
		ifrnd 128
		  {
			ifrnd 128
			  espawn 2081
			else
			  espawn 2082
			seta[RETURN].pal sprite[].pal
		action 0
		move 0
		  }
		else
		  {
			ifrnd 128
			  espawn 2083
			else
			  espawn 2084
			seta[RETURN].pal sprite[].pal
		  }
		killit
		}
    else
    {
      debris SCRAP1 1
      ifaction ATANKSHOOTING break

      ifrnd 192
      {
        move STOP geth
        action ATANKSHOOTING
        stopsound TANK_ROAM
      }
    }
  }

  ifpdistl 1280
    ifhitspace
      ifp pfacing
        ifangdiffl 512
          action ATANKDESTRUCT
enda

useractor notenemy 2085
sizeat 32 32
cstat 546
ifcount 260 killit
enda

state WRECKCODE
ifaction 0
	{
	  sizeat 32 32
	  move 0
	  cstat 257

	  clipdist 100
	  strength 100
	  action ZERO
    }

	ifhitweapon
		{
		debris SCRAP1 2
			ifdead
				{
				sound VENT_BUST
				debris SCRAP1 8
				debris SCRAP2 8
				debris SCRAP3 8
				killit
				}
		}

ends

spriteshadow 2081
spriteshadow 2082
spriteshadow 2083
spriteshadow 2084

useractor notenemy 2081
  state WRECKCODE
enda

useractor notenemy 2082
  state WRECKCODE
enda

useractor notenemy 2083
  state WRECKCODE
enda

useractor notenemy 2084
  state WRECKCODE
enda
