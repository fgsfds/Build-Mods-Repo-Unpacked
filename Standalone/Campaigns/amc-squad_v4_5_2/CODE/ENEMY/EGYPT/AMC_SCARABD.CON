/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile SCARAB_DEMON

action A_SCARABD_IDLE -2816 1 5 1 20
action A_SCARABD_WALK -2811 4 5 1 15
action A_SCARABD_PREPARE -2791 1 5 1 10
action A_SCARABD_FIRE -2791 2 5 1 8
action A_SCARABD_MELEE -2781 2 5 1 16
action A_SCARABD_PAIN -2771 1 1 1 20
action A_SCARABD_DIE -2771 1 1 1 20
action A_SCARABD_DEAD -2772 1

move SCARABD_WALK 160

ai AI_SCARABD_SEEKENEMY A_SCARABD_WALK SCARABD_WALK seekplayer
ai AI_SCARABD_PREPARESHOOT A_SCARABD_PREPARE STOP faceplayer
ai AI_SCARABD_SHOOTENEMY A_SCARABD_FIRE STOP faceplayer //shoottarget
ai AI_SCARABD_PREPARESHOOT2 A_SCARABD_PREPARE STOP faceplayer
ai AI_SCARABD_SHOOTNUKE A_SCARABD_FIRE STOP shoottarget
ai AI_SCARABD_DEAD A_SCARABD_DIE STOP faceplayer

useractor enemy SCARAB_DEMON 12000 A_SCARABD_IDLE
fall

ifai 0
	{
		set actor_type TYPE_CHITIN
		state monst_glow
		clipdist 96
		globalsound SCARAB_DEMON_SIGHT
		quake 120
		set ally_mag SKILL_LEVEL
		spriteflags NORMAL_ENEMY
		set CURBOSS THISACTOR
		set BOSS_TYPE 7
		strength 8000
		sizeat 64 64
		spawn SHOOTME
		cstator 257
		action A_SCARABD_WALK
		ai AI_SCARABD_SEEKENEMY
	}

ifdead nullop
else
ifpdistl 1526
ifcansee
{
wackplayer
palfrom 40 40 0 0
addphealth -100
}

state enemy_fire_damage

ifg sprite[].htextra 0                            // if the actor is set to take some damage from a weapon...
{
  ife sprite[].htowner THISACTOR              // if the owner of the weapon IS this actor
    seta[].htextra -1       // set the damage to zero
}

ifai AI_SCARABD_DEAD
	{
	add INTERNALCOUNT 1
	ife INTERNALCOUNT 1 sound SCARAB_DEMON_DIE
	ife INTERNALCOUNT 15
		{
			flash
			spawn NUCLEAR_EXPLOSION
			globalsound CHOPPER_EXPLODE
			sound HUGE_GIB
			shoot SPARK
			shoot SPARK
			shoot SPARK
			shoot SPARK
			shoot SPARK
			shoot SPARK
			spawn BIG_BLOOD_EXE
			spritepal 27
			ife INVASION 1 nullop else
				{
				state standard_jibs
				state standard_jibs
				state standard_jibs
				state monst_body_jibs
				state monst_body_jibs
				}
			debris SCRAP1 14
			guts JIBS6 2
			killit
		}
	}
else
ifai AI_SCARABD_SEEKENEMY
	{
	state SKILL_SHOOT_LEVELADJUST
	ifcount 6 { ifpdistl 16000 quake 13 ifactorsound THISACTOR CYBERDEMON_METAL nullop else sound CYBERDEMON_METAL }
	ifcount 13 { ifactorsound THISACTOR CYBERDEMON_METAL nullop else sound CYBERDEMON_METAL resetcount }
	ifpdistg 1024
		{
		ifrnd 16 ifg ally_mag 0 ifstrength 3000 { globalsound SCARAB_DEMON_SIGHT quake 260 ai AI_SCARABD_PREPARESHOOT2 }
		 ifcanseetarget
		  ifcanshoottarget
		   ifl RANDOM_CHANCE SKILLCHANCE
			ai AI_SCARABD_PREPARESHOOT
		}
	else ifrnd 8 ai AI_SCARABD_SEEKENEMY
	}
else
ifai AI_SCARABD_PREPARESHOOT
	{
	ifactioncount 3 ai AI_SCARABD_SHOOTENEMY
	}
else
ifai AI_SCARABD_SHOOTENEMY
	{
	ifactioncount 1
		{
			sound SCORP_TAIL
			geta[].z temp2
			add temp2 3672
			seta[].z temp2
			eshoot MPLASMA // shoots it's left gun
			sub temp2 3672
			seta[].z temp2

			geta[].x temp
			geta[].y temp2
			set temp3 temp
			add temp3 1024
			geta[].ang temp4
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			espawn 16113
			seta[RETURN].x temp5 seta[RETURN].y temp6
			geta[].z temp sub temp 5592 seta[RETURN].z temp

			resetactioncount
		ifcansee ifcanseetarget { ifrnd 16 ai AI_SCARABD_SEEKENEMY } else ai AI_SCARABD_SEEKENEMY
		}
	}
else
ifai AI_SCARABD_PREPARESHOOT2
	{
	ifactioncount 40 ai AI_SCARABD_SHOOTNUKE
	}
else
ifai AI_SCARABD_SHOOTNUKE
	{
	ifactioncount 2
		{
		sub ally_mag 1
		geta[].z temp6
		add temp6 8192
		seta[].z temp6
		eshoot ATOM_BOMB
		sub temp6 8192
		seta[].z temp6
		flash
		globalsound Z_MISSILE_FIRE
		soundonce Z_WARNING
		setactorsoundpitch THISACTOR Z_WARNING -2048
		resetactioncount
		ai AI_SCARABD_SEEKENEMY
		}
	}

geta[].htextra temp7
ifhitweapon
	{
	set CURBOSS THISACTOR
	ifrnd 8 soundonce BRUISER_PAIN
	ifwasweapon RPG seta[].xvel 0
	ifwasweapon RADIUSEXPLOSION seta[].xvel 0
	//spawn BLOOD
	guts JIBS6 1
	state random_wall_jibs
	ifdead
		{
			state rf

			ifn HITAGSAVED 0
				{
			   operateactivators HITAGSAVED THISACTOR
			   operatemasterswitches HITAGSAVED
			   operaterespawns HITAGSAVED
				}
			add TREASURE_FOUND 15
			addkills 1
			ai AI_SCARABD_DEAD
		}
}

enda
