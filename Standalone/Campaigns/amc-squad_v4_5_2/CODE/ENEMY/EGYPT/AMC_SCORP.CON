/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile SCORPIONESS

action A_SCORP_FROZEN 35 1 5 1 10
action A_SCORP_IDLE 0 1 5 1 8
action A_SCORP_RUN  0 4 5 1 8
action A_SCORP_CHARGE  0 4 5 1 4
action A_SCORP_RUNBACK 15 4 5 -1 20
action A_SCORP_ATTACK 20 2 5 1 8
action A_SCORP_MISSILE 30 1 5 1 10
action A_SCORP_PAIN 35 1 1 1 10
action A_SCORP_DIE 35 6 1 1 24
action A_SCORP_DEAD 40

move MSCORP_RUN 160

ai AI_SCORP_IDLE A_SCORP_IDLE STOP seekplayer
ai AI_SCORP_SEEKENEMY A_SCORP_RUN MSCORP_RUN seekplayer
ai AI_SCORP_DODGE A_SCORP_RUN MSCORP_RUN dodgebullet
ai AI_SCORP_WALKBACK A_SCORP_RUNBACK TROOPWALKVELSBACK faceplayer
ai AI_SCORP_ATTACK A_SCORP_ATTACK STOP shoottarget
ai AI_SCORP_ATTACK2 A_SCORP_MISSILE STOP shoottarget

ai AI_SCORP_DYING A_SCORP_DIE STOP
ai AI_SCORP_SHRINK A_SCORP_RUN MSCORP_RUN fleeenemy
ai AI_SCORP_GROW A_SCORP_PAIN STOP
ai AI_SCORP_FROZEN A_SCORP_PAIN STOP

useractor enemystayput SCORPIONESS_STAYPUT 1000 A_SCORP_IDLE
spawn SHOOTME
spriteflags NORMAL_ENEMY
globalsound SCORP_SIGHT
	ifspritepal 42
	{
	strength 2500
	sizeat 40 40
	}
	else
	{
	spritepal 36
	strength 1000
	sizeat 28 28
	}
state monst_glow
ai AI_SCORP_SEEKENEMY
cstator 257
cactor SCORPIONESS
enda

useractor enemy SCORPIONESS 1000 A_SCORP_IDLE
fall

ifai 0
{
	set actor_type TYPE_CHITIN
	state monst_glow
	spawn SHOOTME
	spriteflags NORMAL_ENEMY
	ifspritepal 42
		{
        set CURBOSS THISACTOR
		strength 2500
		sizeat 40 40
		}
	else
		{
		spritepal 36
		strength 1000
		sizeat 28 28
		}
	ai AI_SCORP_IDLE
	cstator 257
}

ifaction A_SCORP_DEAD
{
move STOP
    ifhitweapon
		{
		  ifwasweapon RADIUSEXPLOSION
		  {
			state squish_sounds
			spawn 2284
		  state standard_jibs
		  state standard_jibs
		  state monst_body_jibs
			killit
		  }
		  break
		}
}
else
ifai AI_SCORP_FROZEN
{
state frozen_code
}
else
ifai AI_SCORP_GROW state genericgrowcode
else
ifai AI_SCORP_SHRINK
{
    ifcount SHRUNKDONECOUNT
      ai AI_SCORP_SEEKENEMY
    else ifcount SHRUNKCOUNT
	  {
	  sound ACTOR_UNSHRINK
      sizeto 16 16
	  }
    else
      state genericshrunkcode
    break
}
else
ifai AI_SCORP_DYING
{
strength 0
ifactorsound THISACTOR SCORP_MOVE stopactorsound THISACTOR SCORP_MOVE
move STOP

	ifactioncount 6
		{
		ifpdistl 8192 quake 26
		sound BOSSFALL
		action A_SCORP_DEAD
		}

}
else
ifaction A_SCORP_PAIN
{
ifactorsound THISACTOR SCORP_MOVE stopactorsound THISACTOR SCORP_MOVE
sub PAIN_AMOUNT 1
move 0
ife PAIN_AMOUNT 0
	{
	ife allies_present 1 state enemyfindtarget
	ai AI_SCORP_ATTACK2
	}
}
else
ifai AI_SCORP_IDLE
{
 state correctcansee
   ife sightvar YES
    {
	soundonce SCORP_SIGHT
    ai AI_SCORP_SEEKENEMY
	}
}
else
ifai AI_SCORP_WALKBACK
{
ifactorsound THISACTOR SCORP_MOVE nullop else sound SCORP_MOVE
ifactioncount 10 ai AI_SCORP_ATTACK
}
else
ifai AI_SCORP_SEEKENEMY
{
ifactorsound THISACTOR SCORP_MOVE nullop else sound SCORP_MOVE

ifpdistl 1536 ai AI_SCORP_WALKBACK

state SKILL_SHOOT_LEVELADJUST
  ifl RANDOM_CHANCE SKILLCHANCE
   ifcanshoottarget
	{
	set temp9 0
	 state correctcansee
		ife sightvar YES
			{
			state SKILL_SHOOT_LEVELADJUST
			 ifl RANDOM_CHANCE SKILLCHANCE { sound SCORP_SIGHT ai AI_SCORP_ATTACK2 } else
			ai AI_SCORP_ATTACK
			}
	}

}
else
ifai AI_SCORP_ATTACK
{
ifactorsound THISACTOR SCORP_MOVE stopactorsound THISACTOR SCORP_MOVE
ifpdistl 1536 ai AI_SCORP_WALKBACK

 state correctcansee
   ife sightvar YES
	ifcanseetarget nullop
	  else ai AI_SCORP_SEEKENEMY
		ifactioncount 1
			{
			sound SCORP_TAIL
			geta[].z temp2
			add temp2 3672
			seta[].z temp2
		setprojectile[MPLASMA].offset -26
		eshoot MPLASMA // shoots it's left gun
		setthisprojectile[RETURN].extra 5
		setprojectile[MPLASMA].offset 14354

		setprojectile[MPLASMA].offset 64
		eshoot MPLASMA // shoots it's right gun
		setthisprojectile[RETURN].extra 5
		setprojectile[MPLASMA].offset 14354
			sub temp2 3672
			seta[].z temp2

			geta[].x temp
			geta[].y temp2
			set temp3 temp
			add temp3 512
			geta[].ang temp4
			add temp4 256
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			espawn 16113
			seta[RETURN].x temp5 seta[RETURN].y temp6
			geta[].z temp sub temp 3672 seta[RETURN].z temp

			geta[].x temp
			geta[].y temp2
			set temp3 temp
			add temp3 512
			geta[].ang temp4
			sub temp4 256
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			espawn 16113
			seta[RETURN].x temp5 seta[RETURN].y temp6
			geta[].z temp sub temp 3672 seta[RETURN].z temp

			ifrnd 32 ai AI_SCORP_SEEKENEMY else
			ifrnd 16 ai AI_SCORP_ATTACK2 else
			resetactioncount
			}
}
else
ifai AI_SCORP_ATTACK2
{
ifactorsound THISACTOR SCORP_MOVE stopactorsound THISACTOR SCORP_MOVE
ifpdistl 1536 ai AI_SCORP_WALKBACK
	 state correctcansee
		ife sightvar YES
			ifcanseetarget nullop
			else ai AI_SCORP_SEEKENEMY
		ifactioncount 1
			{
			sound PA_MISSILEL
			shoot HOMING_MISSILE
			ai AI_SCORP_SEEKENEMY
			}
}

state enemyfloordamage
state enemy_fire_damage
state enemy_ice_damage
state enemy_spirit_damage

ifhitweapon
{
	ifspritepal 42 set CURBOSS THISACTOR
	state NEWGUNEFFECTS
	//spawn BLOOD
	guts JIBS6 1
	state random_wall_jibs
	ifdead
		{
		state rf
		ifvarn XVELSAVED 0
			{
			operateactivators XVELSAVED THISACTOR
			operaterespawns XVELSAVED
			operatemasterswitches XVELSAVED
			}
		ifg fire_damage 0 set fire_damage 0
		ifwasweapon GROWSPARK { cstat 0 sound ACTOR_GROWING ai AI_SCORP_GROW break }
		else ifg ice_damage 0 { spritepal 1 strength 0 sound SOMETHINGFROZE ai AI_SCORP_FROZEN }
		else
			{
			sound SCORP_DEATH
			add TREASURE_FOUND 1
			addkills 1
			ai AI_SCORP_DYING
			}
		}
	else ifg PAIN_AMOUNT 0 { sound SCORP_PAIN action A_SCORP_PAIN }
	else ifrnd 8 { sound SCORP_PAIN state PAIN_SKILL_LEVELADJUST action A_SCORP_PAIN }
	ifwasweapon SHRINKSPARK { ifspritepal 42 break sound ACTOR_SHRINKING ai AI_SCORP_SHRINK }
}


enda
