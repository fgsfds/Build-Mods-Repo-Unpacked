/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile LAVA_BEAST

action A_LAVA_B_MOVE 0 1 5 1 10
action A_LAVA_B_FIREBALL 0 4 5 1 12
action A_LAVA_B_FIRE 15 0 5 1 12
action A_LAVA_B_PAIN 20
action A_LAVA_B_DIE 20 5 1 1 40

move LAVAB_SWIM 240
move LAVAB_MOVE 30

ai AI_LAVA_B_IDLE A_LAVA_B_MOVE STOP
ai AI_LAVA_B_SUB A_LAVA_B_MOVE STOP
ai AI_LAVA_B_RISE A_LAVA_B_FIRE STOP
ai AI_LAVA_B_SWIM A_LAVA_B_FIRE LAVAB_SWIM faceplayer
ai AI_LAVA_B_SEEK A_LAVA_B_MOVE LAVAB_MOVE seekplayer
ai AI_LAVA_B_FIREBALL A_LAVA_B_FIREBALL STOP faceplayersmart
ai AI_LAVA_B_FIRE A_LAVA_B_FIRE STOP faceplayersmart
ai AI_LAVA_B_PAIN A_LAVA_B_PAIN STOP
ai AI_LAVA_B_DIE A_LAVA_B_DIE STOP

spritenoshade 22093
spritenopal 22093
useractor notenemy 22093 // lava beast head
ifaction 0
	{
	ife sprite[myowner].pal 46
	sizeat 50 50
	else
	sizeat 90 90
	action ZERO
	}
ifaction ZERO
	{
	state getfloordist
		ifl z 16
		{
		ife ally_mag 0 { sound LAV_RIS spawn 10039 set ally_mag 1 }
		geta[].z temp
		add temp 64
		seta[].z temp
		gets[].floorz temp2
		add temp2 49152
		ifg temp temp2
			{
			killit
			}
		}
		else fall
	}

enda

spritenoshade LAVA_BEAST
spritenopal LAVA_BEAST

useractor enemy LAVA_BEAST
seta[].htflags 17866787

ifai 0
	{
	set MONSTER_FLAGS 16
	spawn 10039
	cstat 257
	ife actor_pal 0 geta[].pal actor_pal
	ife actor_pal 46
		{
		quake 26
		soundonce SURFACE
		ifrnd 32 spawn 18138
		spawn WATERBUBBLE
		sizeat 54 54
		strength 2000
		}
	else
		{
		quake 52
		shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL
		shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL
		globalsound LAV_RIS
		set CURBOSS THISACTOR
		set BOSS_TYPE 7
		sizeat 90 90
		strength 4000
		}
	ai AI_LAVA_B_SEEK
	}

ifonwater
	{
	geta[].x CSPAWNPOSX
	geta[].y CSPAWNPOSY
	}
else
	{
	seta[].x CSPAWNPOSX
	seta[].y CSPAWNPOSY
	}

ifstrength 0 nullop else
 ifn sprite[].pal 46
	{
		ifpdistl 4096
		{
			set fire_damage 5
			state playerincurfiredamage
		}
	}

ifg ice_damage 0 ifg sprite[].extra 15 addstrength -15

ifai AI_LAVA_B_DIE
{
fall
ifrnd 2 ifn sprite[].pal 46 shoot LAVABALL
ifactioncount 3
 ifrnd 64
	{
		espawn EXPLOSION2
		sound RPG_EXPLODE
		geta[].x temp
		geta[].y temp2
		set temp3 temp
		add temp3 2048
		randvar temp4 2048
		rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		set temp6 0
	}
ifactioncount 4
	{
	ife ally_mag 0
		{
		espawn 22093
		geta[RETURN].z temp
		sub temp 33248
		seta[RETURN].z temp
		set ally_mag 1
		}
	ifn sprite[].pal 46
		{
		shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL
		}
	}
ifactioncount 5 killit
}
else
ifai AI_LAVA_B_SUB
	{
	ife sprite[].pal 46
		{
		soundonce SUBMERGE
		ifrnd 32 spawn 18138
		spawn WATERBUBBLE
		spawn WATERBUBBLE
		}
	else
		{
		soundonce LAV_RIS
		ifrnd 32 shoot LAVABALL
		}
	geta[].z temp
	add temp 2048
	seta[].z temp
	gets[].floorz temp2
	add temp2 98304
	ifg temp temp2
		{
		ai AI_LAVA_B_SWIM
		resetcount
		}
	}
else
ifai AI_LAVA_B_SWIM
	{
	fall
	cstat 32768
	ifstrength 4000 addstrength 8
	ifpdistl 8192 { seta[].z temp ai AI_LAVA_B_RISE }
	else ifcount 150 { seta[].z temp ai AI_LAVA_B_RISE }
	}
else
ifai AI_LAVA_B_RISE
	{
	cstat 257
	ife actor_pal 46
		{
		soundonce SURFACE
		ifrnd 32 spawn 18138
		spawn WATERBUBBLE
		}
	else
		{
		soundonce LAV_RIS
		ifrnd 32 shoot LAVABALL
		}
	geta[].z temp
	sub temp 2048
	seta[].z temp
	ifl temp sector[].floorz
		{
		fall
		ai AI_LAVA_B_SEEK
		resetcount
		}
	}
else
ifai AI_LAVA_B_IDLE
{
fall
ifcansee
 ifcanseetarget
  ifrnd 96
	ai AI_LAVA_B_SEEK
	else
	ifrnd 8 ai AI_LAVA_B_FIREBALL
}
else
ifai AI_LAVA_B_SEEK
{
fall
 state correctcansee
   ife sightvar YES
  ifrnd 16
	ai AI_LAVA_B_FIREBALL
	else
ifpdistg 8192 ifrnd 8
	{
	// ifand sector[].floorstat 1024 ai AI_LAVA_B_SEEK else // don't submerge if in TROR layer
	ai AI_LAVA_B_SUB
	}
}
else
ifai AI_LAVA_B_FIREBALL
{
fall
	ifactioncount 4 ai AI_LAVA_B_FIRE
}
else
ifai AI_LAVA_B_FIRE
{
fall
	ifactioncount 0
		{
		ife actor_pal 46
			{
			sound KAP_FIRE
			geta[].z temp2
			set temp temp2
			sub temp 14196
			seta[].z temp
			shoot 29020
			seta[].z temp2
			ai AI_LAVA_B_SEEK
			}
		else
			{
			sound LAV_FIRE
			geta[].z temp2
			set temp temp2
			sub temp 14196
			seta[].z temp
			shoot BRUISERFIREBALL
			seta[].z temp2
			}
		}
	ifactioncount 1
		{
		ifstrength 1000 ifrnd 96 ai AI_LAVA_B_SUB else
		ai AI_LAVA_B_SEEK
		}
}
else
ifai AI_LAVA_B_PAIN
{
fall
globalsound LAV_HURT
ifactioncount 5 ai AI_LAVA_B_IDLE
}

ifhitweapon
	{
	ifn sprite[].pal 46 set CURBOSS THISACTOR
	seta[].xvel 0
	ifwasweapon RADIUSEXPLOSION ifrnd 64 { ai AI_LAVA_B_PAIN }
	ifdead 
		{ 
		quake 68 
		sound LAV_DETH 
		ifvarn XVELSAVED 0
			{
			operateactivators XVELSAVED THISACTOR
			operaterespawns XVELSAVED
			operatemasterswitches XVELSAVED
			}
		ai AI_LAVA_B_DIE 
		}
	}

enda
