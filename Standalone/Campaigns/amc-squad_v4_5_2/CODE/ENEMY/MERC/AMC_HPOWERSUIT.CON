/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile HPOWERSUIT

action AHPSUIT_IDLE 0 1 5 1 10
action AHPSUIT_MOVE 5 4 5 1 20
action AHPSUIT_MACHINEG 25 2 5 1 4
action AHPSUIT_SHOCK 35 1 5 1 4
action AHPSUIT_MISSILEP 40 1 5 1 10
action AHPSUIT_MISSILEL 45 1 5 1 20
action AHPSUIT_DIE 51 1 1 1 20
action AHPSUIT_DEAD 51 1 1 1 10

move HPSUIT_STOP 0 0
move HPSUIT_WALK 96 0

ai AIHPSUIT_IDLE AHPSUIT_IDLE HPSUIT_STOP
ai AIHPSUIT_MOVE AHPSUIT_MOVE HPSUIT_WALK geth
ai AIHPSUIT_MACHINEG AHPSUIT_MACHINEG HPSUIT_STOP 
ai AIHPSUIT_MISSILEP AHPSUIT_MISSILEP HPSUIT_STOP 
ai AIHPSUIT_MISSILEL AHPSUIT_MISSILEL HPSUIT_STOP 
ai AIHPSUIT_DIE AHPSUIT_DIE HPSUIT_STOP

useractor enemystayput HPOWERSUITSTAYPUT 5000 AHPSUIT_IDLE state monst_glow cactor HPOWERSUIT enda

useractor enemy HPOWERSUIT 5000 AHPSUIT_IDLE
fall

ifai 0
	{
	set actor_type TYPE_ARMOURED
	state monst_glow
	ifcansee
	 ifcanseetarget
		{
		ifrnd 128
			{
			set PLAYER_VOICEOVER 7
			}
		}
	spawn SHOOTME
	spriteflags ROBOT_ENEMY
	strength 5000
	sizeat 32 32
	cstat 257
	ai AIHPSUIT_IDLE
	ifspritepal 3 nullop else
		{
		set BOSS_TYPE 4
		set CURBOSS THISACTOR
		}
	}

ifdead nullop else
	{
	ifstrength 1500
		{
		ifrnd 64
			{
			sound SHORT_CIRCUIT
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			}
		}
	}

ifaction AHPSUIT_DEAD
	{
	break
	}
else
ifai AIHPSUIT_DIE
	{
	ifactioncount 7
		{
		ifn XVELSAVED 0
				{
				operateactivators XVELSAVED THISACTOR
				operatemasterswitches XVELSAVED
				operaterespawns XVELSAVED
				set XVELSAVED 0
				}
		quake 26 cstat 0 globalsound BOSSFALL action AHPSUIT_DEAD
		geta[].pal temp2 seta[RETURN].pal temp2
		}
	}
else
ifai AIHPSUIT_IDLE
{
state checkfortarget
	ifn my_target -1 ai AIHPSUIT_MOVE
}
else
ifai AIHPSUIT_MOVE
{
	state checkfortarget
	ifpdistl 16000
	{
		add INTERNALCOUNT_2 1
		ifg INTERNALCOUNT_2 16
		{
			quake 13

			ifrnd 128
				sound PA_FOOTSTEP2
			else
				sound PA_FOOTSTEP1

			set INTERNALCOUNT_2 0
		}
	}
	

	ifn my_target -1
	{
		state SKILL_SHOOT_LEVELADJUST
			ifl RANDOM_CHANCE SKILLCHANCE
				{
				set INTERNALCOUNT 0
				ai AIHPSUIT_MISSILEP
				}
			else ifrnd 32 ai AIHPSUIT_MACHINEG
	}

}
else
ifai AIHPSUIT_MISSILEL
{
	state new_validatetarget
	
	ife my_target -1
		ai AIHPSUIT_MOVE
	
	state fronttowardstarget

	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 3
	{
		geta[].z temp3 sub temp3 6096 seta[].z temp3 // change this actor's height so the projectile comes from the right place
		ife player_in_vehicle 1
			{
			set PROJECTILE_FIRING_SOUND MISSILE_LAUNCH
			set PROJECTILE_TO_SHOOT MISSILE
			}
		else
			{
			set PROJECTILE_FIRING_SOUND PA_MISSILEL
			set PROJECTILE_TO_SHOOT MICRO_MISSILE
			}
		state sang_enemyShootProjectile
		geta[].z temp3 add temp3 6096 seta[].z temp3 // set this actor's height to where it was

		geta[].x temp
		geta[].y temp2
		set temp3 temp
		add temp3 256
		geta[].ang temp4
		add temp4 256
		rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		geta[].z temp3 sub temp3 6096 seta[].z temp3

		ife player_in_vehicle 1
			{
			set PROJECTILE_FIRING_SOUND MISSILE_LAUNCH
			set PROJECTILE_TO_SHOOT MISSILE
			}
		else
			{
			set PROJECTILE_FIRING_SOUND PA_MISSILEL
			set PROJECTILE_TO_SHOOT MICRO_MISSILE
			}
		state sang_enemyShootProjectile

		geta[].z temp3 add temp3 6096 seta[].z temp3
		geta[].x temp
		geta[].y temp2
		set temp3 temp
		add temp3 256
		geta[].ang temp4
		sub temp4 256
		rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
		seta[RETURN].x temp5
		seta[RETURN].y temp6

		set INTERNALCOUNT 0
		ai AIHPSUIT_MOVE
	}
}
else
ifai AIHPSUIT_MISSILEP
	{
	ifactioncount 5 ai AIHPSUIT_MISSILEL
	}
else
ifai AIHPSUIT_MACHINEG
{
	state new_validatetarget
	
	ife my_target -1
		ai AIHPSUIT_MOVE

	state fronttowardstarget
	ifn my_target -1
		{
			ife player_in_vehicle 1
			{
				ifactioncount 1
				{
					set HITSCAN_TO_SHOOT ARMOUR_PIERCING_SHOT
					set HITSCAN_FIRING_SOUND ASSAULTRIFLEU
					set CALCZ_TARGET_OFFSET -5000
					state sang_enemyShootHitscan
					state sang_enemyShootHitscan
					resetactioncount
				}
			}
			else
			{
				ifactioncount 1
				{
					geta[].z temp3 add temp3 1024 seta[].z temp3 // change this actor's height so the projectile comes from the right place
					set PROJECTILE_TO_SHOOT ENEMY_BULLET
					set PROJECTILE_FIRING_SOUND ASSAULTRIFLE
					set PROJECTILE_OFFSET -26
					set CALCZ_TARGET_OFFSET -5000
					setprojectile[FAKE_BULLET].offset -26 // shoots its left gun
					state sang_enemyShootProjectile
					shoot FAKE_BULLET
					setprojectile[FAKE_BULLET].offset 14354
					
					set PROJECTILE_OFFSET 64
					setprojectile[FAKE_BULLET].offset 64 // shoots its right gun
					state sang_enemyShootProjectile
					shoot FAKE_BULLET
					setprojectile[FAKE_BULLET].offset 14354
					geta[].z temp3 sub temp3 1024 seta[].z temp3 // reset height
					resetactioncount
				}
			}
			ifrnd 4 ai AIHPSUIT_MISSILEP
		}
		else ai AIHPSUIT_MOVE
}


ifhitweapon
	{
	set CURBOSS THISACTOR
    // spawn debris at low health
    ifstrength 1500 ifrnd 32 { debris SCRAP1 1 debris SCRAP2 1 }

	seta[].xvel 0

	ifdead
		{
		ifn YVELSAVED 0 espawnvar YVELSAVED
		add TREASURE_FOUND 1
		addkills 1

				operateactivators XVELSAVED THISACTOR
				operatemasterswitches XVELSAVED
				operaterespawns XVELSAVED
				set XVELSAVED 0
				soundonce BLOWUP
				sound RPG_EXPLODE
				geta[].x temp
				geta[].y temp2
				set temp3 temp
				add temp3 256
				randvar temp4 2048
				rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
				espawn BIG_EXPLOSION
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				espawn BIG_SMOKE2
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				set temp6 0
				debris SCRAP1 40
				debris SCRAP2 32
				debris SCRAP3 32
				debris SCRAP4 32
				debris SCRAP5 32
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				globalsound PA_DIE
				ai AIHPSUIT_DIE
		}
	}

ifsquished
	{
		ifn YVELSAVED 0 espawnvar YVELSAVED
		add TREASURE_FOUND 1
		addkills 1
		addstrength -10000
				operateactivators XVELSAVED THISACTOR
				operatemasterswitches XVELSAVED
				operaterespawns XVELSAVED
				set XVELSAVED 0
				soundonce BLOWUP
				sound RPG_EXPLODE
				geta[].x temp
				geta[].y temp2
				set temp3 temp
				add temp3 256
				randvar temp4 2048
				rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
				espawn BIG_EXPLOSION
				espawn BIG_EXPLOSION
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				espawn BIG_SMOKE2
				espawn BIG_SMOKE2
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				set temp6 0
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				globalsound PA_DIE
				killit
	}

enda
