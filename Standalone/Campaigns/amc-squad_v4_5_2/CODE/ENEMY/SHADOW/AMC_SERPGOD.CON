/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile SERPENT_GOD

// SERPENT GOD RING OF SKULLS===============================================================================================================

defstate skull_explode
	soundonce EXPLOSION_SMALL
	spawn GORO_FLAME_EX
	killit
ends

spritenoshade 16764
spritenopal 16764

useractor notenemy 16764

ifaction 0
	{
	espawn GORO_FLAME_EX
	seta[RETURN].pal 1
	sizeat 32 32
	strength 15
	seta[].shade -64
	cstator 385
	action ZERO
	}
	
ifaction ZERO
	{
	geta[myowner].x temp
	geta[myowner].y temp2

	set temp3 temp
	add temp3 2048
	ife INTERNALCOUNT 0
		{
		add temp7 16
		ifg temp7 512
		set INTERNALCOUNT 1
		}
	else
	ife INTERNALCOUNT 1
		{
		sub temp7 16
		ifl temp7 16
		set INTERNALCOUNT 0
		}
	add temp3 temp7

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	add temp4 32

	ifg temp4 2048 sub temp4 2048

	seta[].x temp5
	seta[].y temp6
	getp[].posz temp8
	sub temp8 2048
	seta[].z temp8

	ifl sprite[myowner].extra 1 state skull_explode

	add INTERNALCOUNT_2 1
	ifg INTERNALCOUNT_2 130
		{
		ifrnd 32
		ifcansee
			{
			sound SERPGOD_HEADS
			action BREAK
			}
		set INTERNALCOUNT_2 0
		}
	}

ifaction FORM
	{
	spritepal 106
	seta[].shade -127
	move FLYFORWARD geth getv
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 10
		{
		findnearactor3d SERPENT_GOD 16384 target
		ifn target -1
			{
		    getactor[target].x mx
			getactor[target].y my
			getactor[THISACTOR].x x
			getactor[THISACTOR].y y
			subvarvar mx x
			subvarvar my y
			getangle angvar mx my
			setactor[THISACTOR].ang angvar
			dist temp4 THISACTOR target
			ifl temp4 2536
				{
				globalsound EXPLOSION_BIG
				spawn EXPLOSION2
				seta[target].htextra 500
				state skull_explode
				}
			}
		}

		ifg INTERNALCOUNT 300
			 {
			hitradius 512 10 15 20 25
			state skull_explode
			 }
	}

ifaction BREAK
	{
	move FLYFORWARD geth getv faceplayer
	ifpdistl 1536
		{
		 ifn SWORD_BLOCK 0
			 {
			 ifp pfacing
				 {
				 seta[].ang player[].ang
				 sound FORCE_BLAST
				 set screen_shake 6
				 move FLYFORWARD geth getv
				 set INTERNALCOUNT 0
				 set temp4 2000
				 action FORM
				 }
			 else state skull_explode
			 }
		 else
			 {
			hitradius 2048 10 15 20 25
			state skull_explode
			 }
		}
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 200
			 {
			hitradius 512 10 15 20 25
			state skull_explode
			 }
	ifhitweapon
	 ifdead
		 state skull_explode
	}

enda

action ASG_IDLE 0 1 5 1 10
action ASG_MOVE 0 4 5 1 20
action ASG_RUSH 0 4 5 1 10
action ASG_CHOP 20 5 5 1 12
action ASG_BLAST 45 4 5 1 12
action ASG_DYING 65 9 1 1 20
action ASG_DEAD 73 1 1 1 10

move SERP_MOVE 140 0
move SERP_RUSH 260 0

ai AI_SG_IDLE ASG_IDLE STOP faceplayer
ai AI_SG_MOVE ASG_MOVE SERP_MOVE seekplayer
ai AI_SG_RUSH ASG_RUSH SERP_RUSH seekplayer
ai AI_SG_CHOP ASG_CHOP STOP faceplayer
ai AI_SG_BLAST ASG_BLAST STOP seekplayer
ai AI_SG_MAGIC ASG_BLAST STOP seekplayer
ai AI_SG_DYING ASG_DYING STOP faceplayer

spriteflags SERPENT_GOD NORMAL_ENEMY_TRU

useractor enemystayput SERPENT_GOD_STAYPUT 2000
cactor SERPENT_GOD
enda

useractor enemy SERPENT_GOD 2000 ASG_IDLE
fall

ifai 0
	{
	geta[].pal actor_pal
	state monst_glow
	cstat 257
	ifspritepal 0
		{
		set CURBOSS THISACTOR
		set BOSS_TYPE 6
		strength 8000
		sizeat 60 60
		}
	else
		{
		strength 2000
		sizeat 40 40
		}
	spawn SHOOTME
	sound SERPGOD_ALERT
			ifspritepal 0
				{
				spawn 16764
				espawn 16764 setactorvar[RETURN].temp4 205
				espawn 16764 setactorvar[RETURN].temp4 510
				espawn 16764 setactorvar[RETURN].temp4 715
				espawn 16764 setactorvar[RETURN].temp4 920
				espawn 16764 setactorvar[RETURN].temp4 1125
				espawn 16764 setactorvar[RETURN].temp4 1330
				espawn 16764 setactorvar[RETURN].temp4 1535
				espawn 16764 setactorvar[RETURN].temp4 1740
				espawn 16764 setactorvar[RETURN].temp4 1945
				}
	ai AI_SG_MOVE
	}

ifaction ASG_DEAD
	{
	move STOP
	}
else
ifai AI_SG_DYING
	{
	cstat 0
	quake 52
	ifactioncount 9
		{
		ifn HITAGSAVED 0
			{
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			}
		action ASG_DEAD
		}
	}
else
ifai AI_SG_MOVE
	{
	state checkfortarget
	ifrnd 2 soundonce SERPGOD_TAUNT
	ifpdistl 16000
	 ifcansee
	{
	state SKILL_SHOOT_LEVELADJUST
	 ifl RANDOM_CHANCE SKILLCHANCE
	ai AI_SG_RUSH
	}
	else
	ifrnd 64
	 ifn my_target -1
		{
		state SKILL_SHOOT_LEVELADJUST
		ifl RANDOM_CHANCE SKILLCHANCE
		ai AI_SG_BLAST
		}

	}
else
ifai AI_SG_RUSH
	{
	ifrnd 2 soundonce SERPGOD_TAUNT
	state checkfortarget
	ifpdistl 1536
	 ifcansee
	ai AI_SG_CHOP
	else
	ifrnd 96
	 ifn my_target -1
		{
		state SKILL_SHOOT_LEVELADJUST
		ifl RANDOM_CHANCE SKILLCHANCE
		ai AI_SG_BLAST
		}
	else ifrnd 1 ai AI_SG_MOVE

	}
else
ifai AI_SG_CHOP
	{
	state fronttowardstarget
	ifpdistg 1535 { ai AI_SG_MOVE resetactioncount }
	ifactioncount 4
		{
		resetactioncount
		ai AI_SG_MOVE
		}
	ifactioncount 3
		{
		sound GUARDIAN_SWING
			ifn SWORD_BLOCK 0
			 ifp pfacing
				{
				shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
				sub p_stamina 60
				wackplayer
				sound SWORD_CLASH
				set PAIN_AMOUNT 16
				action A_GUARDIAN_FLINCH
				break
				}
			else
				{
				ifn my_target PLAYER_IDENTITY
					setprojectile[17849].WORKSLIKE 2125890
				else
					setprojectile[17849].WORKSLIKE 2129986

				setprojectile[17849].RANGE 4
				setprojectile[17849].EXTRA 50
				shoot 17849
				setprojectile[17849].WORKSLIKE 2129986
				setprojectile[17849].RANGE 25
				sound GUARDIAN_HIT
				palfrom 40 40
				}
		}
	}
else
ifai AI_SG_BLAST
	{
	state fronttowardstarget
	ifactioncount 4
		{
		resetactioncount
		ai AI_SG_MOVE
		}
	ifactioncount 3
		{
		sound SERPGOD_MAGIC
		ldist temp THISACTOR my_target
        geta[].z temp2
        sub temp2 4096 // Offset the Z. Assumes the center of the actor is the same as the player
        geta[my_target].z temp3
        sub temp3 temp2
        shiftvarl temp3 9
        divvarvar temp3 temp

		ife my_target PLAYER_IDENTITY // use built in auto-zaim if shooting at the player
			{
			setprojectile[GORO_FLAME].OFFSET -296
			geta[].z temp sub temp 3072 seta[].z temp
			setprojectile[GORO_FLAME].xrepeat 20
			setprojectile[GORO_FLAME].yrepeat 20
			eshoot GORO_FLAME
			setthisprojectile[RETURN].pal 8
			setthisprojectile[RETURN].extra 40
			setprojectile[GORO_FLAME].OFFSET -64
			eshoot GORO_FLAME
			setprojectile[GORO_FLAME].xrepeat 6
			setprojectile[GORO_FLAME].yrepeat 6
			setthisprojectile[RETURN].pal 8
			setthisprojectile[RETURN].extra 40
			setprojectile[GORO_FLAME].OFFSET 224
			geta[].z temp add temp 3072 seta[].z temp
			}
		else
			{
			setprojectile[GORO_FLAME].workslike 36866
			setprojectile[GORO_FLAME].OFFSET -224
			setprojectile[GORO_FLAME].xrepeat 20
			setprojectile[GORO_FLAME].yrepeat 20
			ezshoot temp3 GORO_FLAME geta[RETURN].z temp sub temp 3072 seta[RETURN].z temp
			setthisprojectile[RETURN].pal 8
			setthisprojectile[RETURN].extra 40
			setprojectile[GORO_FLAME].OFFSET -128
			ezshoot temp3 GORO_FLAME geta[RETURN].z temp sub temp 3072 seta[RETURN].z temp
			setthisprojectile[RETURN].pal 8
			setprojectile[GORO_FLAME].xrepeat 6
			setprojectile[GORO_FLAME].yrepeat 6
			setthisprojectile[RETURN].extra 40
			setprojectile[GORO_FLAME].OFFSET 224
			setprojectile[GORO_FLAME].workslike 32770
			}
		}
	}
else
ifai AI_SG_MAGIC
	{
	ifactioncount 4
		{
		resetactioncount
		ai AI_SG_MOVE
		}
	ifactioncount 3
	 ifn INTERNALCOUNT 0
		{
		globalsound USE_PORTAL
		palfrom 16 16 16 16
		operateactivators INTERNALCOUNT THISACTOR
		operaterespawns INTERNALCOUNT
		operatemasterswitches INTERNALCOUNT

				spawn 16764
				espawn 16764 setactorvar[RETURN].temp4 205
				espawn 16764 setactorvar[RETURN].temp4 510
				espawn 16764 setactorvar[RETURN].temp4 715
				espawn 16764 setactorvar[RETURN].temp4 920
				espawn 16764 setactorvar[RETURN].temp4 1125
				espawn 16764 setactorvar[RETURN].temp4 1330
				espawn 16764 setactorvar[RETURN].temp4 1535
				espawn 16764 setactorvar[RETURN].temp4 1740
				espawn 16764 setactorvar[RETURN].temp4 1945

		set INTERNALCOUNT 0
		}
	}


ifhitweapon
{
	ifspritepal 0 set CURBOSS THISACTOR
	//spawn BLOOD
	guts JIBS6 1
    state random_wall_jibs

	ifstrength 6000 ifn XVELSAVED 0
	{
	set INTERNALCOUNT XVELSAVED
	set XVELSAVED 0
	ai AI_SG_MAGIC
	}

	ifstrength 3000 ifn YVELSAVED 0
	{
	set INTERNALCOUNT YVELSAVED
	set YVELSAVED 0
	ai AI_SG_MAGIC
	}

	ifdead
		{
		geta[].htpicnum PROJ_HIT_TYPE
		ifand tiledata[PROJ_HIT_TYPE].gameflags 8 getprojectile[PROJ_HIT_TYPE].userdata temp4 else set temp4 0
		add TREASURE_FOUND 10
		addkills 1

		ai AI_SG_DYING
		sound SERPGOD_SCREAM
		guts JIBS6 5
		}
	else
	ifrnd 16 soundonce SERPGOD_PAIN
}

enda
