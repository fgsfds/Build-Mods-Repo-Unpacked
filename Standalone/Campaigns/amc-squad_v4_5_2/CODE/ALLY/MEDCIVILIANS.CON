/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

//*************************************************
// MEDIEVAL CIVILIANS
//*************************************************

// Old female peasant
useractor notenemy 25411
ifaction 0 action FIVE_ANG
ifg CIVS_PANIC 1 killit
enda

// Some other one, this is the plant lady in skytown
useractor notenemy 26052
ifg CIVS_PANIC 1 killit
enda

// Female peasant
useractor notenemy 25416
ifaction 0 action FIVE_ANG

ifg npc_talking 0 sub npc_talking 1
ifg ally_v_coold 0 sub ally_v_coold 1

ifpdistl 16384
ifcansee
 ifrnd 4
  ife ally_v_coold 0
   ife camerasprite -1
	{
	ifrnd 16 sound FV_MUSE1
	else ifrnd 16 sound FV_MUSE2
	else ifrnd 16 sound FV_MUSE3
	else ifrnd 16 sound FV_MUSE4
	else ifrnd 16 sound FV_MUSE5
	else ifrnd 16 sound FV_MUSE6
	else ifrnd 16 sound FV_MUSE7
	else ifrnd 16 sound FV_MUSE8
	set ally_v_coold 600
	set npc_talking 120
	}

ifpdistg 4096
 ife ally_mag 1
  ife npc_talking 0
  ifrnd 96
	{
	ifrnd 96 sound FV_BYE1
	else ifrnd 96 sound FV_BYE2
	else ifrnd 96 sound FV_BYE3
	else ifrnd 96 ifg raining 0 sound FV_BYE5
	else sound FV_BYE4
	set npc_talking 90
	set ally_mag 0
	}

ifpdistl 1536
 ifp pfacing
  ifp palive
   ife npc_talking 0
   {
   set player_use 0
   ifhitspace
		{
		ifrnd 64 sound FV_HELLO1
		else ifrnd 64 sound FV_HELLO2
		else ifrnd 64 sound FV_HELLO3
		else ifrnd 64 sound FV_HELLO4
		else sound FV_HELLO5
		set ally_mag 1
		set npc_talking 90
		}
   }

ifg CIVS_PANIC 1 killit
enda

// Female peasant 2
useractor notenemy 25421
ifaction 0 action FIVE_ANG
ifg CIVS_PANIC 1 killit
enda

// Male peasant
useractor notenemy 25431
ifaction 0 action FIVE_ANG

ifg npc_talking 0 sub npc_talking 1
ifg ally_v_coold 0 sub ally_v_coold 1

ifpdistl 16384
ifcansee
 ifrnd 4
  ife ally_v_coold 0
   ife camerasprite -1
	{
	ifrnd 16 sound MV_MUSE1
	else ifrnd 16 sound MV_MUSE2
	else ifrnd 16 sound MV_MUSE3
	else ifrnd 16 sound MV_MUSE4
	else ifrnd 16 sound MV_MUSE5
	else ifrnd 16 sound MV_MUSE6
	else ifrnd 16 ife weather_type 1 sound MV_COLD1
	else ifrnd 16 ife weather_type 1 sound MV_COLD2
	else ifrnd 16 ifg raining 0 sound MV_RAIN1
	set ally_v_coold 600
	set npc_talking 120
	}

ifpdistg 4096
 ife ally_mag 1
  ife npc_talking 0
  ifrnd 96
	{
	sound MV_FAREW1
	set npc_talking 90
	set ally_mag 0
	}

ifpdistl 1536
 ifp pfacing
  ifp palive
   ife npc_talking 0
   {
   set player_use 0
   ifhitspace
		{
		ife ally_mag 0
			{
			ifrnd 128 soundonce MV_GREET1
			else soundonce MV_GREET2
			set ally_mag 1
			}
		else
		 ife userdef.soundtoggle 1
			{
			getcurraddress jump_target_addr
			ifrnd 32 sound MV_PLAYR1
			else ifrnd 32 sound MV_PLAYR2
			else ifrnd 32 ife player[].holster_weapon 0 sound MV_PLAYR3
			else ifrnd 32 sound MV_PLAYR4
			else ifrnd 32 ife CHAR 1 sound MV_PLAYR5
			else ifrnd 32 ife CHAR 14 sound MV_PLAYR5
			ifnosounds jump jump_target_addr
			}
		set npc_talking 90
		}
   }

ifg CIVS_PANIC 1 killit
enda

// Male peasant 2
useractor notenemy 25426
ifaction 0 action FIVE_ANG
ifg CIVS_PANIC 1 killit
enda



//*************************************************
// MEDIEVAL CIVILIAN ANIMATIONS
//*************************************************

action SINGLEFRAME 0 1 5 1 1
action TWOFRAME_SLOWSPEED 0 2 5 1 180
action TWOFRAME_MEDIUMSPEED 0 2 5 1 24
action TWOFRAME_FASTSPEED 0 2 5 1 12

// Male peasant raking
useractor notenemy 25436
cstator 257
ifaction 0 action TWOFRAME_MEDIUMSPEED
ifg ally_v_coold 0 sub ally_v_coold 1

ifaction TWOFRAME_FASTSPEED
	{
	ifactioncount 90
		{
		ifrnd 96 action TWOFRAME_MEDIUMSPEED
		else ifrnd 96 action TWOFRAME_SLOWSPEED
		else resetactioncount
		}
	}

ifaction TWOFRAME_MEDIUMSPEED
	{
	ifactioncount 90
		{
		ifrnd 96 action TWOFRAME_SLOWSPEED
		else ifrnd 96 action TWOFRAME_FASTSPEED
		else resetactioncount
		}
	}

ifaction TWOFRAME_SLOWSPEED
	{
	ifactioncount 90
		{
		ifrnd 96 action TWOFRAME_MEDIUMSPEED
		else ifrnd 96 action TWOFRAME_FASTSPEED
		else resetactioncount
		}
	}

ifpdistl 16384
ifcansee
 ifrnd 4
  ife ally_v_coold 0
   ife camerasprite -1
	{
	ifrnd 16 soundonce MV_TOIL1
	else ifrnd 16 soundonce MV_TOIL2
	else ife weather_type 1 ifrnd 16 soundonce MV_COLD1
	else ife weather_type 1 ifrnd 16 soundonce MV_COLD2
	set ally_v_coold 300
	}

ifpdistl 1536
 ifp pfacing
  ifp palive
   ife ally_v_coold 0
   {
   set player_use 0
   ifhitspace
		{
		ifrnd 128 soundonce MV_GOAWAY1
		else soundonce MV_GOAWAY2
		set ally_v_coold 90
		}
   }

ifg CIVS_PANIC 1 killit
enda

// Female peasant washing cup
useractor notenemy 25475
ifaction 0 action SINGLEFRAME
ifaction SINGLEFRAME ifrnd 8 action TWOFRAME_FASTSPEED
ifaction TWOFRAME_FASTSPEED ifrnd 4 action SINGLEFRAME

ifg npc_talking 0 sub npc_talking 1
ifg ally_v_coold 0 sub ally_v_coold 1

ifpdistl 16384
ifcansee
 ifrnd 4
  ife ally_v_coold 0
   ife camerasprite -1
   ifnosounds
	{
	ifg INEBRIATION 0 sound BARKP_DRINK1
	else ifrnd 16 sound BARKP_MUSE1
	else ifrnd 16 sound BARKP_MUSE2
	else ifrnd 16 sound BARKP_MUSE3
	set ally_v_coold 600
	set npc_talking 120
	}

ifpdistg 4096
 ife ally_mag 1
  ife npc_talking 0
  ifrnd 96
	{
	ifrnd 96 sound BARKP_BYE1
	else ifrnd 96 sound BARKP_BYE2
	else ifrnd 96 sound BARKP_BYE3
	else sound BARKP_BYE4
	set npc_talking 90
	set ally_mag 0
	}

ifpdistl 1536
 ifp pfacing
  ifp palive
   ife npc_talking 0
   {
   set player_use 0
   ifhitspace
		{
		ife TIMEZONE 11 ifrnd 128 sound BARKP_AGAIN1
		else ifrnd 96 sound BARKP_HELLO1
		else ifrnd 96 sound BARKP_HELLO2
		else sound BARKP_DRINK2
		set ally_mag 1
		set npc_talking 90
		}
   }

ifg CIVS_PANIC 1 killit
enda

// Female peasant 2 drinking
useractor notenemy 25455
ifaction 0 action SINGLEFRAME

ifaction SINGLEFRAME ifrnd 8 action TWOFRAME_SLOWSPEED
ifaction TWOFRAME_SLOWSPEED ifrnd 4 action SINGLEFRAME
ifg CIVS_PANIC 1 killit
enda

// Male peasant 2 drinking
useractor notenemy 25465
ifaction 0 action SINGLEFRAME

ifaction SINGLEFRAME ifrnd 8 action TWOFRAME_SLOWSPEED
ifaction TWOFRAME_SLOWSPEED ifrnd 4 action SINGLEFRAME
ifg CIVS_PANIC 1 killit
enda

// Tied up peasant girl in skytown prison becoming not so tied up when correct activator is used..
useractor notenemy 24306
checkactivatormotion 585
ife RETURN 1
{
	// change to non tied up variant
	seta[].picnum 24307

	// bring coordinates forward a bit
	seta[].x -93913
	seta[].y -190721
}
enda
