/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile FR_SORCERER // Ally Sorcerer

action A_FSORCERER_FROZEN 6 1 5 1 10
action A_FSORCERER_FLOAT 1 1 5 1 10
action A_FSORCERER_FLOATSTOP 1 1 5 1 10
action A_FSORCERER_FLOATUP 1 1 5 1 10
action A_FSORCERER_FLOATDOWN 1 1 5 1 10
action A_FSORCERER_CAST 11 3 5 1 20
action A_FSORCERER_PAIN 6 1 5 1 10
action A_FSORCERER_TELEPORT 6 1 5 1 10
action A_FSORCERER_TELEPORTENEMY 6 1 5 1 10
action A_FSORCERER_DIE 26 8 1 1 15

move M_FSORCERER_FLOAT 210

defstate fsorcerer_checktarget
	state checkfortarget

	ifn my_target -1
	{
		ldist xydist THISACTOR my_target
		ifrnd 64
		  ifg xydist 2048 // 192 512
		{
			geta[my_target].x temp
			geta[my_target].y temp2
			set temp3 temp
			add temp3 1024
			randvar temp4 2048
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			updatesector temp5 temp6 temp7
			ifn temp7 -1
			{
				set INTERNALCOUNT 0
				action A_FSORCERER_TELEPORTENEMY
				resetcount
			}
		}
		else
		{
			state SORCERER_ATTACK_VOICES
			soundonce PRI_RDY
			action A_FSORCERER_CAST
		}
	}
ends


useractor notenemy FR_SORCERER

	ifaction 0
	{
		seta[].htwaterzoffset 8
		set blood_type 1
		spritepal 40
		spawn SHOOTME2
		strength 250
		sizeat 24 24
		action A_FSORCERER_FLOAT
		randvar LAST_NAME 22

		ifspawnedby APLAYER
		{
			spawn 13950
			globalsound DEMON_TELEPORT
			soundonce SORC_SPAWN
			state startfollowplayer
			set INTERNALCOUNT_3 960
		}
		else ifspawnedby RESPAWN
		{
			spawn 13950
			globalsound DEMON_TELEPORT
			soundonce SORC_SPAWN
			state startfollowplayer
			set INTERNALCOUNT_3 -1
		}
		else
			set INTERNALCOUNT_3 -1

		cstat 256
	}

	// despawn if the player characters gets changed
	ifspawnedby APLAYER ife just_changed 1 set INTERNALCOUNT_3 0

	ifg INTERNALCOUNT_2 0
		sub INTERNALCOUNT_2 1

	ifn INTERNALCOUNT_3 -1
	{
		ifg INTERNALCOUNT_3 0 sub INTERNALCOUNT_3 1
		else ife INTERNALCOUNT_3 0
		{
			globalsound UNMADE
			spawn SKULLEXPLOSION
			state removefollower
			killit
		}
	}

	ifg targetlock 0 sub targetlock 1

	ifaction A_FSORCERER_FROZEN
		state frozen_code
	else ifaction A_FSORCERER_DIE
	{
		move STOP
		cstat 0
		spriteflags 7
		seta[].shade -64
		ifactioncount 7 fall
		ifactioncount 8 killit
	}
	else ifaction A_FSORCERER_PAIN
	{
		move STOP
		ifactioncount 3
			action A_FSORCERER_FLOAT
	}
	else ifaction A_FSORCERER_CAST
	{
		move STOP

		ife my_target -1
			action A_FSORCERER_FLOAT
		else
		{
			state fronttowardstarget

			spriteflags 7
			seta[].shade -64

			ifactioncount 3
			{
				set PROJECTILE_TO_SHOOT PRIEST_BLAST
				set PROJECTILE_FIRING_SOUND PRI_FIRE
				state sang_enemyShootProjectile
				setthisprojectile[RETURN].extra 140

				action A_FSORCERER_FLOAT
			}
		}
	}
	else ifaction A_FSORCERER_FLOATSTOP
	{
		move STOP faceplayer

		ifpdistl 1024
		  ifp pfacing
		   ifhitspace
			ife INTERNALCOUNT_2 0
			{
				ife FOLLOW_PLAYER 0 state startfollowplayer
				else
				ife FOLLOW_PLAYER 1 state stopfollowplayer
				set INTERNALCOUNT_2 26
			}

		ifstrength 250
		  ifcount 3
		{
			addstrength 1
			resetcount
		}

		ifpdistg 1568
		  ife FOLLOW_PLAYER 1
			action A_FSORCERER_FLOAT
		else
			state fsorcerer_checktarget
	}
	else ifaction A_FSORCERER_FLOAT
	{
		ifmove M_FSORCERER_FLOAT nullop
		else move M_FSORCERER_FLOAT geth faceplayer

		ife FOLLOW_PLAYER 0
			action A_FSORCERER_FLOATSTOP

		sizeto 24 24
		spriteflags 3

		state fsorcerer_checktarget

		ifpdistg 8098
		  ife my_target -1
		  ifrnd 16
		{
			getp[].posx temp
			getp[].posy temp2
			set temp3 temp
			add temp3 1024
			randvar temp4 2048
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			updatesector temp5 temp6 temp7
			ifn temp7 -1
			{
				set INTERNALCOUNT 0
				action A_FSORCERER_TELEPORT
				resetcount
			}
		}
		else ifrnd 128
		{
			ifp phigher
				action A_FSORCERER_FLOATUP
			else
			{
				geta[].z z
				getp[].posz temp
				ifg temp z
					action A_FSORCERER_FLOATDOWN
			}
		}
		else ifpdistl 1568
			action A_FSORCERER_FLOATSTOP
	}
	else ifaction A_FSORCERER_TELEPORT
	{
		move STOP faceplayer

		add INTERNALCOUNT 1
		spawn FRAMEEFFECT1
		ife INTERNALCOUNT 4 { cstat 2 sizeto 2 24 }
		ife INTERNALCOUNT 26
		{
			cstat 32768
			getp[].posx temp
			getp[].posy temp2
			set temp3 temp
			add temp3 1024
			randvar temp4 2048
			rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
			updatesector temp5 temp6 temp7
			ifn temp7 -1
			{
				spawn 8433
				seta[].x temp5
				seta[].y temp6
				getp[].posz temp8
				sub temp8 1754
				seta[].z temp8
			}
			else
			{
				cstat 257
				sizeat 24 24
				action A_FSORCERER_FLOAT
			}
		}
		ife INTERNALCOUNT 27 { soundonce PRIEST_APPEAR cstat 2 sizeto 24 24 spawn 8433 }
		ife INTERNALCOUNT 40 { cstat 257 sizeat 24 24 action A_FSORCERER_FLOAT }
	}
	else ifaction A_FSORCERER_TELEPORTENEMY
	{
		move STOP faceplayer

		add INTERNALCOUNT 1
		spawn FRAMEEFFECT1
		ife INTERNALCOUNT 4 { cstat 2 soundonce PRIEST_CAST02 sizeto 2 24 }
		ife INTERNALCOUNT 26
			{
				cstat 32768
				geta[target].x temp
				geta[target].y temp2
				set temp3 temp
				add temp3 1024
				randvar temp4 2048
				rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6
				updatesector temp5 temp6 temp7
				ifn temp7 -1
				{
					spawn 8433
					seta[].x temp5
					seta[].y temp6
					geta[target].z temp8
					sub temp8 1754
					seta[].z temp8
				}
				else
				{
					cstat 257
					sizeat 24 24
					action A_FSORCERER_FLOAT
				}
			}
		ife INTERNALCOUNT 27 { soundonce PRIEST_APPEAR cstat 2 sizeto 24 24 spawn 8433 }
		ife INTERNALCOUNT 40 { cstat 257 sizeat 24 24 action A_FSORCERER_FLOAT }
	}
	else ifaction A_FSORCERER_FLOATUP
	{
		ifmove M_SORCERER_FLOATUP nullop
		else move M_SORCERER_FLOATUP geth getv faceplayer

		state fsorcerer_checktarget

		ifrnd 16
			action A_FSORCERER_FLOAT
	}
	else ifaction A_FSORCERER_FLOATDOWN
	{
		ifmove M_SORCERER_FLOATDOWN nullop
		else move M_SORCERER_FLOATDOWN geth getv faceplayer

		state fsorcerer_checktarget

		ifrnd 16
			action A_FSORCERER_FLOAT
	}

	state SORCERER_FOLLOW_VOICES
	state ENEMYKNOCKBACKS

	ifhitweapon
	{
		soundonce PRIEST_PAIN
		spawn BLOOD
		guts JIBS6 1
		state random_wall_jibs
		ifwasweapon RPG
			seta[].xvel 5

		ifdead
		{
			state clear_ally_death
			ifwasweapon 7159 { spritepal 1 strength 1 sound SOMETHINGFROZE action A_FSORCERER_FROZEN }
			else ifwasweapon 6875 { set WEP3_BLOODY 21 sound STAB_01 }
			else ifwasweapon FREEZEBLAST { spritepal 1 strength 1 sound SOMETHINGFROZE action A_FSORCERER_FROZEN }
			else
			{
				sound PRIEST_DEATH
				action A_FSORCERER_DIE
				state rf
			}
		}
		else ifrnd 16
			action A_FSORCERER_PAIN
	}
enda
