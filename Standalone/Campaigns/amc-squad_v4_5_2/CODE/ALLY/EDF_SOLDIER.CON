/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile EDFSOLDIER_ACTIVE // Ally

action A_EDFSOLDIER_IDLE 20 1 5 1 11
action A_EDFSOLDIER_EASE -1437 1 5 1 11
action A_EDFSOLDIER_MOVE 0 4 5 1 4

action A_EDFSOLDIER_RELOAD 0 4 5 1 4
action A_EDFSOLDIER_RELOADSTAND 20 1 5 1 11

action A_EDFSOLDIER_JUMP 6792 1 5
action A_EDFSOLDIER_FALL 6792 1 5

action A_EDFSOLDIER_FIRE 20 2 5 1 8
action A_EDFSOLDIER_FIRE2 6797 2 5 1 8

action A_EDFSOLDIER_NADE 6807 2 5 1 12
action A_EDFSOLDIER_HEAL 6817 1 5 1 12

action A_EDFSOLDIER_PAIN 30 1 5 1 8

action A_EDFSOLDIER_DYING 35 6 1 1 11
action A_EDFSOLDIER_DEAD 40 1 1 1 1

action A_EDFSOLDIER_DYING2 6822 5 1 1 11
action A_EDFSOLDIER_DEAD2 6826 1 1 1 1

move M_EDF_WALK 266
move M_EDF_SLOW 96

defstate edf_idle
		move STOP

		sleeptime 0

		// If following player, move if they move away
		ife FOLLOW_PLAYER 1
		{
			ifpdistg 1535
			{
				set HITSCAN_TO_ACTOR PLAYER_IDENTITY
				state hitscanTo

				ife hitsprite PLAYER_IDENTITY
					action A_EDFSOLDIER_MOVE
			}
		}

		// Regen health
		add INTERNALCOUNT 1

		ifge INTERNALCOUNT 6
		{
			geta[].extra temp

			ifspritepal 16
			  ifl temp 200
			{
				addstrength 1
			}
			else ifl temp 100
				addstrength 1

			set INTERNALCOUNT 0
		}

		state checkfortarget

		ifn my_target -1
		{
			ifrnd 16
				state EDFSOLD_INCOMING

			action A_EDFSOLDIER_MOVE
		}
		else
			state rand_lookaround
		ifaction A_EDFSOLDIER_IDLE ifrnd 2 action A_EDFSOLDIER_EASE
ends

defstate edf_fireweapon
		move STOP

		ifn my_target -1
		{
			geta[my_target].extra temp

			ifle temp 0
				state EDFSOLD_TARGET_DOWN
		}
		state checkfortarget

		ifactioncount 1
		  ifn my_target -1
		{
			state fronttowardstarget

			ifspritepal 16
			{
				set PROJECTILE_FIRING_SOUND M16_FIRE
				set PROJECTILE_TO_SHOOT ENEMY_BULLET
				set PROJECTILE_PAL 8
				state sang_enemyShootProjectile
			}
			else
			{
				set PROJECTILE_FIRING_SOUND M16_NPC
				set PROJECTILE_TO_SHOOT ENEMY_BULLET
				ifspritepal 13 nullop else set PROJECTILE_PAL 8
				state sang_enemyShootProjectile
			}

			state npc_rifleshell

			ife PERFMODE 1
			 ifcansee
			{
				set gunsmoke_z 7244
				set gunsmoke_angle 256
				state spawn_gunsmoke_z
				set gunsmoke_z 6044
				state spawn_enemy_ARFLASH
				state spawn_muzzleflash
			}

			sub ally_mag 1
			ife ally_mag 0
			{
				ifspritepal 16 // M60 guy!
				{
					shoot BIG_RIFLE_MAG
					sound PKM_COVER_U
				}
				else
					state bgar_mag_out_sounds

				action A_EDFSOLDIER_RELOADSTAND
			}
			resetactioncount
		}
		else ife my_target -1
			action A_EDFSOLDIER_RELOADSTAND
ends

defstate edfsoldier_handleplayerusepress
	ifpdistl 1024
	  ifp pfacing
	  ifp palive
	   ifn sprite[].pal 13
	{
		ifdead break			
		set player_use 0
		ife use_action_allowed 1
		{
			set hit_key 30
			ifspritepal 3
				break

			ife FOLLOW_PLAYER 0
				state startfollowplayer
			else ife FOLLOW_PLAYER 1
				state stopfollowplayer

			state setIgnoreUse
		}
	}
ends

defstate edfsoldier_reload
	ife ally_mag 2
		state EDFSOLD_RELOAD

	ifspritepal 16 // M60 guy?
	{
		add ally_mag 2
		ife ally_mag 40 sound PKM_BOX_IN
		ifge ally_mag 100 { set ally_mag 100 ife FOLLOW_PLAYER 1 action A_EDFSOLDIER_MOVE else action A_EDFSOLDIER_IDLE }
	}
	else
	{
		add ally_mag 1
		ife ally_mag 15 state bgar_mag_in_sounds
		ifge ally_mag 30 { set ally_mag 30 ife FOLLOW_PLAYER 1 action A_EDFSOLDIER_MOVE else action A_EDFSOLDIER_IDLE }
	}
ends

spriteshadow EDFSOLDIER_ACTIVE
spritenvg EDFSOLDIER_ACTIVE

useractor notenemy EDFSOLDIER_ACTIVE
	fall

	// don't block player
	ifn sprite[].pal 13
	{
	  ifpdistl 1024
		cstat 256
	else cstat 257
	}

	ifg enemy_cooldown1 0 sub enemy_cooldown1 1

	ifaction 0
	{
		seta[].htwaterzoffset 8
		spawn SHOOTME2

		ifspritepal 16
		{
			strength 200
			set enemy_attack 6
			set ally_mag 100
		}
		else
		{
			strength 100
			set enemy_attack 3
			set ally_mag 30
		}

		sizeat 22 22
		randvar LAST_NAME 65
		add LAST_NAME 7200
		cstat 257

		ifspritepal 13 { set faction_flag 512 state monst_glow }

		ifspawnedby BLACK_HAWK
		{
			set ally_mag 0
			action A_EDFSOLDIER_RELOAD
		}
		else
			action A_EDFSOLDIER_IDLE
	}

	state spawn_cold_breathe
	state ENEMYKNOCKBACKS
	state enemyfloordamage
	state enemy_fire_damage
	state enemy_ice_damage
	state enemy_spirit_damage
	state EDF_SOLDIER_MOVE_VOICE

	state edfsoldier_handleplayerusepress

	ifaction A_EDFSOLDIER_DEAD
	{
		move STOP
		strength 0
	}
	else
	ifaction A_EDFSOLDIER_DEAD2
	{
		move STOP
		strength 0
	}
	else
    ifaction A_EDFSOLDIER_DYING
	{
		move STOP
		set ALLY_VOICE 0
		strength 0
		cstat 0
		ifactioncount 6
		{
				action A_EDFSOLDIER_DEAD
		}
	}
	else
    ifaction A_EDFSOLDIER_DYING2
	{
		move STOP
		set ALLY_VOICE 0
		strength 0
		cstat 0
		ifactioncount 5
		{
				action A_EDFSOLDIER_DEAD2
		}
	}
	else
	ifaction A_EDFSOLDIER_IDLE state edf_idle
	else ifaction A_EDFSOLDIER_EASE state edf_idle
	else ifaction A_EDFSOLDIER_MOVE
	{
		move M_EDF_WALK geth

		ifactioncount 3
		{
			state npc_footsteps
			resetactioncount
		}

		ifpdistl 1536
		{
			ifphealthl 25
			 ifrnd 8
				state EDFSOLD_YOUR_HURT

			action A_EDFSOLDIER_IDLE
		}

		state checkfortarget

		ifn my_target -1
		{
			ifrnd 16 state EDFSOLD_INCOMING
			ldist temp THISACTOR my_target
			ifg temp 8192 action A_EDFSOLDIER_FIRE2
			else
				{
				geta[my_target].extra temp
				ifspritepal 13 set temp 300
				ifge temp 70 ife enemy_cooldown1 0 ifg enemy_attack 0 action A_EDFSOLDIER_NADE else
				action A_EDFSOLDIER_FIRE
				}
		}
		else ife FOLLOW_PLAYER 1
			state faceplayerstate

		ifnotmoving
		{
			operate

			geta[].ang angvar
			ifrnd 128
				add angvar 512
			else
				sub angvar 512

			seta[].ang angvar
		}
	}
	else ifaction A_EDFSOLDIER_RELOAD
	{
		move M_EDF_SLOW geth
		state edfsoldier_reload
	}
	else ifaction A_EDFSOLDIER_RELOADSTAND
	{
		move STOP
		state edfsoldier_reload
	}
	else ifaction A_EDFSOLDIER_NADE
	{
	move STOP
		state checkfortarget

			ifactioncount 2
			  ifn my_target -1
			{
				set enemy_cooldown1 300
				sub enemy_attack 1
				state fronttowardstarget
				set PROJECTILE_TO_SHOOT GRENADE
				set PROJECTILE_FIRING_SOUND THROWSOMET
				state sang_enemyShootProjectile
				action A_EDFSOLDIER_MOVE
			}
			else ife my_target -1
				action A_EDFSOLDIER_MOVE
	}
	else ifaction A_EDFSOLDIER_FIRE
	state edf_fireweapon
	else ifaction A_EDFSOLDIER_FIRE2
	state edf_fireweapon
	else ifaction A_EDFSOLDIER_PAIN
	{
		move STOP

		ifactioncount 4
		{
			ife FOLLOW_PLAYER 1
				action A_EDFSOLDIER_MOVE
			else
				action A_EDFSOLDIER_IDLE
		}
	}

	ifhitweapon
	{
	ifaction A_EDFSOLDIER_DEAD break
	ifaction A_EDFSOLDIER_DEAD2 break
		ifdead
		{
			state clear_ally_death
			state rf
			ifpdistl 8192
			 ifn sprite[].pal 13
				set PLAYER_VOICEOVER 1
				
		ifspritepal 13
			{
			ifand npc_killed 512 nullop else xorvar npc_killed 512
			}
		else
			{
			ifand npc_killed 64 nullop else xorvar npc_killed 64
			}

			randvar temp 30
			ifl temp 10 sound EDFSOLD_DIE1
			else ifl temp 20 sound EDFSOLD_DIE2
			else sound EDFSOLD_DIE3

			ifrnd 76
			{
				ifspritepal 16
				{
					espawn M60_SPRITE
					setav[RETURN].YVELSAVED ally_mag
				}
				else
				{
					espawn M16SPRITE
					add ally_mag 30
					setav[RETURN].YVELSAVED ally_mag
				}
			}
			ifrnd 128 action A_EDFSOLDIER_DYING2 else
			action A_EDFSOLDIER_DYING
			ifrnd 76
				spawn BLOODPOOL
		}
		else
		{
			spawn BLOOD
			ifrnd 76 action A_EDFSOLDIER_PAIN
			state ENEMYKNOCKBACKS
			state random_wall_jibs
			state NEWGUNEFFECTS
		}
	}
enda
