/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/

damageeventtile DARK_ELF

action A_DARKELF_GUARD 0 1 5 1 11
action A_DARKELF_IDLE 0 1 5 1 11
action A_DARKELF_MOVE 5 4 5 1 12
action A_DARKELF_ATTACK 25 2 5 1 12

action A_DARKELF_PAIN 35 1

action A_DARKELF_DYING 35 6 1 1 12
action A_DARKELF_DEAD 40 1 1 1 1

spriteshadow DARK_ELF

useractor notenemy DARK_ELF
fall
ifaction 0
{
	seta[].htwaterzoffset 8
	ifspritepal 3 set INVASION 1
	ifspritepal 0 spritepal 49
	sizeat 24 24
	clipdist 60
	set npc_talking 0
	strength 125
	// TODO remove shootme refs
	spawn SHOOTME2

	cstat 257
	action A_DARKELF_GUARD
}

ifg npc_talking 0 sub npc_talking 1

ifdead nullop else state detectledge

state spawn_cold_breathe

state ENEMYKNOCKBACKS
state enemyfloordamage

state enemy_fire_damage
state enemy_ice_damage
state enemy_spirit_damage

ifaction A_DARKELF_DEAD
	{
	move STOP
	ifhitweapon
		{
		ifwasweapon RADIUSEXPLOSION
			{
			state squish_sounds
			state standard_jibs
			state human_jibs
			killit
			}
		}
	}
else
ifaction A_DARKELF_DYING
	{
	strength 0
	ifactioncount 5 { state BODY_FALL_NOISES action A_DARKELF_DEAD ifrnd 76 spawn BLOODPOOL  }
	}
else
ifaction A_DARKELF_PAIN
	{
	ifactioncount 0
	{
	ifrnd 128 sound DARKE_PAIN1
	else sound DARKE_PAIN2
	}
	else { }
	move STOP
	ifdead action A_DARKELF_DYING else
	ifactioncount 4 action A_DARKELF_MOVE
	}
else
ifaction A_DARKELF_ATTACK
{
	move STOP
	state checkfortarget

	ifn my_target -1
	{
		state fronttowardstarget

		ifactioncount 2
			{
			set PROJECTILE_TO_SHOOT GREEN_ARCH_BOLT
			set PROJECTILE_FIRING_SOUND DARKE_FIRE
			state sang_enemyShootProjectile
			action A_DARKELF_MOVE // go back to seeking AI
			}
	}
	else {
		action A_DARKELF_MOVE
	}
}
else
ifaction A_DARKELF_MOVE
{
	move GUARD_MOVE geth

	state checkfortarget
	ifrnd 32
	ifn my_target -1
	{

	ifrnd 1
		 ife npc_talking 0
			{
			ifrnd 96 sound DARKE_SIGHT1
			else ifrnd 96 sound DARKE_SIGHT2
			else ifrnd 96 sound DARKE_SIGHT3
			else sound DARKE_SIGHT4
			set npc_talking 300
			}


		dist temp THISACTOR my_target // if found, get distance

		// If stayput, can snipe from further away
		set temp2 16384

		ifl temp temp2
		{
			sound BOWDRAW
			action A_DARKELF_ATTACK
			resetactioncount
		}
	}
	else ifn my_previous_target -1
	{
		// is this previous target dead? go to idle state
		geta[my_previous_target].extra temp8

		ifl temp8 1
			{
				 ife npc_talking 0
					{
					ifrnd 96 sound DARKE_KILL1
					else ifrnd 96 sound DARKE_KILL2
					else ifrnd 96 sound DARKE_KILL3
					else ifrnd 96 sound DARKE_KILL4
					else sound DARKE_KILL5
					set npc_talking 300
					}
			action A_DARKELF_IDLE
			}
	}
	else // where would we run to? go idle
		action A_DARKELF_IDLE

	ifactioncount 3
	{
		ifrnd 128 sound HEELS_1 else sound HEELS_3
		resetactioncount
	}
	ifnotmoving operate
}
else
ifaction A_DARKELF_IDLE
	{
	move STOP
	state rand_lookaround
	sleeptime 0
	state checkfortarget
	ifn my_target -1
		{
		action A_DARKELF_MOVE
		}

	ifg targetlock 0 sub targetlock 1

	ifn sprite[].pal 42 ifstrength 300 ifcount 6 { addstrength 1 resetcount }
	}
else
ifaction A_DARKELF_GUARD
	{
		move STOP
		sleeptime 0
		state checkfortarget
		ifn my_target -1
			{

			action A_DARKELF_MOVE
			}

		ifpdistl 1536
		 ifp pfacing
		  ifp palive
		   ifhitspace
		    ife npc_talking 0
				{
				set npc_talking 120
				}

		ifg targetlock 0 sub targetlock 1

		ifstrength 300 ifcount 6 { addstrength 1 resetcount }
	}


ifhitweapon
	{
	spawn BLOOD
	ifspritepal 42 state NEWGUNEFFECTS
	state random_wall_jibs
	ifdead
		{
		state clear_ally_death
		ifspritepal 42 { ifand npc_killed 128 nullop else xorvar npc_killed 128 }
		else { ifand npc_killed 64 nullop else xorvar npc_killed 64 }
		state rf
		ifwasweapon RPG
			{
			  state squish_sounds
			  state human_jibs
			  state standard_jibs
			  killit
			}
		  ifwasweapon RADIUSEXPLOSION
			{
			  state squish_sounds
			  state human_jibs
			  state standard_jibs
			  killit
			}
		  ifand PROJ_UDATA PROJ_GIB
			{
			  state squish_sounds
			  state human_jibs
			  state standard_jibs
			  killit
			}
			ifg energy_damage 0
				{
				shoot SPARK2 shoot SPARK2 shoot SPARK2
				spritepal 4
				guts JIBS3 6
				spawn DISINTIGRATE_GUY
				killit
				}
			ifg spirit_damage 0
				{
				shoot SPARK2 shoot SPARK2 shoot SPARK2
				spritepal 1
				guts JIBS3 6
				spawn SPIRIT_DEATH_GUY
				killit
				}
			ifg fire_damage 0
				{
				spawn ONFIREGUY
				killit
				}


				ifpdistl 8192 set PLAYER_VOICEOVER 1
				randvar temp 30
				ifl temp 10 sound DARKE_DIE1 else
				ifl temp 20 sound DARKE_DIE2 else
				sound DARKE_DIE3

			action A_DARKELF_DYING
			resetactioncount

		}
		else ifrnd 64 action A_DARKELF_PAIN
	}
enda
