/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:
This file defines cheat codes and their behavior.
--------------------------------------------------------------------------------
*/

define CHEAT_CORNHOLIO 0
define CHEAT_STUFF 1
define CHEAT_SCOTTY 2
define CHEAT_COORDS 3
define CHEAT_VIEW 4
define CHEAT_TIME 5
define CHEAT_UNLOCK 6
define CHEAT_CASHMAN 7
define CHEAT_ITEMS 8
define CHEAT_RATE 9
define CHEAT_SKILL 10
define CHEAT_BETA 11
define CHEAT_HYPER 12
define CHEAT_MONSTERS 13
define CHEAT_RESERVED 14
define CHEAT_RESERVED2 15
define CHEAT_TODD 16
define CHEAT_SHOWMAP 17
define CHEAT_KROZ 18
define CHEAT_ALLEN 19
define CHEAT_CLIP 20
define CHEAT_WEAPONS 21
define CHEAT_INVENTORY 22
define CHEAT_KEYS 23
define CHEAT_DEBUG 24
define CHEAT_RESERVED3 25
define CHEAT_COMEGETSOME 26

// cheat sequences
dc CHEAT_CORNHOLIO    chim
dc CHEAT_STUFF        gimme
dc CHEAT_SCOTTY       warp###
dc CHEAT_ITEMS        tooltime
dc CHEAT_BETA         woo
dc CHEAT_HYPER        sanic
dc CHEAT_TODD         ammo
dc CHEAT_SHOWMAP      reveal
dc CHEAT_KROZ         weeb
dc CHEAT_ALLEN        sang
dc CHEAT_UNLOCK       home
dc CHEAT_CASHMAN      water

udc CHEAT_COMEGETSOME

var returnvar_inv 0 0

appendevent EVENT_SETDEFAULTS
    cheatkeys 30 20 // A T
endevent

defstate give_char_holoduke
    switch CHAR
    case 0
        set returnvar_inv 900
        break
    case 4
        set returnvar_inv 600
        break
    case 6
        set returnvar_inv 300
        break
    case 9
        set returnvar_inv 600
        break
    default
        set returnvar_inv 2400
        break
    endswitch
ends

onevent EVENT_CHEATGETHOLODUKE
    set cheated 1
    state give_char_holoduke
    set RETURN returnvar_inv
endevent

defstate give_char_heat
    set returnvar_inv 4800
ends

onevent EVENT_CHEATGETHEAT
    set cheated 1
    state give_char_heat
    set RETURN returnvar_inv
endevent

defstate give_char_scuba
    ife CHAR 0 set returnvar_inv 12800
    else set returnvar_inv 6400
ends

onevent EVENT_CHEATGETSCUBA
    set cheated 1
    state give_char_scuba
    set RETURN returnvar_inv
endevent

defstate give_char_firstaid
    getp[].max_player_health returnvar_inv
ends

onevent EVENT_CHEATGETFIRSTAID
    set cheated 1
    state give_char_firstaid
    set RETURN returnvar_inv
endevent

defstate give_char_steroids
	set booster_amount 400
    set returnvar_inv 400
ends

onevent EVENT_CHEATGETSTEROIDS
    set cheated 1
    state give_char_steroids
    set RETURN returnvar_inv
endevent

defstate give_char_boots
    set returnvar_inv 200
ends

onevent EVENT_CHEATGETBOOT
    set cheated 1
    state give_char_boots
    set RETURN returnvar_inv
endevent

defstate give_char_shields
    getp[].max_shield_amount returnvar_inv
ends

onevent EVENT_CHEATGETSHIELD
    set cheated 1
    state give_char_shields
    set RETURN returnvar_inv
endevent

defstate give_char_jetpack
    set returnvar_inv 1600
ends

onevent EVENT_CHEATGETJETPACK
    set cheated 1
    state give_char_jetpack
    set RETURN returnvar_inv
endevent

defstate give_keys
    set ACCESSKEY2 127
    set PLUGKEY 7
    set RUNEKEYS 63
    set OLDKEYS 63
    set NORMAL_KEYS 31
    set SCARABKEYS 511
    set ABYSS_KEYS 31
    set SW_KEYS 255
    set SWIPECARDS 64
    set GENERIC_KEYS 99
    set temple_key 4
    set HELLGKEYS 31
ends

// TODO: define actual maxima for these
defstate give_alt_ammo
    set flechete_shells 20
    set explosive_shells 20
    set dragon_shells 20
    set magnum_shells 20
    set normal_ammo 200
    set silver_ammo 200
    set incend_ammo 150
    set tesla_ammo 100
    set micro_torpedo 20
    set heat_seek_card 5
    set atomic_bomb 1
    set DUP_missile_ammo 20
    set fortymm_grenades 20
    set fortymm_caseless 20
    set fortymm_slug 20
    set fortymm_MIA_shells 20
    set fortymm_plasma 20
    set fortymm_shells 20
    set fortymm_electro 20
    set fortymm_cryo 20
    set silver_bolts 20
    set void_bolts 20
    set p_blue_mana 200
    set p_green_mana 200
	set p_orange_mana 200
	set p_purple_mana 200
    set HEAT_SINKS 5
ends

defstate give_aux_inventory
    set AIRSTRIKE_MARKER 1
    set GOT_FIREWALL 1
    set GOT_FLIPPER 1
    set AIR_FILTER 1
    set EGYPT_ARTIFACTS 15
    set TOOLBOX 1
    set GOT_EXPLOSIVE 99
    set COGS 99
    set ROPE 1
    set GAS_MASK 1
ends

defstate give_mega_alt_ammo
    set flechete_shells 9999
    set explosive_shells 9999
    set dragon_shells 9999
    set magnum_shells 9999
    set normal_ammo 9999
    set silver_ammo 9999
    set incend_ammo 9999
    set tesla_ammo 9999
    set micro_torpedo 9999
    set heat_seek_card 9999
    set atomic_bomb 9999
    set DUP_missile_ammo 9999
    set fortymm_grenades 9999
    set fortymm_caseless 9999
    set fortymm_slug 9999
    set fortymm_MIA_shells 9999
    set fortymm_plasma 9999
    set fortymm_shells 9999
    set fortymm_electro 9999
    set fortymm_cryo 9999
    set silver_bolts 9999
    set void_bolts 9999
    set p_blue_mana 9999
    set p_green_mana 9999
    set HEAT_SINKS 9999
ends

onevent EVENT_ACTIVATECHEAT

    switch RETURN
    case CHEAT_CORNHOLIO
    case CHEAT_KROZ
        ife userdef[].player_skill 5
        {
            set RETURN -1
            setu[].god 0
            stopallsounds
            globalsound BEYOND_HIT
            wackplayer
            set player_spooked 98
            set override_spook_protection 1
            set cheat_spook 1
            state fade_out_white
            state setknockback
            quote 3700
        }
        else set cheated 1
        break
    case CHEAT_STUFF
        ife userdef[].player_skill 5
        {
            set RETURN -1
            quote 22
        }
        else
        {
            // keep hardcoded effects active
            set cheated 1
            state give_keys
            state give_alt_ammo
            state give_char_shields setp[].shield_amount returnvar_inv
            set guns_akimbo 63
        }
        break
    case CHEAT_ITEMS
        set RETURN -1
        ife userdef[].player_skill 5
            quote 22
        else
        {
            quote 3702
            set cheated 1
            state give_char_holoduke setp[].holoduke_amount returnvar_inv
            state give_char_heat setp[].heat_amount returnvar_inv
            state give_char_scuba setp[].scuba_amount returnvar_inv
            state give_char_firstaid setp[].firstaid_amount returnvar_inv
            state give_char_steroids setp[].steroids_amount returnvar_inv
            state give_char_boots setp[].boot_amount returnvar_inv
            state give_char_jetpack setp[].jetpack_amount returnvar_inv
            state give_aux_inventory
        }
        break
    case CHEAT_TODD
        set RETURN -1
        ife userdef[].player_skill 5
            quote 22
        else
        {
            // near infinite ammo
            quote 99
            set cheated 1
            setp[].ammo_amount 0 9999
            setp[].ammo_amount 1 9999
            setp[].ammo_amount 2 9999
            setp[].ammo_amount 3 9999
            setp[].ammo_amount 4 9999
            setp[].ammo_amount 5 9999
            setp[].ammo_amount 6 9999
            setp[].ammo_amount 7 9999
            setp[].ammo_amount 8 9999
            setp[].ammo_amount 9 9999
            setp[].ammo_amount 11 9999
            state give_mega_alt_ammo
        }
        break
    case CHEAT_BETA
        set RETURN -1
        ife userdef[].player_skill 5
            quote 22
        else
        {
			ifg SLO_MO_SHOWOFF 0 // cheat turns OFF slomo, doesnt count as cheat
			{
			set SLO_MO_SHOWOFF 1
			}
			else //slowmo cheat
			{
            set cheated 1
            quote 105
            palfrom 32 32 32 32
            set SLO_MO_SHOWOFF 120
            screensound START_SLOW
			}
        }
        break
    case CHEAT_KEYS
        ife userdef[].player_skill 5
            quote 22
        else
        {
            // keep hardcoded effects active
            set cheated 1
            state give_keys
        }
        break
    case CHEAT_CASHMAN
        set RETURN -1
        ife userdef[].player_skill 5
            quote 22
        else
        {
            quote 3701
            set cheated 1
            set GOT_FLIPPER 1
            set AIR_FILTER 1
        }
        break
    // benign cheats
    case CHEAT_UNLOCK
        set RETURN -1
        state get_furthest_amcbase_position
        startlevel returnvar_amcb_volume returnvar_amcb_level
        break
    case CHEAT_ALLEN
    case CHEAT_DEBUG
	case CHEAT_CLIP
        break
    // all remaining cheats
    default
        ife userdef[].player_skill 5
            quote 22
        else
            set cheated 1
        break
    endswitch

    ife cheated 1
        userquote 25

endevent
