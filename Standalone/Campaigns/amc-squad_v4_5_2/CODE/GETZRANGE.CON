/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
! This particular file was originally created by Phillip Kutin, aka. Helixhorned,
! and reimplements a function from the eduke32 source, ported to CON.
! Adapted for the game by James Stanfield.
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:
This CON module provides 'state thisactor_getzrange', reimplementing
VM_GetZRange() from the C source. This routine obtains actor[].ceilingz and
actor[].floorz for the current actor (THISACTOR), using getzrange() with the
initial z position displaced by -256.
--------------------------------------------------------------------------------
*/

// In
gamevar tagz_z 0 0

// Temp
gamevar tagz_cstat 0 0

// Out
gamevar tagz_ceilz 0 0
gamevar tagz_ceilhit 0 0
gamevar tagz_florz 0 0
gamevar tagz_florhit 0 0

// ZOFFSET in the EDuke32 source.
define TAGZ_ZOFFSET 256
// In the EDuke32 source, the <walldist> passed to VM_GetZRange().
define TAGZ_WALLDIST 127

gamevar check_count 0 2

// Updates the current actor's actor[].floorz and actor[].ceilngz members in
// the same way A_GetZRange() does.
defstate thisactor_getzrange
ifl check_count 1
{
    set tagz_z sprite[].z, sub tagz_z TAGZ_ZOFFSET

    // Back up and clear cstat.
    set tagz_cstat sprite[].cstat
    seta[].cstat 0

    getzrange sprite[].x sprite[].y tagz_z sprite[].sectnum
     /*out:*/ tagz_ceilz tagz_ceilhit tagz_florz tagz_florhit
      /*in:*/ TAGZ_WALLDIST CLIPMASK0

    // Restore cstat.
    seta[].cstat tagz_cstat

    // Set actor[] members for the current actor.
    seta[].htceilingz tagz_ceilz
    seta[].htfloorz tagz_florz
    set check_count 5
}
else sub check_count 1
ends
