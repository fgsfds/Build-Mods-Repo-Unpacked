/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/


defstate cultist_hud

    rotatesprite 34 187 NNORMALSIZE 0 7929 0 0 256 0 0 xdim ydim // HEALTH AND AMMO BOX
    ifg gun_mag -1
    {
        rotatesprite 85 187 NNORMALSIZE 0 7932 0 0 256 0 0 xdim ydim // MAGAZINE BOX
        ifg gun_mag 99 digitalnumber 3139 89 183 gun_mag 0 11 256 0 0 xdim ydim
        else ifg gun_mag 9 digitalnumber 3139 93 183 gun_mag 0 11 256 0 0 xdim ydim
        else digitalnumber 3139 97 183 gun_mag 0 11 256 0 0 xdim ydim // Display the ammo amount
    }

    ifg PHEALTH 99 digitalnumber 3139 21 183 PHEALTH 0 10 256 0 0 xdim ydim
    else ifg PHEALTH 9 digitalnumber 3139 26 183 PHEALTH 0 10 256 0 0 xdim ydim
    else digitalnumber 3139 29 183 PHEALTH 0 10 256 0 0 xdim ydim // HEALTH NUMBER

    // pal temp
    set hud_disp_temp 0
    ife currentweapon 0
    {
        ifand GUN_AMMO_TYPES 1 ifand AMMO_TYPES[currentweapon] 1
        {
            set hud_disp_temp 2
            set hud_disp_pammo explosive_shells
        }
        else ifand GUN_AMMO_TYPES 1 ifand AMMO_TYPES[currentweapon] 4
        {
            set hud_disp_temp 8
            set hud_disp_pammo flechete_shells
        }
        else ifand GUN_AMMO_TYPES 4 ifand AMMO_TYPES[currentweapon] 64
        {
            set hud_disp_temp 32
            set hud_disp_pammo magnum_shells
        }
        else ifand GUN_AMMO_TYPES 4 ifand AMMO_TYPES[currentweapon] 1024
        {
            set hud_disp_temp 55
            set hud_disp_pammo dragon_shells
        }
        else ifand GUN_AMMO_TYPES 2 ifand AMMO_TYPES[currentweapon] 2
        {
            set hud_disp_temp 12
            set hud_disp_pammo silver_ammo
        }
        else ifand GUN_AMMO_TYPES 2 ifand AMMO_TYPES[currentweapon] 32768
        {
            set hud_disp_temp 10
            set hud_disp_pammo incend_ammo
        }
        else ifand GUN_AMMO_TYPES 8 ifand AMMO_TYPES[currentweapon] 8
        {
            set hud_disp_temp 1
            set hud_disp_pammo tesla_ammo
        }
        else ifand GUN_AMMO_TYPES 16 ifand AMMO_TYPES[currentweapon] 16
        {
            set hud_disp_temp 12
            set hud_disp_pammo silver_bolts
        }
        else ifand GUN_AMMO_TYPES 16 ifand AMMO_TYPES[currentweapon] 16384
        {
            set hud_disp_temp 30
            set hud_disp_pammo void_bolts
        }
        else ife temp_weap 32 ifand gun_firemode_two 4
        {
            set hud_disp_temp 11
            set hud_disp_pammo DUP_missile_ammo
        }
        else
        {
            set hud_disp_temp 0
            getp[].ammo_amount currentweapon hud_disp_pammo
        }
    }
    // double barrel
    else ife currentweapon 2
    {
        ifand GUN_AMMO_TYPES 1 ifand AMMO_TYPES[currentweapon] 1
        {
            set hud_disp_temp 2
            set hud_disp_pammo explosive_shells
        }
        else ifand GUN_AMMO_TYPES 1 ifand AMMO_TYPES[currentweapon] 4
        {
            set hud_disp_temp 8
            set hud_disp_pammo flechete_shells
        }
        else
        {
            set hud_disp_temp 0
            getp[].ammo_amount currentweapon hud_disp_pammo
        }
    }
    // tommy gun
    else ife currentweapon 3
    {
        ifand GUN_AMMO_TYPES 2 ifand AMMO_TYPES[currentweapon] 2
        {
            set hud_disp_temp 12
            set hud_disp_pammo silver_ammo
        }
        else
        ifand GUN_AMMO_TYPES 2 ifand AMMO_TYPES[currentweapon] 32768
        {
            set hud_disp_temp 10
            set hud_disp_pammo incend_ammo
        }
        else
        {
            set hud_disp_temp 0
            getp[].ammo_amount currentweapon hud_disp_pammo
        }
    }
    // pipebomb trigger
	else ife currentweapon 10 getp[].ammo_amount 5 hud_disp_pammo
	else getp[].ammo_amount currentweapon hud_disp_pammo

    ifg hud_disp_pammo 99 digitalnumber 3139 55 183 hud_disp_pammo 0 hud_disp_temp 256 0 0 xdim ydim
    else ifg hud_disp_pammo 9 digitalnumber 3139 59 183 hud_disp_pammo 0 hud_disp_temp 256 0 0 xdim ydim
    else digitalnumber 3139 63 183 hud_disp_pammo 0 hud_disp_temp 256 0 0 xdim ydim // Display the ammo amount

    rotatesprite 284 187 NNORMALSIZE 0 7930 0 0 512 0 0 xdim ydim // INVENTORY BOX

	ifn CURSED 0
		{
		add hud_disp_temp5 1
		ifg hud_disp_temp5 2048 set hud_disp_temp5 0
		ifl CURSED 0 set hud_disp_temp6 128
		else ifg CURSED 128 set hud_disp_temp6 128
        else set hud_disp_temp6 CURSED
		mulvar hud_disp_temp6 -1
		add hud_disp_temp6 30
		rotatespritea 29 183 8192 hud_disp_temp5 22772 hud_disp_temp6 18 256 -255 0 0 xdim ydim
		}

	ifg P_FIRE_ARMOUR 0
		{
		rotatesprite 260 178 NORMALSIZE 0 7931 0 21 512 0 0 xdim ydim // Bar
		set hud_disp_temp P_FIRE_ARMOUR
		divvar hud_disp_temp 10
		add hud_disp_temp 260
		rotatesprite hud_disp_temp 178 NORMALSIZE 0 7931 0 4 512 0 0 xdim ydim // Black Bar
		digitalnumber 3087 262 178 P_FIRE_ARMOUR 0 12 512 0 0 xdim ydim // Number
		}

	ifg PARMOUR 0
		{
		rotatesprite 260 186 NORMALSIZE 0 7931 0 0 512 0 0 xdim ydim // Bar
		set hud_disp_temp PARMOUR
		divvar hud_disp_temp 10
		add hud_disp_temp 260
		rotatesprite hud_disp_temp 186 NORMALSIZE 0 7931 0 4 512 0 0 xdim ydim // Black Bar
		digitalnumber 3087 262 186 PARMOUR 0 12 512 0 0 xdim ydim // Number
		}

	ifg P_SPIRIT_ARMOUR 0
		{
		rotatesprite 260 194 NORMALSIZE 0 7931 0 11 512 0 0 xdim ydim // Bar
		set hud_disp_temp P_SPIRIT_ARMOUR
		divvar hud_disp_temp 10
		add hud_disp_temp 260
		rotatesprite hud_disp_temp 194 NORMALSIZE 0 7931 0 4 512 0 0 xdim ydim // Black bar
		digitalnumber 3087 262 194 P_SPIRIT_ARMOUR 0 12 512 0 0 xdim ydim // Number
		}

	rotatesprite 284 187 NNORMALSIZE 0 7925 0 0 512 0 0 xdim ydim // INVENTORY BOX PART 2

    rotatesprite 308 193 60638 0 7931 0 4 512 0 0 xdim ydim // P SHIELD BAR

    ifg P_HAS_SHIELD 0
        {
        digitalnumber 3087 309 193 P_ENERGYSHIELD 0 2 512 0 0 xdim ydim // Number
        }

	getp[].inven_icon hud_disp_temp
	ifn hud_disp_temp 0 // If the player's inventory is 'active'
        {
        ife hud_disp_temp 1 // MEDKIT
            {
            rotatesprite 285 186 INVENSIZE 0 c_medikit 0 0 512 0 0 xdim ydim
            getp[].firstaid_amount hud_disp_temp2
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 2 // STEROIDS
            {
            rotatesprite 285 186 INVENSIZE 0 c_steroids 0 0 512 0 0 xdim ydim
            getp[].steroids_amount hud_disp_temp2
            divvar hud_disp_temp2 4
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 3 // HOLODUKE
            {
            rotatesprite 285 186 INVENSIZE 0 c_holoduke 0 0 512 0 0 xdim ydim
            getp[].holoduke_amount hud_disp_temp2
            divvar hud_disp_temp2 6
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 4 // JETPACK
            {
            rotatesprite 285 186 INVENSIZE 0 c_jetpack 0 0 512 0 0 xdim ydim
            getp[].jetpack_amount hud_disp_temp2
            divvar hud_disp_temp2 16
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 5 // NIGHT VISION GOGGLES
            {
            rotatesprite 285 186 INVENSIZE 0 disp_nvg 0 0 512 0 0 xdim ydim
            getp[].heat_amount hud_disp_temp2
            divvar hud_disp_temp2 48
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 6 // SCUBA GEAR
            {
            rotatesprite 285 186 INVENSIZE 0 c_scuba 0 0 512 0 0 xdim ydim
            getp[].scuba_amount hud_disp_temp2
            divvar hud_disp_temp2 64
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }
        ife hud_disp_temp 7 // BOOTS
            {
            rotatesprite 285 186 INVENSIZE 0 c_boots 0 0 512 0 0 xdim ydim
            getp[].boot_amount hud_disp_temp2
            divvar hud_disp_temp2 2
            digitalnumber 3087 309 182 hud_disp_temp2 0 10 512 0 0 xdim ydim
            }

        ife hud_disp_temp 10 // TOOLBOX
            {
            rotatesprite 285 186 INVENSIZE 0 TOOLBOX_PICKUP 0 0 512 0 0 xdim ydim
            }
        ife hud_disp_temp 11 // ROPE
            {
            rotatesprite 285 186 20000 0 ROPE_PICKUP 0 0 512 0 0 xdim ydim
            }
        }
ends
