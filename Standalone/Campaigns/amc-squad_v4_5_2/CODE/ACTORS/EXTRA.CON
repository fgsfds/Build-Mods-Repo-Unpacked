/*
-------------------------------------------------------------------------------
===============================================================================

         -ooo:             +oo-            `+oo.       ./+oooooooooo+/:
        /NMMMN+            NMMNo`         -dMMM/      .mMMNmmmmmmmmmMMNo
       /NMd:mMMo           NMMNMd:      `oNMNMM/      :MMM/.........mMMh
      oNMd. -mMMy`         NMN:mMNs`   -hMm/mMM/      :MMM:         ::-.
    `sMMh`   .dMMh`        NMN .sNMd:`oNMh. mMM/      :MMM:
   `yMMMdhhhhhmMMMd.       NMN   /mMNdMm+`  mMM/      :MMM:         ss+:
  `hMMdhhhhhhhhhmMMm-      NMN    .yMMh.    mMM/      :MMM/.........mMMh
 .dMMs`         .hMMm:     NMN      :/`     mMM/      .mMMNmmmmmmmmmMMNo
 :oo+            `+oo+     +o+              +oo.       ./++ooooooooo+/:

###############################################################################
+ The AMC Squad
+ CON code by James Stanfield, Cedric "Sangman" Haegeman, Dino Bollinger,
+              Mikko Sandt, Cedric "Zaxtor" Lutes and Dan "Danarama" Gaskill
-------------------------------------------------------------------------------
* See AMC_MAIN.CON for a full list of script authors.

Feel free to use any code in the game for your own uses; just make
sure to mention the authors and/or "The AMC Squad" in your credits.
--------------------------------------------------------------------------------
NOTES:

--------------------------------------------------------------------------------
*/


action FORM 0
action FORMED 0
action UNFORM 0

defstate grab_item_sound
ifrnd 85 sound IBOX1
else ifrnd 170 sound IBOX2
else sound IBOX3
ends

useractor notenemy 3833 killit enda

defstate ITEM_BOX_STUFF
ifaction 0
	{

	ife CHAR 6 // Geoffrey find treasure perk
	 ifrnd 32
		{
		// check for luck artifacts
		ife ARTIFACTS_LOADOUT[6] 1 set temp4 1
		else ife ARTIFACTS_LOADOUT[6] 2 set temp4 1
		else ife ARTIFACTS_LOADOUT[6] 5 set temp4 1
		else set temp4 0
		ife LOTAGSAVED 0
			{
			ife temp4 1 ifrnd 4 set LOTAGSAVED 7856
			else ife temp4 0 ifrnd 2 set LOTAGSAVED 7856
			}
		ife HITAGSAVED 0
			{
			ife temp4 1 ifrnd 4 set HITAGSAVED 7857
			else ife temp4 0 ifrnd 2 set HITAGSAVED 7857
			}
		ife EXTRASAVED -1
			{
			ife temp4 1 ifrnd 4 set EXTRASAVED 7858
			else ife temp4 0 ifrnd 2 set EXTRASAVED 7858
			}
		ife XVELSAVED 0
			{
			ife temp4 1 ifrnd 4 set XVELSAVED 7860
			else ife temp4 0 ifrnd 2 set XVELSAVED 7860
			}
		ife YVELSAVED 0
			{
			ife temp4 1 ifrnd 4 set YVELSAVED 7861
			else ife temp4 0 ifrnd 2 set YVELSAVED 7861
			}
		ife ZVELSAVED 0
			{
			ife temp4 1 ifrnd 4 set ZVELSAVED 7859
			else ife temp4 0 ifrnd 2 set ZVELSAVED 7859
			}
		}

	ifn LOTAGSAVED 0 action ZERO
	ifn HITAGSAVED 0 action ZERO
	ifg EXTRASAVED 0 action ZERO
	ifn XVELSAVED 0 action ZERO
	ifn YVELSAVED 0 action ZERO
	ifn ZVELSAVED 0 action ZERO
	}

ifaction ZERO
	{
	   ifp palive
		 ifpdistl RETRIEVEDISTANCE // quote check ok
		  ifp pfacing
		   ife MOUSEUP 0
		    ife hit_key 0
		  {
		set player_use 0
		ifhitspace
			{
			set hit_key 30
			ifrnd 64
				{
				ifand NOISE_SAID 32 nullop else
					{
					set PLAYER_VOICEOVER 48
					xorvar NOISE_SAID 32
					}
				}
			ife temp7 512 soundonce OPEN_AMMO_B
			else ife temp7 1024 soundonce OPEN_FREEZER
			else ifactor 29505 soundonce PALACE_DOOR1
			set MOUSEUP 1
			getp[].ang PANG
			getp[].horiz PHORIZ
			set CHECKBOX THISACTOR
			geta[].pal temp5
			set CBOXSLOT1PAL temp5
			ifn LOTAGSAVED 0 set CBOXSLOT1 LOTAGSAVED
			ifn HITAGSAVED 0 set CBOXSLOT2 HITAGSAVED
			ifn EXTRASAVED 0 ifn EXTRASAVED -1 set CBOXSLOT3 EXTRASAVED
			ifn XVELSAVED 0 set CBOXSLOT4 XVELSAVED
			ifn YVELSAVED 0 set CBOXSLOT5 YVELSAVED
			ifn ZVELSAVED 0 set CBOXSLOT6 ZVELSAVED
			}
		}



	}


ife CHECKBOX THISACTOR
{
ife MISSION_UP 1 state reset_PDA
set temp9 1

ifg MOUSEX 232
 ifl MOUSEX 243
  ifg MOUSEY 28
   ifl MOUSEY 41
	{
	ifand BITS_PRESS 4
		{
		ife temp7 512 soundonce CLOSE_AMMO_B
		else ife temp7 1024 soundonce CLOSE_FREEZER
		else ifactor 29505 soundonce PALACE_DOOR2
		set MOUSEUP 0
		set CHECKBOX -1
		ife LOTAGSAVED 0
		 ife HITAGSAVED 0
		  ife EXTRASAVED 0
		   ife XVELSAVED 0
			ife YVELSAVED 0
			 ife ZVELSAVED 0
				killit // if empty, killit
		}
	}

	ifand BITS_PRESS 64
		{
		ife temp7 512 soundonce CLOSE_AMMO_B
		else ife temp7 1024 soundonce CLOSE_FREEZER
		else ifactor 29505 soundonce PALACE_DOOR2
		set MOUSEUP 0
		set CHECKBOX -1
		ife LOTAGSAVED 0
		 ife HITAGSAVED 0
		  ife EXTRASAVED 0
		   ife XVELSAVED 0
			ife YVELSAVED 0
			 ife ZVELSAVED 0
				killit // if empty, killit
		}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 46
	   ifl MOUSEY 90
		{
		ifand BITS_PRESS 4
		 ifn LOTAGSAVED 0
			set SELSLOT 1
		}


	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 46
	   ifl MOUSEY 90
		{
		ifand BITS_PRESS 4
		 ifn HITAGSAVED 0
			set SELSLOT 2
		}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 80
	   ifl MOUSEY 116
		{
		ifand BITS_PRESS 4
		 ifn EXTRASAVED 0
			set SELSLOT 3
		}

	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 80
	   ifl MOUSEY 116
		{
		ifand BITS_PRESS 4
		 ifn XVELSAVED 0
			set SELSLOT 4
		}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 117
	   ifl MOUSEY 152
		{
		ifand BITS_PRESS 4
		 ifn YVELSAVED 0
			set SELSLOT 5
		}

	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 117
	   ifl MOUSEY 152
		{
		ifand BITS_PRESS 4
		 ifn ZVELSAVED 0
			set SELSLOT 6

		}

	ife SELSLOT 1
	{
	ifn LOTAGSAVED 0 { espawnvar LOTAGSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set LOTAGSAVED 0
	set CBOXSLOT1 -1
	set SELSLOT 0
	}

	ife SELSLOT 2
	{
	ifn HITAGSAVED 0 { espawnvar HITAGSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set HITAGSAVED 0
	set CBOXSLOT2 -1
	set SELSLOT 0
	}

	ife SELSLOT 3
	{
	ifn EXTRASAVED 0 ifn EXTRASAVED -1 { espawnvar EXTRASAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set EXTRASAVED 0
	set CBOXSLOT3 -1
	set SELSLOT 0
	}

	ife SELSLOT 4
	{
	ifn XVELSAVED 0 { espawnvar XVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set XVELSAVED 0
	set CBOXSLOT4 -1
	set SELSLOT 0
	}

	ife SELSLOT 5
	{
	ifn YVELSAVED 0 { espawnvar YVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set YVELSAVED 0
	set CBOXSLOT5 -1
	set SELSLOT 0
	}

	ife SELSLOT 6
	{
	ifn ZVELSAVED 0 { espawnvar ZVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	set ZVELSAVED 0
	set CBOXSLOT6 -1
	set SELSLOT 0
	}

	   ifp palive
		 ifpdistl 4096
		  ife hit_key 0
		   ife CHECKBOX THISACTOR
		   ifhitspace
			{
			set hit_key 30
			ife temp7 512 soundonce CLOSE_AMMO_B
			ife temp7 1024 soundonce CLOSE_FREEZER
			set MOUSEUP 0
			set CHECKBOX -1
			}

	ifp pjumping
	{
	ife temp7 512 soundonce CLOSE_AMMO_B
	ife temp7 1024 soundonce CLOSE_FREEZER
	set MOUSEUP 0
	set CHECKBOX -1
	}

	ifpdistg RETRIEVEDISTANCE
	{
	ife temp7 512 soundonce CLOSE_AMMO_B
	ife temp7 1024 soundonce CLOSE_FREEZER
	set MOUSEUP 0
	set CHECKBOX -1
	ife LOTAGSAVED 0
	 ife HITAGSAVED 0
	  ife EXTRASAVED 0
	   ife XVELSAVED 0
	    ife YVELSAVED 0
	     ife ZVELSAVED 0
			killit // if empty, killit
	}
}
ends

useractor notenemy 12031
cstat 32768
state ITEM_BOX_STUFF

ifn temp9 1
		{
		ifpdistl 2048
			ifrnd 8 spawn GLINT
		else
			ifrnd 4 spawn GLINT
		}

enda

// *************************************************
// BREATHE VAPOUR
// *************************************************



action BREATHE_VAPOUR_FRAMES 0 4 1 1 20

move b_forward 30 0

useractor notenemy 10737
	ifaction 0
	{
	cstat 32768
	sizeat 2 2
	geta[].z temp
	sub temp 9096
	seta[].z temp

	ifn temp2 0
		{
		geta[].z temp3
		sub temp3 temp2
		seta[].z temp3
		}
	move b_forward geth
	action BREATHE_VAPOUR_FRAMES
	}

	ifaction BREATHE_VAPOUR_FRAMES
	{
	cstat 514
	sizeto 16 16
	  ifactioncount 4
	    killit
	}
enda

// *************************************************
// MAP BEACON
// *************************************************

defstate MAPBEACON
seta[].shade -127
seta[].ang player[].ang
getu .overhead_on temp
ifn myowner -1 ife sprite[myowner].statnum 1024 killit
ife temp 2
	{
	ifactor 8953 action ZERO
	cstat 32
	getp[].zoom temp2
	sub temp2 2048
	mulvar temp2 -1
	divvar temp2 20
	clamp temp2 2 256
	seta[].xrepeat temp2
	seta[].yrepeat temp2
	}
else
	{
	ifactor 8953 { cstat 256 action MINUSONE } else
	cstat 32768
	}

ifactor 9302
 ifn myowner -1
	{
	setsprite THISACTOR sprite[myowner].x sprite[myowner].y sprite[myowner].z
	}

ifactor 8953
	{
	ifhitweapon
	 ifwasweapon RADIUSEXPLOSION killit
	}
ends

// Rune keys
spritenopal 8954
spritenoshade 8954
useractor notenemy 8954 state MAPBEACON enda

// Elder keys
spritenopal 8945
spritenoshade 8945
useractor notenemy 8945 state MAPBEACON enda

spritenopal 8955
spritenoshade 8955
useractor notenemy 8955 state MAPBEACON enda

spritenopal 8956
spritenoshade 8956
useractor notenemy 8956 state MAPBEACON enda

spritenopal 8957
spritenoshade 8957
useractor notenemy 8957 state MAPBEACON enda

spritenopal 8958
spritenoshade 8958
useractor notenemy 8958 state MAPBEACON enda

spritenopal 8959
spritenoshade 8959
useractor notenemy 8959 state MAPBEACON enda

spritenopal 12780
spritenoshade 12780
useractor notenemy 12780 state MAPBEACON enda

spritenopal 12781
spritenoshade 12781
useractor notenemy 12781 state MAPBEACON enda


// Card Keys
useractor notenemy 7879 state MAPBEACON enda
useractor notenemy 7880 state MAPBEACON enda
useractor notenemy 7881 state MAPBEACON enda
useractor notenemy 7882 state MAPBEACON enda

useractor notenemy 7883 state MAPBEACON enda

useractor notenemy 8425 state MAPBEACON enda // Door transport

spritenopal 3062
useractor notenemy 3062 state MAPBEACON enda // supply drop

spritenopal 8946
spritenoshade 8946
useractor notenemy 8946 state MAPBEACON // point of interest
ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED ife RETURN 1 killit
	}
enda

spritenopal 8947
spritenoshade 8947
useractor notenemy 8947 state MAPBEACON enda // Cog

spritenopal 8948
spritenoshade 8948
useractor notenemy 8948 state MAPBEACON enda // Plug

spritenopal 8949
spritenoshade 8949
useractor notenemy 8949 state MAPBEACON enda // Scarab Key

spritenopal 8950
spritenoshade 8950
useractor notenemy 8950 state MAPBEACON enda // Generic Key!

spritenopal 8951 // Oxygen
spritenoshade 8951 // Oxygen
useractor notenemy 8951 state MAPBEACON enda // Oxygen!

spritenopal 8953 // Dynamite!
spritenoshade 8953 // Dynamite!
useractor notenemy 8953 state MAPBEACON enda // Dynamite!

spritenopal 7055 // VR station
useractor notenemy 7055 state MAPBEACON enda // VR station

spritenopal 5408
useractor notenemy 5408 state MAPBEACON enda

useractor notenemy 9302 state MAPBEACON enda // Vehicle marker

spritenopal 3716
useractor notenemy 3716 state MAPBEACON enda // Asian key

spritenopal 3809
spritenoshade 3809
useractor notenemy 3809 state MAPBEACON ifpdistl 1024 killit enda

action BREAK 0

// MEGA CLUSTER CHARGE =================================================================================

useractor notenemy 12969
fall

ifaction 0
	{
	resetcount
	action ZERO
	}

ifaction ZERO
	{
	sizeat 32 32
	ifcount 0 ife temp2 0 { quote 186 globalsound CLAXON_ALARM add temp2 1 }
	ifcount 26 ife temp2 1 { quote 187 globalsound CLAXON_ALARM add temp2 1 }
	ifcount 52 ife temp2 2 { quote 188 globalsound CLAXON_ALARM add temp2 1 }
	ifcount 78 ife temp2 3 { quote 189 globalsound CLAXON_ALARM add temp2 1 }
	ifcount 104 ife temp2 4 { quote 190 globalsound CLAXON_ALARM add temp2 1 }
	ifcount 130
		{
		quote 191
		hitradius 1024 50 100 150 300
		flash
		quake 200
		spawn EXPLOSION2
		globalsound RPG_EXPLODE
		palfrom 20 20 20 20
		killit
		}
	}
enda

// PORTABLE FORCEFIELD =================================================================================

useractor notenemy 5208
ifspritepal 3 cstat 594 else
ifspritepal 8 cstat 594 else
ifspritepal 21 cstat 594 else
cstat 339

ifaction 0
	{
	seta[].blend 255
	ifspawnedby APLAYER { set ally_mag 1 strength 10000 }
	sizeat 32 32
	action ZERO
	}

ifaction ZERO
	{
		strength 10000
		set temp3 player[].posx
		add temp3 512

		rotatepoint player[].posx player[].posy temp3 player[].posy player[].ang temp5 temp6

		getp[].ang temp4
		ifspritepal 3 sub temp4 1024
		ifspritepal 8 sub temp4 1024
		ifspritepal 21 sub temp4 1024
		seta[].ang temp4

		getp[].posz temp
		add temp 8192

		setsprite THISACTOR temp5 temp6 temp

		geta[].sectnum upd_sect
		updatesectorz temp5 temp6 sprite[].z upd_sect
		ifn upd_sect -1 changespritesect THISACTOR upd_sect

		geta[].shade temp2
		ife PS_ITEM 0
			{
			shoot SPARK2
			shoot SPARK2
			shoot SPARK2
			killit
			}

ifg sprite[].htextra -1
 ife ally_mag 1
		{
		set temp4 sprite[].htextra
		mul temp4 3
		sub holoduke_amount temp4
		strength 1000
		}

	ifhitweapon
		{
		ifspritepal 3 break
		ifspritepal 8 break
		ifspritepal 21 break
		sound SOMETHINGHITFORCE
		setactorsoundpitch THISACTOR SOMETHINGHITFORCE 1024
		set temp2 0
		seta[].shade temp2
		// temp8 contains the other side of this field's sprite id; it's set in JAMES.CON and BOMBSHELL.CON
		seta[temp8].shade 0
		setactorvar[temp8].temp2 0
		}

	ifl temp2 25
		{
		add temp2 1
		seta[].shade temp2
		}
	}

enda

// ====================================================================================================================
// ENEMY FORCEFIELD
// ====================================================================================================================

useractor notenemy 22923
ifspritepal 3 cstat 594 else
ifspritepal 8 cstat 594 else
ifspritepal 21 cstat 594 else
cstat 339

ifaction 0
	{
	seta[].blend 255
	strength 1000
	sizeat 32 32
	action ZERO
	}

ifaction ZERO
	{
		set temp3 sprite[myowner].x
		add temp3 512

		rotatepoint sprite[myowner].x sprite[myowner].y temp3 sprite[myowner].y sprite[myowner].ang temp5 temp6

		geta[myowner].ang temp4
		ifspritepal 3 sub temp4 1024
		ifspritepal 8 sub temp4 1024
		ifspritepal 21 sub temp4 1024
		seta[].ang temp4

		geta[].sectnum upd_sect
		updatesectorz temp5 temp6 sprite[].z upd_sect
		ifn upd_sect -1 changespritesect THISACTOR upd_sect

		geta[myowner].z temp
		sub temp 4096

		setsprite THISACTOR temp5 temp6 temp

		geta[].shade temp2
		ifl sprite[myowner].extra 0
			{
			soundonce BOS4_ATTACK
			shoot SPARK2
			shoot SPARK2
			shoot SPARK2
			setactorsoundpitch THISACTOR BOS4_ATTACK 1024
			killit
			}
		ifdead
			{
			soundonce BOS4_ATTACK
			shoot SPARK2
			shoot SPARK2
			shoot SPARK2
			setactorsoundpitch THISACTOR BOS4_ATTACK 1024
			ife SKILL_LEVEL 1 setactorvar[myowner].playsound 3000
			ife SKILL_LEVEL 2 setactorvar[myowner].playsound 300
			ife SKILL_LEVEL 3 setactorvar[myowner].playsound 240
			ife SKILL_LEVEL 4 setactorvar[myowner].playsound 180
			ife SKILL_LEVEL 5 setactorvar[myowner].playsound 130
			killit
			}

	ifhitweapon
		{
		ifspritepal 3 break
		ifspritepal 8 break
		ifspritepal 21 break
		sound SOMETHINGHITFORCE
		setactorsoundpitch THISACTOR SOMETHINGHITFORCE 1024
		set temp2 0
		seta[].shade temp2
		seta[temp8].shade 0
		setactorvar[temp8].temp2 0
		ifspawnedby APLAYER strength 1000
		}

	ifl temp2 25
		{
		add temp2 1
		seta[].shade temp2
		}
	}

enda

// ===============================================================================AMCTELEPORT


action ACAR2 0 1 5 1 1

spriteshadow 1425
spritenopal 1425

useractor notenemy 1425 // statue
ifaction 0 ifspritepal 27 action FIVE_ANG
enda

move PICKEDUP 0
move CARFALL 0
move CARTHROW 400 75

defstate CAR_THROW_CODE
ifmove 0
{
ifactor CAR sizeat 44 44 else
sizeat 40 40
cstat 257
clipdist 64
ifspritepal 3 nullop else fall
ifpdistl 1024
{
ifp pboosted
	{
	ife CHAR 0 set player_use 0
	ife CHAR 2 set player_use 0
	ife CHAR 7 set player_use 0
	ife CHAR 12 set player_use 0
	}
 ifhitspace
   ifcount 13
	{
	set hit_key 30
	switch CHAR
	case 0
	case 2
	case 7
  	ifp pboosted
	{
		soundonce CARSTRAIN
		set PLAYER_VOICEOVER 2
		move PICKEDUP
		resetcount
	}
	else quote 148
	break
	case 12
		soundonce CARSTRAIN
		set PLAYER_VOICEOVER 2
		move PICKEDUP
		resetcount
	break
	default
	quote 147
	break
	endswitch
	}
}
}

ifmove PICKEDUP
{
setp[].runspeed 33200
ifrnd 4 soundonce CARSTRAIN
cstat 0
seta[].x player[].posx
seta[].y player[].posy
getp[].posz temp
sub temp 2096
seta[].z temp
state lower_weapon
ifhitspace
 ifcount 13
	{
	setp[].runspeed RUNNINGSPEED
	sound BIGTHROW
	stopsound CARSMASH
	seta[].ang player[].ang
	move CARTHROW geth getv
	resetcount
	}
ifp pboosted nullop else { move CARFALL addphealth -500 }
}

ifmove CARFALL
{
iffloordistl 2 { soundonce CARSMASH ifcount 26 { debris SCRAP1 12 move 0 resetcount } }
fall
}

ifmove CARTHROW
{
cstat 257
ifcount 13 { hitradius 2048 10 50 150 300 }
iffloordistl 4 { ifcount 26 { debris SCRAP1 12 soundonce CARSMASH  move 0 resetcount } }
fall
}

ifaction 0
	{
	cstat 257
	strength 500
	action FIVE_ANG
	}

ifaction FIVE_ANG
	{
	ifhitweapon { }
	ifstrength 200
		{
		addstrength -1
		soundonce BUILDUP
		add INTERNALCOUNT 1
		ife INTERNALCOUNT 85 strength 0
		ifrnd 64
			{
			sound FIRE_SPIT
			shoot SPARK shoot SPARK
			espawn 14040
			geta[].x temp
			randvar temp2 128
			ifrnd 128 add temp temp2 else sub temp temp2
			seta[RETURN].x temp

			geta[].y temp
			randvar temp2 128
			ifrnd 128 add temp temp2 else sub temp temp2
			seta[RETURN].y temp

			geta[].z temp
			sub temp 8192
			randvar temp2 2048
			sub temp temp2
			seta[RETURN].z temp
			}
		}
	ifstrength 0
		{
		flash
		ifrnd 128 globalsound CAR_EXP1
		else globalsound CAR_EXP2
		debris SCRAP1 5
		debris SCRAP2 5
		debris SCRAP3 6
		lotsofglass 20
		shoot SPARK shoot SPARK
		shoot SPARK shoot SPARK
		hitradius 2048 8 16 64 256
		spawn BIG_SMOKE2
		espawn BIG_EXPLOSION
		geta[].z temp3
		sub temp3 4096
		seta[RETURN].z temp3
		espawn 14057
		ifactor 14058 set temp2 42
		else ifactor 14056 set temp2 57
		else ifactor 14061 set temp2 40
		else ifactor 14062 set temp2 54
		else set temp2 50
		seta[RETURN].xrepeat temp2
		seta[RETURN].yrepeat temp2
		seta[RETURN].ang sprite[].ang
		killit
		}
	}

ends

spriteshadow 27106 // Kagura's bike

useractor notenemy 27106 200 FIVE_ANG
ife EXTRASAVED 1 ifl VOLUME 3 killit
enda

spriteshadow CAR
useractor notenemy CAR state CAR_THROW_CODE enda
useractor notenemy 10858 state CAR_THROW_CODE enda

useractor notenemy 8684 ifaction 0 action FIVE_ANG enda

spritenoshade 7410
useractor notenemy 7410

ifspritepal 0
{
 seta[].mdflags 16
 set command 32768
}

ifspawnedby RESPAWN sizeat 32 32
enda


useractor notenemy 11049

ifspritepal 0
{
 seta[].mdflags 16
 set command 32768
}

enda

// ============================================================================================HOVER SHIP STUFF



useractor notenemy 22520 // Advanced AMC Ship
cstat 32768
ife EXTRASAVED -1
	{
	ife BASE_RESEARCH[7] 2
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
		else
		{
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		}
	killit
	}
else
{
checkactivatormotion EXTRASAVED
ife RETURN 1
	{
	ife BASE_RESEARCH[7] 2
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
		else
		{
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		}
	killit
	}
}
enda

action AAMCSHIP 0 1 7 1 1
action HOVERSHIP 0 1 7 1 1

move CRASH 512 32
move CRASH_FAST 512 256
move FLYFORWARD 512 0
move FLYSLOW 60 0
move FLYUP 0 -96
move FLYDOWN 0 96

move CHOPPER_FLYBY 325 0

defstate HOVERSOUND
ifpdistl 16384
	{
		ifactor AMCSHIP2
		{
		ifactorsound THISACTOR AMCSHIP2_HOVER nullop else
		sound AMCSHIP2_HOVER
		}
		else
		{
		ifactorsound THISACTOR AMCSHIP_HOVER nullop else
		sound AMCSHIP_HOVER
		}
	}
ends

defstate HOVERSHIPCODE

ifspawnedby HOVERCARSPAWNER
{
	ifg sprite[].xrepeat 8
		{
		state HOVERSOUND

	 ifpdistl 2048 soundonce CAR_HORN
	 ifpdistl 1024
			{
			wackplayer
			soundonce CARSMASH
			addphealth -1000
			palfrom 60 60 0 0
			}

		}
	move FLYFORWARD geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 0 cstator 1
else
ife LOTAGSAVED 1
{
	state HOVERSOUND
	move FLYFORWARD geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 2
{
	state thisactor_getzrange
	state HOVERSOUND
	move FLYUP getv
	state getceildist
	ifl z 5 ifoutside killit
}
else
ife LOTAGSAVED 3
{
	state HOVERSOUND
	ifactor AMCSHIP move FLYFORWARD geth else
	ifactor 15329 move FLYFORWARD geth else
	move FLYSLOW geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 4
{
	state HOVERSOUND
	move FLYSLOW geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 5
{
	state HOVERSOUND
	move FLYFORWARD geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 6
{
	state HOVERSOUND
	move FLYSLOW geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 7
{
	ifmove 0 { sound WAR_AMBIENCE2 move CRASH geth getv }
	ifrnd 16
		{
		espawn BIG_SMOKE
		shoot SPARK shoot SPARK
		seta[RETURN].xrepeat 96
		seta[RETURN].yrepeat 96
		seta[RETURN].cstat 2
		seta[RETURN].pal 1
		}
	ifnotmoving
		{
			spawn EXPLOSION2
			ifpdistl 16000 wackplayer
			ifcansee palfrom 60 64 64 64
			flash
			hitradius 6096 100 20 400 500
			ifactor 15329 globalsound CARSMASH
			else globalsound CHOPPER_EXPLODE
			quake 76
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			debris SCRAP1 40
			debris SCRAP2 32
			debris SCRAP3 32
			debris SCRAP4 32
			debris SCRAP5 32
			strength 0
			ifvarn XVELSAVED 0
				{
				operateactivators XVELSAVED THISACTOR
				operaterespawns XVELSAVED
				operatemasterswitches XVELSAVED
				}
			killit
		}
}
else
ife LOTAGSAVED 8
{
	state HOVERSOUND
	move CHOPPER_FLYBY geth
	ifnotmoving killit
}
else
ife LOTAGSAVED 9
{
	state HOVERSOUND
	move FLYFORWARD geth
	geta[].xvel temp3
	mulvar temp3 2
	seta[].xvel temp3
	ifnotmoving killit
}
else
ife LOTAGSAVED 10
{
	state HOVERSOUND
	move FLYDOWN getv
	iffloordistl 32 { cstator 1 sound HOVERC_LAND set LOTAGSAVED 0 }
}
else
ife LOTAGSAVED 11
{
	ifmove 0 { sound WAR_AMBIENCE2 move CRASH_FAST geth getv }
	ifrnd 64
		{
		espawn BIG_SMOKE
		spawn EXPLOSION2
		shoot SPARK shoot SPARK
		seta[RETURN].xrepeat 96
		seta[RETURN].yrepeat 96
		seta[RETURN].cstat 2
		seta[RETURN].pal 1
		}
	ifnotmoving
		{
			spawn EXPLOSION2
			ifpdistl 16000 wackplayer
			ifcansee palfrom 60 64 64 64
			flash
			hitradius 6096 100 20 400 500
			ifactor 15329 globalsound CARSMASH
			else globalsound CHOPPER_EXPLODE
			quake 76
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			debris SCRAP1 40
			debris SCRAP2 32
			debris SCRAP3 32
			debris SCRAP4 32
			debris SCRAP5 32
			strength 0
			ifvarn XVELSAVED 0
				{
				operateactivators XVELSAVED THISACTOR
				operaterespawns XVELSAVED
				operatemasterswitches XVELSAVED
				}
			killit
		}
}
else
ife LOTAGSAVED 12 // dis base exploding ship
{
checkactivatormotion 16
ife RETURN 1
	{
		spawn EXPLOSION2
		ifcansee palfrom 16 64 64 64
		flash
		ifactor 15329 globalsound CARSMASH
		else globalsound CHOPPER_EXPLODE
		quake 76
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		debris SCRAP1 40
		debris SCRAP2 32
		debris SCRAP3 32
		debris SCRAP4 32
		debris SCRAP5 32
		strength 0
		killit
	}
}

ifspawnedby RESPAWN
{
ife LOTAGSAVED 3 sizeat 16 16 else
ife LOTAGSAVED 6 sizeat 16 16 else
	{
	ifactor 21612 sizeat 103 89 else
	ifactor 15329 sizeat 80 80 else
	sizeat 64 64
	}
ifmove 0 spritepal 3
}

ifspritepal 3
{
cstator 1
ifpdistl 16384 soundonce AMCSHIP_HOVER
set command 32768
}
else
ifspritepal 9
{
ifmove 0 { sound WAR_AMBIENCE2 move CRASH geth getv }
ifcount 4
	{
	espawn SMALLSMOKE
	seta[RETURN].xrepeat 96
	seta[RETURN].yrepeat 96
	seta[RETURN].cstat 2
	seta[RETURN].pal 1
	resetcount
	}
ifnotmoving
	{
	quake 13
	spawn EXPLOSION2
	globalsound RPG_EXPLODE
  	debris SCRAP1 10
  	debris SCRAP2 10
  	debris SCRAP3 10
  	debris SCRAP4 10
  	debris SCRAP5 10
	killit
	}
}
ends

spriteflags AMCSHIP SFLAG_SMOOTHMOVE
spriteflags AMCSHIP2 SFLAG_SMOOTHMOVE

spriteshadow AMCSHIP
useractor notenemy AMCSHIP 200 AAMCSHIP
seta[].mdflags 16
ife HITAGSAVED 0  sizeat 64 64
ife EXTRASAVED 1
	{
	ife BASE_RESEARCH[7] 2 cactor AMCSHIP2
	set EXTRASAVED -1
	}
state HOVERSHIPCODE
enda

spriteshadow AMCSHIP2
useractor notenemy AMCSHIP2 200 AAMCSHIP
seta[].mdflags 16
ife HITAGSAVED 0  sizeat 64 64
state HOVERSHIPCODE
enda

spriteflags 21612 SFLAG_SMOOTHMOVE

useractor notenemy 21613 // Snowfall's ship landed

ifaction 0
{
ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 killit
	}
}

enda

useractor notenemy 21612 200 // Snowfall's ship

	// If XVEL saved, don't run ship code until this xvel is activated
	ife XVELSAVED 0
	{
		cstat 1
		seta[].mdflags 16
		action ZERO
	}

	ifaction ZERO nullop
	else ifaction ZERO_TWO nullop
	else
	{
		ife temp 0
		{
			cstat 32768
			checkactivatormotion XVELSAVED
			ife RETURN 1
				set temp 1
		}

		else ife temp 1
		{
			cstat 1
			seta[].mdflags 16
			action ZERO
		}
	}

	// Run ship code => drop down until reaching the floor or until activation of YVEL. YVEL makes it go back up.
	ifaction ZERO
	{
		set temp 0

		ife YVELSAVED 0
			nullop
		else
		{
			checkactivatormotion YVELSAVED
			ife RETURN 1
				set temp 1
			else
				set temp 0
		}

		ife temp 1
			action ZERO_TWO
		else
		{
			ife HITAGSAVED 0 sizeat 64 64
			state HOVERSHIPCODE

			// Hack, don't wanna figure out the huge state
			ifn XVELSAVED 0
			{
				ifpdistl 16384 soundonce AMCSHIP_HOVER
				set command 32768
			}
			// Don't know what this does
			ife HITAGSAVED 40
			{
				checkactivatormotion 40
				ife RETURN 1
				{
					action ONE
				}
			}
		}
	}
	else ifaction ZERO_TWO
	{
		ifpdistl 16384 soundonce AMCSHIP_HOVER

		// Go back up
		geta[].z z
		sub z 2049
		seta[].z z

		gets[].ceilingstat temp

		ifand temp 1024 // TROR, ignore
			nullop
		else
		{
			// If we're close enough to the ceiling, kill sprite
			gets[].ceilingz temp2

			sub temp2 z

			ifg temp2 -10000
				killit
		}
	}
enda

spritenoshade 15329

spriteshadow 15329
useractor notenemy 15329
seta[].shade -32

ife HITAGSAVED 0 sizeat 80 80
state HOVERSHIPCODE

enda

useractor notenemy 17301
ifspritepal 3
 ifaction 0
	{
	seta[].mdflags 16
	set command 32768
	action ZERO
	}
enda

// *************************************************
// HOVER CAR
// *************************************************

defstate SPAWNHOVERCAR
ifrnd 64
{
randvar temp7 30
ifl temp7 11 espawn 8924
else ifl temp7 21 espawn 9388
else ifl temp7 31 espawn 8922
ifspritepal 6
	{
	seta[RETURN].xrepeat 8
	seta[RETURN].yrepeat 8
	geta[RETURN].mdflags spawntmp
    orvar spawntmp 1  // SPREXT_NOTMD
    seta[RETURN].mdflags spawntmp
	}
else { seta[RETURN].xrepeat 64 seta[RETURN].yrepeat 64 }

ife LOTAGSAVED 1 resetcount
else
	{
	geta[].x temp
	geta[].y temp2

	set temp3 temp
	randvar temp8 512
	add temp3 temp8

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	seta[RETURN].x temp5
	seta[RETURN].y temp6


	geta[RETURN].z temp2
	randvar temp 20000
	sub temp 10000
	add temp2 temp
	seta[RETURN].z temp2
	resetcount
	}
}
ends

defstate SPAWN_CYCLOIDSHIP
ifrnd 64
{
espawn 15329
setactorvar[RETURN].HITAGSAVED 1
ifspritepal 8
	{
	seta[RETURN].xrepeat 16
	seta[RETURN].yrepeat 16
	}
else { seta[RETURN].xrepeat 64 seta[RETURN].yrepeat 64 }

ife LOTAGSAVED 1 resetcount
else
	{
	geta[].x temp
	geta[].y temp2

	set temp3 temp
	randvar temp8 512
	add temp3 temp8

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	seta[RETURN].x temp5
	seta[RETURN].y temp6


	geta[RETURN].z temp2
	randvar temp 20000
	sub temp 10000
	add temp2 temp
	seta[RETURN].z temp2
	resetcount
	}
}
ends



useractor notenemy HOVERCARSPAWNER
cstat 32768

ifn HITAGSAVED 0
{
checkactivatormotion HITAGSAVED
	ife RETURN 1 killit
}

ifspritepal 0
{
ifcount 52 state SPAWNHOVERCAR
}
else
ifspritepal 1
{
ifcount 26 state SPAWNHOVERCAR
}
else
ifspritepal 2
{
ifcount 13 state SPAWNHOVERCAR
}
else
ifspritepal 3
{
ifcount 104 state SPAWNHOVERCAR
}
else
ifspritepal 6
{
ifcount 13 state SPAWNHOVERCAR
}
else
ifspritepal 8
{
ifcount 52 state SPAWN_CYCLOIDSHIP
}



enda

useractor notenemy 8924 200

ifaction 0
	{
	ifspritepal 3
		{
		 geta[].mdflags temp
		 orvar temp 16
		 seta[].mdflags temp
		 set command 32768
		}
	randvar temp 18
	ife temp 0  seta[].pal 10 else
	ife temp 1  seta[].pal 11 else
	ife temp 2  seta[].pal 12 else
	ife temp 3  seta[].pal 13 else
	ife temp 4  seta[].pal 14 else
	ife temp 5  seta[].pal 15 else
	ife temp 6  seta[].pal 16 else
	ife temp 7  seta[].pal 49 else
	ife temp 8  seta[].pal 21 else
	ife temp 9  seta[].pal 36 else
	ife temp 10 seta[].pal 23 else
	ife temp 11 seta[].pal 35 else
	ife temp 12 seta[].pal 37 else
	ife temp 13 seta[].pal 39 else
	ife temp 14 seta[].pal 42 else
	ife temp 15 seta[].pal 46 else
	ife temp 16 seta[].pal 50 else
	ife temp 17 seta[].pal 56 else
	ife temp 18 seta[].pal 62 else
	spritepal 0
	action ZERO
	}

ifspawnedby HOVERCARSPAWNER nullop else
ife HITAGSAVED 0 sizeat 64 64
state HOVERSHIPCODE
enda

useractor notenemy 8922 200

ifaction 0
	{

	ifspritepal 3
		{
		 geta[].mdflags temp
		 orvar temp 16
		 seta[].mdflags temp
		 set command 32768
		}
	ifspritepal 0
		{
		ifrnd 16 seta[].pal 10 else
		ifrnd 32 seta[].pal 11 else
		ifrnd 48 seta[].pal 12 else
		ifrnd 64 seta[].pal 13 else
		ifrnd 80 seta[].pal 14 else
		ifrnd 96 seta[].pal 15 else
		ifrnd 112 seta[].pal 16 else
		ifrnd 128 seta[].pal 49 else
		ifrnd 144 seta[].pal 21 else
		ifrnd 160 seta[].pal 37 else
		ifrnd 176 seta[].pal 23 else
		ifrnd 192 seta[].pal 5 else
		spritepal 3
		}
	action ZERO
	}

ifspawnedby HOVERCARSPAWNER nullop else
ife HITAGSAVED 0 sizeat 64 64
state HOVERSHIPCODE
enda

useractor notenemy 9388 200

ifaction 0
	{
	ifrnd 16 seta[].pal 10 else
	ifrnd 32 seta[].pal 11 else
	ifrnd 48 seta[].pal 12 else
	ifrnd 64 seta[].pal 13 else
	ifrnd 80 seta[].pal 14 else
	ifrnd 96 seta[].pal 15 else
	ifrnd 112 seta[].pal 16 else
	ifrnd 128 seta[].pal 18 else
	ifrnd 144 seta[].pal 21 else
	ifrnd 160 seta[].pal 22 else
	ifrnd 176 seta[].pal 23 else
	ifrnd 192 seta[].pal 5 else
	spritepal 0
	action HOVERSHIP
	}

ifspawnedby HOVERCARSPAWNER nullop else
ife HITAGSAVED 0 sizeat 64 64
state HOVERSHIPCODE
enda

// AIRSHIP

useractor notenemy 26035
ifaction 0
	{
	seta[].mdflags 16
	ife LOTAGSAVED 1 set command 32768
	action FIVE_ANG
	}
ifaction FIVE_ANG
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		move FLYFORWARD geth
		}
	}
enda

// *************************************************
// CAR
// *************************************************

defstate SPAWN_CAR
ifrnd 64
{
espawn CAR_MOVE

ife LOTAGSAVED 1
	{
	setactorvar[RETURN].LOTAGSAVED HITAGSAVED
	ife XVELSAVED 0 { seta[RETURN].xrepeat 40 seta[RETURN].yrepeat 40 } else
	ife XVELSAVED 1 { seta[RETURN].xrepeat 20 seta[RETURN].yrepeat 20 } else
	ife XVELSAVED 2 { seta[RETURN].xrepeat 10 seta[RETURN].yrepeat 10 }
	resetcount
	}
else
	{
	geta[].x temp
	geta[].y temp2

	set temp3 temp

	ife OWNERSAVED 0 set OWNERSAVED 2048
	randvarvar temp8 OWNERSAVED

	add temp3 temp8

	randvar temp4 2048

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	geta[].sectnum mysector
	updatesector temp5 temp6 mysector
	ifn mysector -1
		{
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		changespritesect THISACTOR mysector
		}
	setactorvar[RETURN].LOTAGSAVED HITAGSAVED
	ife XVELSAVED 0 { seta[RETURN].xrepeat 40 seta[RETURN].yrepeat 40 } else
	ife XVELSAVED 1 { seta[RETURN].xrepeat 20 seta[RETURN].yrepeat 20 } else
	ife XVELSAVED 2 { seta[RETURN].xrepeat 10 seta[RETURN].yrepeat 10 }
	resetcount
	}
}
ends

move TRAFFIC_JAM 50 0
move SPEEDY 128 0

useractor notenemy CAR_SPAWNER
cstat 32768

ifspritepal 0
{
ifcount 52 state SPAWN_CAR
}
else
ifspritepal 1
{
ifcount 26 state SPAWN_CAR
}
else
ifspritepal 2
{
ifcount 13 state SPAWN_CAR
}
else
ifspritepal 3
{
ifcount 76 state SPAWN_CAR
}
else
ifspritepal 4
{
ifcount 104 state SPAWN_CAR
}
else
ifspritepal 5
{
ifcount 6 state SPAWN_CAR
}
else
ifspritepal 6
{
ifcount 260
	{
	ifrnd 128 state SPAWN_CAR
	resetcount
	}
}

enda

spriteshadow CAR_MOVE

action ACAR_DRIVE 0 2 5 1 14

useractor notenemy CAR_MOVE
fall

ifaction 0
	{
	ifrnd 16 seta[].pal 10 else
	ifrnd 32 seta[].pal 11 else
	ifrnd 48 seta[].pal 12 else
	ifrnd 64 seta[].pal 13 else
	ifrnd 80 seta[].pal 14 else
	ifrnd 96 seta[].pal 15 else
	ifrnd 112 seta[].pal 16 else
	ifrnd 128 seta[].pal 18 else
	ifrnd 144 seta[].pal 21 else
	ifrnd 160 seta[].pal 22 else
	ifrnd 176 seta[].pal 23 else
	ifrnd 192 seta[].pal 5 else
	spritepal 0
	action ACAR_DRIVE
	}

ife LOTAGSAVED 1
{
move FLYFORWARD geth
ifnotmoving killit
}
else
ife LOTAGSAVED 2
{
move FLYSLOW geth
ifnotmoving killit
}
else
ife LOTAGSAVED 3
{
move TRAFFIC_JAM geth
ifrnd 1 ifpdistl 16384 soundonce CAR_HORN
ifnotmoving killit
}
else
ife LOTAGSAVED 0
{
ifpdistl 16384 soundonce VEH_RUN2
move SPEEDY geth
ifnotmoving killit
}

ifspawnedby RESPAWN
{
ife LOTAGSAVED 3 sizeat 20 20 else
sizeat 48 48
}


enda

useractor notenemy 1480 // Car Waypoint
cstat 32768

ifcount 6
{
findnearactor CAR_MOVE 768 temp
	ifn temp -1
		{
		geta[].ang temp2
		seta[temp].ang temp2
		ifspritepal 1 setactorvar[temp].LOTAGSAVED 1
		ifspritepal 2 setactorvar[temp].LOTAGSAVED 2
		ifspritepal 3 setactorvar[temp].LOTAGSAVED 3
		}
resetcount
}

enda


// ===============================================================================FLOATING STUFF


useractor notenemy 6927 // floating mikko
	ifaction 0
	{
	 seta[].mdflags 16
	 ifspritepal 3 set command 32768
	 action ZERO
	}

enda

useractor notenemy 8142 // Strange alien head
	ifaction 0
	{
	seta[].mdflags 16
	ifspritepal 3 set command 32768
	 action ZERO
	}

enda

useractor notenemy 9215 // Shambler hand
	ifaction 0
	{
	seta[].mdflags 16
	ifspritepal 3 set command 32768
	action ZERO
	}

enda

useractor notenemy 9214 // LameDuke Alien weapon
	ifaction 0
	{
	seta[].mdflags 16
	ifspritepal 3 set command 32768
	action ZERO
	}

enda

useractor notenemy 9213 // Shrinker Ring
	ifaction 0
	{
	seta[].mdflags 16
	ifspritepal 3 set command 32768
	action ZERO
	}

enda

spriteshadow 23536

defstate item_slow_float
	ifaction 0
	{
	seta[].mdflags 16
	set command 65536
	action ZERO
	}
ends


// AMC FISH LIST

defstate dec_fish_code
ifspritepal 3
	{
	ife base_fish 0 readgamevar base_fish
	ifactor 28444 { ifand base_fish 1 nullop else killit }
	ifactor 28446 { ifand base_fish 2 nullop else killit }
	ifactor 28448 { ifand base_fish 4 nullop else killit }
	ifactor 28450 { ifand base_fish 8 nullop else killit }
	ifactor 28452 { ifand base_fish 16 nullop else killit }
	ifactor 28454 { ifand base_fish 32 nullop else killit }
	ifactor 28456 { ifand base_fish 64 nullop else killit }
	ifactor 28458 { ifand base_fish 128 nullop else killit }
	ifactor 28496 { ifand base_fish 256 nullop else killit }
	}
ends


useractor notenemy 23536 // seer
state item_slow_float
enda


useractor notenemy 5762 // floating fish
state item_slow_float
enda

useractor notenemy 11450 // floating fish
state item_slow_float
enda

useractor notenemy 28444 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28446 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28448 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28450 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28452 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28454 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28456 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28458 // floating fish
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28496 // swim swim hungry
state dec_fish_code
state item_slow_float
enda

useractor notenemy 28467 // floating spooky thing
state item_slow_float
enda

useractor notenemy 28468 // floating spooky thing
state item_slow_float
enda

useractor notenemy 28469 // floating spooky thing
state item_slow_float
enda

useractor notenemy 28470 // floating spooky thing
state item_slow_float
enda

useractor notenemy 28471 // floating spooky thing
state item_slow_float
enda

spritenoshade 17295
spritenopal 17295

useractor 4 17295 // floating shadow realm island
state item_slow_float
enda

spritenoshade 17296
spritenopal 17296

useractor 4 17296 // floating shadow realm island
state item_slow_float
enda


useractor notenemy 23535 // floating VB blimp
state item_slow_float
enda

useractor notenemy 25335 // floating island
state item_slow_float
enda

useractor notenemy 25336 // floating island
state item_slow_float
enda

useractor notenemy 25337 // floating island
state item_slow_float
enda

useractor notenemy 25338 // floating island
state item_slow_float
enda

useractor notenemy 25339 // floating island
state item_slow_float
enda

useractor notenemy 25340 // floating island
state item_slow_float
enda

useractor notenemy 25341 // floating island
state item_slow_float
enda

useractor notenemy 25342 // floating island
state item_slow_float
enda

useractor notenemy 25343 // floating island
state item_slow_float
enda

// FLOODLIGHTS
spriteshadow 3575
useractor notenemy 3575 ifaction 0 action FIVE_ANG enda

defstate smoke_appear
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		killit
		}
	}
ifn HITAGSAVED 0
	{
	cstat 32768
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		ifactor 25534 sound MAGIC_FIELD_UP
		seta[].cstat CSTATSAVED
		set HITAGSAVED 0
		}
	}
ends

// LIGHTNING SPARK ============================================================================

spritenoshade 17400
spritenopal 17400

useractor notenemy 17400
ifaction 0
	{
	geta[].cstat CSTATSAVED
	ife LOTAGSAVED 0 set LOTAGSAVED 6
	ife HITAGSAVED 0 set HITAGSAVED 255
	seta[].shade 30
	cstat 32768
	ifg EXTRASAVED 0
		{
		checkactivatormotion EXTRASAVED
		ife RETURN 1 set EXTRASAVED 0
		}
	else
	action ZERO
	}
ifaction ZERO
	{
	cstat 32768
	randvarvar temp HITAGSAVED
	ifl temp LOTAGSAVED
		{
		action FORM
		sound BIG_SPARK
		seta[].cstat CSTATSAVED
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
		seta[].shade -127
		}
	}
ifaction FORM
	{
	geta[].shade temp
	ifl temp 0 add temp 16
	add temp 2
	seta[].shade temp
	ifge temp 30 action ZERO
	}

ifn XVELSAVED 0
	{
	checkactivatormotion XVELSAVED
	ife RETURN 1 killit
	}

enda

// SMOKE
useractor notenemy 3665 ifspawnedby RESPAWN { sizeto 128 128 cstat 514 }
state smoke_appear
enda
useractor notenemy 25509 ifspawnedby RESPAWN { sizeto 128 128 cstat 514 }
state smoke_appear
enda

// DIRT PATCH

useractor notenemy 23632
state smoke_appear
enda

useractor notenemy 23633
state smoke_appear
enda

// WAYSHRINE GLOW
useractor notenemy 25534 ifaction 0 { seta[].blend 255 action ZERO }
state smoke_appear
enda

// CART
useractor notenemy 25487
state smoke_appear
enda

useractor notenemy 25488
state smoke_appear
enda


// ==================================================================NEW ACCESS SWITCHES======================================
action LOCKED 0
action UNLOCKING 0
action UNLOCKED 1

defstate checkcounterpart2

set temp 0
set temp2 0
set temp3 0
set temp4 0
whilevarn temp 16384
{
  ifn sprite[temp].statnum 1024
  {
    geta[temp].picnum temp2
    ife temp2 ACCESSSWITCH3
    {
       getav[temp].LOTAGSAVED temp3
       ife LOTAGSAVED temp3 { seta[temp].picnum 3485 }
    }
    ife temp2 ACCESSSWITCH4
    {
       getav[temp].LOTAGSAVED temp3
       ife LOTAGSAVED temp3 { seta[temp].picnum 3487 }
    }
    ife temp2 ACCESSSWITCH5
    {
       getav[temp].LOTAGSAVED temp3
       ife LOTAGSAVED temp3 { seta[temp].picnum 3563 }
    }
  }
  add temp 1
}

ends

defstate CUSTOMACCESSSWITCH

ifaction 0 action LOCKED

ifpdistl 1024 ifp pfacing ifhitspace ifaction LOCKED
{
	ifspritepal 0 // BLUE
		{
			ifand player[].got_access 1
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 0
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon KEY_CARD set key_pal 0 }
		}
else
	ifspritepal 12 // GREY
		{
			ifand ACCESSKEY2 1
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 12
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon KEY_CARD set key_pal 12 }
		}
else
	ifspritepal 11 // GREEN
		{
			ifand ACCESSKEY2 2
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 11
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon KEY_CARD set key_pal 11 }
		}
else
	ifspritepal 15 // BROWN
		{
			ifand ACCESSKEY2 4
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 15
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon KEY_CARD set key_pal 15 }
		}
else
	ifspritepal 49 // PURPLE
		{
			ifand ACCESSKEY2 8
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 49
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 49 }
		}
else
	ifspritepal 10 // DARK RED
		{
			ifand ACCESSKEY2 16
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 10
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 10 }
		}
else
	ifspritepal 21 // RED
		{
			ifand player[].got_access 2
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 21
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 21 }
		}
else
	ifspritepal 36 // DARK BROWN
		{
			ifand ACCESSKEY2 32
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 36
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 36 }
		}
else
	ifspritepal 23 // YELLOW
		{
			ifand player[].got_access 4
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 23
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 23 }
		}
else
	ifspritepal 16 // DARK BLUE
		{
			ifand ACCESSKEY2 64
				{
				ifinwater nullop else lockplayer 13
				set CKEYCARDPAL 16
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
		else { set door_locked 0 set key_icon KEY_CARD set key_pal 16 }
		}
}

ifaction UNLOCKING
{
set KEYCARDTYPE 0
ife CUSTOMKEYCARDPOS 9
	{
	state checkcounterpart2
	ife HITAGSAVED 0 { ifactor ACCESSSWITCH5 sound CSWITCH2 else sound SWITCH_ON } else soundvar HITAGSAVED
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	action UNLOCKED
	ifspritepal 0 { getp[].got_access temp sub temp 1 setp[].got_access temp }
	ifspritepal 12 sub ACCESSKEY2 1 else
	ifspritepal 11 sub ACCESSKEY2 2 else
	ifspritepal 15 sub ACCESSKEY2 4 else
	ifspritepal 16 sub ACCESSKEY2 64 else
	ifspritepal 49 sub ACCESSKEY2 8 else
	ifspritepal 10 sub ACCESSKEY2 16 else
	ifspritepal 21 { getp[].got_access temp sub temp 2 setp[].got_access temp }
	ifspritepal 36 sub ACCESSKEY2 32
	ifspritepal 23 { getp[].got_access temp sub temp 4 setp[].got_access temp }
	}
}
ends

useractor notenemy ACCESSSWITCH3 state CUSTOMACCESSSWITCH enda
useractor notenemy ACCESSSWITCH4 state CUSTOMACCESSSWITCH enda
useractor notenemy ACCESSSWITCH5 state CUSTOMACCESSSWITCH enda

useractor notenemy SW_TECHLOCK

ifaction 0 action LOCKED

ifpdistl 1024 ifp pfacing ifhitspace ifaction LOCKED
{
	ifspritepal 0 // BLUE
		{
			ifand SW_KEYS 1
				{
				action ZERO
				}
			else { set door_locked 0 set key_icon SW_TECHCARD set key_pal 0 }
		}
else
	ifspritepal 10 // DARK RED
		{
			ifand SW_KEYS 2
				{
				action ZERO
				}
		else { set door_locked 0 set key_icon SW_TECHCARD set key_pal 10 }
		}
else
	ifspritepal 11 // GREEN
		{
			ifand SW_KEYS 4
				{
				action ZERO
				}
			else { set door_locked 0 set key_icon SW_TECHCARD set key_pal 11 }
		}
else
	ifspritepal 23 // YELLOW
		{
		ifand SW_KEYS 8
				{
				action ZERO
				}
		else { set door_locked 0 set key_icon SW_TECHCARD set key_pal 23 }
		}
}

ifaction ZERO
{
set temp 0
whilevarn temp 16384
{
  ifn sprite[temp].statnum 1024
  {
    geta[temp].picnum temp2
    ife temp2 SW_TECHLOCK
    {
	  ife temp THISACTOR add temp 1
       getav[temp].LOTAGSAVED temp3
       ife LOTAGSAVED temp3 seta[temp].picnum 16337
    }
  }
  add temp 1
}
ife HITAGSAVED 0 sound SW_TECHK_UNLOCK else soundvar HITAGSAVED
operateactivators LOTAGSAVED THISACTOR
operaterespawns LOTAGSAVED
operatemasterswitches LOTAGSAVED
action UNLOCKED
ifspritepal 0 xorvar SW_KEYS 1 else
ifspritepal 10 xorvar SW_KEYS 2 else
ifspritepal 11 xorvar SW_KEYS 4 else
ifspritepal 23 xorvar SW_KEYS 8
}

enda

useractor notenemy SW_OLDLOCK

ifaction 0
{
ifspritepal 0 spritepal 15
action LOCKED
}

ifpdistl 1024 ifp pfacing ifhitspace ifaction LOCKED
{
	ifspritepal 15 // Bronze
		{
			ifand SW_KEYS 16 action ZERO
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SW_OLDKEY set key_pal 15 }
		}
else
	ifspritepal 12 // Silver
		{
			ifand SW_KEYS 32 action ZERO
		else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SW_OLDKEY set key_pal 12 }
		}
else
	ifspritepal 23 // Yellow, failsafe incase I forget a gold key somewhere
		{
			ifand SW_KEYS 64 action ZERO
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SW_OLDKEY set key_pal 83 }
		}
else
	ifspritepal 83 // Gold
		{
			ifand SW_KEYS 64 action ZERO
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SW_OLDKEY set key_pal 83 }
		}
else
	ifspritepal 10 // Red
		{
		ifand SW_KEYS 128 action ZERO
		else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SW_OLDKEY set key_pal 10 }
		}
}

ifaction ZERO
{
ife HITAGSAVED 0 sound OLDKEY_UNLOCK else soundvar HITAGSAVED
operateactivators LOTAGSAVED THISACTOR
operaterespawns LOTAGSAVED
operatemasterswitches LOTAGSAVED
action UNLOCKED
ifspritepal 15 xorvar SW_KEYS 16 else
ifspritepal 12 xorvar SW_KEYS 32 else
ifspritepal 23 xorvar SW_KEYS 64 else
ifspritepal 83 xorvar SW_KEYS 64 else
ifspritepal 10 xorvar SW_KEYS 128
}

enda

useractor notenemy 22455

ifaction 0
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ife temple_key 8
		{
		set player_use 0
		 ifhitspace
			{
			set hit_key 30
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			set temple_key 0
			soundonce OLD_SWITCH
			action ONE
			}
		}

enda

// ===============================================================CUSTOM SWITCHES============================================

action CHAINPULL 0 2 1 1 10
action PULLED 2 1 1 1 0

defstate CUSTOMSWITCH

ifspritepal 1
 {
 ifmultiplayer spritepal 0 else killit
 }

ifaction 0
 ifn sprite[].zvel 0
	{
	sleeptime 0
	checkactivatormotion sprite[].zvel
	 ife RETURN 1
		seta[].zvel 0
	}

ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ife sprite[].zvel 0
	 ife player[].newowner -1
   {
   ife sprite[].xvel 2
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 break
	}
   ife sprite[].xvel 1 ifaction ONE break
   set player_use 0
    ifhitspace
     ifcount 13
      {
	  set hit_key 30
	  ifaction CHAINPULL break
	  ifaction PULLED break
      stopsound PLYR_SEARCH
      stopsound PLYR_SEARCH2
      ife HITAGSAVED 0
		{
		ifactor NEWSWITCH2 sound M_SWITCH else
		ifactor NEWSWITCH3 sound M_SWITCH else
		ifactor NEWSWITCH4 sound OLD_SWITCH else
		ifactor NEWSWITCH5 sound ANC_SWITCH else
		ifactor NEWSWITCH6 sound ANC_SWITCH else
		ifactor NEWSWITCH13 sound GIB_LAND1 else
		ifactor NEWSWITCH14 sound POOLBALLHIT else
		ifactor NEWSWITCH15 sound M_SWITCH2 else
		ifactor NEWSWITCH16 { sound PLYR_DRINKING sound EGYPT_SWITCH } else
		ifactor NEWSWITCH17 { sound LAVA_HIT sound EGYPT_SWITCH } else
		ifactor NEWSWITCH18 sound EGYPT_SWITCH else
		ifactor NEWSWITCH19 sound M_SWITCH2 else
		ifactor NEWSWITCH20 sound M_SWITCH else
		ifactor NEWSWITCH21 sound OLD_SWITCH3 else
		ifactor NEWSWITCH22 sound OLD_SWITCH2 else
		ifactor NEWSWITCH23 sound OLD_SWITCH2 else
		ifactor NEWSWITCH24 sound OLD_SWITCH4 else
		ifactor NEWSWITCH26 sound IW_SWITCH else
		ifactor NEWSWITCH27 sound M_SWITCH3 else
		ifactor NEWSWITCH28 sound M_SWITCH3 else
		ifactor NEWSWITCH29 sound M_SWITCH3 else
		ifactor NEWSWITCH30 sound SW_SWITCH else
		ifactor NEWSWITCH31 sound SW_SWITCH else
		ifactor NEWSWITCH32 sound SW_SWITCH else
		ifactor NEWSWITCH33 sound SW_SWITCH else
		ifactor NEWSWITCH34 sound BUNK_BUTTON else
		ifactor NEWSWITCH35 sound SF_BUTTON else
		ifactor NEWSWITCH36 sound M_SWITCH else
		ifactor NEWSWITCH37 sound M_SWITCH else
		ifactor 18648 sound CY_SWITCH else
		ifactor VR_REALITY_SWITCH sound VR_REALITY else
		ifactor 9457 { quote 260 set temp 1 sound WRITING } else
		sound CSWITCH1
		}
	  else ifl HITAGSAVED 4096 soundvar HITAGSAVED

	  ifactor NEWSWITCH24 action CHAINPULL
	  else { ifaction 0 action ONE else ifaction ONE action 0 }

      operateactivators LOTAGSAVED THISACTOR
      operaterespawns LOTAGSAVED
      operatemasterswitches LOTAGSAVED
      resetcount
      }
	}
ends

useractor notenemy NEWSWITCH1 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH2 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH3 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH4 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH5 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH6 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH7 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH8 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH9 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH10 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH11 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH12 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH13 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH14 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH15 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH16 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH17 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH18 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH19 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH20 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH21 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH22 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH23 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH24 state CUSTOMSWITCH
ifaction CHAINPULL ifactioncount 2 action PULLED
enda
useractor notenemy NEWSWITCH25 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH26 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH27 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH28 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH29 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH30 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH31 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH32 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH33 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH34 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH35 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH36 state CUSTOMSWITCH enda
useractor notenemy NEWSWITCH37 state CUSTOMSWITCH enda
useractor notenemy 18648 state CUSTOMSWITCH enda

action OPEN_FIRST 1
action OPEN_SECOND 2


useractor notenemy ENVIRO_SUIT_SWITCH

ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
   {
	set player_use 0
    ifhitspace
	{
     ifcount 13
      {
	  set hit_key 30
      stopsound PLYR_SEARCH
      stopsound PLYR_SEARCH2


		ifaction 0 // Close the outer door
			{
			ife FIRE_SUIT 1 break
			sound M_SWITCH
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			action OPEN_FIRST
			}
			else
		ifaction OPEN_FIRST // Open the inner door
			{
			ife FIRE_SUIT 0 // if the player is out of enviro-suit, open outer door
				{
				sound M_SWITCH
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				action 0
				}
				else // open inner door
				{
				action OPEN_SECOND
				sound IW_SWITCH
				operateactivators HITAGSAVED THISACTOR
				operaterespawns HITAGSAVED
				operatemasterswitches HITAGSAVED
				}
			}
			else
		ifaction OPEN_SECOND
			{
			action OPEN_FIRST
			sound IW_SWITCH
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			}

		  resetcount
		  }
	  }
	}
enda

useractor notenemy 9457 state CUSTOMSWITCH enda // Lease

useractor notenemy VR_REALITY_SWITCH state CUSTOMSWITCH enda

defstate civilian_rescued
spawn TRANSPORTERBEAM
globalsound SAVE_PERSON

ife POLYMERS 0 readgamevar POLYMERS
add POLYMERS 30
savegamevar POLYMERS

ife VOLUME 0
 ife LEVEL 7
	{
	ifrnd 48 spawn SPIRIT_ARMOUR else
	ifrnd 48 spawn GOLD_AMULET else
	ifrnd 48 spawn FIRE_ARMOUR else
	ifrnd 24 spawn HEAVYARMOUR else
	ifrnd 12 spawn SUPER_ARMOUR else
	ifrnd 4 spawn MEGASPHERE
	}

ends


// ***************************************************************************************************************************************************
// LIGHT CONDUCT
// ***************************************************************************************************************************************************

spritenoshade LIGHT_SPREAD
spritenopal LIGHT_SPREAD

action CONDUCT_LIGHT 0  5  1  1  4

useractor notenemy LIGHT_SPREAD
fall
ifaction 0
	{
	cstat 0
	flash
	sizeat 48 48
	seta[].shade -127
	action CONDUCT_LIGHT
	resetactioncount
	}

ifaction CONDUCT_LIGHT
	{
	sizeto 96 96
	ifactioncount 5 { shoot SPARK2 shoot SPARK2 shoot SPARK2 killit }
	}


enda


// ===============================================================HELL GATE KEYS=========================================


action ABLOODCHALICE -5 1 1 1 1
action ASOULBOX -4 1 1 1 1
action ASKULLCROWN -3 1 1 1 1
action ADARKBOOK -2 1 1 1 1
action ABROKENTORCH -1 1 1 1 1


useractor notenemy 7668

ifaction 0
{
cstat 32768
action ZERO
}

ifvarvarand LOTAGSAVED HELLGKEYSA ifspritepal 0 { sets[].floorshade 0 sets[].floorpal 2 } else
ifspritepal 1
 {
 ife HELLGKEYSA 31
	{
	quake 128
	palfrom 62 62
	globalsound DEMON_TELEPORT
	operaterespawns 666
	sets[].floorshade 0
	sets[].floorstat 1
	sets[].floorpicnum 7390
	killit
	}
 }
else
ifspritepal 2
{
      ifp palive
        ifpdistl RETRIEVEDISTANCE // quote check ok
          ifcount 6
            ifcanseetarget
			{
			ife HITAGSAVED 1
				{
				ifand HELLGKEYS 1
					{
					ifand HELLGKEYSA 1 break
					action ABLOODCHALICE
					sub HELLGKEYS 1
					sound GET_MAGIC
					sizeat 20 20
					quote 283
					cstat 0
					add HELLGKEYSA 1
					break
					}
				}
			ife HITAGSAVED 2
				{
				ifand HELLGKEYS 2
					{
					ifand HELLGKEYSA 2 break
					action ASOULBOX
					sound GET_MAGIC
					sub HELLGKEYS 2
					sizeat 20 20
					quote 283
					cstat 0
					add HELLGKEYSA 2
					break
					}
				}
			ife HITAGSAVED 4
				{
				ifand HELLGKEYS 4
					{
					ifand HELLGKEYSA 4 break
					action ASKULLCROWN
					sound GET_MAGIC
					sub HELLGKEYS 4
					sizeat 20 20
					quote 283
					cstat 0
					add HELLGKEYSA 4
					break
					}
				}
			ife HITAGSAVED 8
				{
				ifand HELLGKEYS 8
					{
					ifand HELLGKEYSA 8 break
					action ADARKBOOK
					sound GET_MAGIC
					sub HELLGKEYS 8
					sizeat 20 20
					quote 283
					cstat 0
					add HELLGKEYSA 8
					break
					}
				}
			ife HITAGSAVED 16
				{
				ifand HELLGKEYS 16
					{
					ifand HELLGKEYSA 16 break
					action ABROKENTORCH
					sound GET_MAGIC
					sub HELLGKEYS 16
					sizeat 20 20
					quote 283
					cstat 0
					add HELLGKEYSA 16
					break
					}
				}
			}
}
else
ifspritepal 3
 {
 ife HELLGKEYSA 63
	{
	quake 128
	palfrom 62 62
	globalsound DEMON_TELEPORT
      operatemasterswitches 666
	  operateactivators 666 THISACTOR
	sets[].floorpicnum 7417
	killit
	}
 }
 else
ifspritepal 6
 {
 ife HELLGKEYSA 31
	{
	quake 128
	palfrom 62 62
      operatemasterswitches 666
	  operateactivators 666 THISACTOR
	killit
	}
 }
enda

useractor notenemy 7401 // Spinning hellgate
ifn HITAGSAVED 0
{
ifaction 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action ZERO
	}
ifaction ZERO
	{
	geta[].ang temp
	ifspritepal 3 sub temp 8 else
	add temp 8
	seta[].ang temp
	}
}
enda

spritenopal 7402
useractor notenemy 7402 // Spinning hellgate 2nd part
ifn HITAGSAVED 0
{
spritepal 2
ifaction 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action ZERO
	}
ifaction ZERO
	{
	geta[].ang temp
	add temp 64
	seta[].ang temp
	}
}
enda

useractor notenemy 7404 // Dis base hellgate end
ife HITAGSAVED 0
{
	ife HELLGKEYSA 63
	{
	cstat 16
	ifpdistl 16384 soundonce HELLGATE
	ifpdistl 824
	ifaction 0
		{
		palfrom 64 64 0 0
		globalsound DEMON_TELEPORT
		operateactivators 667 THISACTOR
		action ZERO
		}
	}
	else
	cstat 32768
}
else
ife HITAGSAVED 1
	{
	checkactivatormotion LOTAGSAVED
		ife RETURN 1
			{
			set temp4 0
			action ZERO
			resetcount
			}

	ifaction ZERO
		{
		ifl temp4 254 add temp4 8
		set alpha temp4
		state SPRITE_FADE
		ifcount 128 killit
		}

	}
else
ife HITAGSAVED 2
	{
	checkactivatormotion LOTAGSAVED
		ife RETURN 1 action ZERO

	ifaction ZERO
		{
		cstat 16
		ifpdistl 16384 soundonce HELLGATE
		ifpdistl 824
			{
			palfrom 64 64 0 0
			globalsound DEMON_TELEPORT
			operateactivators 667 THISACTOR
			killit
			}
		}
	else
	cstat 32768

	}
enda

// ===============================================================DIS BASE REACTOR=========================================

useractor notenemy 7669

ifaction 0
{
cstat 32768
action ZERO
}

ifpdistl 1024
{
 ife HELLGKEYS 31
	{
	operateactivators 34 THISACTOR
	killit
	}
  else
  ifcount 26
	  {
	  quote 232
	  sound LOCKED_HITECH1
	  resetcount
	  }
}

enda

useractor notenemy 7672

checkactivatormotion HITAGSAVED
ife RETURN 1
{
	ifaction 0
		{
		action ZERO
		resetcount
		}
}

ifaction ZERO
	{
	ifcount 26 ife LOTAGSAVED 1 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	ifcount 52 ife LOTAGSAVED 2 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	ifcount 78 ife LOTAGSAVED 3 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	ifcount 104 ife LOTAGSAVED 4 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	ifcount 130 ife LOTAGSAVED 5 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	ifcount 156 ife LOTAGSAVED 6 { globalsound INTRUDER_ALERT ifn EXTRASAVED -1 operateactivators EXTRASAVED THISACTOR action ONE }
	}

enda

useractor notenemy 7674 // countdown!

checkactivatormotion HITAGSAVED
ife RETURN 1
{
	ifaction 0
		{
		action ZERO
		resetcount
		}
}

ifaction ZERO
	{
	ifcount 208
		{
		starttrackvar 29
		globalsound CLAXON_ALARM
		quake 26
		set countdown 1
		set countdown_type 1
		set countdown_minutes 3
		set countdown_fail LOTAGSAVED
		action ONE
		}
	}

ifaction ONE
{
	ifrnd 8 quake 13
	add count_timer 1
	ifg countdown_seconds -1 ife count_timer 28 { sub countdown_seconds 1 set count_timer 0 }
	else ife countdown_seconds -1
		{
		ifg countdown_minutes 0
			{ sub countdown_minutes 1 set countdown_seconds 59 }
		else
			{
			set countdown_type 0
			setgamepalette 0
			operateactivators countdown_fail THISACTOR
			operatemasterswitches countdown_fail
			operaterespawns countdown_fail
			set countdown -1
			killit
			}
		}
	ifcount 40 { globalsound CLAXON_ALARM resetcount }
}

enda

action COUNT_0 13
action COUNT_1 14
action COUNT_2 15
action COUNT_3 16
action COUNT_4 17
action COUNT_5 18
action COUNT_6 19
action COUNT_7 20
action COUNT_8 21
action COUNT_9 22

useractor notenemy 2459

	ifspritepal 2
		{
		ife countdown_minutes 9 action COUNT_9 else
		ife countdown_minutes 8 action COUNT_8 else
		ife countdown_minutes 7 action COUNT_7 else
		ife countdown_minutes 6 action COUNT_6 else
		ife countdown_minutes 5 action COUNT_5 else
		ife countdown_minutes 4 action COUNT_4 else
		ife countdown_minutes 3 action COUNT_3 else
		ife countdown_minutes 2 action COUNT_2 else
		ife countdown_minutes 1 action COUNT_1 else
		ife countdown_minutes 0 action COUNT_0
		}
	else
	ifspritepal 3
		{
		ifge countdown_seconds 50 action COUNT_5 else
		ifge countdown_seconds 40 action COUNT_4 else
		ifge countdown_seconds 30 action COUNT_3 else
		ifge countdown_seconds 20 action COUNT_2 else
		ifge countdown_seconds 10 action COUNT_1 else
		ifge countdown_seconds 0 action COUNT_0
		}
	else
	ifspritepal 9 // cant be arsed to maths this, brute forcing time
		{
		ifge count_timer 27 sound COUNTD
		set temp countdown_seconds
		ifge countdown_seconds 50 sub temp 50
		else ifge countdown_seconds 40 sub temp 40
		else ifge countdown_seconds 30 sub temp 30
		else ifge countdown_seconds 20 sub temp 20
		else ifge countdown_seconds 10 sub temp 10

		ife temp 9 action COUNT_9 else
		ife temp 8 action COUNT_8 else
		ife temp 7 action COUNT_7 else
		ife temp 6 action COUNT_6 else
		ife temp 5 action COUNT_5 else
		ife temp 4 action COUNT_4 else
		ife temp 3 action COUNT_3 else
		ife temp 2 action COUNT_2 else
		ife temp 1 action COUNT_1 else
		ife temp 0 action COUNT_0
		}

enda

// ===============================================================MISSION FAILED============================================


useractor notenemy 7679

checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	state fade_out_white
	set SLO_MO_SHOWOFF 70
	set FISSION_MAILED 1
	killit
	}

enda

// ===============================================================SLO MO TRIGGER============================================


useractor notenemy 10150

checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	ifg LOTAGSAVED 128 set LOTAGSAVED 128
	set SLO_MO_SHOWOFF LOTAGSAVED
	killit
	}

enda

// ===============================================================SPOOKY TRIGGER============================================



useractor notenemy 10151

checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	ifspritepal 8 set vr_glitched 98 else
	set player_spooked 98
	killit
	}

enda

// ===============================================================PLUG SOCKETS============================================


useractor notenemy BLUEPSOCKET
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    {
	set player_use 0
	 ifhitspace
	  {
	  set hit_key 30
		ifand PLUGKEY 1
			{
			 ifcount 26
			  {
			  sound PLUGPUTIN
			  operateactivators LOTAGSAVED THISACTOR
			  operaterespawns LOTAGSAVED
			  operatemasterswitches LOTAGSAVED
			sub PLUGKEY 1
			seta[].picnum 3460
			resetcount
			  }
			}
		else
		{ set door_locked 0 set key_icon BLUEPLUG set key_pal 0 }
	  }
	}
enda

useractor notenemy REDPSOCKET
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    {
	set player_use 0
	 ifhitspace
	  {
	  set hit_key 30
		ifand PLUGKEY 2
			{
			 ifcount 26
			  {
			  sound PLUGPUTIN
			  operateactivators LOTAGSAVED THISACTOR
			  operaterespawns LOTAGSAVED
			  operatemasterswitches LOTAGSAVED
			  sub PLUGKEY 2
			  seta[].picnum 3461
			  resetcount
			  }
			}
		else
		{ set door_locked 0 set key_icon REDPLUG set key_pal 0 }
	  }
	}
enda

useractor notenemy YELLOWPSOCKET
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    {
	set player_use 0
	 ifhitspace
	  {
	  set hit_key 30
		ifand PLUGKEY 4
			{
			 ifcount 26
			  {
			  sound PLUGPUTIN
			  operateactivators LOTAGSAVED THISACTOR
			  operaterespawns LOTAGSAVED
			  operatemasterswitches LOTAGSAVED
			  sub PLUGKEY 4
			  seta[].picnum 3462
			  resetcount
			  }
			}
		else
		{ set door_locked 0 set key_icon YELLOWPLUG set key_pal 0 }
	  }
	}
enda

useractor notenemy 3460
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
     ifcount 26
      {
	ifand PLUGKEY 1 break
	set player_use 0
	    ifhitspace
		{
		set hit_key 30
	      sound PLUGPULLOUT
	      operateactivators LOTAGSAVED THISACTOR
	      operaterespawns LOTAGSAVED
	      operatemasterswitches LOTAGSAVED
		add PLUGKEY 1
		seta[].picnum BLUEPSOCKET
		resetcount
		}
      }
enda

useractor notenemy 3461
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
     ifcount 26
      {
	ifand PLUGKEY 2 break
	set player_use 0
	    ifhitspace
		{
		set hit_key 30
	    sound PLUGPULLOUT
	    operateactivators LOTAGSAVED THISACTOR
	    operaterespawns LOTAGSAVED
	    operatemasterswitches LOTAGSAVED
		add PLUGKEY 2
		seta[].picnum REDPSOCKET
		resetcount
		}
      }
enda

useractor notenemy 3462
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
     ifcount 26
      {
	ifand PLUGKEY 4 break
	set player_use 0
	    ifhitspace
		{
		set hit_key 30
	    sound PLUGPULLOUT
	    operateactivators LOTAGSAVED THISACTOR
	    operaterespawns LOTAGSAVED
	    operatemasterswitches LOTAGSAVED
		add PLUGKEY 4
		seta[].picnum YELLOWPSOCKET
		resetcount
		}
      }
enda

// *************************************************
// AIR BUBBLES
// *************************************************

useractor notenemy AIRBUBBLES

ifaction 0
{
	spawn 8951

	// never block
	geta[].cstat temp
	ifand temp 1
	{
		sub temp 1
		seta[].cstat temp
	}


	action ZERO
}

ifrnd 8 { espawn WATERBUBBLE geta[].z temp sub temp 7500 seta[RETURN].z temp }
getp[].airleft temp2

ifpdistl 812
 ifp palive
    {
    ifg temp2 389 break
	ifand EGYPT_ARTIFACTS 2 break
    soundonce PLYR_TAKEPILLS
    add temp2 10
    setp[].airleft temp2
    }

enda

// BOAT STUFF

useractor notenemy 631 ifspritepal 3 fall enda

// *************************************************
// WALL SCROLL POINT
// *************************************************

useractor notenemy 3828

ifaction 0
{
	cstat 32768
	// Set EXTRA to only start scrolling if extra is activated
	ifn temp4 1
	{
	set hitwall -1
		ifg EXTRASAVED 0
		{
			set MASTERSWITCH_SUBSCRIBE EXTRASAVED
			checkactivatormotion EXTRASAVED

			ife RETURN 1
				set temp4 1
			else ife MASTERSWITCH_RETURN 1
			{
				set temp4 1
				set MASTERSWITCH_RETURN 0
			}
		}
		else
			set temp4 1
	}

	ife temp4 1
	{
		// geta[].sectnum temp
		// geta[].ang temp3

		sin my sprite[].ang
		cos mx sprite[].ang

		hitscan sprite[].x sprite[].y sprite[].z sprite[].sectnum mx my 0 hitsect hitwall hitsprite hitx hity hitz 16777280
		action ZERO
	}
}

ifn hitwall -1
	{
	getwall[hitwall].hitag temp2
		ifand temp2 16 nullop else
		{
		xor temp2 16
		setwall[hitwall].hitag temp2
		}
	ifspritepal 0
		{
		getwall[hitwall].xpanning temp2
		sub temp2 LOTAGSAVED
		setwall[hitwall].xpanning temp2
		getwall[hitwall].ypanning temp2
		sub temp2 HITAGSAVED
		setwall[hitwall].ypanning temp2
		}
	ifspritepal 1
		{
		getwall[hitwall].xpanning temp2
		add temp2 LOTAGSAVED
		setwall[hitwall].xpanning temp2
		getwall[hitwall].ypanning temp2
		add temp2 HITAGSAVED
		setwall[hitwall].ypanning temp2
		}
	ifspritepal 2
		{
		getwall[hitwall].nextwall next_wall
		getwall[next_wall].xpanning temp2
		sub temp2 LOTAGSAVED
		setwall[next_wall].xpanning temp2
		getwall[next_wall].ypanning temp2
		sub temp2 HITAGSAVED
		setwall[next_wall].ypanning temp2
		}
	}

enda

// *************************************************
// WALL BLOCK
// *************************************************

useractor notenemy 12030

ifaction 0
{
geta[].x x
geta[].y y
geta[].sectnum temp
geta[].ang temp3

sin my temp3
cos mx temp3
geta[].z z

hitscan x y z temp mx my 0 hitsect hitwall hitsprite hitx hity hitz 16777280
action ZERO
}

ifaction ZERO
{
ifn hitwall -1
	{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			getwall[hitwall].cstat temp6
			xorvar temp6 1
			setwall[hitwall].cstat temp6
			killit
			}
	}
}

enda

// *************************************************
// FLOOR CEILING SCROLL
// *************************************************

defstate SETSTAT
	gets[].ceilingstat temp
	xorvar temp 8192
	sets[].ceilingstat temp
ends

useractor notenemy 3829

ifspritepal 0 // SCROLL CEILING
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].ceilingxpanning temp2 add temp2 LOTAGSAVED sets[SECT_NUM].ceilingxpanning temp2
gets[SECT_NUM].ceilingypanning temp2 add temp2 HITAGSAVED sets[SECT_NUM].ceilingypanning temp2
}

ifspritepal 1 // SCROLL FLOOR
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].floorxpanning temp2 add temp2 LOTAGSAVED sets[SECT_NUM].floorxpanning temp2
gets[SECT_NUM].floorypanning temp2 add temp2 HITAGSAVED sets[SECT_NUM].floorypanning temp2
}

ifspritepal 2 // SCROLL BOTH
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].floorxpanning temp2 add temp2 LOTAGSAVED sets[SECT_NUM].floorxpanning temp2
gets[SECT_NUM].floorypanning temp2 add temp2 HITAGSAVED sets[SECT_NUM].floorypanning temp2
gets[SECT_NUM].ceilingxpanning temp2 add temp2 LOTAGSAVED sets[SECT_NUM].ceilingxpanning temp2
gets[SECT_NUM].ceilingypanning temp2 add temp2 HITAGSAVED sets[SECT_NUM].ceilingypanning temp2
}

ifspritepal 3 // SCROLL CEILING BACKWARDS
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].ceilingxpanning temp2 sub temp2 LOTAGSAVED sets[SECT_NUM].ceilingxpanning temp2
gets[SECT_NUM].ceilingypanning temp2 sub temp2 HITAGSAVED sets[SECT_NUM].ceilingypanning temp2
}

ifspritepal 4 // SCROLL FLOOR BACKWARDS
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].floorxpanning temp2 sub temp2 LOTAGSAVED sets[SECT_NUM].floorxpanning temp2
gets[SECT_NUM].floorypanning temp2 sub temp2 HITAGSAVED sets[SECT_NUM].floorypanning temp2
}

ifspritepal 5 // SCROLL BOTH BACKWARDS
{
ifaction 0 { state SETSTAT action ZERO }
gets[SECT_NUM].floorxpanning temp2 sub temp2 LOTAGSAVED sets[SECT_NUM].floorxpanning temp2
gets[SECT_NUM].floorypanning temp2 sub temp2 HITAGSAVED sets[SECT_NUM].floorypanning temp2
gets[SECT_NUM].ceilingxpanning temp2 sub temp2 LOTAGSAVED sets[SECT_NUM].ceilingxpanning temp2
gets[SECT_NUM].ceilingypanning temp2 sub temp2 HITAGSAVED sets[SECT_NUM].ceilingypanning temp2
}

ifspritepal 6 // SCROLL CEILING SLOW FOR SKYBOXES
{
ifaction 0 { state SETSTAT action ZERO }
add INTERNALCOUNT 1
ifg INTERNALCOUNT 1
	{
	gets[SECT_NUM].ceilingxpanning temp2 add temp2 LOTAGSAVED sets[SECT_NUM].ceilingxpanning temp2
	gets[SECT_NUM].ceilingypanning temp2 add temp2 HITAGSAVED sets[SECT_NUM].ceilingypanning temp2
	set INTERNALCOUNT 0
	}
}

enda

// *************************************************
// DEAD MARINE FLASHLIGHT
// *************************************************

action FLASHON -1

useractor notenemy 5869
seta[].blend 255
ifaction 0 ifrnd 16 { ife camerasprite -1 sound FLASHLIGHT action FLASHON }

ifaction FLASHON ifrnd 16 { ife camerasprite -1 sound FLASHLIGHT action 0 }

enda

// *************************************************
// PENTAGRAM
// *************************************************

useractor notenemy PENTAGRAM

ifn LOTAGSAVED 1
{
	ifaction 0
	{
	seta[].shade -127
	seta[].blend 255
	ifspawnedby HADESPHERE
		{
		palfrom 20 20
		cstat 130
		}
	else
		{
		seta[].z sector[].floorz
		sizeat 16 16
		cstat 34
		}
	spriteflags 4
	soundonce COOKINGDEEPFRIER
	action ZERO
	}
	else
	ifaction ZERO
	{
	ifspawnedby HADESPHERE
		{
		sizeto 48 48
		ifg sprite[].xrepeat 47 killit
		ifg sprite[].xrepeat 5 { geta[].shade temp2 add temp2 1 seta[].shade temp2 }
		}
		else
		{
		fall
		geta[].shade temp
		add temp 1
		ifand temp 1 ifl temp -90 spawn 30071
		ifg temp 40 killit
		seta[].shade temp
		}
	}
}

enda


// *************************************************
// BLOODPOOL
// *************************************************

defstate bloodpool_state
ifpdistl 812
{
ifand sprite[].cstat 32
	{
	ifp pshrunk break
	setp[].footprintcount 4
	setp[].footprintpal 2
	}
}
ends

useractor notenemy 7400 state bloodpool_state enda
useractor notenemy 10567 state bloodpool_state enda
useractor notenemy 10568 state bloodpool_state enda
useractor notenemy 10586 state bloodpool_state enda
useractor notenemy 10643 state bloodpool_state enda
useractor notenemy 10644 state bloodpool_state enda

action REDPOOL -40
action REDPOOL_2 13
action REDPOOL_3 14
action REDPOOL_4 15

action GREENPOOL -44
action BLUEPOOL -43
action BLACKPOOL -42

useractor notenemy 2244
ifaction 0 { state SlapSpriteToFloor action ZERO } enda

spritenoshade 2284

useractor notenemy 2284
state lowperf

sizeto 36 36

ifaction 0
{
	seta[].hitag 0
	cstator 32
	set ally_mag 0
	move 0 randomangle
	ifinspace killit

	geta[].sectnum upd_sect
	updatesectorz sprite[].x sprite[].y sprite[].z upd_sect

	ifn upd_sect -1 changespritesect THISACTOR upd_sect else killit

	ife sprite[].sectnum -1 killit
	ifand sector[].floorstat 1 killit
	ife sector[].lotag 1 killit
	findnearactor3d 2284 768 temp ifn temp -1 killit
	resetcount

	ifspawnedby ICE_DEBRIS_F action BLUEPOOL
	else ifspawnedby 8127 action BLUEPOOL
	else ifspawnedby 8129 action BLUEPOOL
	else ifspawnedby 8131 action BLUEPOOL
	else ifspawnedby 8132 action BLUEPOOL
	else ifspawnedby 8133 action BLUEPOOL
	else ifspawnedby 8134 action BLUEPOOL
	else ifspawnedby 19540 action BLACKPOOL
	else ifspawnedby 18112 action BLACKPOOL
	else ifspawnedby 4714 action GREENPOOL
	else ifspawnedby ORC_CORPSE_FLY action GREENPOOL
	else ifspawnedby ALUDRAN { spritepal 30 action BLUEPOOL }
	else ifspritepal 14 action GREENPOOL
	else ifspritepal 8 action GREENPOOL
	else ifspritepal 1 action BLUEPOOL
	else
		{
		randvar temp8 40
		ifl temp8 10 action REDPOOL
		else ifl temp8 20 action REDPOOL_2
		else ifl temp8 30 action REDPOOL_3
		else action REDPOOL_4
		}
}

ife ally_temp 1
{
ifoutside killit
ifand sector[].ceilingstat 8192 killit // is the ceiling scrolling?
ifand sector[].ceilingstat 1024 killit // is it a TROR sector?
ifand sector[].ceilingstat 2 state SlapSpriteToCeiling
seta[].pal sector[].ceilingpal
seta[].shade sector[].ceilingshade
seta[].z sector[].ceilingz
ifrnd 4
 ifaction ZERO
  ifl ally_mag 5
	{
	espawn WATERDRIP
	add ally_mag 1
	ifaction GREENPOOL seta[RETURN].pal 8
	else ifspritepal 6 seta[RETURN].pal 8
	else ifaction BLACKPOOL seta[RETURN].pal 13
	else ifspritepal 14 seta[RETURN].pal 14
	else ifspritepal 30 seta[RETURN].pal 30
	else ifaction BLUEPOOL seta[RETURN].pal 16
	else seta[RETURN].pal 10
	}
}
else
{
ifand sector[].floorstat 8192 killit // is the floor scrolling?
ifand sector[].floorstat 1024 killit // is it a TROR sector?
ifand sector[].floorstat 2 state SlapSpriteToFloor else seta[].z sector[].floorz
seta[].shade sector[].floorshade
seta[].pal sector[].floorpal
}

ifpdistl 812
{
ifspritepal 3 break
	ifand sprite[].cstat 32
		{
		setp[].footprintcount 4
		ifaction ZERO setp[].footprintpal 2 else
		ifaction GREENPOOL setp[].footprintpal 8 else
		ifaction BLUEPOOL setp[].footprintpal 1 else
		ifaction BLACKPOOL setp[].footprintpal 13 else
		ifaction REDPOOL setp[].footprintpal 2 else
		ifaction REDPOOL_2 setp[].footprintpal 2 else
		ifaction REDPOOL_3 setp[].footprintpal 2 else
		ifaction REDPOOL_4 setp[].footprintpal 2 else
		setp[].footprintpal sprite[].pal
		}
}
else
ifpdistgvar opt_particle_distance sizeto 0 0

enda

// high-res flame ================================================

spritenoshade 24268
spritenopal 24268

useractor notenemy 24268
ifaction 0
	{
    cstat 1024
	seta[].blend 255
	seta[].cstat 1026
	seta[].shade -127
	action ZERO
	}
enda

// LOGS ================================================

defstate log_fire
		espawn 14040
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 128
		sub temp temp2
		randvar temp2 1024
		sub temp temp2
		seta[RETURN].z temp
ends

useractor notenemy 8002
ifspritepal 5 break
ifpdistl 16384 soundonce SOFTFIRE_AMBIENCE

ifrnd 1 { shoot SPARK ifpdistl 16384 sound FIRE_SPIT }

ifspritepal 3 { sleeptime 0 state log_fire } else
	ifrnd 96
	 ife opt_advanced_flame YES
	  ifpdistlvar opt_particle_distance
		state log_fire
enda

// HELL TORCH ================================================

useractor notenemy 7392

ife ally_mag 0
	{
	espawn 24268
		seta[RETURN].xrepeat 11
		seta[RETURN].yrepeat 11
		seta[RETURN].pal 106
		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 256
		sub temp temp2
		seta[RETURN].z temp
	espawn 7392
		setav[RETURN].ally_mag 1
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].pal sprite[].pal
		seta[RETURN].shade sprite[].shade
	killit
	}
enda

// EGYPT OIL BARREL ================================================

spriteshadow 23625

useractor notenemy 23625

ifaction 0
{
strength 5
sizeat 40 40
fall
cstator 257
action ZERO
}

ifaction ZERO
{
ifpdistl 16384 { ifactorsound THISACTOR TORCHFIRE nullop else sound TORCHFIRE }
	ifrnd 96
	 ife opt_advanced_flame YES
	  ifpdistlvar opt_particle_distance
		{
		espawn 14040
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 32
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 32
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 180
		sub temp temp2
		randvar temp2 1024
		sub temp temp2
		seta[RETURN].z temp
		}

	ifhitweapon
	{
	debris SCRAP3 2
	shoot SPARK
	ifdead
		{
		debris SCRAP3 16
		spawn GROUNDEXPLOSION
		shoot LAVABALL shoot LAVABALL shoot LAVABALL shoot LAVABALL
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		flash
		quake 13
		sound BARREL_EXPLODE
		hitradius 2048 100 125 175 250
		ifrnd 128 sound POTBREAK_2 else
		sound POTBREAK
		killit
		}
	}
}

enda

useractor notenemy 20107
	ifrnd 96
	  ifpdistlvar opt_particle_distance
		{
		soundonce SOFTFIRE_AMBIENCE
		espawn 14040
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 32
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 32
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 128
		sub temp temp2
		randvar temp2 1024
		sub temp temp2
		seta[RETURN].z temp
		}
enda

spritenoshade 7936
useractor notenemy 7936 // standard torch on wall
ife sprite[].httempang 0
	{
	espawn 24268
		seta[RETURN].xrepeat 20
		seta[RETURN].yrepeat 20
		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 124
		sub temp temp2
		seta[RETURN].z temp
	espawn 7936
		seta[RETURN].httempang 1
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].pal sprite[].pal
		seta[RETURN].shade sprite[].shade
	killit
	}
enda

spritenoshade 10074
useractor notenemy 10074
	ifrnd 96
	 ife opt_advanced_flame YES
	  ifpdistlvar opt_particle_distance
		{
		espawn 14040
		seta[RETURN].pal sprite[].pal
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 96
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 96
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 64
		sub temp temp2
		randvar temp2 512
		sub temp temp2
		seta[RETURN].z temp
		}
enda

useractor notenemy 10082

ifn HITAGSAVED 0
{
state check_player_prox
	ife is_player_close 1
		{
		set player_use 0
			ifhitspace
				{
				set hit_key 30
				soundonce ZIPPOLIGHT
				soundonce ENGULF_FIRE
				palfrom 10 10 10 10
				operateactivators HITAGSAVED THISACTOR
				operaterespawns HITAGSAVED
				operatemasterswitches HITAGSAVED
				cactor 10078
				}
		}
}

enda

spritenoshade 10078
useractor notenemy 10078

ifaction 0
{
espawn 10082
seta[RETURN].xrepeat sprite[].xrepeat
seta[RETURN].yrepeat sprite[].yrepeat
seta[].blend 255
cstator 2
action ZERO
}

ifaction ZERO
{
	ifrnd 96
	 ife opt_advanced_flame YES
	  ifpdistlvar opt_particle_distance
		{
		ifpdistl 16384 { ifactorsound THISACTOR SOFTFIRE_AMBIENCE nullop else sound SOFTFIRE_AMBIENCE }
		espawn 14040
		seta[RETURN].pal sprite[].pal
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 184
		sub temp temp2
		randvar temp2 512
		sub temp temp2
		seta[RETURN].z temp
		}
}
enda

spritenoshade 11651
useractor notenemy 11651
	ifrnd 96
	 ife opt_advanced_flame YES
	  ifpdistlvar opt_particle_distance
		{
		espawn 14040
		seta[RETURN].pal sprite[].pal
		seta[RETURN].cstat 32768
		geta[].x temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 64
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		geta[].yrepeat temp2
		mulvar temp2 160
		sub temp temp2
		randvar temp2 512
		sub temp temp2
		seta[RETURN].z temp
		}
enda

// *************************************************
// ENEMY MUZZLEFLASH
// *************************************************

defstate sprite_muzzleflash
ifaction 0
	{
	ifspritepal 3 break
	spriteflags 2116
	seta[].shade -127
	seta[].blend 255
	cstat 18
	ifrnd 128 cstator 4 ifrnd 128 cstator 8
	ifactor 16113 sizeat 5 5 else
	sizeat 3 3
	action ZERO
	}

ifaction ZERO
	{
	geta[].shade temp
	add temp 64
	ifge temp 40 killit
	seta[].shade temp
	}
ends

useractor notenemy 16113 state sprite_muzzleflash ifspawnedby SCARAB_DEMON sizeat 16 16 enda
useractor notenemy 16114 state sprite_muzzleflash enda
useractor notenemy 16115 state sprite_muzzleflash enda
useractor notenemy 16116 state sprite_muzzleflash enda
useractor notenemy 16117 state sprite_muzzleflash enda
useractor notenemy 16118 state sprite_muzzleflash enda
useractor notenemy 16119 state sprite_muzzleflash enda
useractor notenemy 16120 state sprite_muzzleflash enda
useractor notenemy 16121 state sprite_muzzleflash enda

// *************************************************
// BIG SMOKE
// *************************************************

action BIGSMOKE_FRAMES 0 6 1 1 30

move BIGSMOKE_UP 0 -60
move BIGSMOKE_DOWN 0 60
move BIGSMOKE_FORWARD 60 0
move BIGSMOKE_FORWARDUP 60 -60
move BIGSMOKE_FORWARDDOWN 60 60

useractor notenemy BIG_SMOKE
	ifaction 0
	{
	ifspawnedby ONFIREGUY sizeat 24 24
	ifspawnedby DISINTIGRATE_GUY sizeat 24 24
	ifrnd 128 cstat 6 else cstat 10
	ifspawnedby 14040 { seta[].blend 30 cstat 514 sizeat 24 24 }
	ifspawnedby MI_24_HIND { fall set YVELSAVED 5 sizeat 16 16 cstator 514 }
	ifspawnedby BLACK_HAWK { fall set YVELSAVED 5 sizeat 16 16 cstator 514 }
	ifspawnedby MELTA_BEAM { sizeat 16 16 cstator 514 ifpdistl 768 killit }
	ifspawnedby MDF_PLASMA { sizeat 16 16 cstator 514 }
	ifspawnedby HOVERTANK { seta[].blend 30 spritepal 27 cstat 514 sizeat 24 24 }
	ifspawnedby PLASMA_EX { sizeat 16 16 spritepal 13 cstator 514 }
	action BIGSMOKE_FRAMES
	}

	ifaction BIGSMOKE_FRAMES
	{
	ife YVELSAVED 1 move BIGSMOKE_UP getv else
	ife YVELSAVED 2 move BIGSMOKE_DOWN getv else
	ife YVELSAVED 3 move BIGSMOKE_FORWARD geth else
	ife YVELSAVED 4 move BIGSMOKE_FORWARDUP geth getv else
	ife YVELSAVED 5 move BIGSMOKE_FORWARDDOWN geth getv
	ifactioncount 0
		{
		geta[].xrepeat temp5
		add temp5 2
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		add temp5 2
		seta[].yrepeat temp5
		}
	ifactioncount 2 sizeto 255 255
	  ifactioncount 6
	    killit
	}
enda

// *************************************************
// BIG SMOKE 2
// *************************************************

action OBJECTSMOKE_FRAMES 8 17 1 1 4
action BIGSMOKE2_FRAMES 0 25 1 1 8
action BIGNUKE_FRAMES 0 25 1 1 30
action CHOPPER_SMOKE 8 17 1 1 10

move NSSLOW2 32 0

spritenoshade BIG_SMOKE2

useractor notenemy BIG_SMOKE2
	ifaction 0
	{
	seta[].blend 255
	ifspawnedby 11249 { spritepal 28 sizeat 32 32 } else
	ifspawnedby 11250 { spritepal 28 sizeat 32 32 } else
	ifspawnedby 5664 { spritepal 28 sizeat 32 32 } else
	ifspawnedby 5665 { spritepal 1 sizeat 32 32 } else
	ifspawnedby 25575 sizeat 32 32 else
	ifspawnedby 25576 sizeat 32 32 else
	ifspawnedby 25577 sizeat 32 32 else
	ifspawnedby RPG sizeat 32 32 else
	ifspawnedby 3624 sizeat 32 32 else
	ifspawnedby 3586 sizeat 32 32 else
	ifspawnedby GEO_NADE sizeat 32 32 else
	ifspawnedby GP30_NADE sizeat 32 32 else
	ifspawnedby WTANKGUN sizeat 128 128 else
	sizeat 64 64
	cstat 642
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	ifspawnedby 7677 action BIGNUKE_FRAMES else
	ifspawnedby NUCLEAR_EXPLOSION action BIGNUKE_FRAMES else
	ifspawnedby RESPAWN { move BIGSMOKE_FORWARD geth action CHOPPER_SMOKE } else
	ifspawnedby 18271 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby RPG { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby RPG7_ROCKET { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 3624 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 29370 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby ALUDRAN { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 3586 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 18134 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby FORTYMM_NADE { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby GEO_NADE { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby ANUBIS_MUMMY { spritepal 19 move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 29298 { spritepal 19 move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby EXPLOSION2 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby MISSILE { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby 16616 { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby VILMOS_ROBOT { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby NUCLEAR_EXPLOSION { move BIGSMOKE_FORWARD geth randomangle action CHOPPER_SMOKE } else
	ifspawnedby MI_24_HIND action CHOPPER_SMOKE else
	ifspawnedby KA_50_BLACK_SHARK action CHOPPER_SMOKE else
	ifspawnedby 11249 action OBJECTSMOKE_FRAMES else
	ifspawnedby 11250 action OBJECTSMOKE_FRAMES else
	ifspawnedby 5664 action OBJECTSMOKE_FRAMES else
	ifspawnedby 5665 action OBJECTSMOKE_FRAMES else
	ifspawnedby 25575 action OBJECTSMOKE_FRAMES else
	ifspawnedby 25576 action OBJECTSMOKE_FRAMES else
	ifspawnedby 25577 action OBJECTSMOKE_FRAMES else
	action BIGSMOKE2_FRAMES
	}

	ifspawnedby 7677 move NSSLOW2 geth
	ifspawnedby MI_24_HIND move BIGSMOKE_UP getv
	ifspawnedby CULT_CHAINSAW move BIGSMOKE_UP getv
	ifspawnedby WTANKGUN move BIGSMOKE_UP getv
	ifspawnedby KA_50_BLACK_SHARK move BIGSMOKE_UP getv

	ifaction CHOPPER_SMOKE
	{
	ifpdistl 2048 killit
	ifactioncount 1
		{
		geta[].xrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].yrepeat temp5
		}
	  ifactioncount 17
	    killit
	}

	ifaction OBJECTSMOKE_FRAMES
	{
	ifactioncount 1
		{
		geta[].xrepeat temp5
		ifl temp5 64 add temp5 2
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		ifl temp5 64 add temp5 2
		seta[].yrepeat temp5
		}
	  ifactioncount 17
	    killit
	}

	ifaction BIGSMOKE2_FRAMES
	{
	ife YVELSAVED 1 move BIGSMOKE_UP getv else
	ife YVELSAVED 2 move BIGSMOKE_DOWN getv else
	ife YVELSAVED 3 move BIGSMOKE_FORWARD geth else
	ife YVELSAVED 4 move BIGSMOKE_FORWARDUP geth getv else
	ife YVELSAVED 5 move BIGSMOKE_FORWARDDOWN geth getv
	ifactioncount 1
		{
		geta[].xrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].yrepeat temp5
		}
	  ifactioncount 25
	    killit
	}

	ifaction BIGNUKE_FRAMES
	{
	move BIGSMOKE_FORWARD geth
	seta[].blend 255
	ifactioncount 1
		{
		geta[].xrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		ifl temp5 200 add temp5 6
		seta[].yrepeat temp5
		}
	  ifactioncount 25
	    killit
	}

enda

// *************************************************
// BIG SMOKE3
// *************************************************

action BIGSMOKE3_FRAMES -5 5 1 1 30

useractor notenemy 18365
	ifaction 0
	{
	ifspritepal 0
		{
		ifoutside seta[].pal sector[].ceilingpal else
		seta[].pal sector[].floorpal
		}
	ifspawnedby ONFIREGUY sizeat 24 24
	ifspawnedby DISINTIGRATE_GUY sizeat 24 24
	ifspawnedby 14040 sizeat 24 24
	ifrnd 128 cstat 6 else cstat 10
	ifspawnedby MI_24_HIND { fall set YVELSAVED 5 sizeat 16 16 cstator 514 }
	ifspawnedby BLACK_HAWK { fall set YVELSAVED 5 sizeat 16 16 cstator 514 }
	ifspawnedby MELTA_BEAM { sizeat 16 16 cstator 514 }
	ifspawnedby PLASMA_EX { sizeat 16 16 spritepal 13 cstator 514 }
	ifspawnedby PE_SENTRY { sizeat 16 16 cstator 514 set YVELSAVED 1 }
	ifspawnedby SENTRY { sizeat 16 16 cstator 514 set YVELSAVED 1 }
	action BIGSMOKE3_FRAMES
	}

	ifaction BIGSMOKE3_FRAMES
	{
	ife YVELSAVED 1 move BIGSMOKE_UP getv else
	ife YVELSAVED 2 move BIGSMOKE_DOWN getv else
	ife YVELSAVED 3 move BIGSMOKE_FORWARD geth else
	ife YVELSAVED 4 move BIGSMOKE_FORWARDUP geth getv else
	ife YVELSAVED 5 move BIGSMOKE_FORWARDDOWN geth getv
	ifactioncount 0
		{
		geta[].xrepeat temp5
		add temp5 2
		seta[].xrepeat temp5
		geta[].yrepeat temp5
		add temp5 2
		seta[].yrepeat temp5
		}
	ifactioncount 2 sizeto 255 255
	  ifactioncount 5 killit
	}
enda

// *************************************************
// DRIP
// *************************************************


useractor notenemy 5480

ifaction 0
	{
	ife sprite[].sectnum -1 killit
	geta[].cstat CSTATSAVED
	ife CSTATSAVED 0 set CSTATSAVED 514
	cstat 32768
	action ZERO
	resetcount
	}

ifaction ZERO
	{
	ifpdistl 16384
	 ifcount 60
	  ifrnd 48
		{
		espawn WATERDRIP
		seta[RETURN].pal sprite[].pal
		seta[RETURN].cstat CSTATSAVED
		seta[RETURN].shade sprite[].shade
		resetcount
		}
	}

enda

action DRIP_SPLASH 1 9 1 1 0

useractor notenemy 2379
fall
ifaction 0
	{
	ifpdistl 8192 sound DRIP_SOUND
	sizeat 32 32
	cstat 514
	action DRIP_SPLASH
	}

ifaction DRIP_SPLASH
	{
	ifactioncount 9 killit
	}

enda


// *************************************************
// PARTICLE SPAWNER EFFECTOR
// *************************************************

spritenoshade 3666
spritenopal 3666
spritenoshade 3670
spritenopal 3670

useractor notenemy 3670
sleeptime 0
ifaction 0 action MINUSONE
ifpdistlvar opt_particle_distance cstat 32768
else
seta[].cstat CSTATSAVED
enda

useractor notenemy 3666
sleeptime 0
ifaction 0 action MINUSONE
ifpdistlvar opt_particle_distance cstat 32768
else
seta[].cstat CSTATSAVED
enda

spriteflags PARTICLE_SPAWNER 16

useractor notenemy PARTICLE_SPAWNER
ifaction 0
	{
	cstat 32768
	action ZERO
	}
sleeptime 0
ife LOTAGSAVED 0
 ifpdistlvar opt_particle_distance
  ifcansee
   ifand player[].player_par 1
	{
	move 0
	ife HITAGSAVED 0 set HITAGSAVED 256

	set temp3 sprite[].x
	randvarvar temp4 HITAGSAVED
	add temp3 temp4
	randvar temp4 2048

	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

	espawn GUNSMOKE

	seta[RETURN].x temp5
	seta[RETURN].y temp6

	seta[RETURN].pal sprite[].pal
	}
else
ife LOTAGSAVED 1
 ifpdistlvar opt_particle_distance
	{
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
		else
		{
		ifrnd 2 { sound LAVABALL_THROW shoot LAVABALL }
		}
	}
else
ife LOTAGSAVED 2
	{
	move 0
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
		{
		ife HITAGSAVED 0 set HITAGSAVED 256
		ifpdistlvar opt_particle_distance
			{
			set temp3 sprite[].x
			randvarvar temp4 HITAGSAVED
			add temp3 temp4

			// randomise angle with only a 512 variation from its own
			ifn sprite[].pal 12
			 ifn sprite[].pal 13
				 {
				randvar temp4 512
				ifrnd 128 inv temp4 // either randomly add or subtract from the angle
				add temp4 sprite[].ang
				}
				else
				randvar temp4 2048

			rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

			ifspritepal 15
			{
			add INTERNALCOUNT 1
			ifge INTERNALCOUNT 15
				{
				espawn WFRAMES
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				seta[RETURN].shade sprite[].shade
				seta[RETURN].pal sprite[].pal
				set INTERNALCOUNT 0
				}
			}
			else
				{
				ifspritepal 2 espawn LAVASPLASH else espawn WFRAMES
				seta[RETURN].x temp5
				seta[RETURN].y temp6
				seta[RETURN].shade sprite[].shade
				seta[RETURN].pal sprite[].pal
				}
			}
		}
	}
else
ife LOTAGSAVED 3
	{
	move 0
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	 ifpdistlvar opt_particle_distance
		{
		ife HITAGSAVED 0
			{
			ifrnd 4
				{ sound SHORT_CIRCUIT shoot SPARK shoot SPARK shoot SPARK spawn 8433 }
			}
		else
		ife HITAGSAVED 1
			{
			ifrnd 16
				{ eshoot SPARK setthisprojectile[RETURN].pal sprite[].pal espawn 8433 seta[RETURN].pal sprite[].pal }
			}
		}
	}
else
ife LOTAGSAVED 4
	{
	move 0
	checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			ifrnd 16 { sound SHORT_CIRCUIT espawn 8433 shoot SPARK }
			}
	}
else
ife LOTAGSAVED 5
	{
	ife HITAGSAVED 0 set HITAGSAVED 256
	ife EXTRASAVED -1 set EXTRASAVED 8
	ife OWNERSAVED 1 fall

	ifspritepal 8
	 ifpdistl 16384
		{
		ifactorsound THISACTOR STEAM_HISSING nullop
		else sound STEAM_HISSING
		}

	ifn ZVELSAVED 0
		{
		checkactivatormotion ZVELSAVED
		ife RETURN 1 killit
		}

	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	ifcount 6
		{

		set temp3 sprite[].x
		randvarvar temp4 HITAGSAVED
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		espawn 18365
		// espawn BIG_SMOKE2
		seta[RETURN].xrepeat EXTRASAVED
		seta[RETURN].yrepeat EXTRASAVED
		ifn YVELSAVED 0 setactorvar[RETURN].YVELSAVED YVELSAVED

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		seta[RETURN].pal sprite[].pal

		resetcount
		}
	}
else
ife LOTAGSAVED 6
	{
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
		{
		move 0
		ife HITAGSAVED 0 set HITAGSAVED 256

		ifpdistl 16384
			 {
			set temp3 sprite[].x
			randvarvar temp4 HITAGSAVED
			add temp3 temp4
			randvar temp4 2048
			rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
			espawn 18574
			seta[RETURN].x temp5
			seta[RETURN].y temp6
			seta[RETURN].pal sprite[].pal
			}
		}
	}
else
ife LOTAGSAVED 7
	{
	move 0
	ife HITAGSAVED 0 set HITAGSAVED 256

	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	 ifpdistl 16384
		{
		set temp3 sprite[].x
		randvarvar temp4 HITAGSAVED
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		espawn WATERBUBBLE

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		seta[RETURN].pal sprite[].pal
		}
	}
else
ife LOTAGSAVED 8 // glistening
	{
	move 0
	ife HITAGSAVED 0 set HITAGSAVED 256

	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	 ifrnd 16
		{
		set temp3 sprite[].x
		randvarvar temp4 HITAGSAVED
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		espawn GLINT

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		geta[].sectnum upd_sect
		updatesectorz temp5 temp6 sprite[].z upd_sect
		ifn upd_sect -1 changespritesect THISACTOR upd_sect

		seta[RETURN].pal sprite[].pal
		}
	}
else
ife LOTAGSAVED 9 // spawn particles that fly inwards towards a sprite 23747
	{
	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1 set HITAGSAVED 0
		}
	else
	ifrnd 64
		{
		espawn 23746
		seta[RETURN].pal sprite[].pal
		set temp3 sprite[].x
		randvarvar temp4 1024
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		geta[RETURN].z temp2
		randvar temp 40000
		sub temp 20000
		add temp2 temp
		seta[RETURN].z temp2
		resetcount
		ifn XVELSAVED 0
			{
			checkactivatormotion XVELSAVED
			ife RETURN 1 killit
			}
		}
	}
else
ife LOTAGSAVED 10 // ghost ship cannon charge
	{
	ifg BIG_GUN_CHARGE 800
	 ifl BIG_GUN_CHARGE 1200
		{
		espawn 23746
		seta[RETURN].pal 21
		set temp3 sprite[].x
		randvarvar temp4 1024
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		geta[RETURN].z temp2
		randvar temp 40000
		sub temp 20000
		add temp2 temp
		seta[RETURN].z temp2
		resetcount
		}
	}
else
ife LOTAGSAVED 11
	{
	ife HITAGSAVED 0 set HITAGSAVED 256
	ife EXTRASAVED -1 set EXTRASAVED 8
	ife ZVELSAVED 1 fall

	ifspritepal 8
	 ifpdistl 16384
		{
		ifactorsound THISACTOR STEAM_HISSING nullop
		else sound STEAM_HISSING
		}

	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	ifcount 6
		{

		set temp3 sprite[].x
		randvarvar temp4 HITAGSAVED
		add temp3 temp4
		randvar temp4 2048

		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

		espawn BIG_SMOKE2
		seta[RETURN].xrepeat EXTRASAVED
		seta[RETURN].yrepeat EXTRASAVED
		ifn YVELSAVED 0 setactorvar[RETURN].YVELSAVED YVELSAVED

		seta[RETURN].x temp5
		seta[RETURN].y temp6

		seta[RETURN].pal sprite[].pal

		resetcount
		}
	}
else
ife LOTAGSAVED 12
	{
	ifpdistl 8192 ifrnd 2 money 1
	}
else
ife LOTAGSAVED 13
	{
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 set XVELSAVED 0
		}
	else
	ifcount 6
		{
		ifpdistl 32767
			{
			espawn 30070
			seta[RETURN].pal sprite[].pal
			}
		resetcount
		}
	}
else
ife LOTAGSAVED 14 // pressurise effect
	{
	move 0
	checkactivatormotion XVELSAVED
		ife RETURN 1
		{
		ife HITAGSAVED 0 set HITAGSAVED 256
		ifpdistlvar opt_particle_distance
			{
			ifactorsound THISACTOR PRESSURIZE nullop else sound PRESSURIZE
			set temp3 sprite[].x
			randvarvar temp4 HITAGSAVED
			add temp3 temp4
			randvar temp4 2048
			rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
			espawn WFRAMES
			seta[RETURN].x temp5
			seta[RETURN].y temp6
			seta[RETURN].shade sprite[].shade
			seta[RETURN].pal sprite[].pal
			}
		}
	}
enda

move PARTICLE_FLY 60 0

// spritenoshade 23746

useractor notenemy 23746 // flying particle

	ifaction 0
	{
	set INTERNALCOUNT 1
	sizeat 4 4
	cstator 130
	spriteflags 10324
	seta[].shade 30
	seta[].blend 255
	action ZERO
	}

	ifaction ZERO
	{
	add INTERNALCOUNT 1
	ifg sprite[].shade -127
		{
		geta[].shade temp2
		sub temp2 1
		seta[].shade temp2
		}
	move PARTICLE_FLY geth getv
	ifspawnedby MAEPHISTO set target myowner else
	findnearactor3d 23747 16384 target
	ife target -1 killit

    ife sprite[myowner].picnum MAEPHISTO
        ifl sprite[myowner].extra 1
            killit

    geta[target].x mx
	geta[target].y my
	geta[].x x
	geta[].y y
	sub mx x
	sub my y
	getangle angvar mx my
	seta[].ang angvar
	ldist temp THISACTOR target
	geta[].z temp2
	sub temp2 8192 // Offset the Z. Assumes the center of the actor is the same as the player
	geta[target].z temp3

	ife target myowner
		{
		sub temp3 20384 // Offset the Z. assumes the center of the actor is the same as the player
		sub temp3 temp2
		}
		else
		{
		sub temp3 8592 // Offset the Z. assumes the center of the actor is the same as the player
		sub temp3 temp2
		}
	shiftvarl temp3 8
	ifn temp 0 divvarvar temp3 temp
	seta[].zvel temp3
	dist temp THISACTOR target
	ifspawnedby MAEPHISTO ifl temp 1024 { ifspritepal 21 shoot SPARK else shoot SPARK2 killit } else
	ifl temp 546 { ifspritepal 21 shoot SPARK else shoot SPARK2 killit }
	ifnotmoving ifcount 30 killit
	ifg INTERNALCOUNT 150 killit
	}

enda

useractor notenemy 23747
cstat 32768
enda

// *************************************************
// NUKE EXPLOSION
// *************************************************

move MOVEUP 0 -30

spritenoshade 7678
spritenopal 7678

useractor notenemy 7678

ifaction 0
{
seta[].shade -127
seta[].blend 255
cstat 34
ifcount 2
	{
	geta[].xrepeat temp
	ifl temp 160
		{
		add temp 1
		seta[].xrepeat temp
		seta[].yrepeat temp
		}
	resetcount
	}
}

enda

defstate spawn_nuke_smokering
spawn BIG_SMOKE2
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 128 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 256 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 384 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 512 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 640 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 768 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 896 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1024 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1152 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1280 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1408 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1536 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1664 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1792 seta[RETURN].ang temp
espawn BIG_SMOKE2 geta[RETURN].ang temp add temp 1920 seta[RETURN].ang temp
ends

spritenoshade 7677
spritenopal 7677

useractor notenemy 7677

ifaction 0
{
seta[].shade -127
seta[].blend 255
cstat 2
state fade_out_white
sizeat 25 25
quake 300
globalsound NUKE_DISTANCE
state spawn_nuke_smokering
action ZERO
}

ifaction ZERO
{
ifcount 6
	{
	geta[].xrepeat temp
	ifl temp 160
		{
		ife temp 50 state spawn_nuke_smokering
		ife temp 80 state spawn_nuke_smokering
		ife temp 100 state spawn_nuke_smokering
		add temp 2
		seta[].xrepeat temp
		}
	geta[].yrepeat temp
	ifl temp 110
		{
		add temp 1
		seta[].yrepeat temp
		}
	resetcount
	}
setp[].visibility 0
}

enda

// *************************************************
// THAT LENS FLARE EVERYBODY LOVES SO MUCH
// *************************************************

defstate oldflare
ifaction 0
	{
	seta[].blend 255
	cstator 1024
	ife HITAGSAVED 1 spriteflags 68
	action ZERO
	}
ends


useractor notenemy 1632
state oldflare
enda

useractor notenemy 1633
state oldflare
enda

useractor notenemy 1634
state oldflare
enda


useractor notenemy 62 cstator 1024 enda

useractor notenemy 3704 cstator 1024 enda
useractor notenemy 3882 cstator 1024 enda
useractor notenemy 3883 cstator 1024 enda
useractor notenemy 3885 cstator 1024 enda
useractor notenemy 3886 cstator 1024 enda


useractor notenemy 12351 cstator 1024 enda
useractor notenemy 12352 cstator 1024 enda
useractor notenemy 9575 cstator 1024 enda

// *************************************************
// GHOSTS ORB
// *************************************************

move ORB_GLIDE 140

spritenoshade 6249

useractor notenemy 6249
state lowperf

ifaction 0
{
cstat 642
seta[].shade -127
sizeat 2 2
spawn FRAMEEFFECT1
move ORB_GLIDE geth
ifnotmoving killit
}

enda

// *************************************************
// RAIN/SNOW - RAIN CODE BY DEEPERTHOUGHT FROM DUKE PLUS
// *************************************************

defstate killsnow
    ife sector[].floorbunch -1
    {
        iffloordistl 2 killit
    }
    else ifand sector[].floorstat 2560
    {
        iffloordistl 2 killit
    }
    ifnotmoving killit
ends

define RAINSPRITE 2287

action RAINFRAME -1627 1 1 1 1
action RAINFRAME2 -2 1 1 1 1

action GSNOWFRAME1 312 1 1 1 1
action GSNOWFRAME2 313 1 1 1 1
action GSNOWFRAME3 314 1 1 1 1
action GSNOWFRAME4 315 1 1 1 1

action FIRE_SPARKLE -44 1 1 1 1

action RAINSPLASH 93 9 1 1 3
move SNOW_FALL 20 96

move FIRESPARKLEFLY 96 10

useractor notenemy RAINSPRITE 0
	ife PERFMODE 0 killit
	ifge raining 0 fall
	ifinwater killit

	seta[].htflags 2240

	// kill the rain sprite if the sector ceiling is not paralaxed (that's what ifoutside does)
	// EXCEPT if ceilingbunch is not -1, because then it's a TROR layer
	ifoutside nullop
	else
	{
		geta[].sectnum temp

		ife sector[temp].ceilingbunch -1
			killit
		else ifand sector[temp].ceilingstat 512
			killit
	}

	add INTERNALCOUNT 1

	ife NUKE_ACTIVE 1 killit

ifaction 0
{
set INTERNALCOUNT 0
seta[].pal rainpal
ifn ceiltext -1
	{
	ifvarvarn sector[].ceilingpicnum ceiltext killit
	}
	ifge raining 0
	{
		ifspritepal 3
		{
			ife sector[].ceilingpal 128 seta[].pal 128
			action RAINFRAME2
			sizeat 64 128
			cstat 514
			ifrnd 128 cstator 4
			ifrnd 128 cstator 8
		}
		else
		{
			ifand raincstat 512 cstator 514
			else ifand raincstat 2 cstator 2
			action RAINFRAME
			sizeat 10 15
		}
	}
	else
	{
		geta[].z z
		ife TIMEZONE 3
			{
			cstator 2
			seta[].shade -127
			seta[].blend 255
			action FIRE_SPARKLE
			}
		else
			{
			ifrnd 64 action GSNOWFRAME1
			else ifrnd 128 action GSNOWFRAME2
			else ifrnd 192 action GSNOWFRAME3
			else action GSNOWFRAME4
			}
		ifrnd 128 cstator 4
		ifrnd 128 cstator 8
		ifaction FIRE_SPARKLE move FIRESPARKLEFLY geth getv randomangle else
		move SNOW_FALL geth randomangle
		randvar x 3
		randvar y 3
		ifaction FIRE_SPARKLE nullop
		else
			{
			add x 4
			add y 4
			}
		seta[].xrepeat x
		seta[].yrepeat y
		add x y // between 6 and 12
		set mtype 1040
		mulvar x 32
		sub mtype x
	}

}
else
ifaction RAINFRAME
{
state getfloordist
    ifl z 5	{  ifcansee spawn 13762 action RAINSPLASH } else
	ife sprite[].zvel 0 { action RAINSPLASH }
}
else
ifaction RAINFRAME2
{
state getfloordist
ifl z 5
	{
	ifcansee spawn 13762 action RAINSPLASH
	}
ife sprite[].zvel 0 killit
}
else
ifaction RAINSPLASH
{
ifand sprite[].htflags 4 nullop else
	{
	geta[].htflags temp
	ifand temp 4 nullop else xor temp 4
	seta[].htflags temp
	seta[].shade sector[].floorshade
	}
	ifactioncount 9 killit
}
else
{
	geta[].z z
	add z mtype //768
	geta[].sectnum mysector
	updatesectorz sprite[].x sprite[].y z mysector
	ife mysector -1 killit else
	{
		seta[].z z
		changespritesect THISACTOR mysector
	}
	ifpdistg 16384 killit

}

ifge INTERNALCOUNT 300 killit

ifaction GSNOWFRAME1 state killsnow
else ifaction GSNOWFRAME2 state killsnow
else ifaction GSNOWFRAME3 state killsnow
else ifaction GSNOWFRAME4 state killsnow

enda

defstate raincode
	// do not spawn rain if it's far enough away from the player (laterally)
	getp[].posx my_x
	getp[].posy my_y

	set mx x
	set my y

	state distcoordinates

	ifle distance 10000
	{
		getceilzofslope mysector x y z // z is the return var
		ifl raining 0 // don't put snow too high up
		{
			getp[].posz rain_temp
			set temp3 rain_temp // save posz in temp3
			sub rain_temp z // positive value is distance above player to ceiling
			ifg rain_temp snowceiling
			{
				sub temp3 snowceiling
				set z temp3
			}
		}

		geta[].sectnum mysector
		updatesectorz x y z mysector
		ifn mysector -1
		{
			// This will fix snow in AMCB2 but break it elsewhere probably
			ifn sector[mysector].ceilingbunch -1
			 ifn sector[mysector].ceilingpicnum 179
				break

			ifn HITAGSAVED 0
			ifn ceiltext -1
			{
				ifvarvarn sector[mysector].ceilingpicnum ceiltext nullop else
				{
					espawn RAINSPRITE
					seta[RETURN].x x
					seta[RETURN].y y
					seta[RETURN].z z
					seta[RETURN].sectnum mysector
					changespritesect RETURN mysector
				}
			}
			else ife HITAGSAVED 0
			{
				espawn RAINSPRITE
				seta[RETURN].x x
				seta[RETURN].y y
				seta[RETURN].z z
				seta[RETURN].sectnum mysector
				changespritesect RETURN mysector
			}
		}
	}
ends

useractor notenemy RAINSNOWEFFECT

ifaction 0
	{
	gets[].ceilingpicnum ceiltext
	action ZERO
	}

ifn raining 0
{
	set INTERNALCOUNT 0
	set INTERNALCOUNT_2 raining
	ifmultiplayer nullop else
	ifg INTERNALCOUNT_2 framerate set INTERNALCOUNT_2 framerate
	ifl INTERNALCOUNT_2 0 mulvar INTERNALCOUNT_2 -1
	ifg INTERNALCOUNT_2 64 set INTERNALCOUNT_2 64
		ifl enemies_cleared 150
		div INTERNALCOUNT_2 4
	whilevarvarn INTERNALCOUNT INTERNALCOUNT_2
	{
		add INTERNALCOUNT 1
		set xydist player[].posx
		randvarvar rain_temp rainradius // the random component of the distance from player
		add rain_temp 512
		add xydist rain_temp
		randvar angvar 2047

		rotatepoint player[].posx player[].posy xydist player[].posy angvar x y
		// x and y are the new x and y coords
		getp[].cursectnum mysector // prefill
		updatesectorz x y player[].posz mysector
		ifn mysector -1
		{
			ife sector[mysector].ceilingpicnum 179 state raincode
			else ifand sector[mysector].ceilingstat 1 state raincode

			// if TROR sector, find top of bunch
			else ifn sector[mysector].ceilingbunch -1
			{
				state gettopsectorofbunch
				set mysector SECT_NUM

				ife sector[mysector].ceilingpicnum 179 state raincode
				else ifand sector[mysector].ceilingstat 1 state raincode
			}
		}
	}
}
enda

useractor notenemy RAINSNOWSOUND
cstat 32768

ifg raining 0
 ifpdistl 16384
	{
	ifspritepal 1 soundonce RAIN_HIT_DIRT
	else ifspritepal 2 soundonce RAIN_HIT_METAL
	else ifspritepal 3 soundonce RAIN_HIT_WINDOW
	else { ifoutside soundonce RAIN else soundonce RAININ }
	}

enda

// ZVELSAVED = trigger on activation instead of player proximity
useractor notenemy RAINSNOWSTARTER
cstat 32768

set temp 0
ifn ZVELSAVED 0
{
	checkactivatormotion ZVELSAVED
	set MASTERSWITCH_SUBSCRIBE ZVELSAVED

	ife RETURN 1
		set temp 1
	else ife MASTERSWITCH_RETURN 1
	{
		set temp 1
		set MASTERSWITCH_RETURN 0
	}
}
else ifpdistl 2048
	set temp 1

ife temp 1
	{
	ifspritepal 0
		{
		set rainradius 12000
		ife XVELSAVED 0 set raining 16 else set raining XVELSAVED
		geta[].cstat raincstat
		geta[].pal rainpal
		ifn YVELSAVED 0
		{
		set temp 0
		whilevarvarn temp NUMSECTORS
			{
	  		ifand sector[temp].ceilingstat 1
	  			{
				switch sector[temp].floorpicnum
	    			case 755 sets[temp].floorpicnum 4846 break
	    			case 772 sets[temp].floorpicnum 5068 break
	    			case 780 sets[temp].floorpicnum 4849 break
	    			case 782 sets[temp].floorpicnum 5065 break
	    			case 790 sets[temp].floorpicnum 5823 break
					case 805 sets[temp].floorpicnum 9142 break
	    			case 814 sets[temp].floorpicnum 4858 break
	    			case 815 sets[temp].floorpicnum 4861 break
					case 823 sets[temp].floorpicnum 4840 break
	    			case 4151 sets[temp].floorpicnum 4855 break
					case 4276 sets[temp].floorpicnum 4843 break
	    			case 442 sets[temp].floorpicnum 4852 break
					case 9578 sets[temp].floorpicnum 10990 break
				endswitch
					ifand sector[temp].ceilingstat 1
						{
						ife sector[temp].ceilingpicnum LOTAGSAVED sets[temp].ceilingpicnum HITAGSAVED
						sets[temp].visibility 0
						}
	  			}
	  		add temp 1
			}
		}
	}
	else
	ifspritepal 1 // Stop rain
		{
		set rainstart 0
		set raining 0
		set rainradius 0
		set RAINREPLACE -1
		stopsound RAIN
		stopsound RAININ

			ifn YVELSAVED 0
			{
			set temp 0
			set temp3 0
			whilevarvarn temp NUMSECTORS
				{
				ifand sector[temp].ceilingstat 1
					{
					switch sector[temp].floorpicnum
						case 4846 sets[temp].floorpicnum 755 break
						case 5068 sets[temp].floorpicnum 772 break
						case 4849 sets[temp].floorpicnum 780 break
						case 5065 sets[temp].floorpicnum 782 break
						case 5823 sets[temp].floorpicnum 790 break
						case 9142 sets[temp].floorpicnum 805 break
						case 4858 sets[temp].floorpicnum 814 break
						case 4861 sets[temp].floorpicnum 815 break
						case 4840 sets[temp].floorpicnum 823 break
						case 4855 sets[temp].floorpicnum 4151 break
						case 4843 sets[temp].floorpicnum 4276 break
						case 4852 sets[temp].floorpicnum 442 break
						case 10990 sets[temp].floorpicnum 9578 break
					endswitch
					}

				add temp 1
				}
			}
		}
	killit
	}


enda

useractor notenemy 17057
ifaction 0
	{
	cstat 32768
	ifg raining 0 { action MINUSONE seta[].cstat CSTATSAVED }
	}
enda

// *************************************************
// BROWN STAIN - COFFEE, BEER....Maybe something else less wholesome
// *************************************************

useractor notenemy BROWNSTAIN
fall

ifaction 0
{
ifspawnedby POT1 { cstat 34 sizeto 24 24 spritepal 8 } else
ifspawnedby POT2 { cstat 34 sizeto 24 24 spritepal 2 } else
ifspawnedby COFFEEMUG { cstat 34 sizeto 12 12 } else
ifspawnedby COFFEEMACHINE { cstat 34 sizeto 32 32 } else
ifspawnedby COFFEEMACHINE2 { cstat 34 sizeto 24 24 } else
ifspawnedby COFFEEMACHINE3 { cstat 34 sizeto 12 12 } else
ifspawnedby FECES { cstat 32 sizeto 24 24 } else
ifspawnedby MOP { spritepal 27 cstat 34 sizeto 32 32 } else
{ cstat 34 sizeto 32 32 }
seta[].shade sector[].floorshade

ifpdistl 1024
 ifp palive
	{
	ifp pshrunk break
  	setp[].footprintcount 4
	ifspritepal 0 set temp 19 else set temp sprite[].pal
	setp[].footprintpal temp
	}

}

enda

// *************************************************
// DEBRIS SPAWNER
// *************************************************


defstate debris_stuff

	ifspritepal 2
		{
		sound BUST_CONCRETE
		spawn BIG_SMOKE2
		spawn BIG_SMOKE2
		}
	ifand sprite[].pal 1
		{
		spawn BIG_SMOKE2
		spawn BIG_SMOKE2
		}
	ifand sprite[].pal 4 set gun_recoil 40

	ife LOTAGSAVED 1 // WOOD
		{
		sound WOODBREAK
		shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
		shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
		killit
		}
	else
	ife LOTAGSAVED 2 // STONE
		{
		shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
		shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
		shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
		shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
		killit
		}
	else
	ife LOTAGSAVED 3 // ROCK DEBRIS 3
		{
		shoot ROCKDEB_3 shoot ROCKDEB_3 shoot ROCKDEB_3
		shoot ROCKDEB_3 shoot ROCKDEB_3 shoot ROCKDEB_3
		shoot ROCKDEB_3 shoot ROCKDEB_3 shoot ROCKDEB_3
		shoot ROCKDEB_3 shoot ROCKDEB_3 shoot ROCKDEB_3
		killit
		}
	else
	ife LOTAGSAVED 4 // ROCK DEBRIS 4
		{
		shoot ROCKDEB_4 shoot ROCKDEB_4 shoot ROCKDEB_4
		shoot ROCKDEB_4 shoot ROCKDEB_4 shoot ROCKDEB_4
		shoot ROCKDEB_4 shoot ROCKDEB_4 shoot ROCKDEB_4
		shoot ROCKDEB_4 shoot ROCKDEB_4 shoot ROCKDEB_4
		killit
		}
	else
	ife LOTAGSAVED 5 // scrap debris
		{
		sound VENT_BUST
		sound BUST_METAL
		debris SCRAP1 40
		debris SCRAP2 32
		debris SCRAP3 32
		debris SCRAP4 32
		debris SCRAP5 32
		killit
		}
	else
	ife LOTAGSAVED 6 // heavy scrap debris
		{
		sound BUST_METAL
		quake 26
		ifrnd 128 globalsound STRUCT_DAMAGE1
		else globalsound STRUCT_DAMAGE2
		debris SCRAP1 40
		debris SCRAP2 32
		debris SCRAP3 32
		debris SCRAP4 32
		debris SCRAP5 32
		killit
		}
	else
	ife LOTAGSAVED 7 // WOOD
		{
		globalsound WOOD_DEBRIS
		quake 26
		sound WOODBREAK
		shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
		shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
		shoot SWOODSCRAP shoot SWOODSCRAP shoot SWOODSCRAP
		shoot SWOODSCRAP shoot SWOODSCRAP shoot SWOODSCRAP
		killit
		}
	else
	ife LOTAGSAVED 8 // Ice
		{
		quake 26
		sound ICE_SMASH
		shoot ICE_DEBRIS shoot ICE_DEBRIS shoot ICE_DEBRIS
		shoot ICE_DEBRIS shoot ICE_DEBRIS shoot ICE_DEBRIS
		shoot ICE_DEBRIS shoot ICE_DEBRIS shoot ICE_DEBRIS
		shoot ICE_DEBRIS shoot ICE_DEBRIS shoot ICE_DEBRIS
		killit
		}
ends

useractor notenemy 3624
cstat 256

ifaction 0 action ONE
strength 1

ifge EXTRASAVED 0
	{
	checkactivatormotion EXTRASAVED
	ife RETURN 1 state debris_stuff
	}
else
ifn XVELSAVED 0
	 {
	 ife destruct_trigger XVELSAVED state debris_stuff
	 }
else
 {
ifhitweapon
 ifwasweapon RADIUSEXPLOSION
	{
	ifn XVELSAVED 0 set destruct_trigger XVELSAVED
	ifn HITAGSAVED 0 operaterespawns HITAGSAVED
	state debris_stuff
	}
ifhitweapon
 ifwasweapon RPG
	{
	ifn XVELSAVED 0 set destruct_trigger XVELSAVED
	ifn HITAGSAVED 0 operaterespawns HITAGSAVED
	state debris_stuff
	}
 }

enda

// *************************************************
// WOOD PLANK SNAP
// *************************************************

useractor notenemy 8055
ifaction 0
{
ifn HITAGSAVED 0 action ZERO
else
	{
	ifand sprite[].cstat 32 // if it's a flat sprite
		{
		ifand sprite[].cstat 16 break // 16 and 32 combined = sloped floor sprite
		ifrnd 128 cstator 4
		ifrnd 128
			{
			geta[].ang temp
			sub temp 1024
			seta[].ang temp
			}
		}
	action FORMED
	}
}

ifaction ZERO
{
sleeptime 0
ifpdistl 1024 soundonce WOOD_STRAIN
checkactivatormotion HITAGSAVED
ife RETURN 1 action FORM
}

ifaction FORM
{
sleeptime 0
ifpdistl 1024
	{
	sound WOOD_SNAP
	shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
	killit
	}
}

enda

// *************************************************
// DOOR TRANSPORT
// *************************************************

defstate move_followers
	ife actorvar[ally_temp].FOLLOW_PLAYER 0 nullop else
		{
		set temp3 ENDPOINTX
		add temp3 256
		randvar temp4 1024
		rotatepoint ENDPOINTX ENDPOINTY temp3 ENDPOINTY temp4 temp5 temp6
		updatesector temp5 temp6 temp2
		ifn temp2 -1 setsprite ally_temp temp5 temp6 ENDPOINTZ
		}
ends

var dtexit_sectnum -1 2

useractor notenemy DOORTRANSPORTEXIT
	cstat 32768
	ife used_door_trans THISACTOR
		{
		add INTERNALCOUNT 1
		ifpdistg 1536 set used_door_trans -1
		else ifg INTERNALCOUNT 90 { set used_door_trans -1 set INTERNALCOUNT 0 }
		}
enda

useractor notenemy DOORTRANSPORT
cstat 32768

ifmultiplayer set YVELSAVED 13
 ife YVELSAVED 0 set YVELSAVED 26

ifaction 0
{
	set temp 0
	whilevarn temp 16384
	{
	  ifn sprite[temp].statnum 1024
	  {
		ife sprite[temp].picnum DOORTRANSPORTEXIT
		{
		 getav[temp].HITAGSAVED temp3
		 ife LOTAGSAVED temp3
			{
			geta[temp].x ENDPOINTX
			geta[temp].y ENDPOINTY
			geta[temp].z ENDPOINTZ
			geta[temp].ang temp7
			geta[temp].sectnum dtexit_sectnum
			geta[temp].pal play_sound
			set exit_id temp
			set temp 16383
			}
		}
	  }
	  add temp 1
	}
	spawn 8425
	ifn HITAGSAVED 0 action FORM else
	action ZERO
}

ifaction FORM
ifn HITAGSAVED 0
ife used_door_trans -1
{
	sleeptime 0

state check_player_prox
	ife is_player_close 1
	   ifn sprite[].pal 8 // if NOT the ship
	{
		ifspritepal 5 set player_door -10 else
		ifspritepal 7 set player_door -10 else
		set player_door 0

		ife use_action_allowed 1
		{
			set hit_key 30
			set INTERNALCOUNT 0
			ifn HITAGSAVED 0
			{
				set door_locked 0
				set key_icon 3587
				ifspritepal 10
				{
					quote 8599
					soundonce OLDKEY_LOCKED
				}
			}

			state setIgnoreUse
		}
	}

	checkactivatormotion HITAGSAVED
	ife RETURN 1 { set HITAGSAVED 0 action ZERO }
}

ifspritepal 11
{
	state check_player_prox
	ife is_player_close 1
		ifaction ZERO
			  {
			lockplayer 60
			set INTERNALCOUNT 0
			action OK
			  }
}
else
ife used_door_trans -1
{
	state check_player_prox
	ife is_player_close 1
	 ifaction ZERO
	{
		ifspritepal 5 set player_door -10 else
		ifspritepal 7 set player_door -10 else
		ifspritepal 8 set player_ship 0 else
		ifspritepal 18 set player_unlock 0 else
		set player_door 0

		ife use_action_allowed 1
		{
			set hit_key 30
			set INTERNALCOUNT 0
			ifn HITAGSAVED 0
			{
				set door_locked 0
				set key_icon 3587
				ifspritepal 10
				{
					quote 8599
					soundonce OLDKEY_LOCKED
				}
			}
			else
			{
				action OK
				ifspritepal 5
				{
					set temp YVELSAVED
					mul temp 2
					set END_LEVEL temp
				}
				else
				ifspritepal 4
				 ifn OWNERSAVED -1
				{
					operateactivators OWNERSAVED THISACTOR
					operatemasterswitches OWNERSAVED
					operaterespawns OWNERSAVED
				}
				else
				ifspritepal 18
				 ifn OWNERSAVED -1
				{
					operateactivators OWNERSAVED THISACTOR
					operatemasterswitches OWNERSAVED
					operaterespawns OWNERSAVED
					quote 8600
					globalsound OLDKEY_UNLOCK
					screensound OLDKEY_UNLOCK
					spritepal 0
				}
				ifspritepal 6 state fade_in_white
				else ifspritepal 7 state fade_in_white
				else ifspritepal 5 nullop
				else state fade_in_black
				ife play_sound 1
					{
					screensound CART_BUMP
					set gun_recoil 15
					}
				ifn XVELSAVED 0 globalsoundvar XVELSAVED
				resetcount
			}

			state setIgnoreUse
		}
	}
}

ifaction BREAK
{
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 30 { set PLAYER_PROTECTED 0 ifspritepal 20 killit else action ZERO }
}

ifaction OK
{
	add INTERNALCOUNT 1
	set PLAYER_PROTECTED 1

    set temp8 dtexit_sectnum
    ifl temp8 0
	    updatesectorz ENDPOINTX ENDPOINTY ENDPOINTZ temp8

    ifn temp8 -1
	  ifg INTERNALCOUNT YVELSAVED
		{
		ifspritepal 4
		 ifn OWNERSAVED -1
			{
			operateactivators OWNERSAVED THISACTOR
			operatemasterswitches OWNERSAVED
			operaterespawns OWNERSAVED
			}
		ifspritepal 15 set screen_pal 11
		ifspritepal 16 set screen_pal 10
		ifn ZVELSAVED 0 globalsoundvar ZVELSAVED
		state fade_out_black
		set cus_fade_duration 5
		setp[].cursectnum temp8
		setp[].posx ENDPOINTX
		setp[].posy ENDPOINTY
		setp[].posz ENDPOINTZ
		set used_door_trans exit_id
		setp[].ang temp7
		set INTERNALCOUNT 0
		ife play_sound 1
			{
			screensound CART_BUMP
			set gun_recoil 15
			}
		ifspritepal 40 state hard_removefollower
		else
			{
			ifn ALLYSLOT1 -1 { set ally_temp ALLYSLOT1 state move_followers }
			ifn ALLYSLOT2 -1 { set ally_temp ALLYSLOT2 state move_followers }
			ifn ALLYSLOT3 -1 { set ally_temp ALLYSLOT3 state move_followers }
			ifn ALLYSLOT4 -1 { set ally_temp ALLYSLOT4 state move_followers }
			ifn ALLYSLOT5 -1 { set ally_temp ALLYSLOT5 state move_followers }
			ifn ALLYSLOT6 -1 { set ally_temp ALLYSLOT6 state move_followers }
			ifn ALLYSLOT7 -1 { set ally_temp ALLYSLOT7 state move_followers }
			ifn ALLYSLOT8 -1 { set ally_temp ALLYSLOT8 state move_followers }
			}
		action BREAK
		}
	else
	 ife temp8 -1
		{
		addlogvar ENDPOINTX
		addlogvar ENDPOINTY
		addlogvar ENDPOINTZ
		addlogvar temp8
		quote 513
		killit
		}
	else set cus_fade_duration 300
}

enda


// *************************************************
// BULLETHOLES
// *************************************************

useractor notenemy 3778 // Wood
ifaction 0
{
cstat 144
randvar temp 6
ifl temp 2 set temp 2
seta[].xrepeat temp
seta[].yrepeat temp
action ZERO
}
enda

useractor notenemy 3780 // Stone
ifaction 0
{
cstat 144
randvar temp 3
ifl temp 1 set temp 1
seta[].xrepeat temp
seta[].yrepeat temp
action ZERO
}
enda

// *************************************************
// BLOODPOOLS
// *************************************************

defstate CUSTOMBLOODPOOLS

ifspritepal 3 nullop else
{
fall
state lowperf

ifaction 0
	{
	cstat 32
	action ZERO
	ifand sector[].floorstat 2 state SlapSpriteToFloor
	ife sector[].lotag 1 killit
	}

ifaction ZERO
	{
	sizeto 32 32
	ifg sector[].floorpal 25 seta[].pal sector[].floorpal
	ifpdistl 1024
	 ifp palive
		{
		ifp pshrunk break
		setp[].footprintcount 4
		ifactor 4679 set temp 8 else set temp 2
		setp[].footprintpal temp
		}
	}
}

ends

useractor notenemy 7990 state CUSTOMBLOODPOOLS enda
useractor notenemy 7991 state CUSTOMBLOODPOOLS enda
useractor notenemy 16368 state CUSTOMBLOODPOOLS enda

spritenoshade 4679
useractor notenemy 4679 seta[].shade -127 state CUSTOMBLOODPOOLS enda

// *************************************************
// WELCOME MAT
// *************************************************

useractor notenemy 7997

ifaction 0
{
ifand sprite[].cstat 32 action OK else action ZERO
}

ifaction OK
{
ifpdistl 812
	{
	ifg player[].footprintcount 0
       ife player[].footprintpal 2
		{
		action ONE
		setp[].footprintcount 0
		setp[].footprintpal 0
		}
	}
}



enda

// *************************************************
// OLD KEYS
// *************************************************



// 1 = Elder Key
// 2 = Spider Key
// 4 = Eagle Key
// 8 = Moon key
// 16 = Skull key
// 32 = Eye Key

useractor notenemy OLDLOCK
ifaction 0 action LOCKED

ifvarvarand HITAGSAVED OLDKEYS { ifrnd 4 spawn GLINT }

ifpdistl 1024 ifhitspace ifaction LOCKED
{
	ife HITAGSAVED 0 // Generic Key
		{
		ifg GENERIC_KEYS 0 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon GENERICKEY }
		}
else
	ifand HITAGSAVED 1 // Elder Key
		{
		ifand OLDKEYS 1 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon FLAMEKEY }
		}
else
	ifand HITAGSAVED 2 // Spider Key
		{
		ifand OLDKEYS 2 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SPIDERKEY }
		}
else
	ifand HITAGSAVED 4 // Eagle Key
		{
		ifand OLDKEYS 4 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon DAGGERKEY }
		}
else
	ifand HITAGSAVED 8 // Moon Key
		{
		ifand OLDKEYS 8 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon MOONKEY }
		}
else
	ifand HITAGSAVED 16 // Skull key
		{
		ifand OLDKEYS 16 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SKULLKEY }
		}
else
	ifand HITAGSAVED 32 // Eye key
		{
		ifand OLDKEYS 32 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon EYEKEY }
		}
else
	ifand HITAGSAVED 64 // Normal key 1
		{
		ifand NORMAL_KEYS 1 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon DIAMOND_KEY }
		}
else
	ifand HITAGSAVED 128 // Normal key 2
		{
		ifand NORMAL_KEYS 2 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon CLUB_KEY }
		}
else
	ifand HITAGSAVED 256 // Normal key 3
		{
		ifand NORMAL_KEYS 4 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SPADE_KEY }
		}
else
	ifand HITAGSAVED 512 // Normal key 4
		{
		ifand NORMAL_KEYS 8 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon HEART_KEY }
		}
else
	ifand HITAGSAVED 1024 // Normal key 5
		{
		ifand NORMAL_KEYS 16 action UNLOCKING else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon NORMAL_KEY5 }
		}
}

ifaction UNLOCKING
{
globalsound OLDKEY_UNLOCK
operateactivators LOTAGSAVED THISACTOR
operaterespawns LOTAGSAVED
operatemasterswitches LOTAGSAVED
action UNLOCKED
ife HITAGSAVED 0 sub GENERIC_KEYS 1 else
ifand HITAGSAVED 1 xorvar OLDKEYS 1 else
ifand HITAGSAVED 2 xorvar OLDKEYS 2 else
ifand HITAGSAVED 4 xorvar OLDKEYS 4 else
ifand HITAGSAVED 8 xorvar OLDKEYS 8 else
ifand HITAGSAVED 16 xorvar OLDKEYS 16 else
ifand HITAGSAVED 32 xorvar OLDKEYS 32 else
ifand HITAGSAVED 64 xorvar NORMAL_KEYS 1 else
ifand HITAGSAVED 128 xorvar NORMAL_KEYS 2 else
ifand HITAGSAVED 256 xorvar NORMAL_KEYS 4 else
ifand HITAGSAVED 512 xorvar NORMAL_KEYS 8 else
ifand HITAGSAVED 1024 xorvar NORMAL_KEYS 16
}
enda


// *************************************************
// RANDOM TREE/ROCK CODE
// *************************************************

useractor notenemy 5676 fall enda

useractor notenemy TREESPAWNER
cstat 32768

ifrnd 32
{
espawn 7944
randvar temp 30
add temp 60
seta[RETURN].xrepeat temp
seta[RETURN].yrepeat temp
ifn LOTAGSAVED 0 setactorvar[RETURN].LOTAGSAVED LOTAGSAVED

	set temp3 sprite[].x
	randvar temp8 1536
	add temp3 temp8

	randvar temp4 2048

	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

	seta[RETURN].x temp5
	seta[RETURN].y temp6
	seta[RETURN].ang sprite[].ang
	resetcount
}

enda

move TREESPEEDUP 256

action TREE_DEAD

defstate TREE_FIRE_STUFF
ifg fire_damage 0
	{
	ifrnd 190
	 ifg framerate 15
		{
		espawn 14040
		geta[].x temp
		randvar temp2 128
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].x temp

		geta[].y temp
		randvar temp2 128
		ifrnd 128 add temp temp2 else sub temp temp2
		seta[RETURN].y temp

		geta[].z temp
		sub temp 8096
		randvar temp2 4096
		ifrnd 128 add temp temp2
		else sub temp temp2
		seta[RETURN].z temp
		}
	addstrength -1
	ifdead { ifaction TREE_DEAD nullop else { sound ENGULF_FIRE action TREE_DEAD } }
	sub fire_damage 1
	ifpdistl 16384 soundonce FLESH_BURNING
	}
ends

move TREE_SLOW 48


defstate RANDOMTREE

ife TIMEZONE 5
	{
	sleeptime 0
	ife global_trigger 6363 killit
	}

ifspawnedby 8939
	{
	fall
	ife LOTAGSAVED 3 move TREE_SLOW geth
	else move TREESPEEDUP geth
	ifnotmoving killit
	}

ifaction 0
	{
	strength 300
	 ifspritepal 4 break
	 ifand sector[].ceilingstat 8192 nullop else { espawn TARGETFX setav[RETURN].is_flammable 1 }
	 ifspritepal 3 nullop
	 else
		 {
		  ifrnd 128
			cstat 5
		  else
			cstat 1
		randvar g_temp 32
		ifactor 23543 add g_temp 128 else
		add g_temp sprite[].xrepeat
		seta[].xrepeat g_temp
		seta[].yrepeat g_temp
		}
	action ZERO
	}

ifaction ZERO
	{
	setactor[].pal sector[].floorpal
	state GREEN_BOOK_ARTIFACT
	ifhitweapon
		{
		ifwasweapon RADIUSEXPLOSION
			{
			set fire_damage 96
			}
		else
		ifwasweapon ATOM_BOMB
			{
			set fire_damage 96
			}
		else
		ifwasweapon RPG
			{
			set fire_damage 96
			}
		}

ifpdistl 3072
ife player_in_vehicle 1
	{
	cstat 0
	quake 13
	sound TREE_FALL
	shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
	spritepal 8
	debris SCRAP3 18
	espawn 13243
	seta[RETURN].cstat 34
	seta[RETURN].xrepeat 16
	seta[RETURN].yrepeat 16
	seta[RETURN].z sector[].floorz
	seta[RETURN].shade sector[].floorshade
	randvar g_temp 2048
	seta[RETURN].ang g_temp
	killit
	}


}

ifaction TREE_DEAD
	{
	ifrnd 4 set fire_damage 0
	geta[].shade temp
	ifl temp 30
		{
		add temp 1
		seta[].shade temp
		}
	geta[].xrepeat temp
	ifg temp 4
		{
		sub temp 1
		seta[].xrepeat temp
		}
	spriteflags 5
	}

state TREE_FIRE_STUFF

ends

defstate RANDOMROCK

ifaction 0
	{
	ifspritepal 3 action ZERO
	else
		{
		ifand sprite[].cstat 8 nullop else
			{
			  ifrnd 128
				cstat 261
			  else
				cstat 257
			randvarvar g_temp 48
			add g_temp 8
			seta[].xrepeat g_temp
			seta[].yrepeat g_temp
			}
		action ZERO
		}
	}

ends

action STEPON 0

useractor notenemy TARGETFX // generic targetting sprite for various fx, customize it with specific variables (e.g. is_flammable)
	cstat 32768
	sizeat 32 32
	spriteflags 2048

	ife sprite[myowner].statnum -1 killit
	ife sprite[myowner].cstat 32768 killit
	ife sprite[myowner].statnum 1024 killit

	geta[myowner].x temp
	geta[myowner].y temp2
	geta[myowner].z temp3
	setsprite THISACTOR temp temp2 temp3

	// Workaround for a TROR Bug. Sectnum doesn't update correctly when traversing TROR Layers, in such cases do this manually.
	geta[myowner].sectnum temp
	geta[].sectnum temp2
	ifn temp temp2
		seta[].sectnum temp
enda

defstate bush_sound
ifrnd 128 sound RUSTLE_BUSH
else ifrnd 128 sound RUSTLE_BUSH2
else sound RUSTLE_BUSH3
ends

useractor notenemy 16908 ifpdistl 32767 ifrnd 16 spawn WATERBUBBLE enda
useractor notenemy 16903 ifpdistl 32767 ifrnd 16 spawn WATERBUBBLE enda

defstate RANDOMFOLIAGE

ife global_trigger 6363 killit

ifaction 0
	{
	ifspritepal 3 break
	  ifrnd 128
		cstat 4
	  else
		cstat 0
	randvarvar g_temp 32
	add g_temp 8
	strength 1
	spriteflags 2176
	ifand sector[].ceilingstat 8192 nullop else { espawn TARGETFX setav[RETURN].is_flammable 1 }
	seta[].xrepeat g_temp
	seta[].yrepeat g_temp
	action ZERO
	}

ifaction ZERO
	{
	state GREEN_BOOK_ARTIFACT
	ifpdistl 612
		{
		geta[].yrepeat temp2
		ifg temp2 32 state bush_sound
		seta[].yrepeat 10
		action STEPON
		}
	}

ifaction STEPON
ifpdistg 612
	{
	ifg temp2 32 state bush_sound
	seta[].yrepeat temp2
	action ZERO
	}

state TREE_FIRE_STUFF

ifaction TREE_DEAD
	{
	geta[].shade temp
	ifl temp 30
		{
		add temp 1
		seta[].shade temp
		}
	geta[].xrepeat temp
	ifg temp 4
		{
		sub temp 1
		seta[].xrepeat temp
		}
		else
		killit
	spriteflags 5
	}

ends

useractor notenemy 3868 state RANDOMTREE

ifn DAYNIGHT 0
	{
	gettimedate temp temp temp2 temp temp3 temp temp temp
	ife temp3 8 { spawn 13916 cactor 4071 } // Autumn tree
	ife temp3 9 { spawn 13916 cactor 4071 } // Autumn tree
	ife temp3 10 { spawn 13916 cactor 4071 } // Autumn tree
	ife temp3 11 cactor 5835 // Winter tree
	ife temp3 0 cactor 5835 // Winter tree
	ife temp3 1 cactor 5835 // Winter tree
	}

enda

useractor notenemy 22740 state RANDOMTREE

ifn DAYNIGHT 0
	{
	gettimedate temp temp temp2 temp temp3 temp temp temp
	ife temp3 8 { spawn 13916 cactor 22742 } // Autumn tree
	ife temp3 9 { spawn 13916 cactor 22742 } // Autumn tree
	ife temp3 10 { spawn 13916 cactor 22742 } // Autumn tree
	ife temp3 11 cactor 22743 // Winter tree
	ife temp3 0 cactor 22743 // Winter tree
	ife temp3 1 cactor 22743 // Winter tree
	}

enda

defstate TREE_RUNOVER
ifpdistl 4500
ife player_in_vehicle 1
	{
	cstat 0
	quake 13
	sound TREE_FALL
	shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
	spritepal 8
	debris SCRAP3 18
	espawn 13243
	seta[RETURN].cstat 34
	seta[RETURN].xrepeat 16
	seta[RETURN].yrepeat 16
	seta[RETURN].z sector[].floorz
	seta[RETURN].shade sector[].floorshade
	randvar g_temp 2048
	seta[RETURN].ang g_temp
	killit
	}
ends

useractor notenemy 3863
state TREE_FIRE_STUFF
ifaction 0
	{
	ifrnd 96 cactor 23652
	else ifrnd 96 cactor 23653
	action ZERO
	}
state TREE_RUNOVER
enda

useractor notenemy 23652
state TREE_FIRE_STUFF
state TREE_RUNOVER
enda

useractor notenemy 23653
state TREE_FIRE_STUFF
state TREE_RUNOVER
enda


useractor notenemy 9223 state RANDOMTREE enda
useractor notenemy 9224 state RANDOMTREE enda
useractor notenemy 3393 state RANDOMTREE enda
useractor notenemy 3862 state RANDOMTREE enda
useractor notenemy 4718 state RANDOMTREE enda
useractor notenemy 4720 state RANDOMTREE enda
useractor notenemy 4722 state RANDOMTREE enda
useractor notenemy 5675 state RANDOMTREE enda
useractor notenemy 4071 state RANDOMTREE ifpdistl 8192 ifrnd 2 money 1 enda
useractor notenemy 5835 state RANDOMTREE enda
useractor notenemy 7943 state RANDOMTREE enda
useractor notenemy 7944 state RANDOMTREE enda
useractor notenemy 8670 state RANDOMTREE enda
useractor notenemy 8671 state RANDOMTREE enda
useractor notenemy 10059 state RANDOMTREE enda
useractor notenemy 11664 state RANDOMTREE enda
useractor notenemy 11665 state RANDOMTREE enda
useractor notenemy 11666 state RANDOMTREE enda
useractor notenemy 11667 state RANDOMTREE enda
useractor notenemy 11668 state RANDOMTREE enda
useractor notenemy 14335 state RANDOMTREE enda
useractor notenemy 17919 state RANDOMTREE enda
useractor notenemy 20353 state RANDOMTREE enda
useractor notenemy 23649 state RANDOMTREE enda
useractor notenemy 23654 state RANDOMTREE enda
useractor notenemy 23655 state RANDOMTREE enda
useractor notenemy 23656 state RANDOMTREE enda
useractor notenemy 23657 state RANDOMTREE enda
useractor notenemy 23543 state RANDOMTREE enda
useractor notenemy 23548 state RANDOMTREE enda
useractor notenemy 23549 state RANDOMTREE enda
useractor notenemy 26094 state RANDOMTREE enda

useractor notenemy 22742 state RANDOMTREE ifpdistl 8192 ifrnd 2 money 1 enda
useractor notenemy 22743 state RANDOMTREE enda

useractor notenemy 25115 state RANDOMTREE enda
useractor notenemy 25116 state RANDOMTREE enda
useractor notenemy 25117 state RANDOMTREE enda
useractor notenemy 25120 state RANDOMTREE enda

useractor notenemy 5748 state RANDOMTREE enda

useractor notenemy 5857 state RANDOMROCK enda
useractor notenemy 5828 fall state RANDOMROCK enda
useractor notenemy 5829 fall state RANDOMROCK enda

useractor notenemy 23547 state RANDOMROCK enda

useractor notenemy 5420 state RANDOMFOLIAGE enda
useractor notenemy 5421 state RANDOMFOLIAGE enda
useractor notenemy 5422 state RANDOMFOLIAGE enda
useractor notenemy 5623 state RANDOMFOLIAGE enda
useractor notenemy 8065 state RANDOMFOLIAGE enda
useractor notenemy 16372 state RANDOMFOLIAGE enda
useractor notenemy 16373 state RANDOMFOLIAGE enda
useractor notenemy 23650 state RANDOMFOLIAGE enda
useractor notenemy 23651 state RANDOMFOLIAGE enda

useractor notenemy 25202 state RANDOMFOLIAGE enda
useractor notenemy 25211 state RANDOMFOLIAGE enda
useractor notenemy 25213 state RANDOMFOLIAGE enda

useractor notenemy 5830

ife global_trigger 6363 killit

ifaction 0
	{
	strength 1
	state rf
	spriteflags 2176
	ife sector[].ceilingstat 8192 nullop else { espawn TARGETFX setav[RETURN].is_flammable 1 } // spawn targetting sprite
	action ZERO
	}

ifaction ZERO
	{
	ifpdistl 612
		{
		geta[].yrepeat temp2
		state bush_sound
		seta[].yrepeat 10
		action STEPON
		}
	}

ifaction STEPON
ifpdistg 612
	{
	state bush_sound
	seta[].yrepeat temp2
	action ZERO
	}

state TREE_FIRE_STUFF

ifaction TREE_DEAD
	{
	geta[].shade temp
	ifl temp 30
		{
		add temp 1
		seta[].shade temp
		}
	geta[].xrepeat temp
	ifg temp 4
		{
		sub temp 1
		seta[].xrepeat temp
		}
		else
		killit
	spriteflags 5
	}

enda

// *************************************************
// PADLOCK
// *************************************************

action PADLOCKUN 0 7 1 1 10
action PADLOCKUNL 6

useractor notenemy 8038

ifaction 0 action ZERO

ifp palive
 ifp pfacing
  ifpdistl 1536
   ifaction ZERO
   {
   set player_use 0
    ifhitspace
	{
	set hit_key 30
	ifand HITAGSAVED 64 // Generic Key
		{
		ifg GENERIC_KEYS 0 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon GENERICKEY }
		}
	ifand HITAGSAVED 1 // Flame Key
		{
		ifand OLDKEYS 1 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon FLAMEKEY }
		}
	ifand HITAGSAVED 2 // Spider Key
		{
		ifand OLDKEYS 2 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SPIDERKEY }
		}
	else
	ifand HITAGSAVED 4 // Dagger Key
		{
		ifand OLDKEYS 4 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon DAGGERKEY }
		}
	else
	ifand HITAGSAVED 8 // Moon Key
		{
		ifand OLDKEYS 8 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon MOONKEY }
		}
	else
	ifand HITAGSAVED 16 // Skull key
		{
		ifand OLDKEYS 16 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SKULLKEY }
		}
	else
	ifand HITAGSAVED 32 // Eye key
		{
		ifand OLDKEYS 32 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN } else
			{ set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon EYEKEY }
		}
	else
		ifand HITAGSAVED 64 // Normal key 1
			{
			ifand NORMAL_KEYS 1 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN }
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon DIAMOND_KEY }
			}
	else
		ifand HITAGSAVED 128 // Normal key 2
			{
			ifand NORMAL_KEYS 2 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN }
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon CLUB_KEY }
			}
	else
		ifand HITAGSAVED 256 // Normal key 3
			{
			ifand NORMAL_KEYS 4 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN }
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon SPADE_KEY }
			}
	else
		ifand HITAGSAVED 512 // Normal key 4
			{
			ifand NORMAL_KEYS 8 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN }
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon HEART_KEY }
			}
	else
		ifand HITAGSAVED 1024 // Normal key 5
			{
			ifand NORMAL_KEYS 16 { stopsound OLDKEY_LOCKED globalsound OLDKEY_UNLOCK quote 8 action PADLOCKUN }
			else { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon NORMAL_KEY5 }
			}
	}
   }

ifaction PADLOCKUN
	{
	set door_locked 26
	set PLAYER_VOICEOVER 0
	ifactioncount 7
		{
		action PADLOCKUNL
		ifand HITAGSAVED 0 sub GENERIC_KEYS 1 else
		ifand HITAGSAVED 1 xorvar OLDKEYS 1 else
		ifand HITAGSAVED 2 xorvar OLDKEYS 2 else
		ifand HITAGSAVED 4 xorvar OLDKEYS 4 else
		ifand HITAGSAVED 8 xorvar OLDKEYS 8 else
		ifand HITAGSAVED 16 xorvar OLDKEYS 16 else
		ifand HITAGSAVED 32 xorvar OLDKEYS 32 else
		ifand HITAGSAVED 64 xorvar NORMAL_KEYS 1 else
		ifand HITAGSAVED 128 xorvar NORMAL_KEYS 2 else
		ifand HITAGSAVED 256 xorvar NORMAL_KEYS 4 else
		ifand HITAGSAVED 512 xorvar NORMAL_KEYS 8 else
		ifand HITAGSAVED 1024 xorvar NORMAL_KEYS 16
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		}
	}


enda

// *************************************************
// CUSTOM NUKE SWITCH
// *************************************************

action NUKEIDLE 0 1 1 1 1
action NUKEOPEN 0 3 1 1 26
action NUKEHANG 2 1 1 1 1
action NUKEHITA 3 1 1 1 1

defstate CUSTOM_NUKESWITCH
ifaction 0
	{
	readarrayfromfile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
	action NUKEIDLE
	}


ifpdistl 812
 ifp palive
  ifp pfacing
   ifcansee
    {
	set player_use 0
    ifhitspace
     ifaction NUKEIDLE
      {
	  set hit_key 30
	soundonce NUKESWITCH_OPEN
      action NUKEOPEN
      }
	}

ifaction NUKEOPEN
ifactioncount 3
ifp palive
{
action NUKEHANG
setp[].fist_incs 1
}

ifaction NUKEHANG
{
ife player[].fist_incs 25 ife EXTRASAVED -1 set END_LEVEL 60
ife player[].fist_incs 30 { ifactor CUSTOMNUKESWITCH sound GLASS_HEAVYBREAK action NUKEHITA }
}

ifaction NUKEHITA
{
ifp pdead resetplayer
ifg player[].fist_incs 41
	{
	action NUKEHITA
	ifn EXTRASAVED -1
	{
	operateactivators EXTRASAVED THISACTOR
	operaterespawns EXTRASAVED
	operatemasterswitches EXTRASAVED
	setp[].fist_incs 0
	}
	else
		{
			ife VOLUME 1 // Episode 2?
				{

				ife LEVEL 10 // Repent your sins
					{
					ifand BEAT_EPISODES[2] 64 nullop else
						{
						set temp BEAT_EPISODES[2]
						xorvar temp 64
						setarray BEAT_EPISODES[2] temp
						writearraytofile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
						}

					ifn cheated 1
						{
						readarrayfromfile SKILL_MEDALS F_SKILL_MEDALS_AMCTC
						ifl SKILL_MEDALS[20] SKILL_LEVEL setarray SKILL_MEDALS[20] SKILL_LEVEL
						writearraytofile SKILL_MEDALS F_SKILL_MEDALS_AMCTC
						}

					}

				ife LEVEL 18 // Xuglop train
					{
					ifand BEAT_EPISODES[2] 512 nullop else
						{
						set temp BEAT_EPISODES[2]
						xorvar temp 512
						setarray BEAT_EPISODES[2] temp
						writearraytofile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
						}

					ifn cheated 1
						{
						readarrayfromfile SKILL_MEDALS F_SKILL_MEDALS_AMCTC
						ifl SKILL_MEDALS[24] SKILL_LEVEL setarray SKILL_MEDALS[24] SKILL_LEVEL
						writearraytofile SKILL_MEDALS F_SKILL_MEDALS_AMCTC
						}

					}
				}
			setp[].fist_incs 0
			clearmapstate LEVEL
			state save_arrays
			ife YVELSAVED 1 nullop else state fatigue_system
			ife ZVELSAVED 1 nullop else state budget_system

            set cheated 0
			ife XVELSAVED 1 ifn amcbase_level -1 ifn amcbase_volume -1 startlevel amcbase_volume amcbase_level else
			ife XVELSAVED 2 ifn amcbase_level -1 ifn amcbase_volume -1 ifn amcbase_volume VOLUME startlevel amcbase_volume amcbase_level
				else
			startlevel HITAGSAVED LOTAGSAVED


			}
	}
}

ends

useractor notenemy CUSTOMNUKESWITCH state CUSTOM_NUKESWITCH enda
useractor notenemy IWNUKESWITCH state CUSTOM_NUKESWITCH enda

// ==================================================================RUNE SWITCH======================================

defstate checkcounterpart4

set temp 0
whilevarn temp 16384
{
  ifn sprite[temp].statnum 1024
  {
    ife sprite[temp].picnum RUNESWITCH
    {
       getav[temp].LOTAGSAVED temp3
       ife LOTAGSAVED temp3 { seta[temp].picnum 8107 }
    }
  }
  add temp 1
}

ends

defstate RUNESWITCHCODE

ifaction 0 action LOCKED

ifpdistl 1024 ifhitspace ifaction LOCKED
{
	ifspritepal 0 // BLUE
		{
			ifand RUNEKEYS 1
				{
				lockplayer 13
				set CKEYCARDPAL 0
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 0 }
		}
else
	ifspritepal 21 // RED
		{
			ifand RUNEKEYS 2
				{
				lockplayer 13
				set CKEYCARDPAL 21
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 21 }
		}
else
	ifspritepal 23 // YELLOW
		{
			ifand RUNEKEYS 4
				{
				lockplayer 13
				set CKEYCARDPAL 23
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 23 }
		}
else
	ifspritepal 12 // GREY
		{
			ifand RUNEKEYS 8
				{
				lockplayer 13
				set CKEYCARDPAL 12
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 12 }
		}
else
	ifspritepal 11 // GREEN
		{
			ifand RUNEKEYS 16
				{
				lockplayer 13
				set CKEYCARDPAL 11
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 11 }
		}
else
	ifspritepal 49 // PURPLE
		{
			ifand RUNEKEYS 32
				{
				lockplayer 13
				set CKEYCARDPAL 49
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon RUNEKEY set key_pal 49 }
		}
}

ifaction UNLOCKING
{
set KEYCARDTYPE 1
ife CUSTOMKEYCARDPOS 9
	{
	state checkcounterpart4
	ife HITAGSAVED 0 { sound RUNE_USE } else { soundvar HITAGSAVED }
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	action UNLOCKED
	ifspritepal 0 sub RUNEKEYS 1
	else ifspritepal 21 sub RUNEKEYS 2
	else ifspritepal 23 sub RUNEKEYS 4
	else ifspritepal 12 sub RUNEKEYS 8
	else ifspritepal 11 sub RUNEKEYS 16
	else ifspritepal 49 sub RUNEKEYS 32
	}
}
ends

useractor notenemy RUNESWITCH state RUNESWITCHCODE enda

// ==================================================================SWIPE CARDS========================================

defstate SWIPEUNLOCK
	lockplayer 13
	action UNLOCKING
ends

defstate SWIPE_CODE

ifaction 0 action LOCKED

ifpdistl 1024 ifp pfacing ifhitspace ifaction LOCKED
switch sprite[].pal
	case 0
	case 3
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 1 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 0 }
	break
	case 21 // RED
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 2 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 21 }
	break
	case 23 // YELLOW
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 4 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 23 }
	break
	case 12 // GREY
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 8 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 12 }
	break
	case 11 // GREEN
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 16 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 11 }
	break
	case 49 // PURPLE
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 32 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 49 }
	break
	case 15 // BROWN
			ife SWIPECARDS 64 state SWIPEUNLOCK
			else ifand SWIPECARDS 128 state SWIPEUNLOCK
			else { set door_locked 0 set key_icon SWIPE_CARD set key_pal 15 }
	break
endswitch

ifaction UNLOCKING
{
	sound PC_LOAD_FLOPPY
	setactorsoundpitch THISACTOR PC_LOAD_FLOPPY 2048
	state lower_weapon
	ife SWIPECARDS 64 action THREE
	else action TWO
	resetactioncount
}

ifaction TWO
{
ifactioncount 60
	{
	stopsound PC_LOAD_FLOPPY
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	sound SWIPE_UNLOCK
	action ONE
	}
}

ifaction THREE
{
ifactioncount 60
	{
	stopsound PC_LOAD_FLOPPY
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	sound SWIPE_UNLOCK
	action ONE
	}
}

ends

useractor notenemy SWIPE_LOCK state SWIPE_CODE enda

// ==================================================================SCARAB SWITCH======================================

defstate SCARABSWITCHCODE

ifaction 0 { action LOCKED }

ifpdistl 1024 ifhitspace ifaction LOCKED
switch sprite[].pal
	case 0
	case 3
			ifand SCARABKEYS 1
				{
				lockplayer 13
				set CKEYCARDPAL 0
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 0 }
	break
	case 21 // RED
			ifand SCARABKEYS 2
				{
				lockplayer 13
				set CKEYCARDPAL 21
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 21 }
	break
	case 23 // YELLOW
			ifand SCARABKEYS 4
				{
				lockplayer 13
				set CKEYCARDPAL 23
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 23 }
	break
	case 11 // GREEN
			ifand SCARABKEYS 8
				{
				lockplayer 13
				set CKEYCARDPAL 11
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 11 }
	break
	case 49 // PURPLE
			ifand SCARABKEYS 16
				{
				lockplayer 13
				set CKEYCARDPAL 49
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 49 }
	break
	case 35 // ORANGE
			ifand SCARABKEYS 32
				{
				lockplayer 13
				set CKEYCARDPAL 35
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 35 }
	break
	case 15 // BROWN
			ifand SCARABKEYS 64
				{
				lockplayer 13
				set CKEYCARDPAL 15
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 15 }
	break
	case 37 // LIGHT BLUE
			ifand SCARABKEYS 128
				{
				lockplayer 13
				set CKEYCARDPAL 37
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 37 }
	break
	case 12 // GREY
			ifand SCARABKEYS 256
				{
				lockplayer 13
				set CKEYCARDPAL 12
				set CUSTOMKEYCARDPOS 21
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon SCARABKEY set PLAYER_VOICEOVER 3 set key_pal 12 }
	break
endswitch

ifaction UNLOCKING
{
set KEYCARDTYPE 2
ife CUSTOMKEYCARDPOS 9
	{
	ife HITAGSAVED 0 { sound SCARAB_KEY sound EGYPT_SWITCH } else soundvar HITAGSAVED
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	action UNLOCKED
	ifspritepal 0 xorvar SCARABKEYS 1
	else ifspritepal 21 xorvar SCARABKEYS 2
	else ifspritepal 23 xorvar SCARABKEYS 4
	else ifspritepal 11 xorvar SCARABKEYS 8
	else ifspritepal 49 xorvar SCARABKEYS 16
	else ifspritepal 35 xorvar SCARABKEYS 32
	else ifspritepal 15 xorvar SCARABKEYS 64
	else ifspritepal 37 xorvar SCARABKEYS 128
	else ifspritepal 12 xorvar SCARABKEYS 256
	}
}
ends

spritenopal SCARABSWITCH
useractor notenemy SCARABSWITCH state SCARABSWITCHCODE enda

// ==================================================================ABYSS SWITCH======================================

defstate ABYSS_SWITCHCODE

ifaction 0 action LOCKED

ifpdistl 1024 ifhitspace ifaction LOCKED
{
	ifspritepal 0 // BLUE
		{
			ifand ABYSS_KEYS 1
				{
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon ABYSS_KEY set key_pal 0 }
		}
else
	ifspritepal 21 // RED
		{
			ifand ABYSS_KEYS 2
				{
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon ABYSS_KEY set key_pal 21 }
		}
else
	ifspritepal 23 // YELLOW
		{
			ifand ABYSS_KEYS 4
				{
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon ABYSS_KEY set key_pal 23 }
		}
else
	ifspritepal 11 // GREEN
		{
			ifand ABYSS_KEYS 8
				{
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon ABYSS_KEY set key_pal 11 }
		}
else
	ifspritepal 49 // PURPLE
		{
			ifand ABYSS_KEYS 16
				{
				action UNLOCKING
				}
			else { set door_locked 0 set key_icon ABYSS_KEY set key_pal 49 }
		}
}

ifaction UNLOCKING
{
set KEYCARDTYPE 2
ifcount 6
	{
	ife HITAGSAVED 0 { globalsound DIS_CRYING sound OLD_SWITCH } else soundvar HITAGSAVED
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	action UNLOCKED
	ifspritepal 0 xorvar ABYSS_KEYS 1
	else ifspritepal 21 xorvar ABYSS_KEYS 2
	else ifspritepal 23 xorvar ABYSS_KEYS 4
	else ifspritepal 11 xorvar ABYSS_KEYS 8
	else ifspritepal 49 xorvar ABYSS_KEYS 16
	resetcount
	}
}
ends

spritenopal ABYSS_LOCK

useractor notenemy ABYSS_LOCK state ABYSS_SWITCHCODE enda

// *************************************************
// LADDER CODE BY DEEPERTHOUGHT FROM DUKE PLUS
// *************************************************

useractor notenemy 18481
ifn HITAGSAVED 0
	{
	cstat 32768
	ife global_trigger HITAGSAVED
		{
		set HITAGSAVED 0
		seta[].cstat CSTATSAVED
		}
	}
enda

useractor notenemy 3803

	ifn CLIPDISTSAVED 32
	{
		ife CHAR 1 nullop
		else ife CHAR 17 nullop
		else break
	}

	ifspritepal 1
	{
		ife GOT_KNIFE 4 nullop
		else break
	}

	// temp9 appears to be used as some kind of init var here to find the other sprite in the ladder pair
	ife temp9 0
	{
		set temp9 1
		set temp 0
		whilevarn temp 16384
		{
			ife sprite[temp].picnum 3803 ifvarvarn temp THISACTOR
			{
				getav[temp].HITAGSAVED temp3
				ife HITAGSAVED temp3 // it is the other of the pair
				{
					set myspawner temp
					set temp9 1
					set temp 16383 // we can stop the loop now
					ifg sprite[].z sprite[myspawner].z
					{
						set mtype 1 // the bottom of the ladder
						set ally_mag sprite[myspawner].z
					}
				}
			}
			add temp 1
		}
	}


	ifn YVELSAVED 0
	{
		checkactivatormotion YVELSAVED
		ife RETURN 1
		{
			set YVELSAVED 0
		}
	}
	else ifn ZVELSAVED 0 // code to throw down a rope from inventory, I think
	{
		ife mtype 0 // at top of ladder?
		{
			ifpdistl 1536
			{
				set player_use 0
				ife ROPE 1
				{
					setp[].inven_icon 11

					ifhitspace
					{
						set hit_key 30
						soundonce THROWSOMET
						set global_trigger ZVELSAVED
						setp[].inven_icon 0
						set ROPE 0
						set ZVELSAVED 0
					}
				}
				else
				{
					ifhitspace
					{
						 set hit_key 30
						 set door_locked 0
						 set key_icon ROPE_PICKUP
						 set key_pal 0
					}
				}
			}
			else ifpdistg 1536
			{
				ife player[].inven_icon 11 setp[].inven_icon 0
			}
		}

		ife global_trigger ZVELSAVED
			set ZVELSAVED 0
	}
	else ife mtype 1 // at bottom of ladder
	{
		getp[].posz mz
		ifp pducking sub mz 4096
		ifg mz ally_mag ifl mz sprite[].z
		{
			ldist xydist THISACTOR player[].i

			// if pal is 3 we don't require the player to look at the ladder to stay on it
			ifspritepal 3
			{
				ifl xydist 768 // 192 512
				{
				setp[].kickback_pic 0
					set LADDERTYPE 2
					ife onladder 0
					{
						getp[].posx lastladderx
						getp[].posy lastladdery
					}
					set onladder 3
					ifn LOTAGSAVED 0 set laddersound LOTAGSAVED else set laddersound -1
				}
			}
			else ifangdiffl 384 ifl xydist 256 // 192 512
			{
			setp[].kickback_pic 0
				set LADDERTYPE XVELSAVED
				ife onladder 0
				{
					getp[].posx lastladderx
					getp[].posy lastladdery
				}
				set onladder 3
				ifspritepal 1 { set LADDERTYPE 3 set laddersound ICE_AXE }
				else ifn LOTAGSAVED 0 set laddersound LOTAGSAVED
				else set laddersound -1
			}
		}
	}
	else ife mtype 0 // at top of ladder, player can press use to face the direction of the ladder
	{
		ifpdistl 1536
		 ifp pfacing
		  ifcansee
		  ife onladder 0
		{
			set player_use 0
			ifhitspace
			{
				set hit_key 30
				soundonce JMOVE6
				setp[].one_eighty_count -1024
				setp[].cursectnum sprite[].sectnum
				setp[].posx sprite[].x
				setp[].posy sprite[].y
				setp[].posz sprite[].z
			}
		}

	}
enda

defstate wood_ladder
	ifrnd 96 sound WOOD_LADDER1
	else ifrnd 96 sound WOOD_LADDER2
	else sound WOOD_LADDER3
ends

defstate rock_climb
	ifrnd 96 sound ROCK_CLIMB1
	else ifrnd 96 sound ROCK_CLIMB2
	else sound ROCK_CLIMB3
ends

defstate rope_climb
	ifrnd 96 sound ROPE_CLIMB
	else ifrnd 96 sound ROPEC2
	else ifrnd 96 sound ROPEC3
	else sound ROPEC4
ends

defstate laddercode
    state lower_weapon
    ifp ponground { stopsound SLIDE_DOWN set bpress 0 }
    setp[].falling_counter 0

ife LADDERTYPE 2
	{
	 ifcount 13 ifand EXTBITS_PRESS 4
		{
		setp[].kickback_pic 0
		ife laddersound 408 state wood_ladder
		else ife laddersound 2617 state rock_climb
		else ife laddersound 906 state rope_climb
		else ifn laddersound -1 soundvar laddersound
		resetcount
		}
	 ifcount 13 ifand EXTBITS_PRESS 8
		{
		setp[].kickback_pic 0
		ife laddersound 408 state wood_ladder
		else ife laddersound 2617 state rock_climb
		else ife laddersound 906 state rope_climb
		else ifn laddersound -1 soundvar laddersound
		resetcount
		}
	}

ife fpress 0 ife bpress 0
{
	 set laddervel -256
	 getp[].posx lastladderx
	 getp[].posy lastladdery
}
 else
 {
	 ifcount 13 ifg fpress 0
		{
		setp[].kickback_pic 0
		ifrnd 8 set PLAYER_VOICEOVER 25
		ife laddersound 408 state wood_ladder
		else ife laddersound 2617 state rock_climb
		else ife laddersound 906 state rope_climb
		else ifn laddersound -1 soundvar laddersound
		resetcount
		}

	 ife LADDERTYPE 2 { ifg bpress 0 { soundonce SLIDE_DOWN } }
	 ife LADDERTYPE 1 { ifg bpress 0 { soundonce SLIDE_DOWN } }
	 else
	 ife LADDERTYPE 0
		{ ifcount 13
			ifg bpress 0
				{
				setp[].kickback_pic 0
				ife laddersound 408 state wood_ladder
				else ife laddersound 2617 state rock_climb
				else ife laddersound 906 state rope_climb
				else soundvar laddersound
				resetcount
				}
		}
	 geta[].sectnum mysector
	 updatesectorz lastladderx lastladdery player[].posz mysector
	 ifn mysector -1
	 {
		 setp[].posx lastladderx
		 setp[].posy lastladdery
		 getp[].i spriteid
		 seta[spriteid].x lastladderx
		 seta[spriteid].y lastladdery
		 changespritesect spriteid mysector
	 }
 }
	ifp ponground nullop
	else
	{
		setp[].posxv 0
		setp[].posyv 0
	}
sub onladder 1
setp[].poszv laddervel

ends


// *************************************************
// CROW
// *************************************************

move CROWFLY 128 0

useractor notenemy 8576
sizeat 8 8
move CROWFLY geth
ifnotmoving killit
ifpdistl 16384 soundonce CROWFLAP
ifrnd 4 soundonce CROWCAW
enda


// *************************************************
// MOUNTED MINIGUN
// *************************************************

action MGUN_IDLE 0 1 5 1 1
action MGUN_USE 0 1 5 1 1

defstate disengage_minigun
	set firing_count 0
	sound MG_RELE
	stopsound MINIGUN_SPIN
	stopsound MINIGUN_FIRE
	setp[].movement_lock 0
	set using_turret -1
	set turret_type -1
	action MGUN_IDLE
	resetcount
ends

spriteshadow MOUNTED_MINIGUN
useractor 6 MOUNTED_MINIGUN
fall
sizeat 28 28

ifaction 0
{
cstat 257
action MGUN_IDLE
}

ifaction MGUN_IDLE
{
cstat 257
ifpdistl 812
 ifp pfacing
  ife player_using_hovercraft -1
  ife player_using_mech -1
  ife using_turret -1
  {
  set player_use 0
  ifhitspace
   ifcount 16
   ife using_turret -1
		{
		set hit_key 30
		sound MG_GRAB
		set using_turret THISACTOR
		set turret_type 0
		set firing_count 0
		resetcount
		}
	}
}

ife using_turret THISACTOR action MGUN_USE

ifaction MGUN_USE
{
	// geta[].x mx
	// geta[].y my
	// getp[].posx x
	// getp[].posy y
	// sub mx x
	// sub my y
	// getangle angvar mx my
	// setp[].ang angvar
ifp pdead state disengage_minigun
ifpdistg 1224 state disengage_minigun
ifmultiplayer cstat 32768 else
{
ife player[].over_shoulder_on 1 cstat 256 else cstat 32768
}
soundonce MINIGUN_SPIN
ifhitspace
 ifcount 16
	state disengage_minigun
}

enda

// *************************************************
// MOUNTED .50 CAL
// *************************************************

defstate disengage_turret
	set firing_count 0
	setp[].movement_lock 0
	set using_turret -1
	set turret_type -1
	action MGUN_IDLE
	resetcount
ends

useractor 6 MOUNTED_FIFTY_CAL
fall
sizeat 32 32

ifaction 0
{
cstat 257
ifspritepal 3
	{
	set using_turret THISACTOR
	set firing_count 0
	set turret_type 1
	setp[].posx sprite[].x
	setp[].posy sprite[].y
	geta[].z temp4
	sub temp4 8192
	setp[].posz temp4
	action MGUN_USE
	}
else
action MGUN_IDLE
}

ifaction MGUN_IDLE
{
cstat 257
ifpdistl 812
 ifp pfacing
  ife player_using_hovercraft -1
  ife player_using_mech -1
  ife using_turret -1
  {
  set player_use 0
  ifhitspace
   ifcount 16
   ife using_turret -1
		{
		set hit_key 30
		set firing_count 0
		set using_turret THISACTOR
		set turret_type 1
		resetcount
		}
	}
}

ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		ife using_turret THISACTOR state disengage_turret
		killit
		}
	}

ife using_turret THISACTOR action MGUN_USE

ifaction MGUN_USE
{
	// geta[].x mx
	// geta[].y my
	// getp[].posx x
	// getp[].posy y
	// sub mx x
	// sub my y
	// getangle angvar mx my
	// setp[].ang angvar
ifp pdead state disengage_turret
ifpdistg 1224 ifn sprite[].pal 3 state disengage_turret
ifmultiplayer cstat 32768 else
{
ife player[].over_shoulder_on 1 cstat 256 else cstat 32768
}
ifhitspace
 ifcount 16
	state disengage_turret
}

enda

// *************************************************
// MOUNTED MAGIC BALLISTA
// *************************************************

spritenoshade MAGIC_BALLISTA
spritenopal MAGIC_BALLISTA

useractor 6 MAGIC_BALLISTA
fall
sizeat 23 23

ifaction 0
{
	geta[].shade SHADESAVED
	geta[].pal PALSAVED
	cstat 257
	set stationary_gun_ammo 100

	ifspritepal 3
	{
		set using_turret THISACTOR
		set firing_count 0
		set turret_type 2
		setp[].posx sprite[].x
		setp[].posy sprite[].y
		geta[].z temp4
		sub temp4 8192
		setp[].posz temp4
		action MGUN_USE
	}
	else
		action MGUN_IDLE
}

ifaction MGUN_IDLE
{
	cstat 257
	ifpdistl 1024
	 ifp pfacing
	  ife player_using_hovercraft -1
	  ife player_using_mech -1
	  ife using_turret -1
	  {
	  set player_use 0
	   ife use_action_allowed 1
	   ife using_turret -1
			{
			set hit_key 30
			set firing_count 0
			set using_turret THISACTOR
			set turret_type 2
			state setIgnoreUse
			resetcount
			}
		}

	ife stationary_gun_ammo 99 { spritepal 94 seta[].shade -127 }
	else ife stationary_gun_ammo 100 { seta[].pal PALSAVED seta[].shade SHADESAVED }

	ifl stationary_gun_ammo 100
		{
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 4
			{
			ife stationary_gun_ammo 99 sound MAGIC_FIELD_UP
			add stationary_gun_ammo 1
			set INTERNALCOUNT 0
			}
		}
}

ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		ife using_turret THISACTOR state disengage_turret
		killit
		}
	}

ife using_turret THISACTOR action MGUN_USE

ifaction MGUN_USE
{
	ifp pdead state disengage_turret
	ifpdistg 1224 ifn sprite[].pal 3 state disengage_turret
	ifmultiplayer cstat 32768 else
		{
		ife player[].over_shoulder_on 1 cstat 256 else cstat 32768
		}

	ife use_action_allowed 1
	{
		state disengage_turret
		state setIgnoreUse
	}
}
enda

// *************************************************
// MOUNTED BIG BALLISTA
// *************************************************

spritenoshade BIG_BALLISTA
spritenopal BIG_BALLISTA

useractor 6 BIG_BALLISTA
	fall
	sizeat 32 32

	ifaction 0
	{
		geta[].shade SHADESAVED
		geta[].pal PALSAVED
		cstat 257
		set stationary_gun_ammo 15
		ifspritepal 3
			{
			set using_turret THISACTOR
			set firing_count 0
			set turret_type 2
			setp[].posx sprite[].x
			setp[].posy sprite[].y
			geta[].z temp4
			sub temp4 8192
			setp[].posz temp4
			action MGUN_USE
			}
		else
		action MGUN_IDLE
	}

	ifaction MGUN_IDLE
	{
		cstat 257
		ifpdistl 1260
		 ifp pfacing
		  ife player_using_hovercraft -1
		  ife player_using_mech -1
		  ife using_turret -1
		  ifg stationary_gun_ammo 0
		  {
		  set player_use 0
		  ife use_action_allowed 1
		   ife using_turret -1
				{
				set hit_key 30
				set firing_count 0
				set using_turret THISACTOR
				set turret_type 3
				state setIgnoreUse
				resetcount
				}
			}
	}

	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			ife using_turret THISACTOR state disengage_turret
			killit
			}
		}

	ife using_turret THISACTOR action MGUN_USE

	ifaction MGUN_USE
	{
		ifp pdead
			state disengage_turret
		ifpdistg 1224
		  ifn sprite[].pal 3
			state disengage_turret

		ifmultiplayer
			cstat 32768
		else
		{
			ife player[].over_shoulder_on 1
				cstat 256
			else
				cstat 32768
		}

		ife use_action_allowed 1
		{
			state disengage_turret
			state setIgnoreUse
		}

		ife stationary_gun_ammo 0
			state disengage_turret
	}

enda

// *************************************************
// CANNON
// *************************************************

spritenoshade CANNON_TURRET
spritenopal CANNON_TURRET

useractor 6 CANNON_TURRET
	fall
	sizeat 60 60

	ife stationary_gun_ammo 0
		{
		set temp SHADESAVED
		add temp 15
		seta[].shade temp
		}
		else seta[].shade SHADESAVED

	ifaction 0
	{
		geta[].shade SHADESAVED
		geta[].pal PALSAVED
		cstat 257
		set stationary_gun_ammo 1
		ifspritepal 3
			{
			set using_turret THISACTOR
			set firing_count 0
			set turret_type 3
			setp[].posx sprite[].x
			setp[].posy sprite[].y
			geta[].z temp4
			sub temp4 8192
			setp[].posz temp4
			action MGUN_USE
			}
		else
		action MGUN_IDLE
	}

	ifaction MGUN_IDLE
	{
	cstat 257
	ifpdistl 1536
	 ifp pfacing
	  ife player_using_hovercraft -1
	  ife player_using_mech -1
	  ife using_turret -1
	  {
	  ifg stationary_gun_ammo 0
		  {
		  set player_use 0
		  ife use_action_allowed 1
		   ife using_turret -1
				{
				set hit_key 30
				set firing_count 0
				set using_turret THISACTOR
				set turret_type 4
				state setIgnoreUse
				}
			}
		else ifhitspace quote 582
		}
	}

	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			ife using_turret THISACTOR state disengage_turret
			killit
			}
		}

	ife using_turret THISACTOR action MGUN_USE

	ifaction MGUN_USE
	{
	ifp pdead state disengage_turret
	ifpdistg 1224 ifn sprite[].pal 3 state disengage_turret
	ifmultiplayer cstat 32768 else
		{
		ife player[].over_shoulder_on 1 cstat 256 else cstat 32768
		}
	ife use_action_allowed 1
	{
		state disengage_turret
		state setIgnoreUse
	}
	ife stationary_gun_ammo 0 ife firing_count 0 state disengage_turret
	}

enda

// *************************************************
// LIGHT TURN ON
// *************************************************

spritenoshade 3582
useractor notenemy 3582
ifn LOTAGSAVED 0
	{
	cstat 32768
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		seta[].cstat CSTATSAVED
		set LOTAGSAVED 0
		}
	}
enda


// *************************************************
// LIGHT FLICKER
// *************************************************

defstate LIGHT_FLICKER
ifn SHADESAVED 255
	{
	ife SHADESAVED -128 set SHADESAVED -126 // manual fix for sprites set to max vis
	ifrnd 128
		{
		set temp SHADESAVED
		ifrnd 128 sub temp 1 else add temp 1
		seta[].shade temp
		}
		else
		seta[].shade SHADESAVED
	}
ends

spritenopal 19518
spritenopal 19519
spritenopal 19571
spritenopal 5515
spritenopal 23693

useractor notenemy 19518 state LIGHT_FLICKER enda
useractor notenemy 19519 state LIGHT_FLICKER enda
useractor notenemy 19571 state LIGHT_FLICKER enda
useractor notenemy 23693 state LIGHT_FLICKER enda
useractor notenemy 5515 state LIGHT_FLICKER enda

spritenoshade 17323
spritenopal 17323
useractor notenemy 17323 state LIGHT_FLICKER enda


useractor notenemy 17922 state LIGHT_FLICKER enda

// *************************************************
// WATER RIPPLE
// *************************************************

spritenoshade 8435

useractor notenemy 8435

ifaction 0
	{
	findnearactor 8435 32 temp
	ifn temp -1 killit

	ifspawnedby 29020 // water projectile, don't align to floor
	{
		cstat 18
	}
	else
	{
		// Account for slopes
		geta[].sectnum temp
		geta[].x temp2
		geta[].y temp3

		getflorzofslope temp temp2 temp3 temp4
		seta[].z temp4
		cstat 34
	}

	seta[].shade sector[].floorshade
	seta[].blend 255

	sizeat 4 4
	action ZERO
	}
else
ifaction ZERO
	{
	sizeto 64 64
	geta[].shade temp
	add temp 1
	ifg temp 35 killit
	seta[].shade temp
	}

enda

// *************************************************
// RAIN RIPPLE
// *************************************************

spritenoshade 13762

useractor notenemy 13762
seta[].blend 255
seta[].shade sector[].floorshade
geta[].xrepeat temp add temp 4 seta[].xrepeat temp
geta[].yrepeat temp add temp 4 seta[].yrepeat temp
ifg temp 32 killit else
ifg temp 16 cstat 546 else
ifg temp 8 cstat 34 else
cstat 32

enda

// *************************************************
// COG MACHINERY
// *************************************************


action COGS_NOWORK 1
action COGS_WORK 3

useractor notenemy COG_MACHINERY

ifaction 0 action ZERO

ifaction ZERO
 {
 ifp pfacing
  ifp palive
   ifpdistl 1024
    ifhitspace
		{
		ifg COGS 0
			{
			sub COGS 1
			sound OLD_SWITCH2
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			action COGS_WORK
			}
		else
			{
			quote 216
			action COGS_NOWORK
			}
		}
 }

 ifaction COGS_WORK ifpdistl 16384 soundonce COG_WORK
 ifaction COGS_NOWORK { ifcount 104 { action ZERO resetcount } }

enda

// *************************************************
// COMBAT CHECK
// *************************************************

useractor notenemy 22446
cstat 32768

ifaction 0
{
	ifg LOTAGSAVED 0
	{
		checkactivatormotion LOTAGSAVED
		ife RETURN 1
		{
			set COMBAT_CHECK HITAGSAVED
			resetcount
			action ZERO
		}
	}
	else
	{
		set COMBAT_CHECK HITAGSAVED
		resetcount
		action ZERO
	}
}

ifaction ZERO
{
	ifcount 30 { action FORM resetcount }
}

ifaction FORM
{
	ife YVELSAVED 1
	{
		// find all non friendly targets within radius provided by ZVELSAVED
		set temp3 -1

		set targets_iterator 0
		whilel targets_iterator targets_range
		{
			ife temp3 1
			{
				set targets_iterator targets_range
				exit
			}

			set my_target targets[targets_iterator]

			// early break
			ife my_target -2
			{
				set targets_iterator targets_range
				set my_target -1 // most code checks on -1 specifically so compensate
				exit
			}

			ifg my_target -1
			{
				getav[my_target].faction_flag temp4

				// The sprite "BLOODTRAIL" sometimes gets picked up here, dunno why, it shouldn't be in targets array unless there's a well hidden cactor somewhere.
				// Added some protection against this...
				ifand temp4 ALLIED_FACTION_FLAG
					nullop
				else ifn temp4 0
				{
					// is close enough to be taken into account
					ifspritepal 1 dist temp5 THISACTOR my_target
					else ldist temp5 THISACTOR my_target
					ifl temp5 ZVELSAVED
					{
						geta[my_target].extra temp5
						ifg temp5 0
						{
							geta[my_target].picnum temp6

							// ignore BLOODTRAIL
							ifn temp6 7869
								set temp3 1
						}
					}
				}
			}

			add targets_iterator 1
		}

		// temp3 is still -1 if not a single live target was found
		ife temp3 -1
		{
			operateactivators COMBAT_CHECK THISACTOR
			operaterespawns COMBAT_CHECK
			operatemasterswitches COMBAT_CHECK
			set COMBAT_CHECK -1
			killit
		}
	}
	else
	{
		ifn EXTRASAVED -1 set CC_MUSIC EXTRASAVED
			ife enemies_cleared 150
			{
				ifn EXTRASAVED -1 stopsoundvar CC_MUSIC
				ifn XVELSAVED 0 globalsoundvar XVELSAVED
				ifspritepal 12 state fade_out_black
				ifspritepal 13 state fade_out_white
				operateactivators COMBAT_CHECK THISACTOR
				operaterespawns COMBAT_CHECK
				operatemasterswitches COMBAT_CHECK
				set COMBAT_CHECK -1
				killit
			}
	}
}

enda

// *************************************************
// C4 PLACE HERE
// *************************************************

action PREPLACED -2
action MINUSTWO -2

spritenopal C4_PLACE_HERE
spritenoshade C4_PLACE_HERE
useractor notenemy C4_PLACE_HERE

ifaction 0
	{
	sizeat 22 22
	cstat 658
	seta[].blend 255
	ifspritepal 9 cstator 256
	ifn EXTRASAVED 0 action PREPLACED
	else action ZERO
	}
else
ifaction PREPLACED
	{
	checkactivatormotion EXTRASAVED
	ife RETURN 1 { set INTERNALCOUNT 0 action MINUSTWO }
	}
ifaction ZERO
	{
	ifpdistl 4096
	 ifg GOT_EXPLOSIVE 0
		{
		geta[].xrepeat temp
		add temp 1 ifg temp 36 set temp 22
		seta[].xrepeat temp
		seta[].yrepeat temp
		}
	else sizeat 22 22

	ifpdistl 1024
	 ifp pfacing
	 {
	 set player_use 0
	  ifhitspace
	   ifcount 13
	  {
	  ife GOT_EXPLOSIVE 0 quote 185
	  else ifg GOT_EXPLOSIVE 0
		{
		set hit_key 30
		state lower_weapon
		set temp2 0
		globalsound C4_ATTACH
		set PLAYER_VOICEOVER 24
		sub GOT_EXPLOSIVE 1
		set INTERNALCOUNT 0
		action MINUSTWO
		ifn HITAGSAVED 0
			{
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			}
		}
	  resetcount
	  }
	}

	ifspritepal 9
		{
		strength 0
		ifhitweapon
		 ifwasweapon RADIUSEXPLOSION
			{
			hitradius 4096 50 100 150 300
			spawn EXPLOSION2
			globalsound RPG_EXPLODE
			palfrom 20 20 20 20
			ifn LOTAGSAVED 0
				{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				}
			killit
			}
		 }

	}
else
ifaction MINUSTWO
	{
	sleeptime 0
	seta[].blend 0
	sizeat 16 16
	cstat 144
	add INTERNALCOUNT 1
	ife INTERNALCOUNT 2 { quote 186 sound C4_BEEP setactorsoundpitch THISACTOR C4_BEEP -256 }
	ife INTERNALCOUNT 30 { quote 187 sound C4_BEEP setactorsoundpitch THISACTOR C4_BEEP -128 }
	ife INTERNALCOUNT 60 { quote 188 sound C4_BEEP setactorsoundpitch THISACTOR C4_BEEP 0 }
	ife INTERNALCOUNT 90 { quote 189 sound C4_BEEP setactorsoundpitch THISACTOR C4_BEEP 128 }
	ife INTERNALCOUNT 120 { quote 190 sound C4_BEEP setactorsoundpitch THISACTOR C4_BEEP 256 }
	ife INTERNALCOUNT 150
		{
		quote 191
		ifspritepal 3 hitradius 256 1 1 1 1
		else
		hitradius 4096 50 100 150 300
		quake 26
		spawn EXPLOSION2
		globalsound RPG_EXPLODE
		palfrom 20 20 20 20
		ifn LOTAGSAVED 0
			{
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			}
		killit
		}
	}



enda

// *************************************************
// CODEC BLIPPY THING
// *************************************************

action CBT0 0
action CBT1 1
action CBT2 2
action CBT3 3
action CBT4 4
action CBT5 5
action CBT6 6

useractor notenemy 9124

ifcount 2
{
ifrnd 8 action CBT0 else
ifrnd 16 action CBT1 else
ifrnd 16 action CBT2 else
ifrnd 16 action CBT3 else
ifrnd 16 action CBT4 else
ifrnd 16 action CBT5 else
ifrnd 32 action CBT6
resetcount
}

enda

// *************************************************
// COUNTDOWN
// *************************************************


useractor notenemy 3806
cstat 32768

ifspritepal 0
ifaction 0
{
ife LOTAGSAVED 0
	{
	ifspawnedby 19340
		{
		set countdown_type 1
		set EXTRASAVED 31976
		set HITAGSAVED 1
		}
	set countdown THISACTOR
	set countdown_minutes HITAGSAVED
	ifn XVELSAVED 0 set countdown_seconds XVELSAVED
	ifn EXTRASAVED -1 set countdown_fail EXTRASAVED
	action ZERO
	}
else
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set countdown THISACTOR
		set countdown_minutes HITAGSAVED
		ifn XVELSAVED 0 set countdown_seconds XVELSAVED
		ifn EXTRASAVED -1 set countdown_fail EXTRASAVED
		action ZERO
		}
	}
}
else
ifspritepal 1
{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		quote 194
		set countdown -1
		ifg countdown_type 0 { setgamepalette 0 set countdown_type 0 }
		ifn YVELSAVED 0 globalsoundvar YVELSAVED
		action ZERO
		}
}
else
ifspritepal 2
ifaction 0
{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set countdown THISACTOR
		set countdown_minutes HITAGSAVED
		palfrom 32 64 0 0
		quake 104
		set countdown_type 2
		ifn EXTRASAVED -1 set countdown_fail EXTRASAVED
		action ZERO
		}
}
else
ifspritepal 3
ifaction 0
{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set countdown THISACTOR
		set countdown_minutes HITAGSAVED
		palfrom 32 64 0 0
		quake 104
		set countdown_type 3
		ifn EXTRASAVED -1 set countdown_fail EXTRASAVED
		action ZERO
		}
}

ifaction ZERO
{
    ife countdown -1 killit
	ife countdown_type 1
		{
		ifn player[].palette 5 setgamepalette 5
		ifrnd 8 quake 13
		ifcount 40 { globalsound CLAXON_ALARM resetcount }
		}
	ife countdown_type 2
		{
		ifrnd 8 quake 13
		ifcount 50 { globalsound CY_CLAXON resetcount }
		}
	ife countdown_type 3
		{
		ifrnd 8 quake 13
		ifcount 160 { globalsound CLAXON_ALARM5 resetcount }
		}
	add count_timer 1
	ifg countdown_seconds -1 ife count_timer 28 { sub countdown_seconds 1 set count_timer 0 }
	else ife countdown_seconds -1
		{
		ifg countdown_minutes 0
			{ sub countdown_minutes 1 set countdown_seconds 59 }
		else
			{
			ifn countdown_fail -1
				{
				set countdown_type 0
				setgamepalette 0
				operateactivators countdown_fail THISACTOR
				operatemasterswitches countdown_fail
				operaterespawns countdown_fail
				set countdown -1
				killit
				}
			else
				{
				state fade_out_white
				set SLO_MO_SHOWOFF 70
				set FISSION_MAILED 1
				set countdown -1
				killit
				}
			}
		}
}
enda

// *************************************************
// WEAK DOOR
// *************************************************

useractor notenemy 9693 250

ifaction 0 action ZERO

ifstrength 125 action ONE

ifpdistl 1024
 ifp pfacing
  ifhitspace
   ifcount 16
	{
	set PLAYER_VOICEOVER 4
	soundonce LOCKED_GATE1
	quote 219
	resetcount
	}

ifhitweapon
	{
	ifwasweapon KNEE
		{
		shoot SWOODSCRAP shoot SWOODSCRAP
		sound WOOD_DOORKICK
		}
		else sound HITWOOD
	ifdead
		{
	  quake 13
	  globalsound DOOR_KICK2
	  shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
	  shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP shoot WOODSCRAP
	  killit
		}
	}

enda

// *************************************************
// ALARM SYSTEM
// *************************************************

useractor notenemy ALARM_SYSTEM
cstat 32768

ifn HITAGSAVED 0
{
ifg ALARM_ACTIVE 0
	{
   operateactivators HITAGSAVED THISACTOR
   operatemasterswitches HITAGSAVED
   operaterespawns HITAGSAVED
   killit
	}
}
else
ifpdistl 16384
	{
	set ALARM_DETECT 2
	ifg ALARM_COUNTER 100
		{
		ife SKILL_LEVEL 1 set ALARM_ACTIVE 240
		else ife SKILL_LEVEL 2 set ALARM_ACTIVE 360
		else ife SKILL_LEVEL 3 set ALARM_ACTIVE 480
		else ife SKILL_LEVEL 4 set ALARM_ACTIVE 760
		resetcount
		}

	ifg ALARM_ACTIVE 0
		{
		set ALARM_COUNTER 0
		set PLAYER_VOICEOVER 37
		ifcount 26 { ifn LOTAGSAVED 0 soundvar LOTAGSAVED palfrom 10 10 0 0 resetcount }
		ifspritepal 3 nullop else killit
		}

	}

enda

action ALARM_ON -2

spritenoshade 551
spritenoshade 552

useractor notenemy 552

ifn HITAGSAVED 0
{
checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		seta[].shade -127
		cactor 551
		espawn 18427
		ifand sprite[].cstat 8 nullop else
			{
			geta[].sectnum temp3
			seta[RETURN].z sector[temp3].ceilingz
			}
		geta[].pal temp4
		ife temp4 0 seta[RETURN].pal 2
		else seta[RETURN].pal temp4
		setactorvar[RETURN].myowner THISACTOR
		}
}

ifhitweapon
{
	sound GLASS_BREAKING
	lotsofglass 8
	espawn 3179
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	seta[RETURN].cstat sprite[].cstat
	seta[RETURN].pal sprite[].pal
	killit
}

enda

spritenoshade 553
useractor notenemy 553

ife HITAGSAVED 1
{
	ifaction 0
		{
		ifg ALARM_ACTIVE 0
			{
			action ALARM_ON
			}
		}
	else
	ifaction ALARM_ON
		{
		ifpdistl 16384
			{
			ife LOTAGSAVED 0 { ifactorsound THISACTOR CLAXON_ALARM nullop else sound CLAXON_ALARM }
			else { ifactorsound THISACTOR LOTAGSAVED nullop else globalsoundvar LOTAGSAVED }
			}
		ife ALARM_ACTIVE 0
			{
			stopsoundvar LOTAGSAVED
			stopsound CLAXON_ALARM
			action 0
			}
		}
}
else
ifn HITAGSAVED 0
{
checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		seta[].shade -127
		soundonce ALARM2
		action MINUSTWO
		}
	else
		{
		action 0
		seta[].shade sector[].ceilingshade
		ifactorsound THISACTOR ALARM2 stopactorsound THISACTOR ALARM2
		}
}

ifhitweapon
{
	sound GLASS_BREAKING
	lotsofglass 8
	espawn 3179
	seta[RETURN].xrepeat sprite[].xrepeat
	seta[RETURN].yrepeat sprite[].yrepeat
	seta[RETURN].cstat sprite[].cstat
	seta[RETURN].pal sprite[].pal
	killit
}

enda

// *************************************************
// TARGET RANGE SILHOUETTE
// *************************************************

spriteflags 8088 SFLAG_NODAMAGEPUSH
damageeventtile 8088

useractor enemy 8088
	strength 1
	set actor_type TYPE_CASE_SPECIFIC
	seta[].x initx
	seta[].y inity
	seta[].z initz
	seta[].cstat CSTATSAVED
	
	// The dummy can have a lotag and then it'll do stuff
	ife HITAGSAVED 0 ifg LOTAGSAVED 0
	{
		ifhitweapon
		  ifaction 0
		{
			operateactivators LOTAGSAVED THISACTOR
			globalsound MONITOR_ACTIVE
			action ZERO
		}
	}
	
	// Dummes in the training map have specific hitags that mean certain things
	// Technically HITAGSAVED 1 and HITAGSAVED 0 + LOTAGSAVED > 0 do the same thing now, but let's not mix the concepts of regular usable target dummy and training range dummy...
	else ife HITAGSAVED 1
	{
		ifhitweapon
		  ifaction 0
		{
			operateactivators LOTAGSAVED THISACTOR
			globalsound MONITOR_ACTIVE
			action ZERO
		}
	}
	else ife HITAGSAVED 2
	{
		ifaction 0
		  ifhitweapon
		{
			ife sprite[].htpicnum SHOTGUN
			{
				operateactivators LOTAGSAVED THISACTOR
				globalsound MONITOR_ACTIVE
				action ZERO
			}
			strength 1
		}
	}
	else ife HITAGSAVED 3
	{
		ifaction 0
		 ifhitweapon
		{
			ife sprite[].htpicnum 6826
			{
				shoot SPARK
				shoot SPARK
				sound HITARMOUR
				operateactivators LOTAGSAVED THISACTOR
				globalsound MONITOR_ACTIVE
				action ZERO
			}
			else ife sprite[].htpicnum 6816
			{
				shoot SPARK
				shoot SPARK
				sound HITARMOUR
				operateactivators LOTAGSAVED THISACTOR
				globalsound MONITOR_ACTIVE
				action ZERO
			}
			else
				sound PROTECT_DING2
				
			strength 1
		}
	}
	else
	{
		move STOP
		strength 500
		ifg sprite[].htextra -1
		{
			set dummy_dam sprite[].htextra
			set dummy_count 120
			seta[].extra 500
			ifspritepal 3 quote 390
			ifspritepal 13 quote 391
			ifspritepal 27 quote 392
			ifspritepal 28 quote 394
			ifspritepal 30 quote 393
			ifspritepal 6 quote 389
			seta[].htextra -1
		}
	}
enda

// *************************************************
// STRIP WEAPONS
// *************************************************


defstate strip_ammo_and_weapons
set guns_akimbo 0
set gun_firemode 0
set gun_firemode_two 0
set temp_weap 0
set silver_ammo 0
set incend_ammo 0
set silver_bolts 0
set void_bolts 0
set explosive_shells 0
set tesla_ammo 0
set flechete_shells 0
set riot_shield 0
set magnum_shells 0
set dragon_shells 0
set micro_torpedo 0
set heat_seek_card 0
set atomic_bomb 0
set DUP_missile_ammo 0
set p_blue_mana 0
set p_green_mana 0
set p_orange_mana 0
set p_purple_mana 0
set fortymm_grenades 0
set fortymm_shells 0
set fortymm_MIA_shells 0
set fortymm_plasma 0
set fortymm_caseless 0
set fortymm_slug 0
set fortymm_electro 0
set fortymm_cryo 0
set SHOTGUNMAG 0
set SHOTGUNBOXMAG 0
set gun_mag 0
set rifle_mag 0
set ALTMAG 0
set temp 1
set scope 0
	ife give_loadout 1 // hard remove for mission interface
	{
	whilevarn temp 12
		{
		setp[].gotweapon temp 0
		setp[].ammo_amount temp 0
		add temp 1
		}
	ife temp 12 setp[].curr_weapon 0
	whilevarn temp 16
		{
		setarray AMMO_TYPES[temp] 0
		add temp 1
		}
	}
	else
	ifspritepal 6 // hard remove
	{
	whilevarn temp 12
		{
		setp[].gotweapon temp 0
		setp[].ammo_amount temp 0
		add temp 1
		}
	ife temp 12 setp[].curr_weapon 0
	whilevarn temp 16
		{
		setarray AMMO_TYPES[temp] 0
		add temp 1
		}
	}
	else
	{
	whilevarn temp 12
		{
		ifspritepal 4 ife temp 1 nullop else
		ifand weap_special[temp] 256 nullop else
			{
			setp[].gotweapon temp 0
			setp[].ammo_amount temp 0
			}
		add temp 1
		}
	ife temp 12 setp[].curr_weapon 0
	whilevarn temp 12
		{
		ifspritepal 4 ife temp 1 nullop else
		ifand weap_special[temp] 256 nullop else
			{
			setarray AMMO_TYPES[temp] 0
			}
		add temp 1
		}
	}
ends

defstate strip_keys
set ACCESSKEY2 0
set PLUGKEY 0
set HELLGKEYS 0
set HELLGKEYSA 0
set RUNEKEYS 0
set SWIPECARDS 0
set OLDKEYS 0
set GENERIC_KEYS 0
set ABYSS_KEYS 0
set NORMAL_KEYS 0
set SW_KEYS 0
set EGYPT_KEYS 0
ends

defstate strip_inventory
set HEAT_SINKS 0
set GOT_EXPLOSIVE 0
set AIR_FILTER 0
set FIRE_SUIT 0
set JUGGERN_SUIT 0
set GAS_MASK 0
set GOT_FLIPPER 0
set JUMP_BOOTS 0
set TOOLBOX 0
set ROPE 0
set GOT_FIREWALL 0
set COGS 0
set FLOPPY_DISK 0
set P_SPIRIT_ARMOUR 0
set P_FIRE_ARMOUR 0
set EGYPT_ARTIFACTS 0
setp[].boot_amount 0
setp[].firstaid_amount 0
setp[].heat_amount 0
setp[].holoduke_amount 0
setp[].jetpack_amount 0
setp[].scuba_amount 0
setp[].steroids_amount 0
setp[].inven_icon 0
ends

useractor notenemy 3610
cstat 32768

ifaction 0
{
seta[].mdflags 16
	ife HITAGSAVED 0
	{
	ifspritepal 10
		{
		ifpdistl 1024
			{
			setp[].ammo_amount 0 0
			set temp_weap 0
			}
		}
	else
		{
		ife CHARSELECT 1 nullop else
			{
			ifspritepal 0 { state strip_inventory state strip_keys state strip_ammo_and_weapons set give_loadout 0 } else
			ifspritepal 1 { state strip_keys state strip_inventory } else
			ifspritepal 2 state strip_ammo_and_weapons else
			ifspritepal 3
				{
				state strip_inventory state strip_ammo_and_weapons state strip_keys
				globalsound WEAPON_TAKE
				quote 228
				state PISSED_QUOTES
				palfrom 20 20 20 20
				}
			else
			ifspritepal 4 { state strip_inventory state strip_ammo_and_weapons state give_assigned_weapon }
			else
			ifspritepal 5 state strip_keys
			else
			ifspritepal 6 { state strip_inventory state strip_keys state strip_ammo_and_weapons set give_loadout 0 } else
			ifspritepal 8
				{
				state strip_inventory
				state strip_ammo_and_weapons
				ifl PHEALTH pmax_health addphealth 255
				setp[].shield_amount 0
				ife CHAR -1 set CHARSELECT 1

				// Ensure we don't hit the hardcoded break
				ife player[].gotweapon 1 1
				{
					getp[].ammo_amount 1 temp
					add temp 48
					getp[].max_ammo_amount 1 temp2
					clamp temp 0 temp2
					setp[].ammo_amount 1 temp
				}
				else
					addweapon 1 48

				}
			killit
			}
		}
	}
	else
	{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
		{
		ifspritepal 0 { state strip_inventory state strip_keys state strip_ammo_and_weapons } else
		ifspritepal 1 { state strip_inventory state strip_keys } else
		ifspritepal 2 state strip_ammo_and_weapons else
		ifspritepal 3
			{
			state strip_inventory state strip_keys state strip_ammo_and_weapons
			globalsound WEAPON_TAKE
			quote 228
			palfrom 20 20 20 20
			}
		ifspritepal 4 { state strip_inventory state strip_ammo_and_weapons state give_assigned_weapon }
		killit
		}
	}
}

enda

// *************************************************
// ZIPWIRE
// *************************************************

var zipline_delta 0 2
// zipline can slide at any angle, to use this over the hardcoded value set the XVEL to the id of the sprite (probably a pole) to aim at

move SLFORWARD 512 0
move SLFORWARD2 512 0
move SLSTOP 0

action SLSTATIONARY 0
action SLRESET 0
action SLMOVE 0

spritenopal 8940

useractor notenemy 8940
clipdist 128

ifaction 0
{
	cstat 16

	geta[].xvel XVELSAVED

	ifn XVELSAVED 0
	{
		set my_target XVELSAVED
		set CALCZ_TARGET_OFFSET 8192
		set PROJECTILE_VEL 150
		state calczvel
		set zipline_delta temp3
	}
	action SLSTATIONARY
}

ifaction SLSTATIONARY
{
	move SLSTOP 0
	ifpdistl 2048
		ifp palive
	{
		set player_use 0
		ifhitspace { set hit_key 30 action SLMOVE }
	}
}

ifaction SLRESET
{
	seta[].x temp7
	seta[].y temp8
	seta[].z temp9
	geta[].sectnum upd_sect
	updatesectorz temp7 temp8 temp9 upd_sect
	ifn upd_sect -1 changespritesect THISACTOR upd_sect
	action SLSTATIONARY
}

ifaction SLMOVE
{
	ifspritepal 3 move SLFORWARD2 geth getv
	else move SLFORWARD geth getv

	ifmove SLFORWARD
	{
		geta[].z temp2

		ifn XVELSAVED 0
			add temp2 zipline_delta
		else
			add temp2 384

		seta[].z temp2
	}
	else ifmove SLFORWARD2
	{
		geta[].z temp2
		add temp2 1024
		seta[].z temp2
	}

	ifp pdead { set on_tripwire 0 action SLRESET }
	else ifp palive
	{
		set on_tripwire 1
		geta[].z temp4
		geta[].sectnum upd_sect
		updatesectorz sprite[].x sprite[].y sprite[].z upd_sect
		setp[].cursectnum upd_sect
		setp[].posx sprite[].x
		setp[].posy sprite[].y
		add temp4 4096
		setp[].posz temp4
		setp[].poszv 128
		setp[].ang sprite[].ang
		setp[].jumping_counter 0
		setp[].falling_counter 0
		setp[].hard_landing 0
		state STOPSTEPSOUNDS
		soundonce ZIPLINE
		ifnotmoving
		{
			pkick
			stopsound ZIPLINE
			getp[].ang knockbackang
			set knockback2 8
			set on_tripwire 0
			action SLRESET
		}
		else ifn sprite[].pal 9
		{
			ifand BITS_PRESS 1
			{
				pkick
				stopsound ZIPLINE
				getp[].ang knockbackang
				set knockback2 8
				set on_tripwire 0
				action SLRESET
			}
			else ifand BITS_PRESS 2
			{
				stopsound ZIPLINE
				set on_tripwire 0
				action SLRESET
			}
		}
	}
}

enda

// *************************************************
// EARTH SPRITE
// *************************************************

action EARTHNIGHT 11

useractor notenemy 8448

ifspritepal 3
	{
	ifmultiplayer break
	ifaction 0
		{
		gettimedate temp temp temp2 temp temp temp temp temp
		ifg temp2 19 action EARTHNIGHT else
		ifl temp2 6 action EARTHNIGHT else
		action ZERO
		}
	}

enda

// *************************************************
// GRANDFATHER CLOCK
// *************************************************

action NOCHIME 0
action CHIME 0


useractor notenemy 3745
sleeptime 0
ifaction 0 { cstat 32768 action NOCHIME }

ifaction CHIME
	{
	soundonce OLDCLOCK_CHIME
	stopsound OLDCLOCK_TICK
	ifcount 184 { spawn 3745 killit }
	}
else
ifaction NOCHIME
	{
	ifn LOTAGSAVED 0
		{
		gettimedate temp3 temp2 temp temp temp temp temp temp
		ife temp2 0 // minutes
			 ifl temp3 5 // seconds
			{ operateactivators LOTAGSAVED THISACTOR action CHIME resetcount }
		}
	ifpdistl 16384 soundonce OLDCLOCK_TICK
	}
enda

// *************************************************
// BELL
// *************************************************

action BELLFORWARD 0 3 1 1 16 // Go forward
action BELLFSTOP 3 // Bell forward stopped
action BELLFBACK 3 3 1 -1 16 // Go back from forward
action BELLMSWING 0 // Bell mid swing
action BELLBACK 0 3 1 -1 16 // Go back
action BELLBSTOP -3 // Bell back stopped
action BELLBACKF -3 3 1 1 16 // Go forward from back

spritenoshade 9630
useractor notenemy 9630
sleeptime 0
ifaction 0 action ZERO

ifaction ZERO { ifg BELLTIME 0 action BELLFORWARD }
ifaction BELLFORWARD { ifactioncount 3
						{
						sub BELLTIME 1
						ifpdistg 65534 globalsound BIG_BELL_DIST else globalsound BIG_BELL
						action BELLFSTOP
						resetactioncount
						}
					}
ifaction BELLFSTOP { ifactioncount 8 { action BELLFBACK resetactioncount } }
ifaction BELLFBACK { ifactioncount 3 { action BELLMSWING resetactioncount } }
ifaction BELLMSWING { ifactioncount 3 { action BELLBACK resetactioncount } }
ifaction BELLBACK { ifactioncount 3 { action BELLBSTOP resetactioncount } }
ifaction BELLBSTOP { ifactioncount 8 { action BELLBACKF resetactioncount } }
ifaction BELLBACKF { ifactioncount 3 { action ZERO resetactioncount } }

enda

// *************************************************
// BIG CLOCK
// *************************************************

action CLOCK12 0
action CLOCK1 1
action CLOCK2 2
action CLOCK3 3

action CLOCK4 2
action CLOCK5 1
action CLOCK6 0

action CLOCK7 1
action CLOCK8 2
action CLOCK9 3

action CLOCK10 2
action CLOCK11 1


useractor notenemy 9615
	ifmultiplayer killit
	gettimedate temp4 temp2 temp3 temp temp temp temp temp

	ifaction CLOCK12 cstat 144
	ifaction CLOCK1 cstat 144
	ifaction CLOCK2 cstat 144
	ifaction CLOCK3 cstat 144
	ifaction CLOCK4 cstator 152
	ifaction CLOCK5 cstator 152
	ifaction CLOCK6 cstator 152
	ifaction CLOCK6 cstator 152
	ifaction CLOCK7 cstator 156
	ifaction CLOCK8 cstator 156
	ifaction CLOCK9 cstator 156
	ifaction CLOCK10 cstator 148
	ifaction CLOCK11 cstator 148


	ifspritepal 27
		{
		ifle temp2 5 action CLOCK1 else
		ifle temp2 10 action CLOCK2 else
		ifle temp2 15 action CLOCK3 else
		ifle temp2 20 action CLOCK4 else
		ifle temp2 25 action CLOCK5 else
		ifle temp2 30 action CLOCK6 else
		ifle temp2 35 action CLOCK7 else
		ifle temp2 40 action CLOCK8 else
		ifle temp2 45 action CLOCK9 else
		ifle temp2 50 action CLOCK10 else
		ifle temp2 55 action CLOCK11
		}
	else
		{
		switch temp3
		case 0 case 12
		action CLOCK12 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 12 break // Noon/midnight
		case 1 case 13
		action CLOCK2 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 1 break // 1 o clock
		case 2 case 14
		action CLOCK2 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 2 break // 2 o clock
		case 3 case 15 // 3 o clock
		action CLOCK3 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 3  break
		case 4 case 16 // 4 o clock
		action CLOCK4 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 4  break
		case 5 case 17 // 5 o clock
		action CLOCK5 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 5  break
		case 6 case 18 // 6 o clock
		action CLOCK6 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 6  break
		case 7 case 19 // 7 o clock
		action CLOCK7 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 7  break
		case 8 case 20 // 8 o clock
		action CLOCK8 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 8  break
		case 9 case 21 // 9 o clock
		action CLOCK9 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 9  break
		case 10 case 22 // 10 o clock
		action CLOCK10 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 10  break
		case 11 case 23 // 11 o clock
		action CLOCK11 ife temp2 0 ife temp4 0 ife BELLTIME 0 set BELLTIME 11  break
		endswitch
		}


enda

// *************************************************
// WEB
// *************************************************

action WEBOK 0
action WEBREAK 0 3 1 1 8
action WEBROKE 3

useractor notenemy 9585

ifaction 0 { cstator 257 action WEBOK }

ifaction WEBOK
{
ifpdistl 1024
	{
	quote 201
	set caught_in_web 26
	geta[].cstat temp
	sub temp 257
	seta[].cstat temp
	action WEBREAK
	}
else
ifhitweapon
	{
	geta[].cstat temp
	sub temp 257
	seta[].cstat temp
	action WEBREAK
	}
}

ifaction WEBREAK ifactioncount 3 action WEBROKE

enda

// *************************************************
// TESLA GENERATOR
// *************************************************

spritenoshade 9602
spritenoshade 9603
spritenoshade 9606

useractor notenemy 9602 // Tesla Generator

checkactivatormotion LOTAGSAVED

ife RETURN 1
ifaction 0
   {
   set GENERATOR_ACTIVE LOTAGSAVED
   operateactivators HITAGSAVED THISACTOR
   operatemasterswitches HITAGSAVED
   operaterespawns HITAGSAVED
   action ZERO
   }

ife LOTAGSAVED GENERATOR_ACTIVE action ZERO

ifaction ZERO
	{
	spritepal 0
	seta[].shade -64
	}

enda

useractor notenemy 9603 // Tesla ball

ifspritepal 3 action ZERO

ifaction ZERO
{
ifrnd 16 shoot SPARK2

ifspritepal 3
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	killit
	}
}
}

ifaction 0
{
cstator 32768

ife LOTAGSAVED GENERATOR_ACTIVE
	{
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	action ZERO
	seta[].shade -64
	geta[].cstat temp
	xorvar temp 32768
	seta[].cstat temp
	}
 }

enda

useractor notenemy 9606 // Tesla beam

ifaction ZERO
	{
	ifpdistl 2048
		{
		getp[].posx mx
		getp[].posy my
		sub mx sprite[].x
		sub my sprite[].y
		getangle knockbackang mx my
		set knockback 3
		addphealth -2
		palfrom 30 20 0 60
		wackplayer
		soundonce THUNDERBALL_HIT
		}
	ifpdistl 16384 soundonce TESLA_ARC
	}

ifaction 0
{
cstator 32768

ife LOTAGSAVED GENERATOR_ACTIVE
	{
	action ZERO
	seta[].shade -64
	geta[].cstat temp
	xorvar temp 32768
	seta[].cstat temp
	}
}

enda

// *************************************************
// LOCKED DOOR
// *************************************************


action LOCK_DOOR_A -9

useractor notenemy 3744
cstator 1

	ifaction 0 action LOCK_DOOR_A

	ifn XVELSAVED 0
	{
		sleeptime 0
		checkactivatormotion XVELSAVED
		ife RETURN 1 killit
	}

ifpdistl 1024
 ifp pfacing
  ifp palive
  {
  ifn HITAGSAVED 0 ifg GENERIC_KEYS 0 set player_unlock 0 else
  set player_use 0
   ifhitspace
    ifcount 26
	{
	set hit_key 30
	ifn LOTAGSAVED 0
		{
		ifspritepal 16 nullop else
			{
			set PLAYER_VOICEOVER 4
			quote 219
			}
		ifspritepal 0 sound LOCKED_DOOR1 else
		ifspritepal 1 sound LOCKED_GATE1 else
		ifspritepal 2 sound LOCKED_DOOR2 else
		ifspritepal 3 sound LOCKED_HITECH1 else
		ifspritepal 4 sound LOCKED_DOOR3 else
		ifspritepal 5 sound LOCKED_DOOR4 else
		ifspritepal 6 sound LOCKED_HITECH2
		}
		else
	ifn HITAGSAVED 0
	 ifg GENERIC_KEYS 0
		{
		sub GENERIC_KEYS 1
		globalsound OLDKEY_UNLOCK
		operateactivators HITAGSAVED THISACTOR
		killit
		}
	else
	ifn XVELSAVED 0 // display "locked, it must open somewhere else" message along with a sound if the XVEL is not activated yet.
	{
		checkactivatormotion XVELSAVED
		ife RETURN 1 killit
		else
		{
			quote 233
			ifspritepal 0 sound LOCKED_DOOR1 else
			ifspritepal 1 sound LOCKED_GATE1 else
			ifspritepal 2 sound LOCKED_DOOR2 else
			ifspritepal 3 sound LOCKED_HITECH1 else
			ifspritepal 4 sound LOCKED_DOOR3 else
			ifspritepal 5 sound LOCKED_DOOR4 else
			ifspritepal 6 sound LOCKED_HITECH2
		}
	}
	else
		{
		ifn HITAGSAVED 0 { set PLAYER_VOICEOVER 3 set door_locked 0 set key_icon GENERICKEY } else quote 4
		set temp9 26
		ifspritepal 0 sound LOCKED_DOOR1 else
		ifspritepal 1 sound LOCKED_GATE1 else
		ifspritepal 2 sound LOCKED_DOOR2 else
		ifspritepal 3 sound LOCKED_HITECH1 else
		ifspritepal 4 sound LOCKED_DOOR3 else
		ifspritepal 5 sound LOCKED_DOOR4 else
		ifspritepal 6 sound LOCKED_HITECH2 else
		ifspritepal 20 { sound LOCKED_DOOR2 set INTERNALCOUNT 20 }
		}
	resetcount
	}
 }

 ifn INTERNALCOUNT 0
 {
 sub INTERNALCOUNT 1
 ife INTERNALCOUNT 1 soundonce PISS_OFF
 }

 ifg temp9 0
	{
	sub temp9 1
	 ife temp9 1
		set PLAYER_VOICEOVER 5
	}

ifn LOTAGSAVED 0
{
cstator 256
ifhitweapon
	{
	ifspritepal 16
		{
		globalsound BUST_CONCRETE
		operateactivators LOTAGSAVED THISACTOR
		killit
		}
		else
		{
		ifwasweapon KNEE
			{
			ifspritepal 16
			globalsound BUST_CONCRETE
			else
			globalsound DOOR_KICK
			operateactivators LOTAGSAVED THISACTOR
			killit
			}
			else strength 1
		}
	}
}


enda

// ------------------------------------------------------------
// FOOTPRINT IN SNOW
// ------------------------------------------------------------

defstate FOOTPRINT_SNOW
ifaction 0
	{
	insertspriteq
	sizeat 24 24
	cstat 32
	action ZERO
	}
fall
ends

useractor notenemy SNOWPRINT1 state FOOTPRINT_SNOW enda
useractor notenemy SNOWPRINT2 state FOOTPRINT_SNOW enda

// ------------------------------------------------------------
// GLASS CRACK
// ------------------------------------------------------------

useractor notenemy 19615
ifn HITAGSAVED 0
	{
	cstat 32768
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		seta[].cstat CSTATSAVED
		globalsound HITGLASS
		globalsound SOMETHINGFROZE
		set HITAGSAVED 0
		}
	}
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	 ife RETURN 1 killit
	}
enda

// ------------------------------------------------------------
// CEILING FAN
// ------------------------------------------------------------


useractor notenemy 9682
ifand sprite[].cstat 32
ife LOTAGSAVED 0
{
ifaction 0
	{
	seta[].mdflags 16
	action ZERO
	}
ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 set HITAGSAVED 0
	}
else
	set command 256
}

enda

// ------------------------------------------------------------
// TORCH OFF
// ------------------------------------------------------------

useractor notenemy 9687
cstat 32768
killit
enda

// ------------------------------------------------------------
// GAS!
// ------------------------------------------------------------

defstate gas_stuff
ifn player_in_vehicle 1
	{
	ife FIRE_SUIT 0
		ife GAS_MASK 0
		   {
		  set ingas 10
		  soundonce CHOKING
		  getp[].posxv temp divvar temp 2 setp[].posxv temp divvar temp 2
		  getp[].posyv temp divvar temp 2 setp[].posyv temp divvar temp 2
		  switch CHAR
			  case 12
			  case 13
			  case 14
			  ifg PARMOUR 0 { getp[].shield_amount temp sub temp 1 setp[].shield_amount temp }
			  else addphealth -1
		  break
			  default
			  addphealth -1
		  break
		  endswitch
		  ifn player[].palette 2 setgamepalette 2
		  palfrom 40 20 20 0
		   }
		else
		  {
		  setp[].sound_pitch -256
		  ife ingas 0 { set GAS_MASK_PUTON 0 soundonce PUT_ON_EQUIP }
		  set ingas 30
		  ife FIRE_SUIT 0 set GAS_MASK 2
		  ifn player[].palette 2 setgamepalette 2
		   }
	}
ends

defstate heat_stuff
ife player_in_vehicle 0
	{
		soundonce COOKINGDEEPFRIER
		ife FIRE_SUIT 0
		   {
		  getp[].posxv temp divvar temp 2 setp[].posxv temp divvar temp 2
		  getp[].posyv temp divvar temp 2 setp[].posyv temp divvar temp 2
		  addphealth -2
		  palfrom 40 60 0 0
		   }
		else
		ife FIRE_SUIT 1 palfrom 10 60 0 0
	}
ends

useractor notenemy 7924
cstat 32768

ifaction ZERO
{
	ifn YVELSAVED 0
		{
		checkactivatormotion YVELSAVED
		ife RETURN 1 action 0
		}

}
else
ifaction 0
{
	ife ZVELSAVED 1 { action ZERO set ZVELSAVED 0 }
	ifn XVELSAVED 0
		{
		checkactivatormotion XVELSAVED
		ife RETURN 1 action ZERO
		}

	ifl EXTRASAVED 1
	{
	ifpdistl 6000 state hard_removefollower
		ifpdistl 5000
		ifcansee
		{
		ifspritepal 0 state gas_stuff
		else ifspritepal 2 state heat_stuff
		}
	}
	else
	ife EXTRASAVED 1
	{
	getp[].i PLAYER_IDENTITY
		ife sprite[PLAYER_IDENTITY].sectnum sprite[].sectnum
		{
		state hard_removefollower
		ifspritepal 0 state gas_stuff
		else ifspritepal 2 state heat_stuff
		}
	}
	else
	ife EXTRASAVED 2
	{
	getp[].i PLAYER_IDENTITY

		ife sprite[PLAYER_IDENTITY].sectnum sprite[].sectnum
		 ifg player[].posz sprite[].z
		{
		state hard_removefollower
		ifspritepal 0 state gas_stuff
		else ifspritepal 2 state heat_stuff
		}
	}
	else
	{
	dist temp6 THISACTOR player[].i
	ifl temp6 EXTRASAVED
		{
			state hard_removefollower
			ifcansee
			{
			ifspritepal 0 state gas_stuff
			else ifspritepal 2 state heat_stuff
			}
		}
	}
	ifspritepal 3
	{
	ifpdistl 1024
		{
		stopsound JGASMASK
		setp[].sound_pitch 0
		setgamepalette 0
		ifg ingas 0 sub ingas 1
		killit
		}
	}
}

enda

// ------------------------------------------------------------
// SEASONAL STUFF
// ------------------------------------------------------------

defstate XMAS_TYME
	ifaction 0
		{
		ifspritepal 3 break
		gettimedate temp temp temp temp temp2 temp temp temp
		ife temp2 11 action ZERO else killit
		}
ends

defstate JNEW_YEAR
ifaction 0
        {
        ifspritepal 3 nullop else
            {
            gettimedate temp temp temp temp2 temp3 temp temp temp
            ife temp2 28 ife temp3 11 action ZERO else
            ife temp2 6 ife temp3 0 action ZERO else
           killit
            }
        }
	ends

defstate HALOWEEN_TYME
	ifaction 0
		{
		ifspritepal 3 nullop else
			{
			gettimedate temp temp temp temp temp2 temp temp temp
			ife temp2 9 action ZERO else killit
			}
		}
ends

defstate AUTUMN_TYME
fall
	ifaction 0
		{
		ifspritepal 3 nullop else
			{
			gettimedate temp temp temp temp temp2 temp temp temp
			ife temp2 8 action ZERO else
			ife temp2 9 action ZERO else
			ife temp2 10 action ZERO else
			killit
			}
		}
ends

defstate BIRTHDAYS
	ifaction 0
		{
		gettimedate temp temp temp temp3 temp2 temp temp temp
		ife temp2 HITAGSAVED
			{
			ife temp3 LOTAGSAVED action ONE else killit
			}
		else ifvarvarn temp2 HITAGSAVED killit
		}
ends

useractor notenemy 4726 state XMAS_TYME enda
useractor notenemy 9697 state XMAS_TYME enda
spriteshadow 9699
useractor notenemy 9699 state XMAS_TYME enda
spriteshadow 9701
useractor notenemy 9701 state XMAS_TYME enda
useractor notenemy 9702 state XMAS_TYME enda

useractor notenemy 27010 state JNEW_YEAR enda

useractor notenemy 12514 state BIRTHDAYS enda

spriteshadow 9703
spritenoshade 9703
useractor notenemy 9703 state HALOWEEN_TYME // pumpkin

ifaction ZERO
	{
	ifrnd 16 seta[].shade 2
	else ifrnd 32 seta[].shade 3
	else ifrnd 64 seta[].shade 0
	else seta[].shade -2
	}

enda

spritenoshade 13916

useractor notenemy 13916 // fallen leaves

		 ifaction 0
			{
			randvar temp 64
			add temp 64
			seta[].xrepeat temp
			seta[].yrepeat temp
			seta[].shade sector[].floorshade
			randvar temp 2048
			seta[].ang temp
			cstat 32
			ifrnd 64 cstator 4
			ifrnd 64 cstator 8
			action ZERO
			}

state AUTUMN_TYME
enda

// ------------------------------------------------------------
// Lift arrow
// ------------------------------------------------------------


useractor notenemy 19623
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	 ife RETURN 1 { set LOTAGSAVED 0 spritepal 8 }
	}
enda

useractor notenemy 19624
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	 ife RETURN 1 { set LOTAGSAVED 0 spritepal 8 }
	}
enda

// ------------------------------------------------------------
// INVISIBLE BANG
// ------------------------------------------------------------

useractor notenemy 10510
sizeat 2 2
cstat 32768
hitradius 1024 20 20 20 128
ifspritepal 1 soundonce BUST_CONCRETE
ifspritepal 2 soundonce BUST_METAL
ifspritepal 3 { quake 26 soundonce BUST_CONCRETE }
ifspritepal 4 { quake 26 soundonce BUST_METAL }
ifspritepal 5 quake 26
ifspritepal 6 { quake 26 globalsound SLAB_FALL }
killit
enda

// ------------------------------------------------------------
// DOCUMENTS
// ------------------------------------------------------------


defstate document_evidence

			ife DOCUMENT 7670 // Are they reading the Dis Base mission sheet?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 2 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 2
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 7671 // Are they reading the gate opening sheet?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 64 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 64
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1

						killit
						}
				}
			ife DOCUMENT 9696 // Are they reading the 'For Providence' stock sheet?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 4 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 4
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1

						killit
						}
				}
			ife DOCUMENT 9748 // Are they reading the 'For Providence' paradigm sheet?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 1024 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 1024
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 9705 // Are they reading the 'Snow base' letter from Robinson?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 8 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 8
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1

						killit
						}
				}
			ife DOCUMENT 9706 // Are they reading the 'Snow base' letter from Costanza?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 16 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 16
						savegamevar EVIDENCE
						soundonce READ_DOCUMENT
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ifg DOCUMENT 9709 // Are they reading Lilith's diary from Providence?
			 ifl DOCUMENT 9714
				{
				set STORY_TRIGGER1 1
				readgamevar EVIDENCE
				ifand EVIDENCE 32 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 32
						savegamevar EVIDENCE
						soundonce BOOK_CLOSE
						add TREASURE_FOUND 4
						quote 220
						set DOCUMENT -1
						set MAXPAGES 0
						killit
						}
				}
			ife DOCUMENT 13848 // Are they reading the 'med-lab' letter from megabase?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 128 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 128
						savegamevar EVIDENCE
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 13849 // Are they reading the 'precog-lab' letter from megabase?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 256 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 256
						savegamevar EVIDENCE
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1

						killit
						}
				}
			ife LOTAGSAVED TRENT_MSG1 // Are they listening to Trenton's message to Jekyl?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 2048 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 2048
						savegamevar EVIDENCE
						add TREASURE_FOUND 4
						soundonce AUDIO_LOG
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife LOTAGSAVED TRENT_MSG2 // Are they listening to Trenton's message to Jekyl?
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 4096 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 4096
						savegamevar EVIDENCE
						add TREASURE_FOUND 4
						soundonce AUDIO_LOG
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ifg DOCUMENT 13912 // Are they reading Dr Jekyl's diary?
			 ifl DOCUMENT 13916
				{
				readgamevar EVIDENCE
				ifand EVIDENCE 512 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE 512
						savegamevar EVIDENCE
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1

						killit
						}
				}

			// ======================================================
			// EPISODE 2 DOCUMENTS
			// ======================================================

			ife DOCUMENT 17028 // Are they reading the letter to Yeung from Cheung?
				{
				readgamevar EVIDENCE_EP2
				set GOT_PASSWORD 1
				ifand EVIDENCE_EP2 1 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 1
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 17253 // Are they reading the note to Cheung about Yakuza?
				{
				readgamevar EVIDENCE_EP2
				ifand EVIDENCE_EP2 32 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 32
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 17255 // Are they reading the note from Cheung to Yoshima?
				{
				readgamevar EVIDENCE_EP2
				ifand EVIDENCE_EP2 16 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 16
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 18396 // Are they reading the password to the EAF file?
				{
				readgamevar EVIDENCE_EP2
				set GOT_PASSWORD 2
				ifand EVIDENCE_EP2 4 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 4
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 17325 // Are they reading captain's diary
				{
				readgamevar EVIDENCE_EP2
				ifand EVIDENCE_EP2 64 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 64
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ife DOCUMENT 17257 // Are they reading the PC password thing 2?
				{
				readgamevar EVIDENCE_EP2
				set GOT_PASSWORD 3
				ifand EVIDENCE_EP2 128 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 128
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ifg DOCUMENT 18338 // Are they reading the PATCOS project info?
			ifl DOCUMENT 18342
				{
				readgamevar EVIDENCE_EP2
				ifand EVIDENCE_EP2 1024 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 1024
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}
			ifg DOCUMENT 20148 // Are they reading the Zeta base beyonder file?
			ifl DOCUMENT 20154
				{
				readgamevar EVIDENCE_EP2
				ifand EVIDENCE_EP2 2048 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP2 2048
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

			// ======================================================
			// EPISODE 3 DOCUMENTS
			// ======================================================
			ife DOCUMENT 23737 // Are they reading the Gorrilla diary?
				{
				readgamevar EVIDENCE_EP3
				ifand EVIDENCE_EP3 1 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP3 1
						savegamevar EVIDENCE_EP3
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

			ife DOCUMENT 23801 // Are they reading the substance 34 report?
				{
				readgamevar EVIDENCE_EP3
				ifand EVIDENCE_EP3 8 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP3 8
						savegamevar EVIDENCE_EP3
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

			ife DOCUMENT 23804 // Are they reading the Shrinker theory?
				{
				readgamevar EVIDENCE_EP3
				ifand EVIDENCE_EP3 16 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP3 16
						savegamevar EVIDENCE_EP3
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

			ife DOCUMENT 23805 // Are they reading the SMART suit info?
				{
				readgamevar EVIDENCE_EP3
				ifand EVIDENCE_EP3 32 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP3 32
						savegamevar EVIDENCE_EP3
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

			ife DOCUMENT 20479 // Are they reading the Alien queen info?
				{
				readgamevar EVIDENCE_EP3
				ifand EVIDENCE_EP3 64 set DOCUMENT -1
					else
						{
						xorvar EVIDENCE_EP3 64
						savegamevar EVIDENCE_EP3
						add TREASURE_FOUND 4
						soundonce READ_DOCUMENT
						quote 220
						set DOCUMENT -1
						killit
						}
				}

ends

action DOC_SHEET -5300
action DOC_CLIPBOARD -210
action DOC_BOOK -1708

var document_read -1 2
useractor notenemy 9695
ifaction 0
{
set temp9 0
ifspritepal 1
	{
	spritepal 0
	action DOC_SHEET
	}
else
ifspritepal 2
	{
	spritepal 0
	action DOC_CLIPBOARD
	}
else
ifspritepal 3
	{
	spritepal 0
	action DOC_BOOK
	}
else
	{
	action ZERO
	cstat 32768
	}
}

ife temp9 0
{
ifpdistl 2048
	ifrnd 8 spawn GLINT
	else
ifrnd 4 spawn GLINT
}

ifpdistl 1024
{
ifn DOCUMENT -1
	{
	ife DOCUMENT 7935 // audio log?
		{
		ifactorsound THISACTOR LOTAGSAVED
			setmusicvolume 50
			else
			{
			sound AUDIO_LOG
			setmusicvolume 100
			state document_evidence
			set DOCUMENT -1
			set MAXPAGES 0
			set PAGETURNSOUND 0
			set temp9 1
			resetcount
			}
		}
	ifcount 13
	ifhitspace
		{
		ifaction DOC_BOOK soundonce BOOK_CLOSE
		else ifspritepal 5 soundonce BOOK_CLOSE
		state document_evidence
		set DOCUMENT -1
		set MAXPAGES 0
		set PAGETURNSOUND 0
		set temp9 1
		resetcount
		}
	}
else
 ifp pfacing
  ifcansee
  ife DOCUMENT -1
  {
   set player_use 0
   ifcount 13
   ifhitspace
		{
		set hit_key 30
		set temp9 1
		ifaction DOC_BOOK soundonce BOOK_OPEN
		else ifspritepal 5 soundonce BOOK_OPEN
		else ifn LOTAGSAVED 0 soundoncevar LOTAGSAVED
		else soundonce READ_DOCUMENT
		set DOCUMENT HITAGSAVED
		ife EXTRASAVED -1 set MAXPAGES 0
		ifg EXTRASAVED 0 { set MAXPAGES EXTRASAVED set PAGENUMBER 0 }

		// A bit hacky - add YVELSAVED to STORY_TRIGGER2, only when triggered for the first time
		// Use this to tie map story progression to whether or not the player found certain documents
		ife document_read -1
			ifg YVELSAVED 0 { add STORY_TRIGGER2 YVELSAVED }
		set document_read 1

		ifaction DOC_BOOK set PAGETURNSOUND 1199 else
		set PAGETURNSOUND XVELSAVED
		lockplayer 13
		resetcount
		}
   }
}
enda

// ------------------------------------------------------------
// LIGHT
// ------------------------------------------------------------

action NULLL 0

useractor notenemy 5792
ifspritepal 3 break

ife DAYNIGHT 1 killit
else ife DAYNIGHT 3 killit

ifaction 0
{
geta[].pal temp
randvar temp2 10
ifl temp2 5 action ZERO else spritepal 4
}

ifaction ZERO
{
ifcount 520
	{
	ifrnd 6 cstat 32768 else seta[].pal temp
	resetcount
	}
}

enda

// ------------------------------------------------------------
// EVIDENCE FOR PROVIDENCE
// ------------------------------------------------------------

useractor notenemy 9666

ifpdistl 1024
 ifp pfacing
  ifcansee
  {
  readgamevar EVIDENCE
  ifand EVIDENCE 1 break
   set player_use 0
   ifhitspace
	    {
		set hit_key 30
		xorvar EVIDENCE 1
		add TREASURE_FOUND 4
		soundonce READ_DOCUMENT
		savegamevar EVIDENCE
		quote 220
		killit
		}
   }


enda

// *************************************************
// AUTOSAVE
// *************************************************

useractor notenemy 9412
cstat 32768

ifn HITAGSAVED 0
{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
	{
        ife userdef[].autosave YES
		    set saving AUTOSAVE_START
		killit
	}
}
else
{
	ife sprite[].ang 512
	 ife player[].cursectnum sprite[].sectnum
	{
        ife userdef[].autosave YES
		    set saving AUTOSAVE_START
		killit
	}
	ifpdistl 1024
	{
        ife userdef[].autosave YES
		    set saving AUTOSAVE_START
		killit
	}
}

enda

// *************************************************
// NIGHT
// *************************************************

useractor notenemy 3821
cstat 32768
ifmultiplayer killit
ife DAYNIGHT 0
	{
	gettimedate temp temp temp2 temp temp3 temp temp temp
		ifg temp3 9 // if it's near winter, become darker 'earlier'
			{
			ifg temp2 15 set DAYNIGHT 2 else
			ifg temp2 13 set DAYNIGHT 3 else
			ifl temp2 6 set DAYNIGHT 2 else
			ifl temp2 9 set DAYNIGHT 4 else
			set DAYNIGHT 1
			}
			else
			{
			ifg temp2 19 set DAYNIGHT 2 else // Night
			ifg temp2 17 set DAYNIGHT 3 else // Evening
			ifl temp2 5 set DAYNIGHT 2 else // Early morning
			ifl temp2 8 set DAYNIGHT 4 else // Morning
			set DAYNIGHT 1  // Daytime
			}
	}

ifspritepal 0
{
		set temp 0
		whilevarvarn temp NUMSECTORS
		{
  		ifand sector[temp].ceilingstat 1
  			{
				ifn sector[temp].ceilingpicnum 179
				{
				ife DAYNIGHT 1
					{
					ife temp3 11 sets[temp].ceilingpicnum 13260 else
						{
						sets[temp].ceilingpicnum 94
						ifg VOLUME 3 sets[temp].ceilingpal 106 else
						sets[temp].ceilingpal 1
						}
					}
				ife DAYNIGHT 2 sets[temp].ceilingpicnum 95
				ife DAYNIGHT 3 sets[temp].ceilingpicnum 99
				ife DAYNIGHT 4 sets[temp].ceilingpicnum 94
				}
			ife sector[temp].visibility 0
				{
				ife DAYNIGHT 1
					{
					ife temp3 11 sets[temp].visibility 244 else
					sets[temp].visibility 240
					}
				ife DAYNIGHT 2 sets[temp].visibility 0
				ife DAYNIGHT 3 sets[temp].visibility 244
				ife DAYNIGHT 4 sets[temp].visibility 244
				}
  			}
  		add temp 1
		}
		killit
}
else
ifspritepal 1
{
ife DAYNIGHT 2 killit else
ifn DAYNIGHT 2
	{
	geta[].ang temp3

	sin my temp3
	cos mx temp3

	hitscan sprite[].x sprite[].y sprite[].z sprite[].sectnum mx my 0 hitsect hitwall hitsprite hitx hity hitz 16777280

	ifn hitwall -1
		{
		setwall[hitwall].picnum HITAGSAVED
		ife LOTAGSAVED 0
			{
			setwall[hitwall].xrepeat 32
			setwall[hitwall].yrepeat 32
			}
		setwall[hitwall].shade sprite[].shade
		}

	killit
	}
}
else
ifspritepal 2
{
ife DAYNIGHT 2 killit else
ifn DAYNIGHT 2
	{
	geta[].ang temp3

	sin my temp3
	cos mx temp3

	hitscan sprite[].x sprite[].y sprite[].z sprite[].sectnum mx my 0 hitsect hitwall hitsprite hitx hity hitz 16777280

	ifn hitwall -1
		{
		setwall[hitwall].picnum HITAGSAVED
		ife LOTAGSAVED 0
			{
			setwall[hitwall].xrepeat 128
			setwall[hitwall].yrepeat 128
			}
		setwall[hitwall].shade sprite[].shade
		}

	killit
	}
}
else
ifspritepal 3 // AMC Base
{
set amc_base 1
ifaction 0
	{
	gettimedate temp temp temp2 temp temp3 temp temp temp
	ife temp3 11 action ZERO
	else ife temp3 1 action ZERO
	else ife temp3 2 action ZERO
	else
		{
		readgamevar seed_initializer
		ifrnd 2
			{
			set rainpal 13
			set raincstat 514
			set rainstart 16
			set raining 16
			set rainradius 12000
			set RAINREPLACE 1
			set temp9 1
			}
		action ZERO
		}
	}

gettimedate temp temp temp2 temp temp3 temp temp temp
		set temp 0
		whilevarvarn temp NUMSECTORS
		{
			ifand sector[temp].ceilingstat 1
				{
					ife sector[temp].ceilingpicnum 95
					{
					ife DAYNIGHT 1 // daytime?
						{
						ifg VOLUME 3 sets[temp].ceilingpal 54
						ife temp3 11 sets[temp].ceilingpicnum 13260 // December?
						else ife temp3 0 sets[temp].ceilingpicnum 13260 // January?
						else ife temp3 1 sets[temp].ceilingpicnum 13260 // Febuary?
						else
							{
							ife temp9 1 sets[temp].ceilingpicnum 79
							else
								{
								ifg VOLUME 3 sets[temp].ceilingpal 54 else
								sets[temp].ceilingpal 1
								sets[temp].ceilingpicnum 94 // Default
								}
							}
						}
					ife DAYNIGHT 2
						{
						ifg VOLUME 3 sets[temp].ceilingpal 124
						ife temp3 11 sets[temp].ceilingpicnum 78 // December?
						else ife temp3 0 sets[temp].ceilingpicnum 78 // January?
						else ife temp3 1 sets[temp].ceilingpicnum 78 // Febuary?
						else sets[temp].ceilingpicnum 95 // Default
						}
					ife DAYNIGHT 3 { ifg VOLUME 3 sets[temp].ceilingpal 30 sets[temp].ceilingpicnum 99 }
					ife DAYNIGHT 4 { ifg VOLUME 3 sets[temp].ceilingpal 57 sets[temp].ceilingpicnum 94 }
					}
				ife sector[temp].visibility 0
					{
					ife DAYNIGHT 1
						{
						ife temp3 11 sets[temp].visibility 244 // December?
						else ife temp3 0 sets[temp].visibility 244 // January?
						else ife temp3 1 sets[temp].visibility 244 // Febuary?
						else sets[temp].visibility 240
						}
					ife DAYNIGHT 2 sets[temp].visibility 0
					ife DAYNIGHT 3 sets[temp].visibility 244
					ife DAYNIGHT 4 sets[temp].visibility 244
					}
				}
  		add temp 1
		}
		killit
}
else
ifspritepal 6 // AMC Base
{
set amc_base 2
ifaction 0
	{
	gettimedate temp temp temp2 temp temp3 temp temp temp
	ife temp3 11 action ZERO
	else ife temp3 1 action ZERO
	else ife temp3 2 action ZERO
	else
		{
		readgamevar seed_initializer
		ifrnd 96
			{
			set rainpal 13
			set raincstat 514
			set rainstart 16
			set raining 16
			set rainradius 12000
			set RAINREPLACE 1
			set temp9 1
			}
		action ZERO
		}
	}

gettimedate temp temp temp2 temp temp3 temp temp temp
		set temp 0
		whilevarvarn temp NUMSECTORS
		{
			ifand sector[temp].ceilingstat 1
				{
					ife sector[temp].ceilingpicnum 95
					{
					ife DAYNIGHT 1
						{
						ife temp3 11 sets[temp].ceilingpicnum 13260 // December?
						else ife temp3 0 sets[temp].ceilingpicnum 13260 // January?
						else ife temp3 1 sets[temp].ceilingpicnum 13260 // Febuary?
						else
							{
							ife temp9 1 sets[temp].ceilingpicnum 79
							else
								{
								sets[temp].ceilingpal 1
								sets[temp].ceilingpicnum 94 // Default
								}
							}
						}
					ife DAYNIGHT 2
						{
						ife temp3 11 sets[temp].ceilingpicnum 78 // December?
						else ife temp3 0 sets[temp].ceilingpicnum 78 // January?
						else ife temp3 1 sets[temp].ceilingpicnum 78 // Febuary?
						else sets[temp].ceilingpicnum 95 // Default
						}
					ife DAYNIGHT 3 sets[temp].ceilingpicnum 99
					ife DAYNIGHT 4 sets[temp].ceilingpicnum 94
					}
				ife sector[temp].visibility 0
					{
					ife DAYNIGHT 1
						{
						ife temp3 11 sets[temp].visibility 244 // December?
						else ife temp3 0 sets[temp].visibility 244 // January?
						else ife temp3 1 sets[temp].visibility 244 // Febuary?
						else sets[temp].visibility 240
						}
					ife DAYNIGHT 2 sets[temp].visibility 0
					ife DAYNIGHT 3 sets[temp].visibility 244
					ife DAYNIGHT 4 sets[temp].visibility 244
					}
				}
  		add temp 1
		}
		killit
}
else
ifspritepal 8
	{
	geta[].sectnum temp
	ife DAYNIGHT 1
		{
		ife temp3 11 sets[temp].visibility 244 // December?
		else ife temp3 0 sets[temp].visibility 244 // January?
		else ife temp3 1 sets[temp].visibility 244 // Febuary?
		else sets[temp].visibility 240
		}
	ife DAYNIGHT 2 sets[temp].visibility 0
	ife DAYNIGHT 3 sets[temp].visibility 244
	ife DAYNIGHT 4 sets[temp].visibility 244
	killit
	}
	else ifspritepal 10 // shooting range
	{
		// If close enough and within view, raise weapon
		ldist temp THISACTOR PLAYER_IDENTITY

		ifl temp 6096
		{
			set HITSCAN_TO_ACTOR PLAYER_IDENTITY
			state hitscanTo

			ife hitsprite -1
			{
				ife amc_base 0
				{
					set hide_xhair 1
					set amc_base 1
				}
			}
		    else ifn hitsprite PLAYER_IDENTITY
			  ifn sprite[hitsprite].picnum 9154 // those metal divider things
			{
				ife amc_base 0
				{
					set hide_xhair 1
					set amc_base 1
				}
			}
			else ife amc_base 1
			{
				set hide_xhair 0
				set amc_base 0
			}
		}
		else ife amc_base 0
		{
			set hide_xhair 1
			set amc_base 1
		}

		/*
		ifpdistl 6096 ifcansee
		 ife amc_base 1
		{
			set hide_xhair 0
			set amc_base 0
		}
		else ifpdistg 6095
			ife amc_base 0
		{
			set hide_xhair 1
			set amc_base 1
		}
		*/
	}
enda

// ====================================================================================
// SEASON CHANGER
// ====================================================================================

useractor notenemy 13559
cstat 32768

	ifspritepal 3 // changes floor to snow if it's winter
	{
		gettimedate temp temp temp temp temp2 temp temp temp
		ife temp2 11 sets[].floorpicnum 9704
		ife temp2 0 sets[].floorpicnum 9704
		ife temp2 1 sets[].floorpicnum 9704
		killit
	}
	else
	ifspritepal 4 // changes floor to leaves if it's autumn
	{
		gettimedate temp temp temp temp temp2 temp temp temp
		ife temp2 8 sets[].floorpicnum 13917
		ife temp2 9 sets[].floorpicnum 13917
		ife temp2 10 sets[].floorpicnum 13917
		killit
	}
	else
	ifspritepal 5 // sets weather to cold and snowy if it's Winter, or just cold if Autumn
	{
		gettimedate temp temp temp temp temp2 temp temp temp
		ife temp2 8 // September?
			{
			set weather_type 1
			}
			else
		ife temp2 9 // October?
			{
			set weather_type 1
			}
			else
		ife temp2 10 // November?
			{
			set weather_type 1
			}
			else
		ife temp2 11 // December?
			{
			set RAINREPLACE 0
			set weather_type 1
			set rainstart -8
			set raining -8
			set rainradius 12000
			set snowceiling 65536
			set ceiltext -1
			}
			else
		ife temp2 0 // January?
			{
			set RAINREPLACE 0
			set weather_type 1
			set rainstart -8
			set raining -8
			set rainradius 12000
			set snowceiling 65536
			set ceiltext -1
			}
			else
		ife temp2 1 // Feburary?
			{
			set RAINREPLACE 0
			set weather_type 1
			set rainstart -8
			set raining -8
			set rainradius 12000
			set snowceiling 65536
			set ceiltext -1
			}

			else killit
	}

enda

//
// ------------------------------------------------------------
// ABYSS FORMING PATH
// ------------------------------------------------------------
//

useractor notenemy 8444

ifaction 0
{
ifspritepal 3 break
sizeat 1 1
cstat 32768
spawn 10353
ifn HITAGSAVED 0 action BREAK
else action ZERO
}

ifaction BREAK
{
sleeptime 0
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	seta[].cstat temp4
	action FORM
	}
}
else
ifaction ZERO
{
ifpdistl 3072
	{
	seta[].cstat temp4
	action FORM
	}
}
else
ifaction FORM
{
geta[].ang temp3
add temp3 256
seta[].ang temp3
geta[].xrepeat temp2
add temp2 2
seta[].xrepeat temp2
seta[].yrepeat temp2
ifg temp2 24
	{
	sizeat 24 24
	seta[].ang temp8
	ifspritepal 6 sound RETRO_DOOR else
	sound ROCK_IMPACT
	action FORMED
	}
}
else
ifaction FORMED
{
move 0
ife HITAGSAVED 0
ifpdistg 3071
	{
	action UNFORM
	}
}
else
ifaction UNFORM
{
geta[].ang temp3
sub temp3 256
seta[].ang temp3
sizeto 1 1
ife sprite[].xrepeat 1
	{
	seta[].ang temp8
	cstat 32768
	spawn GUNSMOKE
	action ZERO
	}
}
enda

//
// ------------------------------------------------------------
// EGYPT FORMING PATH
// ------------------------------------------------------------
//

useractor notenemy 23784

ifaction 0
{
ifspritepal 3 break
sizeat 1 1
cstat 32768
seta[].blend 255
spawn 10353
ifn HITAGSAVED 0 action BREAK
else action ZERO
}

ifaction BREAK
{
sleeptime 0
checkactivatormotion HITAGSAVED
ife RETURN 1 action ZERO
}
else
ifaction ZERO
{
ifpdistl 3072
	{
	seta[].cstat 35
	action FORM
	}
}
else
ifaction FORM
{
geta[].ang temp3
add temp3 256
seta[].ang temp3
geta[].xrepeat temp2
add temp2 2
seta[].xrepeat temp2
seta[].yrepeat temp2
ifg temp2 24
	{
	sizeat 24 24
	seta[].ang temp8
	sound SANG_SHIELD_UP
	action FORMED
	}
}
else
ifaction FORMED
{
move 0
ifpdistg 3071
	{
	action UNFORM
	}
}
else
ifaction UNFORM
{
geta[].ang temp3
sub temp3 256
ifl temp3 0 set temp3 2048
clamp temp 0 2048
seta[].ang temp3
sizeto 1 1
ife sprite[].xrepeat 1
	{
	seta[].ang temp8
	cstat 32768
	action ZERO
	}
}
enda

//
// ------------------------------------------------------------
// DAMAGED WIRE
// ------------------------------------------------------------
//

defstate shorted_stuff

ifaction 0
 ifrnd 8
  ifcansee
	{
	action ONE
	shoot SPARK shoot SPARK shoot SPARK
	espawn 8433
	seta[RETURN].pal 1
	sound SHORT_CIRCUIT
	resetcount
	}

ifaction ONE
 ifcount 4
	{
	action 0
	resetcount
	}

ends

useractor notenemy 4430 state shorted_stuff enda
useractor notenemy 4433 state shorted_stuff enda
useractor notenemy 4480 state shorted_stuff enda


//
// ------------------------------------------------------------
// ZAX BOSS
// ------------------------------------------------------------
//

useractor notenemy 5460

ife LOTAGSAVED 1
{
	ifaction 0
	{
	cstat 257
	strength 1
	action ONE
	}

	ifaction ONE
	{
	ifhitweapon
		{
		spawn EXPLOSION2
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
		spawn 8433
		hitradius 2048 60 70 80 90
		quake 26
		globalsound LASERTRIP_EXPLODE
		ifn HITAGSAVED 0
			{
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			}
		killit
		}
	}
}
else
{
	ifaction 0
	{
	cstator 257
	ifspritepal 3 strength 1600 else
	strength 600
	action ONE
	}

	ifaction ONE
	{
	ifspritepal 3 nullop else
		{
		ifcansee
		ifcanseetarget
		 set CURBOSS THISACTOR


		ifhitweapon palfrom 20 20 0 20
		}
	ifdead killit

	}
}

enda

//
// ------------------------------------------------------------
// NET
// ------------------------------------------------------------
//

action NETOPENUP 0 4 1 1 15
action NETOPEN 3

move NETFLY 248

spriteshadow 10298

useractor notenemy 10298 // NET FLY

ifaction 0
{
sizeat 32 32
move NETFLY geth
geta[].z temp
sub temp 4098
seta[].z temp
action NETOPENUP
}

ifaction NETOPENUP
{
ifactioncount 3 { action NETOPEN resetcount }
}

ifaction NETOPEN
{
ifnotmoving { sound ROPE_CLIMB sound METALCLINK killit }
else
ifcount 24 { fall iffloordistl 4 { spawn 10301 killit } }
}

ifpdistl 898
{
sound ROPE_CLIMB
sound METALCLINK
wackplayer
ife SKILL_LEVEL 1 set caught_in_net 30 else
ife SKILL_LEVEL 2 set caught_in_net 50 else
ife SKILL_LEVEL 3 set caught_in_net 60 else
ife SKILL_LEVEL 4 set caught_in_net 70
killit
}

enda

useractor notenemy 10301 // NET ON FLOOR
fall

ifaction 0
	{
	cstat 32
	sizeat 32 32
	move 0
	resetcount
	action ZERO
	}

ifcount 256 killit
enda

//
// ------------------------------------------------------------
// SCANNER DATA
// ------------------------------------------------------------
//

var scanned_already 0 2

defstate scan_info_blips

ife MISSION_UP 1
 ife MISSIONPOS 100
  ife MISSIONSCREEN 5
   ifp pfacing
    ifpdistl 4096
   {
   ife move_flag 0
	{
	soundonce PU_PICKUP
	ife POLYMERS 0 readgamevar POLYMERS
	// WHAT TO ADD N SHIT
	ifactor 10337 { set scanned_already 6 setactorsoundpitch THISACTOR PU_PICKUP 45 }
	else ifactor 10338 { set scanned_already 10 setactorsoundpitch THISACTOR PU_PICKUP 64 }
	else ifactor 10339 { set scanned_already 20 setactorsoundpitch THISACTOR PU_PICKUP 128 }
	else ifactor 103341 { set scanned_already 20 setactorsoundpitch THISACTOR PU_PICKUP 128 }
	else ifactor 10346 { set scanned_already 6 setactorsoundpitch THISACTOR PU_PICKUP 45 }
	else ifactor 10347 { set scanned_already 10 setactorsoundpitch THISACTOR PU_PICKUP 64 }
	else ifactor 10353 set scanned_already 1
	else ifactor 10359 { set scanned_already 10 setactorsoundpitch THISACTOR PU_PICKUP 64 }
	else ifactor 10360 { set scanned_already 20 setactorsoundpitch THISACTOR PU_PICKUP 128 }
	else ifactor 10361 { set scanned_already 30 setactorsoundpitch THISACTOR PU_PICKUP 192 }
	else ifactor 10362 { set scanned_already 40 setactorsoundpitch THISACTOR PU_PICKUP 256 }
	else ifactor 20466 { set scanned_already 10 setactorsoundpitch THISACTOR PU_PICKUP 64 }
	else set scanned_already 4

	// RS_B_SCANMODULE
	ife BASE_RESEARCH[6] 2 // has the scan module researched?
		{
		set temp4 TECH_LEVEL
		mul temp4 2 // the bonus is the the tech level x2 as a percentage

		set temp5 scanned_already
		mul temp5 temp4
		div temp5 100
		// this gets the percentage increase
		set temp6 scanned_already
		add temp6 temp5

		add POLYMERS temp6
		ifg temp5 0 // only add bonus if they actually got one
			{
			qputs 2090 +         POLYMERS: %d ^1+%d SCAN ^32+%d BONUS
			qsprintf 2090 2090 POLYMERS scanned_already	temp5
			}
		else
			{
			qputs 2090 +         POLYMERS: %d ^1+%d SCAN
			qsprintf 2090 2090 POLYMERS scanned_already
			}
		}
	else
		{
		add POLYMERS scanned_already
		qputs 2090 +         POLYMERS: %d ^1+%d SCAN
		qsprintf 2090 2090 POLYMERS scanned_already
		}
	userquote 2090
	savegamevar POLYMERS
	set move_flag 1
	ifactor 10352
		{
		ifn LOTAGSAVED 0
				{
				ife LOTAGSAVED 5 // Charon mission?
					{
					readgamevar EVIDENCE_EP3
					ifand EVIDENCE_EP3 4 nullop
					else
						{
						xorvar EVIDENCE_EP3 4
						savegamevar EVIDENCE_EP3
						globalsound PROMOTION
						userquote 289
						state getfactornormalizedframerate
						set DISP_KEY -150
						mul DISP_KEY NORMALIZED_FACTOR
						set DISP_KEY_PAL 12
						set DISP_KEY_PIC 20376
						killit
						}
					}
				else
					{
					readgamevar FOUND_ADS
					ifvarvarand FOUND_ADS LOTAGSAVED nullop
						else
							{
							soundonce RESEARCH_DATA
							xorvarvar FOUND_ADS LOTAGSAVED
							savegamevar FOUND_ADS
							ife LOTAGSAVED 1 quote 284 // MDF Plasma gun?
                                                        else quote 135
							ife LOTAGSAVED 2 // acupressure melee research?
								{
								readarrayfromfile MELEE_RESEARCH F_DOJO_AMCTC
								ife MELEE_RESEARCH[3] 0
									{
									state getfactornormalizedframerate
									set DISP_RES -240
									mul DISP_RES NORMALIZED_FACTOR
									set DISP_RES_PAL 22
									set DISP_RES_PIC 13344
									setarray MELEE_RESEARCH[3] 1
									writearraytofile MELEE_RESEARCH F_DOJO_AMCTC
									}
								}
							ife LOTAGSAVED 4 // scuba research?
								{
								readarrayfromfile PERSONNEL_RESEARCH F_PERSONNEL_AMCTC
								ife PERSONNEL_RESEARCH[17] 0
									{
									state getfactornormalizedframerate
									set DISP_RES -240
									mul DISP_RES NORMALIZED_FACTOR
									set DISP_RES_PAL 1
									set DISP_RES_PIC 22500
									setarray PERSONNEL_RESEARCH[17] 1
									writearraytofile PERSONNEL_RESEARCH F_PERSONNEL_AMCTC
									}
								}
							ife LOTAGSAVED 8 // Ceres location
								{
								userquote 323
								state getfactornormalizedframerate
								set DISP_KEY -150
								mul DISP_KEY NORMALIZED_FACTOR
								set DISP_KEY_PAL 15
								set DISP_KEY_PIC 9714
								}
							ife LOTAGSAVED 16 // Haumea location
								{
								userquote 323
								state getfactornormalizedframerate
								set DISP_KEY -150
								mul DISP_KEY NORMALIZED_FACTOR
								set DISP_KEY_PAL 15
								set DISP_KEY_PIC 9714
								}
							ife LOTAGSAVED 32 // Makemake location
								{
								userquote 323
								state getfactornormalizedframerate
								set DISP_KEY -150
								mul DISP_KEY NORMALIZED_FACTOR
								set DISP_KEY_PAL 15
								set DISP_KEY_PIC 9714
								}
							ife LOTAGSAVED 64 // Eris location
								{
								userquote 323
								state getfactornormalizedframerate
								set DISP_KEY -150
								mul DISP_KEY NORMALIZED_FACTOR
								set DISP_KEY_PAL 15
								set DISP_KEY_PIC 9714
								}
							}
					}
				}
		}
	}
   sizeto 16 16
   cstat 1152
   ifaction 0 action ZERO
   }
   else
   {
   action 0
   sizeat 2 2
   cstat 32768
   }
ends

useractor notenemy 10334 state scan_info_blips enda
useractor notenemy 10335 state scan_info_blips enda
useractor notenemy 10336 state scan_info_blips enda
useractor notenemy 10337 state scan_info_blips enda
useractor notenemy 10338 state scan_info_blips enda
useractor notenemy 10339 state scan_info_blips enda
useractor notenemy 10340 state scan_info_blips enda
useractor notenemy 10341 state scan_info_blips enda
useractor notenemy 10342 state scan_info_blips enda
useractor notenemy 10343 state scan_info_blips enda
useractor notenemy 10344 state scan_info_blips enda
useractor notenemy 10345 state scan_info_blips enda
useractor notenemy 10346 state scan_info_blips enda
useractor notenemy 10347 state scan_info_blips enda
useractor notenemy 10348 state scan_info_blips enda
useractor notenemy 10349 state scan_info_blips enda
useractor notenemy 10350 state scan_info_blips enda
useractor notenemy 10351 state scan_info_blips enda
useractor notenemy 10352 state scan_info_blips enda
useractor notenemy 10353 state scan_info_blips enda
useractor notenemy 10354 state scan_info_blips enda

useractor notenemy 10359 state scan_info_blips enda
useractor notenemy 10360 state scan_info_blips enda
useractor notenemy 10361 state scan_info_blips enda
useractor notenemy 10362 state scan_info_blips enda


useractor notenemy 20464 state scan_info_blips enda
useractor notenemy 20465 state scan_info_blips enda
useractor notenemy 20466 state scan_info_blips enda
useractor notenemy 20467 state scan_info_blips enda
useractor notenemy 20468 state scan_info_blips enda
useractor notenemy 20469 state scan_info_blips enda
useractor notenemy 20470 state scan_info_blips enda
useractor notenemy 20471 state scan_info_blips enda

//
// ------------------------------------------------------------
// POSTERS
// ------------------------------------------------------------
//

useractor notenemy 7038

ifaction 0
	{
	seta[].blend 255
	espawn 10352
	set temp3 sprite[].x
	add temp3 64
	geta[].ang temp4
	add temp4 512
	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
	seta[RETURN].x temp5
	seta[RETURN].y temp6
	setactorvar[RETURN].LOTAGSAVED 1
	action ZERO
	}

enda

useractor notenemy 17133

ifaction 0
	{
	seta[].blend 255
	espawn 10352
	set temp3 sprite[].x
	add temp3 64
	geta[].ang temp4
	add temp4 512
	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
	seta[RETURN].x temp5
	seta[RETURN].y temp6
	setactorvar[RETURN].LOTAGSAVED 2
	action ZERO
	}

enda

useractor notenemy 22495 // Scuba research

ifaction 0
	{
	seta[].blend 255
	espawn 10352
	set temp3 sprite[].x
	add temp3 64
	geta[].ang temp4
	add temp4 512
	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
	seta[RETURN].x temp5
	seta[RETURN].y temp6
	setactorvar[RETURN].LOTAGSAVED 4
	action ZERO
	}

enda

//
// ------------------------------------------------------------
// DOOR LIFT
// ------------------------------------------------------------
//

useractor notenemy DOOR_LIFT
cstat 32768

ifaction 0
{
ifg temp9 0 sub temp9 1
ifpdistl 768
 ifp pfacing
	switch CHAR
	case 0
	case 1
	case 2
	case 7
	case 12
    ife p_stamina 100
	ife temp9 0
	{
	set player_use 0
	ifhitspace
		{
		ifp pducking
			{
			set hit_key 30
			action ZERO
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			ife CHAR 0 soundonce JSTRUGGLE
			setp[].movement_lock 15
			resetcount
			}
		else quote 246
		}
	}
	break
	default
	break
	endswitch
}

ifaction ZERO
{
add temp9 1 // This is the counter
state lower_weapon
ife CHAR 0 soundonce JSTRUGGLE
ifrnd 6 wackplayer

set player_use 0
sub p_stamina 2
	ife player_hitspace 1
		{
		set hit_key 30
		randvar temp 7
		add temp 2
		sub temp SKILL_LEVEL
		ifp pboosted add p_stamina 20 else add p_stamina temp
		set player_hitspace 2
		ifrnd 128 setp[].rotscrnang -16 else setp[].rotscrnang 16
		}

ifl p_stamina 0 // If the player runs out of strength...
	{
	stopsound JSTRUGGLE
	ife CHAR 0 sound JGASP2
	wackplayer
	setp[].movement_lock 0
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	action 0
	}
else
ifg temp9 LOTAGSAVED // Otherwise, success!
	{
	setp[].movement_lock 0
	killit
	}
}


enda

//
// ------------------------------------------------------------
// SANG BLOOD SIGIL
// ------------------------------------------------------------
//

action SIGIL_UNLOCK 0
action SIGIL_OPEN 0

useractor notenemy 9433

ifaction 0
{
seta[].blend 255
geta[].shade temp
ifl temp 15 ifcount 1 { add temp 1 seta[].shade temp resetcount }
spritepal 5
ifpdistl 1024
 ifp pfacing
  ife CHAR 4
	{
	action ZERO
	}
}

ifaction ZERO
{
spritepal 0
geta[].shade temp
ifg temp -15 ifcount 3 { sub temp 1 seta[].shade temp resetcount }
ifpdistg 1024 action 0
 else
  ifp pfacing
  {
  set player_use 0
  ifge player[].steroids_amount 400 setp[].inven_icon 2
   ifhitspace
	{
	ifl PHEALTH 4 set PLAYER_VOICEOVER 14
	else
		{
		set hit_key 30
		globalsound CULTIST_HAUNT
		action SIGIL_UNLOCK
		setp[].movement_lock 15
		resetcount
		}
	}
	else
	ifp pboosted
		{
		userquote 12
		setp[].steroids_amount 0
		setp[].inven_icon 0
		palfrom 20 0 20 0
		addphealth 50
		soundonce HEART_LEECHF
		action SIGIL_OPEN
		}
  }
}

ifaction SIGIL_UNLOCK
{
palfrom 20 40 0 0
state lower_weapon
ifcount 26
	{
	addphealth -5
	soundonce HEART_LEECHF
	soundonce SHURT2
	wackplayer
	action SIGIL_OPEN
	setp[].movement_lock 0
	}
}

ifaction SIGIL_OPEN
{
geta[].shade temp
ifl temp 25 ifcount 1 { add temp 1 seta[].shade temp resetcount }
ife temp 25 { operateactivators HITAGSAVED THISACTOR operaterespawns HITAGSAVED operatemasterswitches HITAGSAVED killit }
}

enda

//
// ------------------------------------------------------------
// WOOD BOARDS
// ------------------------------------------------------------
//

action WBROKE 0

defstate WOOD_BOARD_STUFF
ifspritepal 5 { ifaction 0 {  action WBROKE strength 0 set temp4 0 spritepal 3 } }
ifspritepal 3
{
ifaction 0 { action ZERO strength 500 cstat 273 set temp4 1 }


ifaction ZERO
{
cstat 273

ifcount 13
	{
	findnearactor3d SHOOTME 1024 temp
	ifn temp -1
		{
		sound ZOMBIE_BATTER
		geta[].extra temp2
		sub temp2 3
		ifl temp2 0
			{
			set temp4 0
			sound WOODBREAK
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			action WBROKE
			break
			}
		seta[].extra temp2
		}
	resetcount
	}
}


ifhitweapon
ife temp4 1
{
ifstrength 0
	{
	set temp4 0
	sound WOODBREAK
	shoot WOODSCRAP
	shoot WOODSCRAP
	shoot WOODSCRAP
	shoot WOODSCRAP
	shoot WOODSCRAP
	action WBROKE
	}
}


ifaction WBROKE
{
cstat 32768
ife temp4 2
	{
	strength 500
	action ZERO
	set temp4 1
	}
}

}
ends

useractor notenemy 3906 state WOOD_BOARD_STUFF enda
useractor notenemy 8172 state WOOD_BOARD_STUFF enda
useractor notenemy 10646 state WOOD_BOARD_STUFF enda
useractor notenemy 12429 state WOOD_BOARD_STUFF enda

//
// ------------------------------------------------------------
// WOODEN BOARDS TO REBUILD
// ------------------------------------------------------------
//

spriteshadow 10578

defstate rebuild_board
ifn temp -1
{
getav[temp].temp4 temp2
ife temp2 0
	{
	setactorvar[temp].temp4 2
	quote 238
	set PLAYER_VOICEOVER 11
	soundonce COFFIN_KNOCK
	ifmultiplayer
		{
		qgetsysstr 356 STR_PLAYERNAME
		qstrcat 356 551
		userquote 356
		}
	killit
	}
}
ends

useractor notenemy 10578
fall
sizeat 16 16
cstator 257

ifaction 0 action ZERO

ifaction ZERO cstat 257

ifpdistl 1024
 ifp pfacing
  ifp palive
  ifaction ZERO
  {
  set player_use 0
   ifhitspace
    ifcount 13
	{
	set hit_key 30
	set PLAYER_VOICEOVER 2
	sound LAND_WOOD
	action OK
	resetcount
	}
  }

ifaction OK
	{
	ifp pstanding
		{
		findnearactor3d 8172 1024 temp state rebuild_board
		findnearactor3d 3906 1024 temp state rebuild_board
		findnearactor3d 10646 1024 temp state rebuild_board
		}
	cstat 256
	setp[].runspeed 45200
	set temp3 player[].posx
	add temp3 512
	rotatepoint player[].posx player[].posy temp3 player[].posy player[].ang temp5 temp6

	geta[].sectnum upd_sect
	updatesectorz temp5 temp6 sprite[].z upd_sect
	ife upd_sect -1 action ZERO else
	{
	seta[].x temp5
	seta[].y temp6
	changespritesect THISACTOR upd_sect
	}
	getp[].posz temp
	add temp 5600
	seta[].z temp
	state lower_weapon

	ifhitspace
	 ifcount 13
		{
			sound LAND_WOOD
			setp[].runspeed RUNNINGSPEED
			action ZERO
		resetcount
		}

	ifpdistg 1024 { sound LAND_WOOD setp[].runspeed RUNNINGSPEED action ZERO }

	}

enda

//
// ------------------------------------------------------------
// ALLY MEDIKIT
// ------------------------------------------------------------
//

spriteshadow ALLY_MEDIKIT

useractor notenemy ALLY_MEDIKIT
fall
sizeat 14 14


ifaction 0 action ZERO

ifaction ZERO
{
 cstat 257

ifpdistl 1024
 ifp pfacing
  ifp palive
  {
  set player_use 0
   ifhitspace
    ifcount 13
	{
	set hit_key 30
	getp[].i temp7
	geta[temp7].yvel temp9
	sound ENDSEQVOL2SND1
	action OK
	resetcount
	}
  }
 }

ifaction OK
	{
	findnearspritez SHOOTME2 512 16384 target
		ifn target -1
			{
			getav[target].myowner temp6
			geta[temp6].picnum temp8
			geta[temp6].extra temp7
			ife temp8 VLADMIR
				{
				ifg temp7 1499 nullop
				else
					{
					set temp7 1500 seta[temp6].extra temp7
					sound USE_MEDIKIT
					killit
					}
				}
			ife temp8 RUSSIAN
				{
				ifg temp7 99 nullop
				else
					{
					set temp7 100 seta[temp6].extra temp7
					sound USE_MEDIKIT
					killit
					}
				}
			}
		else
		ifpdistl 512
			{
			ife player[].i temp9 break
			addphealth 100
			palfrom 60 0 60 0
			sound USE_MEDIKIT
			killit
			}
			else
			ifpdistl 1024
			ifhitspace
			ifcount 13
			{
			sound ENDSEQVOL2SND1
			action ZERO
			resetcount
			}


	set temp3 player[temp9].posx
	add temp3 512

	rotatepoint player[temp9].posx player[temp9].posy temp3 player[temp9].posy player[temp9].ang temp5 temp6

	geta[].sectnum upd_sect
	updatesectorz temp5 temp6 sprite[].z upd_sect
	ife upd_sect -1 action ZERO else
	{
	seta[].x temp5
	seta[].y temp6
	changespritesect THISACTOR upd_sect
	}
	getp[temp9].posz temp
	add temp 2600
	seta[].z temp
	setp[temp9].weapon_pos 10
	}

enda

//
// ------------------------------------------------------------
// HEALTH CHARGER
// ------------------------------------------------------------
//

defstate MED_CHARGER
ifaction 0
	{
		spawn 12780
		cstator 1024
		ifactor MEDCHARGER2 espawn 9467
		else ifactor MEDCHARGER espawn 9465
		set temp3 sprite[].x
		add temp3 4
		seta[RETURN].ang sprite[].ang
		rotatepoint sprite[].x sprite[].y temp3 sprite[].y sprite[].ang temp5 temp6

		geta[].sectnum upd_sect
		updatesectorz temp5 temp6 sprite[].z upd_sect
		ife upd_sect -1 nullop else
		{
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		changespritesect RETURN upd_sect
		}
		geta[].z temp
		seta[RETURN].z temp
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		set INTERNALCOUNT_2 RETURN
	ife INTERNALCOUNT 0
		{
		ife SKILL_LEVEL 1 set INTERNALCOUNT 100 else
		ife SKILL_LEVEL 2 set INTERNALCOUNT 75 else
		set INTERNALCOUNT 50
		}
	action ZERO
	}

seta[INTERNALCOUNT_2].pal sprite[].pal
ifl INTERNALCOUNT 25 spritepal 21
else ifl INTERNALCOUNT 50 spritepal 23
else ifl INTERNALCOUNT 75 spritepal 11
else spritepal 0

ifpdistl 1024
 ifp pfacing
  ifg INTERNALCOUNT 0
    {
   ifl PHEALTH pmax_health
		{
		set player_use 0
		 ifhitspace
		   ifcount 2
		   ifaction ZERO
			   {
				palfrom 20 0 10 0
				soundonce MEDACTIVE
				addphealth 1
				ifg INTERNALCOUNT 2000 nullop else sub INTERNALCOUNT 1
				resetcount
			   }
		}
	else
	ife PERSONNEL_RESEARCH[13] 2 // medical foam converter?
	 ifl player[].firstaid_amount pmax_health
		{
		set player_use 0
		 ifhitspace
		   ifcount 2
		   ifaction ZERO
			   {
				palfrom 20 0 10 0
				soundonce MEDACTIVE
				setp[].inven_icon 1
				getp[].firstaid_amount temp
				add temp 1
				setp[].firstaid_amount temp
				ifg INTERNALCOUNT 2000 nullop else sub INTERNALCOUNT 1
				resetcount
			   }
		}
	else
		{
		set player_use 0
		 ifhitspace
		   ifcount 2
		   ifaction ZERO
			   {
				soundonce RED_COMPBEEP
				stopsound MEDACTIVE
				}
		}
	}

ife INTERNALCOUNT 0
ifaction ZERO
{
setactorvar[INTERNALCOUNT_2].temp 1
stopsound MEDACTIVE
soundonce RED_COMPBEEP
action ONE
}

ends

useractor notenemy MEDCHARGER state MED_CHARGER enda
useractor notenemy MEDCHARGER2 state MED_CHARGER enda

spritenoshade 9467

useractor notenemy 9467

ifaction 0
	{
	seta[].htflags 2052
	seta[].shade -127
	seta[].blend 255
	cstat 1042
	action ZERO
	}

ifaction ZERO ife temp 1 killit

enda

spritenoshade 9465

useractor notenemy 9465

ifaction 0
	{
	seta[].htflags 2052
	seta[].shade -127
	seta[].blend 255
	cstat 1042
	action ZERO
	}

ifaction ZERO ife temp 1 action ONE

enda

//
// ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// ENGINEER CHARGER
// ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
//

defstate engineer_charger
ifaction 0
	{
			ifactor 26063 spawn 12781
			else spawn 12780
			cstator 1024
			espawn 26066
			sizeat 20 20
			set temp3 sprite[].x
			add temp3 4
			seta[RETURN].ang sprite[].ang
			rotatepoint sprite[].x sprite[].y temp3 sprite[].y sprite[].ang temp5 temp6

			geta[].sectnum upd_sect
			updatesectorz temp5 temp6 sprite[].z upd_sect
			ife upd_sect -1 nullop else
			{
			seta[RETURN].x temp5
			seta[RETURN].y temp6
			changespritesect RETURN upd_sect
			}
			geta[].z temp
			sub temp 1664
			seta[RETURN].z temp
			seta[RETURN].xrepeat sprite[].xrepeat
			set INTERNALCOUNT_2 RETURN
		ife INTERNALCOUNT 0
			{
			ife SKILL_LEVEL 1 set INTERNALCOUNT 100 else
			ife SKILL_LEVEL 2 set INTERNALCOUNT 75 else
			set INTERNALCOUNT 50
			}
		action ZERO
		}

	ifl INTERNALCOUNT 25 set temp4 21
	else ifl INTERNALCOUNT 50 set temp4 23
	else ifl INTERNALCOUNT 75 set temp4 11
	else set temp4 0
	set temp5 INTERNALCOUNT
	div temp5 4
	clamp temp5 0 20
	seta[INTERNALCOUNT_2].yrepeat temp5
	seta[INTERNALCOUNT_2].pal temp4

	ifactor 26063
	{
	switch CHAR
		case 0
		case 3
		case 7
		case 12
		case 13
		case 14
		ife CHAR 3 { ifand CHAR_APP 16 nullop else break }
		ifpdistl 1024
		 ifp pfacing
		  ifg INTERNALCOUNT 0
			{
		   ifl PARMOUR 100
				{
				set player_use 0
				 ifhitspace
				   ifcount 2
				   ifaction ZERO
					   {
						palfrom 20 0 10 0
						soundonce ARMCHARG
						getp[].shield_amount temp
						add temp 1
						setp[].shield_amount temp
						sub INTERNALCOUNT 1
						resetcount
					   }
				}
			else
				{
				set player_use 0
				 ifhitspace
				   ifcount 2
				   ifaction ZERO
					   {
						soundonce RED_COMPBEEP
						stopsound ARMCHARG
						}
				}
			}
		break
		default
			ifpdistl 1024
			 ifp pfacing
				ifhitspace
				 ifaction ZERO
						quote 380
		break
	endswitch
	ife INTERNALCOUNT 0
	ifaction ZERO
		{
		setactorvar[INTERNALCOUNT_2].temp 1
		stopsound ARMCHARG
		soundonce RED_COMPBEEP
		action TWO
		}
	}
	else
	{
	ifpdistl 1024
	 ifp pfacing
	  ifg INTERNALCOUNT 0
		{
	   ifl PHEALTH pmax_health
			{
			set player_use 0
			 ifhitspace
			   ifcount 2
			   ifaction ZERO
				   {
					palfrom 20 0 10 0
					soundonce MEDACTIVE
					addphealth 1
					ifg INTERNALCOUNT 2000 nullop else sub INTERNALCOUNT 1
					resetcount
				   }
			}
		else
		ife PERSONNEL_RESEARCH[13] 2 // medical foam converter?
		 ifl player[].firstaid_amount pmax_health
			{
			set player_use 0
			 ifhitspace
			   ifcount 2
			   ifaction ZERO
				   {
					palfrom 20 0 10 0
					soundonce MEDACTIVE
					setp[].inven_icon 1
					getp[].firstaid_amount temp
					add temp 1
					setp[].firstaid_amount temp
					ifg INTERNALCOUNT 2000 nullop else sub INTERNALCOUNT 1
					resetcount
				   }
			}
		else
			{
			set player_use 0
			 ifhitspace
			   ifcount 2
			   ifaction ZERO
				   {
					soundonce RED_COMPBEEP
					stopsound MEDACTIVE
					}
			}
		}

	ife INTERNALCOUNT 0
	ifaction ZERO
		{
		setactorvar[INTERNALCOUNT_2].temp 1
		stopsound MEDACTIVE
		soundonce RED_COMPBEEP
		action ONE
		}
	}
ends

useractor notenemy 26063 state engineer_charger enda
useractor notenemy 26064 state engineer_charger enda

spritenoshade 26066

useractor notenemy 26066

ifaction 0
	{
	seta[].htflags 2052
	seta[].shade -127
	seta[].blend 255
	cstat 1042
	action ZERO
	}

ifaction ZERO ife temp 1 killit

enda


//
// ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// ARMOUR CHARGER
// ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
//

defstate ARM_CHARGER
ifaction 0
	{
		cstator 1024
		spawn 12781
		ifactor VEST_CHARGER espawn 9463
		set temp3 sprite[].x
		add temp3 4
		seta[RETURN].ang sprite[].ang
		rotatepoint sprite[].x sprite[].y temp3 sprite[].y sprite[].ang temp5 temp6

		geta[].sectnum upd_sect
		updatesectorz temp5 temp6 sprite[].z upd_sect
		ife upd_sect -1 nullop else
		{
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		changespritesect RETURN upd_sect
		}
		geta[].z temp
		seta[RETURN].z temp
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		set INTERNALCOUNT_2 RETURN
		ife INTERNALCOUNT 0
			{
			ife SKILL_LEVEL 1 set INTERNALCOUNT 100 else
			ife SKILL_LEVEL 2 set INTERNALCOUNT 75 else
			set INTERNALCOUNT 50
			}
	action ZERO
	}

switch CHAR
	case 0
	case 3
	case 7
	case 12
	case 13
	case 14
	ife CHAR 3 { ifand CHAR_APP 16 nullop else break }
	ifpdistl 1024
	 ifp pfacing
	  ifg INTERNALCOUNT 0
		{
	   ifl PARMOUR 100
			{
			set player_use 0
			 ifhitspace
			   ifcount 2
			   ifaction ZERO
				   {
					palfrom 20 0 10 0
					soundonce ARMCHARG
					getp[].shield_amount temp
					add temp 1
					setp[].shield_amount temp
					sub INTERNALCOUNT 1
					resetcount
				   }
			}
		else
			{
			set player_use 0
			 ifhitspace
			   ifcount 2
			   ifaction ZERO
				   {
					soundonce RED_COMPBEEP
					stopsound ARMCHARG
					}
			}
		}
	break
	default
		ifpdistl 1024
		 ifp pfacing
			ifhitspace
			 ifaction ZERO
					quote 380
	break
endswitch

ife INTERNALCOUNT 0
ifaction ZERO
{
setactorvar[INTERNALCOUNT_2].temp 1
stopsound ARMCHARG
soundonce RED_COMPBEEP
action ONE
}

ends

useractor notenemy VEST_CHARGER state ARM_CHARGER enda

spritenoshade 9463

useractor notenemy 9463

ifaction 0
	{
	seta[].htflags 2052
	seta[].shade -127
	seta[].blend 255
	cstator 1024
	cstat 18
	action ZERO
	}

ifaction ZERO ife temp 1 action ONE

enda


//
// ------------------------------------------------------------
// SPRITE ELEVATOR
// ------------------------------------------------------------
//

useractor notenemy SPRITE_LIFT
cstat 32768

ifaction 0
{
ifspritepal 1
	{
	set SPRITE_ELEV_MOVEMENT 2
	seta[].z SPRITE_LIFT_TOP
	}
action ZERO
}

ifn HITAGSAVED 0 killit
ife SPRITE_ELEV_MOVEMENT 1
{
ifn EXTRASAVED -1 soundoncevar EXTRASAVED
ife sprite[].z SPRITE_LIFT_TOP { soundoncevar ZVELSAVED stopsoundvar EXTRASAVED set SPRITE_ELEV_MOVEMENT 2 }
else ifl sprite[].z SPRITE_LIFT_TOP { soundoncevar ZVELSAVED stopsoundvar EXTRASAVED set SPRITE_ELEV_MOVEMENT 2 }
set temp 0
		whilevarn temp 128
		{
			set temp4 TRAIN[temp]
			seta[temp4].statnum 1
			geta[temp4].z temp5
			sub temp5 XVELSAVED
			seta[temp4].z temp5
			ifn SPRITE_LIFT_SIDE 0
				{
				geta[].ang temp6
				sub temp6 512
				cos temp7 temp6
				sin temp8 temp6
				set temp6 64
				mulvarvar temp6 SPRITE_LIFT_SIDE
				divvarvar temp7 temp6
				divvarvar temp8 temp6
				movesprite temp4 temp7 temp8 0 0 temp9
				ifn player[].spritebridge 0
				ife player[].sbs temp4
							{
							geta[].ang temp6
							sub temp6 512
							cos xvel temp6
							sin yvel temp6
							getp[].posxv temp9
							add xvel temp9
							setp[].posxv xvel
							getp[].posyv temp9
							add yvel temp9
							setp[].posyv yvel
							}
				}
			add temp 1
		}
		geta[].z temp5
		sub temp5 XVELSAVED
		seta[].z temp5
}

ife SPRITE_ELEV_MOVEMENT 3
{
soundoncevar EXTRASAVED
ife temp5 SPRITE_LIFT_BOTTOM { soundoncevar ZVELSAVED stopsoundvar EXTRASAVED set SPRITE_ELEV_MOVEMENT 0 }
set temp 0
		whilevarn temp 128
		{
			set temp4 TRAIN[temp]
			seta[temp4].statnum 1
			geta[temp4].z temp5
			add temp5 XVELSAVED
			seta[temp4].z temp5
			ifn SPRITE_LIFT_SIDE 0
				{
				geta[].ang temp6
				add temp6 512
				cos temp7 temp6
				sin temp8 temp6
				set temp6 64
				mulvarvar temp6 SPRITE_LIFT_SIDE
				divvarvar temp7 temp6
				divvarvar temp8 temp6
				movesprite temp4 temp7 temp8 0 0 temp9
				ifn player[].spritebridge 0
				ife player[].sbs temp4
							{
							geta[].ang temp6
							add temp6 512
							cos xvel temp6
							sin yvel temp6
							getp[].posxv temp9
							add xvel temp9
							setp[].posxv xvel
							getp[].posyv temp9
							add yvel temp9
							setp[].posyv yvel
							}
				}
			add temp 1
		}
		geta[].z temp5
		add temp5 XVELSAVED
		seta[].z temp5
}

checkactivatormotion LOTAGSAVED
 ife RETURN 1
	{
	ife SPRITE_ELEV_MOVEMENT 0
		{
		soundoncevar YVELSAVED
		set SPRITE_ELEV_MOVEMENT 1
		}
	else
	ife SPRITE_ELEV_MOVEMENT 2
		{
		soundoncevar YVELSAVED
		set SPRITE_ELEV_MOVEMENT 3
		}
	}


enda

//
// ------------------------------------------------------------
// STALAG...STALAGM....CEILING THING
// ------------------------------------------------------------
//

spritenoshade 10546

action STALAG_FALL

useractor notenemy 10546

ife HITAGSAVED 1
	{
	ifaction 0
		{
		clipdist 128
		ifrnd 128 cstat 269 else
		cstat 265
		action ZERO
		}

	ifaction ZERO
	ifhitweapon
		{
		shoot ROCKDEB_1
		shoot ROCKDEB_1
		fall
		action STALAG_FALL
		}

	ifaction STALAG_FALL
		{
		fall
		geta[].shade temp
		 ifcount 3
			{
			sub temp 1
			seta[].shade temp
			resetcount
			}
		iffloordistl 8
			{
			quake 13
			globalsound BOLDSTOP
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			hitradius 4096 512 1024 2048 4096
			killit
			}
		}
	}

enda

//
// ------------------------------------------------------------
// BOULDER
// ------------------------------------------------------------
//

spriteshadow 10547

move MBOULDER_SLOW 50 0
move MBOULDER_ROLL 200 0

action BOULDER_ROLL_SLOW 0 15 1 1 40
action BOULDER_ROLL 0 15 1 1 10

useractor notenemy 10547
fall
	ifspawnedby RESPAWN nullop else seta[].ang temp7

ifaction 0
{
strength 100
sizeat 48 48
cstat 257
ife sector[].lotag 1
	{
	ife sector[].floorpicnum 10526 spawn 10039
	ife sector[].floorpicnum 10030 spawn 10039
	}
action ZERO
}

ifaction ZERO
	{
	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			soundonce SLABMOVE
			action BOULDER_ROLL_SLOW
			}
		}
	else
	ifspawnedby RESPAWN
		{
		soundonce SLABMOVE
		action BOULDER_ROLL_SLOW
		}
	else
	ifpdistl 2048
	 ifp pfacing
	 {
	 ifand NOISE_SAID 2 nullop else { set PLAYER_VOICEOVER 41 xorvar NOISE_SAID 2 }
	   ifangdiffl 512
		{
		ifhitweapon
			{
			 ifwasweapon KNEE
				 {
				 quake 6
				 sound SPUNCHHIT
				 ifmultiplayer { sound SLABMOVE action BOULDER_ROLL_SLOW } else
				  ifdead
					{
					sound SLABMOVE
					action BOULDER_ROLL_SLOW
					}
				 }
			 else strength 100
			}
		}
	 }
	}

ifaction BOULDER_ROLL_SLOW
	{
	move MBOULDER_SLOW geth
	ifactioncount 5
		{
		action BOULDER_ROLL
		}
	}

ifaction BOULDER_ROLL
	{
	move MBOULDER_ROLL geth
	soundonce EARTHQUAKE
	hitradius 1024 64 128 256 512
	ifpdistl 4096 quake 13
		ifnotmoving
			{
			strength -16
			quake 26
			globalsound BOLDSTOP
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			hitradius 4096 256 1048 3096 4192
			killit
			}
	}

enda


//
// ------------------------------------------------------------
// ENEMY SPAWNER
// ------------------------------------------------------------
//

action ALARM_SP 0

action ALARM_OFF 0

useractor notenemy MONS_SPAWNER

ifaction 0
{
cstat 32768
ife OWNERSAVED 1 setvar INVASION 1
ife ZVELSAVED 1 action ALARM_SP
else ife HITAGSAVED 0 action ZERO
else ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action ZERO
	}
}

ifaction ALARM_SP
{
	ifg ALARM_ACTIVE 0
	ifpdistl 32768
	{
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT YVELSAVED
			{
			ifspritepal 3
				{
				ifcansee
				 ifcanseetarget
					{
					ife XVELSAVED 1 spawn TRANSPORTERBEAM
					espawnvar EXTRASAVED
					add temp2 1
					set INTERNALCOUNT 0
					}
				else set INTERNALCOUNT 0
				}
			else
				{
				ife XVELSAVED 1 spawn TRANSPORTERBEAM
				espawnvar EXTRASAVED
				add temp2 1
				set INTERNALCOUNT 0
				}
			}
		ife temp2 LOTAGSAVED action ALARM_OFF
	}
}

ifaction ALARM_OFF
{
	ife ALARM_ACTIVE 0 action ALARM_SP
}

ifaction ZERO
{
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT YVELSAVED
	{
		ifspritepal 3
			{
			ifcansee
			 ifcanseetarget
				{
				ife XVELSAVED 1 spawn TRANSPORTERBEAM
				espawnvar EXTRASAVED
				add temp2 1
				set INTERNALCOUNT 0
				}
			else set INTERNALCOUNT 0
			}
		else
			{
			ife XVELSAVED 1 spawn TRANSPORTERBEAM
			espawnvar EXTRASAVED
			add temp2 1
			set INTERNALCOUNT 0
			}
	}

	ife temp2 LOTAGSAVED killit
	ifn ZVELSAVED 0
	{
		checkactivatormotion ZVELSAVED
		ife RETURN 1 killit
	}
}
enda

//
// ------------------------------------------------------------
// MINIGUN AMMO
// ------------------------------------------------------------
//

spriteshadow 10573

useractor notenemy 10573
fall
sizeat 16 16
cstator 257

findnearactor3d MOUNTED_MINIGUN 1024 temp
ifn temp -1
{
getav[temp].stationary_gun_ammo temp2
add temp2 1000
setactorvar[temp].stationary_gun_ammo temp2
quote 237
soundonce BIG_AMMO
killit
}

ifaction 0 action ZERO

ifaction ZERO cstat 257

ifpdistl 1024
 ifp pfacing
  ifp palive
  ifaction ZERO
  {
  set player_use 0
   ifhitspace
    ifcount 13
	{
	set hit_key 30
	set PLAYER_VOICEOVER 2
	sound METALB_PICKUP
	action OK
	resetcount
	}
  }

ifaction OK
	{
	cstat 256
	setp[].runspeed 45200

	set temp3 player[].posx
	add temp3 512

	rotatepoint player[].posx player[].posy temp3 player[].posy player[].ang temp5 temp6

	geta[].sectnum upd_sect
	updatesectorz temp5 temp6 sprite[].z upd_sect
	ife upd_sect -1 action ZERO else
	{
	seta[].x temp5
	seta[].y temp6
	changespritesect THISACTOR upd_sect
	}
	getp[].posz temp
	add temp 5600
	seta[].z temp
	state lower_weapon

	ifhitspace
	 ifcount 13
		{
		sound METALB_PICKUP
		setp[].runspeed RUNNINGSPEED
		action ZERO
		resetcount
		}

	ifpdistg 1024 { sound METALB_PICKUP setp[].runspeed RUNNINGSPEED action ZERO }

	}

enda

//
// ------------------------------------------------------------
// FLAME TURRET AMMO
// ------------------------------------------------------------
//

spriteshadow 12984

useractor notenemy 12984
fall
sizeat 32 32
cstator 257

findnearactor3d FLAMER_TURRET 1024 temp
ifn temp -1
{
getav[temp].ally_mag temp2
set temp2 1000
setactorvar[temp].ally_mag temp2
quote 1052
soundonce BIG_AMMO
killit
}

ifaction 0 action ZERO

ifaction ZERO cstat 257

ifpdistl 1024
 ifp pfacing
  ifp palive
  ifaction ZERO
  {
  set player_use 0
   ifhitspace
    ifcount 13
	{
	set hit_key 30
	set PLAYER_VOICEOVER 2
	sound METALB_PICKUP
	action OK
	resetcount
	}
  }

ifaction OK
	{
	cstat 256
	setp[].runspeed 45200

	set temp3 player[].posx
	add temp3 512
	rotatepoint player[].posx player[].posy temp3 player[].posy player[].ang temp5 temp6
	geta[].sectnum upd_sect
	updatesectorz temp5 temp6 sprite[].z upd_sect
	ife upd_sect -1 action ZERO else
	{
	seta[].x temp5
	seta[].y temp6
	changespritesect THISACTOR upd_sect
	}
	getp[].posz temp
	add temp 5600
	seta[].z temp
	state lower_weapon

	ifhitspace
	 ifcount 13
		{
		sound METALB_PICKUP
		setp[].runspeed RUNNINGSPEED
		action ZERO
		resetcount
		}

	ifpdistg 1024 { sound METALB_PICKUP setp[].runspeed RUNNINGSPEED action ZERO }

	}

enda

//
// ------------------------------------------------------------
// ELECTROCUTE GUY!
// ------------------------------------------------------------
//

defstate electrocute_anim
ifmove 0
	{
	ifspawnedby ALUDRAN nullop else // set in actor code itself
	ifspawnedby ORC sizeat 26 26 else
	ifspawnedby GUARDIAN sizeat 28 28 else
	ifspawnedby BERSERKER sizeat 26 26 else
	ifspawnedby PIGCOP sizeat 30 30 else
	ifspawnedby PIGCOP_RIOT sizeat 30 30 else
	ifspawnedby PIG_SF sizeat 32 32 else
	ifspawnedby LIZMAN sizeat 27 27 else
	ifspawnedby ELITE_LIZMAN sizeat 31 31 else
	ifspawnedby THOTH sizeat 32 32 else
	sizeat 24 24
	seta[].shade -127
	ifspawnedby PIGCOP action ZERO else
	ifspawnedby PIGCOP_RIOT action ZERO else
	ifspawnedby PIG_SF action ZERO else
	ifspawnedby LIZMAN action ZERO else
	ifspawnedby ELITE_LIZMAN action ZERO else
	ifspawnedby THOTH action ZERO else
	action FIVE_ANG
	move STOP
	}
else
ifmove STOP
	{
	randvar temp 40
	sub temp 20
	seta[].shade temp
	ifrnd 128 { spritepal 102 shoot SPARK2 shoot SPARK2 shoot SPARK2 }
	else ifrnd 128 { spritepal 67 shoot SPARK2 shoot SPARK2 shoot SPARK2 }
	ifcount 45
		{
		spritepal 27
		shoot SPARK2 shoot SPARK2 shoot SPARK2
		shoot SPARK2 shoot SPARK2 shoot SPARK2
        sound LIGHT_CONDUCT
        spawn LIGHT_SPREAD
		state standard_jibs
		spawn BLOODPOOL
		espawn BIG_SMOKE seta[RETURN].pal 27
		killit
		}
	}
ends

spritenoshade ELECTROCUTE_GUY
spritenoshade ELECTROCUTE_MARV
spritenoshade ELECTROCUTE_LIZT
spritenoshade ELECTROCUTE_ORC
spritenoshade ELECTROCUTE_ALUDRAN
spritenoshade ELECTROCUTE_BIGGUY
spritenoshade ELECTROCUTE_PIGCOP
spritenoshade ELECTROCUTE_LIZMAN
spritenoshade ELECTROCUTE_THOTH

// 5 tiles
useractor notenemy ELECTROCUTE_GUY state electrocute_anim enda
useractor notenemy ELECTROCUTE_MARV state electrocute_anim enda
useractor notenemy ELECTROCUTE_LIZT state electrocute_anim enda
useractor notenemy ELECTROCUTE_ORC state electrocute_anim enda
useractor notenemy ELECTROCUTE_ALUDRAN state electrocute_anim enda
useractor notenemy ELECTROCUTE_BIGGUY state electrocute_anim enda

// single tile
useractor notenemy ELECTROCUTE_PIGCOP state electrocute_anim enda
useractor notenemy ELECTROCUTE_LIZMAN state electrocute_anim enda
useractor notenemy ELECTROCUTE_THOTH state electrocute_anim enda

//
// ------------------------------------------------------------
// DISINTIGRATE GUY!
// ------------------------------------------------------------
//

action DISOLVE_GUY 0 6 1 1 16
action DISOLVED 5 1 1 1 1
action DISOLVED_COOL 5 1 1 1 1

spriteshadow DISINTIGRATE_GUY

defstate disint_guy
ifaction 0
	{
	espawn TRANSPORTERSTAR
	geta[RETURN].z temp
	sub temp 3096
	seta[RETURN].z temp
	shoot SPARK2 shoot SPARK2 shoot SPARK2
	shoot SPARK2 shoot SPARK2 shoot SPARK2
	shoot SPARK2 shoot SPARK2 shoot SPARK2
	sound ENERGY_BURST
	flash
	ifspawnedby BERSERKER sizeat 30 26 else
	ifspawnedby GORILLA sizeat 40 27 else
	ifspawnedby THOTH sizeat 32 32 else
	ifspawnedby ANUBIS_MUMMY sizeat 24 27 else
	ifspawnedby ORC sizeat 25 25 else
	ifspawnedby PIGCOP sizeat 28 28 else
	ifspawnedby PIGCOP_RIOT sizeat 28 28 else
	ifspawnedby PIG_SF sizeat 28 28 else
	spritepal 4
	ifactor DISINTIGRATE_GUY
		{
		guts HUMAN_TORSO 1
		guts HUMAN_ARM 1
		shoot NJIB2
		shoot NJIB2
		shoot NJIB3
		}
	state standard_jibs
	getlastpal
	sizeat 24 24
	cstat 0
	ifrnd 128 cstator 4
	action DISOLVE_GUY
	}

ifaction DISOLVED
	{
	ifrnd 4 { espawn BIG_SMOKE seta[RETURN].pal 27 }
	ifcount 256 action DISOLVED_COOL
	}

ifaction DISOLVE_GUY
	{
	ifrnd 32 { shoot SPARK2 shoot SPARK2 }
	ifrnd 4 { espawn BIG_SMOKE geta[RETURN].z temp sub temp 4096 seta[RETURN].z temp seta[RETURN].pal 27 }
	ifactioncount 6 { cstat 0 resetcount action DISOLVED }
	}
ends

useractor notenemy DISINTIGRATE_GUY fall state disint_guy enda
useractor notenemy DISINTIGRATE_BIG_GUY fall state disint_guy enda
useractor notenemy DISINTIGRATE_PIGCOP fall state disint_guy enda
useractor notenemy DISINTIGRATE_LIZMAN fall state disint_guy enda
useractor notenemy DISINTIGRATE_LIZTR fall state disint_guy enda

//
// ------------------------------------------------------------
// SPIRIT DEATH GUY!
// ------------------------------------------------------------
//

action SPIRIT_DISOLVE_GUY 0 17 1 1 16
action SPIRIT_DISOLVED 16 1 1 1 1

spriteshadow SPIRIT_DEATH_GUY
spritenoshade SPIRIT_DEATH_GUY

useractor notenemy SPIRIT_DEATH_GUY
	fall
	ifaction 0
	{
		sound SPD_1
		ifspawnedby ORC sizeat 36 36
		else ifspawnedby GORILLA sizeat 42 34
		else ifspawnedby ANUBIS_MUMMY sizeat 38 38
		else ifspawnedby SKNIGHT sizeat 38 38
		else ifspawnedby SSQUIRE sizeat 34 34
		else sizeat 30 30

		cstat 0
		ifrnd 128
			cstator 4

		seta[].shade -16
		action SPIRIT_DISOLVE_GUY
	}

	ifaction SPIRIT_DISOLVE_GUY
	{
		ifrnd 64 { shoot SPARK2 shoot SPARK2 }
		ifactioncount 9 { soundonce SPD_2 seta[].shade sector[].floorshade }
		ifactioncount 15 soundonce SPD_3
		ifactioncount 17
		{
			geta[].cstat temp
			ifand temp 257
			{
				xorvar temp 257
				seta[].cstat temp
			}

			resetcount
			action SPIRIT_DISOLVED
		}
	}

enda

//
// ------------------------------------------------------------
// ONFIRE GUY!
// ------------------------------------------------------------
//

spritenoshade ONFIREGUY
spriteshadow ONFIREGUY

action FIRERUN 0 4 5 1 8
action FIREDIE 21 5 1 1 16
action FIREBOD 24

action PIG_FIRERUN 2318 4 5 1 8
action PIG_FIREDIE 2338 5 1 1 16
action PIG_FIREBOD 2342

action BIG_FIRERUN 2345 4 5 1 8
action BIG_FIREDIE 2365 5 1 1 16
action BIG_FIREBOD 2369

action LIZ_FIRERUN 4961 4 5 1 8
action LIZ_FIREDIE 4981 5 1 1 16
action LIZ_FIREBOD 4985

action BURN_OUT 4229 4 1 1 17
action BURNED_OUT 4233 1

move ASSONFIRE 140
move NOMOVE 0

action GOR_BURN 0 8 1 1 12
action GOR_BURNING 7

spriteshadow 33964
spritenoshade 33964

// GORILLA BURNING
useractor notenemy 33964
fall
seta[].shade -127
move STOP
sizeat 30 30
ifaction GOR_BURN nullop else ifaction GOR_BURNING nullop else { set temp4 0 set temp5 0 action GOR_BURN resetactioncount }
ifaction GOR_BURN
	{
	ifactioncount 8 action GOR_BURNING
	}
ifaction GOR_BURNING
	{
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	}
enda

action OCTA_BURN 0 7 1 1 12
action OCTA_BURNING 6

spriteshadow 33978
spritenoshade 33978

// OCTABRAIN BURNING
useractor notenemy 33978
fall
seta[].shade -127
move STOP
sizeat 30 30
ifaction OCTA_BURN nullop else ifaction OCTA_BURNING nullop else { set temp4 0 set temp5 0 action OCTA_BURN resetactioncount }
ifaction OCTA_BURN
	{
	ifactioncount 7 action OCTA_BURNING
	}
ifaction OCTA_BURNING
	{
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	}
enda

useractor notenemy ONFIREGUY
fall

ifaction 0
{
sound ENGULF_FIRE
shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
ifspawnedby ZSCIEN sizeat 22 22
else ifspawnedby GORILLA cactor 33964
else ifspawnedby ANUBIS_MUMMY sizeat 32 32
else ifspawnedby GUARDIAN sizeat 32 32
else ifspawnedby LIZMAN sizeat 26 26
else ifspawnedby ELITE_LIZMAN sizeat 28 28
else ifspawnedby GOBLINBROS sizeat 23 23
else ifspawnedby GOBLIN sizeat 23 23
else ifspawnedby SKNIGHT sizeat 30 30
else ifspawnedby SSQUIRE sizeat 28 28
else sizeat 26 26
ifspawnedby RESPAWN
	{
	ifrnd 64 sound MALE_ONFIRE1
	else ifrnd 64 sound MALE_ONFIRE2
	else ifrnd 64 sound MALE_ONFIRE3
	else sound MALE_ONFIRE4
	}
cstat 257
seta[].shade -127
strength 100
	ifspawnedby PIGCOP action PIG_FIRERUN
	else ifspawnedby PIG_SF action PIG_FIRERUN
	else ifspawnedby PIGCOP_RIOT action PIG_FIRERUN
	else ifspawnedby LIZMAN action LIZ_FIRERUN
	else ifspawnedby ELITE_LIZMAN action LIZ_FIRERUN
	else ifspawnedby ORC action BIG_FIRERUN
	else ifspawnedby ZCHAING action BIG_FIRERUN
	else ifspawnedby Z_GRENADEL action BIG_FIRERUN
	else ifspawnedby GUARDIAN action BIG_FIRERUN
	else ifspawnedby GORILLA action BIG_FIRERUN
	else
	action FIRERUN
move ASSONFIRE geth
set INTERNALCOUNT 0
}

ifaction FIRERUN
	{
	add INTERNALCOUNT 1
	ifpdistl 16384 soundonce FIRE_CRACKLE
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	ifg INTERNALCOUNT 130
	 ifrnd 32
		{
		action FIREDIE
		move NOMOVE
		}
	ifrnd 8
		{
		move ASSONFIRE geth randomangle
		}
	ifnotmoving
		{
		move ASSONFIRE geth randomangle
		}
	ifinwater { action FIREDIE move NOMOVE }
	ifonwater { action FIREDIE move NOMOVE }
	ifhitweapon ifdead { action FIREDIE move NOMOVE }
	}

ifaction LIZ_FIRERUN
	{
	add INTERNALCOUNT 1
	ifpdistl 16384 soundonce FIRE_CRACKLE
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	ifg INTERNALCOUNT 130
	 ifrnd 32
		{
		action LIZ_FIREDIE
		move NOMOVE
		}
	ifrnd 8
		{
		move ASSONFIRE geth randomangle
		}
	ifnotmoving
		{
		move ASSONFIRE geth randomangle
		}
	ifinwater { action LIZ_FIREDIE move NOMOVE }
	ifonwater { action LIZ_FIREDIE move NOMOVE }
	ifhitweapon ifdead { action LIZ_FIREDIE move NOMOVE }
	}

ifaction PIG_FIRERUN
	{
	add INTERNALCOUNT 1
	ifpdistl 16384 soundonce FIRE_CRACKLE
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	ifg INTERNALCOUNT 130
	 ifrnd 32
		{
		action PIG_FIREDIE
		move NOMOVE
		}
	ifrnd 8
		{
		soundonce PIG_DYING
		move ASSONFIRE geth randomangle
		}
	ifnotmoving
		{
		soundonce PIG_DYING
		move ASSONFIRE geth randomangle
		}
	ifinwater { action PIG_FIREDIE move NOMOVE }
	ifonwater { action PIG_FIREDIE move NOMOVE }
	ifhitweapon ifdead { action PIG_FIREDIE move NOMOVE }
	}

ifaction BIG_FIRERUN
	{
	add INTERNALCOUNT 1
	ifpdistl 16384 soundonce FIRE_CRACKLE
	ifrnd 8 { shoot SPARK espawn BIG_SMOKE geta[RETURN].z temp sub temp 8096 seta[RETURN].z temp seta[RETURN].pal 17 }
	ifg INTERNALCOUNT 130
	 ifrnd 32
		{
		action BIG_FIREDIE
		move NOMOVE
		}
	ifrnd 8
		{
		move ASSONFIRE geth randomangle
		}
	ifnotmoving
		{
		move ASSONFIRE geth randomangle
		}
	ifinwater { action BIG_FIREDIE move NOMOVE }
	ifonwater { action BIG_FIREDIE move NOMOVE }
	ifhitweapon ifdead { action BIG_FIREDIE move NOMOVE }
	}

ifaction BURN_OUT
ifactioncount 4
	{
			ifrnd 128
			cstat 4
			else
			cstat 0
			spriteflags 1
			seta[].pal sector[].floorpal
			seta[].shade sector[].floorshade
	action BURNED_OUT
	move NOMOVE
	}

ifaction FIREDIE
 ifactioncount 5
	{
	shoot SPARK
	shoot SPARK
	shoot SPARK
	shoot SPARK
	action FIREBOD
	move NOMOVE
	}
ifaction LIZ_FIREDIE
 ifactioncount 5
	{
	shoot SPARK
	shoot SPARK
	shoot SPARK
	shoot SPARK
	action LIZ_FIREBOD
	move NOMOVE
	}
ifaction PIG_FIREDIE
 ifactioncount 5
	{
	shoot SPARK
	shoot SPARK
	shoot SPARK
	shoot SPARK
	action PIG_FIREBOD
	move NOMOVE
	}
ifaction BIG_FIREDIE
 ifactioncount 5
	{
	shoot SPARK
	shoot SPARK
	shoot SPARK
	shoot SPARK
	action BIG_FIREBOD
	move NOMOVE
	}
ifaction LIZ_FIREBOD
	{
	move NOMOVE
	cstat 0
	ifcansee nullop
	else
	action BURN_OUT
	}
ifaction PIG_FIREBOD
	{
	move NOMOVE
	cstat 0
	ifcansee nullop
	else
	action BURN_OUT
	}
ifaction BIG_FIREBOD
	{
	move NOMOVE
	cstat 0
	ifcansee nullop
	else
	action BURN_OUT
	}
ifaction FIREBOD
	{
	move NOMOVE
	cstat 0
	ifcansee nullop
	else
	action BURN_OUT
	}


enda

//
// ------------------------------------------------------------
// REACTOR PHLEG
// ------------------------------------------------------------
//

useractor notenemy 10615
ife LOTAGSAVED 1
{
spritepal 2
ifg REACTOR_BREACH 0 seta[].cstat CSTATSAVED else cstat 32768
}
enda

useractor notenemy 3507

ifaction 0
{
set REACTOR_VAR 1000
ifspritepal 5 cstat 32768 else cstat 257
action ZERO
}

ifg REACTOR_BREACH 0 sub REACTOR_BREACH 1

ifl REACTOR_VAR 100 { ifrnd 32 quake 13 }

ifl REACTOR_VAR 500
	{
	ifn player[].palette 5 setgamepalette 5
	}

ifl REACTOR_VAR 0
	{
	state fade_out_white
	set SLO_MO_SHOWOFF 70
	set FISSION_MAILED 1
	killit
	}

ifcount 30
{
	findnearactorz SHOOTME 4096 32768 temp
	ifn temp -1
		{
		set REACTOR_BREACH 28
		palfrom 30 30 0 0
		quake 6
		ifspritepal 3 globalsound CLAXON_ALARM
		sub REACTOR_VAR 5
		ifn LOTAGSAVED 0
		{
			ifl REACTOR_VAR 820
			 ifg REACTOR_VAR 780
			  ife INTERNALCOUNT 0
				{
				operateactivators LOTAGSAVED THISACTOR
				operatemasterswitches LOTAGSAVED
				operaterespawns LOTAGSAVED
				add LOTAGSAVED 1
				set INTERNALCOUNT 1
				}
			ifl REACTOR_VAR 620
			 ifg REACTOR_VAR 580
			  ife INTERNALCOUNT 1
				{
				operateactivators LOTAGSAVED THISACTOR
				operatemasterswitches LOTAGSAVED
				operaterespawns LOTAGSAVED
				add LOTAGSAVED 1
				set INTERNALCOUNT 2
				}
			ifl REACTOR_VAR 420
			 ifg REACTOR_VAR 380
			  ife INTERNALCOUNT 2
				{
				operateactivators LOTAGSAVED THISACTOR
				operatemasterswitches LOTAGSAVED
				operaterespawns LOTAGSAVED
				add LOTAGSAVED 1
				set INTERNALCOUNT 3
				}
			ifl REACTOR_VAR 220
			 ifg REACTOR_VAR 180
			  ife INTERNALCOUNT 3
				{
				operateactivators LOTAGSAVED THISACTOR
				operatemasterswitches LOTAGSAVED
				operaterespawns LOTAGSAVED
				add LOTAGSAVED 1
				set INTERNALCOUNT 4
				}
			}
		}
	resetcount
}

enda

//
// ------------------------------------------------------------
// SOUND PLAY
// ------------------------------------------------------------
//

useractor notenemy 3602
ifaction 0 { cstat 32768 ifspritepal 5 action FORMED else action ZERO  }

ifaction ZERO
{
sleeptime 0
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	ifspritepal 3 action FORM
	else
		{
		globalsoundvar LOTAGSAVED
		screensound LOTAGSAVED
		killit
		}
	}
}

ifaction FORM
	{
	soundoncevar LOTAGSAVED
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT EXTRASAVED killit
	}

ifaction FORMED
	{
	sleeptime 0
	ifactorsound THISACTOR LOTAGSAVED nullop else
	soundvar LOTAGSAVED
	}


enda

//
// ------------------------------------------------------------
// PRELOAD MUSIC
// ------------------------------------------------------------
//

useractor notenemy PRELOAD_MUSIC
cstat 32768
ifaction 0
	{

	ifn HITAGSAVED 0 starttrackvar HITAGSAVED
	ifn LOTAGSAVED 0 starttrackvar LOTAGSAVED
	ifn EXTRASAVED -1 starttrackvar EXTRASAVED
	ifn XVELSAVED 0 starttrackvar XVELSAVED
	ifn YVELSAVED 0 starttrackvar YVELSAVED
	ifn ZVELSAVED 0 starttrackvar ZVELSAVED

	// for autosave_playtest.map, skip this, it gives annoying errors on testing in case the level is not properly defined in volume 0
	qgetsysstr 7503 STR_MAPNAME
	qputs 7504 /autosave_playtest.map
	qstrcmp 7503 7504 temp

	ifn temp 0
		starttrackvar LEVEL

	killit
	}
enda

//
// ------------------------------------------------------------
// WEATHER TYPE
// ------------------------------------------------------------
//

useractor notenemy 10742
ifaction 0
{
cstat 32768
ifn LOTAGSAVED 0
	{
	gets[].floorstat temp
	xorvar temp 8192
	sets[].floorstat temp
	killit
	}
ifspritepal 0 set weather_type 0 else
ifspritepal 1 set weather_type 1 else
ifspritepal 2 set weather_type 2 else
ifspritepal 3 set weather_type 3 else
ifspritepal 4 set weather_type 4
killit
}

enda

//
// ------------------------------------------------------------
// UNLOCKABLE DISPLAY STUFF
// ------------------------------------------------------------
//

useractor notenemy 10390 // James' 1911s
ifmultiplayer killit

ifaction 0
	{
	readarrayfromfile IW_COMPLETE F_IW_COMPLETE_AMCTC
	action ZERO
	}

ifaction ZERO
	{
	ifspritepal 0
		{
		ife IW_COMPLETE[1] 1 action ZERO
		 else killit
		 }
	else
	ifspritepal 3
		{
		ifpdistl 1024
		 ifp pfacing
		  ife IW_COMPLETE[1] 0
		  {
		  set player_use 0
		   ifhitspace
			{
			sound S1911_RELOAD
			setarray IW_COMPLETE[1] 1
			palfrom 32 32 32 32
			writearraytofile IW_COMPLETE F_IW_COMPLETE_AMCTC
			killit
			}
		  }
		}
	}

enda

// ********************************************************************************
// DARK N STORMY
// ********************************************************************************

defstate change_visibility
set temp 0
		whilevarvarn temp NUMSECTORS
			{
	  		gets[temp].ceilingstat temp4
	  		ifand temp4 1
	  			{
				gets[temp].ceilingpal temp5
				ifn temp5 3
					{
					ifspritepal 3 sets[temp].visibility 0
					else
					ifspritepal 5
						{
						gets[temp].ceilingstat temp7
						ifand temp7 1
							{
							gets[temp].ceilingpicnum temp5
							ifn temp5 179 sets[temp].ceilingpicnum LOTAGSAVED
							sets[temp].ceilingpal 0
							sets[temp].visibility 0
							sets[temp].ceilingypanning 0
							}
						}
					else
					ifspritepal 1
						{
						gets[temp].ceilingstat temp7
						ifand temp7 1
							{
							gets[temp].ceilingpicnum temp5
							ifn temp5 179 sets[temp].ceilingpicnum LOTAGSAVED
							sets[temp].ceilingpal 0
							sets[temp].visibility 240
							sets[temp].ceilingypanning 0
							}
						}
					else sets[temp].visibility 80
					}
	  			}
	  		add temp 1
			}
ends


useractor notenemy 12430

ifaction 0
{
cstat 32768
ife HITAGSAVED 0 { state change_visibility action ZERO }
else
ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 { state change_visibility action ZERO }
	}
}

ifaction ZERO
	{
	ifspritepal 1 killit
	ifspritepal 5 killit
	ifrnd 1 { flash ifrnd 128 globalsound LIGHTNING_SLAP2 else globalsound LIGHTNING_SLAP }
	}

enda

// ********************************************************************************
// FIXABLE SWITCHES
// ********************************************************************************

defstate TOOLBOX_USAGE
ife player[].fta 0
	{
	ifactor 12432 ife sprite[].ang 0 quote 1804
	else ife CHAR 3 quote 1801
	else ife CHAR 5 quote 1802
	else ife CHAR 10 quote 1803
	else ife CHAR 13 quote 1800
	else quote 1804
	}
ends

defstate TOOLBOX_USE_CODE
			ifmultiplayer
				{
				qgetsysstr 356 STR_PLAYERNAME
				qstrcat 356 550
				userquote 356
				}
			soundonce TOOLBOXUSE
			ifactor 12432 ife sprite[].ang 0
				{
				set PLAYER_VOICEOVER 13
				quote 254 set TOOLBOX 0
				}
			else
				{
				set PLAYER_VOICEOVER 13
				ife CHAR 3 { ifrnd 128 quote 253 else { quote 254 set TOOLBOX 0 } }
				else ife CHAR 5 { ifrnd 128 quote 253 else { quote 254 set TOOLBOX 0 } }
				else ife CHAR 10 { ifrnd 128 quote 253 else { quote 254 set TOOLBOX 0 } }
				else ife CHAR 13 quote 253
				else { quote 254 set TOOLBOX 0 }
				}
			setp[].inven_icon 0
ends

useractor notenemy 4483

ifn LOTAGSAVED 0
{
ifrnd 2
	{
	spawn 8433 shoot SPARK shoot SPARK
	sound SHORT_CIRCUIT
	}
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
     ifcount 13
	{
	ife TOOLBOX 1
		{
		setp[].inven_icon 10
		set player_use 0
		state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
      		seta[].picnum 164
			strength 1
			seta[].statnum 0
			seta[].lotag LOTAGSAVED
			seta[].hitag HITAGSAVED
			break
			}
		}
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}
}

enda



useractor notenemy 4954

ifn LOTAGSAVED 0
{
ifrnd 2
	{
	spawn 8433 shoot SPARK shoot SPARK
	sound SHORT_CIRCUIT
	}
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ifcount 13
	{
	 ife TOOLBOX 1
		{
		setp[].inven_icon 10
		set player_use 0
		state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
			seta[].picnum 860
			seta[].lotag LOTAGSAVED
			seta[].hitag HITAGSAVED
			seta[].statnum 0
			break
			}
		}
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}
}

enda

useractor notenemy 12358

ifn LOTAGSAVED 0
{
ifrnd 2
	{
	spawn 8433 shoot SPARK shoot SPARK
	sound SHORT_CIRCUIT
	}
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ifcount 13
	{
	 ife TOOLBOX 1
      {
		setp[].inven_icon 10
		set player_use 0
		state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
			seta[].picnum 12356
			seta[].lotag LOTAGSAVED
			seta[].hitag HITAGSAVED
			break
			}
		}
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}
}

enda

// toolbox fix sprite

useractor notenemy 12432

ifaction 0
{
cstat 32768
action ZERO
}

ifspritepal 3 ifrnd 32 { spawn SMALLSMOKE shoot SPARK shoot SPARK espawn 8433 sound SHORT_CIRCUIT }

ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
     ifcount 13
	{
	set player_use 0
	ife TOOLBOX 1
      {
	setp[].inven_icon 10
	state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
      		operateactivators LOTAGSAVED THISACTOR
      		operaterespawns LOTAGSAVED
      		operatemasterswitches LOTAGSAVED
			ifn XVELSAVED 0 globalsoundvar XVELSAVED
			ifn HITAGSAVED 0
				{
				espawn 5
				seta[RETURN].hitag 6000
				seta[RETURN].lotag HITAGSAVED
				}
			ifn YVELSAVED 0
				{
				seta[].picnum YVELSAVED
				seta[].cstat CSTATSAVED
				}
				else
			killit
			}
	  }
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}

enda

useractor notenemy 17100

ifn LOTAGSAVED 0
{
ifrnd 2
	{
	spawn 8433 shoot SPARK shoot SPARK
	sound SHORT_CIRCUIT
	}
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ifcount 13
	{
	 ife TOOLBOX 1
      {
		setp[].inven_icon 10
		set player_use 0
		state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
			seta[].picnum 17125
			seta[].lotag LOTAGSAVED
			seta[].hitag HITAGSAVED
			break
			}
		}
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}
}

enda


useractor notenemy 19707

ifn LOTAGSAVED 0
{
ifrnd 2
	{
	spawn 8433 shoot SPARK shoot SPARK
	sound SHORT_CIRCUIT
	}
ifpdistl 1548
 ifp palive
  ifp pfacing
   ifcansee
    ifcount 13
	{
	 ife TOOLBOX 1
      {
		setp[].inven_icon 10
		set player_use 0
		state TOOLBOX_USAGE
		 ifhitspace
			{
			set hit_key 30
			state TOOLBOX_USE_CODE
			seta[].picnum NEWSWITCH35
			seta[].lotag LOTAGSAVED
			seta[].hitag HITAGSAVED
			break
			}
		}
	else
	ife TOOLBOX 0
		ifhitspace
			{
			set PLAYER_VOICEOVER 12
			quote 252
			}
	}
}

enda

//
// ------------------------------------------------------------
// BATTERY
// ------------------------------------------------------------
//

action BATT_FULLP -3
action BATT_LOWP -2
action BATT_DEAD -1

useractor notenemy 12437

ife temp2 0
	{
	ifspritepal 3 cstat 32768
	action BATT_DEAD
	ifpdistl 1024
		ifp pfacing
		 ifp palive
			{
			set player_use 0
			 ifhitspace { set hit_key 30 quote 255 }
			}
	}
else
ife temp2 1
 ifaction BATT_DEAD
	{
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operatemasterswitches HITAGSAVED
		operaterespawns HITAGSAVED
		}
	ife LOTAGSAVED 0
		{
		ifspritepal 3 killit
		action BATT_FULLP
		}
	else
	ifn LOTAGSAVED 0
		{
		set temp6 LOTAGSAVED
		set temp2 2
		action BATT_FULLP
		}
	}
else
ife temp2 2
	{
	add temp3 1
	ifg temp3 1560
		{
		sub temp6 1
		ife temp6 0
			{
			operateactivators HITAGSAVED THISACTOR
			operatemasterswitches HITAGSAVED
			operaterespawns HITAGSAVED
			soundonce PLUGPULLOUT
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			action BATT_DEAD
			set temp2 24
			}
		else
		ife temp6 1
			{
			action BATT_LOWP
			soundonce INTRUDER_ALERT
			}
		set temp3 0
		}
	}
else ifg temp2 2
	{
	sub temp2 1
	ifl temp2 5 set temp2 0
	}

enda

spriteshadow BATTERY_SPRITE

useractor notenemy BATTERY_SPRITE
fall

ifaction 0
	{
	sizeat 28 28
	cstat 257
	action ZERO
	}

ifpdistl 1024
 ifp pfacing
  ifp palive
  ifaction ZERO
  {
  set player_use 0
   ifhitspace
    ifcount 13
	{
	set hit_key 30
	set PLAYER_VOICEOVER 2
	action OK
	resetcount
	}
  }

ifaction ZERO cstat 257

ifaction OK
	{
	ifp pstanding
		{
		findnearactor3d 12437 1536 temp
			ifn temp -1
			{
			getav[temp].temp2 temp2
			ife temp2 0
				{
				globalsound PLUGPUTIN
				quote 256
				setactorvar[temp].temp2 1
				killit
				}
			}
		}
	cstat 2

	set temp3 player[].posx
	add temp3 512

	rotatepoint player[].posx player[].posy temp3 player[].posy player[].ang temp5 temp6

	getp[].posz temp
	add temp 4096
	seta[].z temp

	geta[].sectnum upd_sect
	updatesectorz temp5 temp6 sprite[].z upd_sect
	ife upd_sect -1 action ZERO else
		{
		seta[].x temp5
		seta[].y temp6
		changespritesect THISACTOR upd_sect
		}

	state lower_weapon

	ifhitspace
	 ifcount 13
		{
		action ZERO
		resetcount
		}

	ifpdistg 1024 { setp[].runspeed RUNNINGSPEED action ZERO }

	}

enda

// *************************************************
// VR STATION POD => hitag value = Requires firewall chip in place before being able to use. The value must match the hitag of the firewall slot
// *************************************************

action P_NEAR -5557
action P_ENEAR -5570

useractor notenemy VR_BOOTH

	ifaction 0
	{
		seta[].blend 255
		set temp 0
		whilevarn temp 16384
		{
			ifn sprite[temp].statnum 1024
			  ife sprite[temp].picnum DOORTRANSPORTEXIT
			{
				getav[temp].HITAGSAVED temp3
				ife LOTAGSAVED temp3
				{
					geta[temp].x ENDPOINTX
					geta[temp].y ENDPOINTY
					geta[temp].z ENDPOINTZ
					geta[temp].ang temp7
					set temp 16383
				}
			}
			add temp 1
		}
		action ZERO
	}

	ifpdistl 2048
	  ifcansee
	  ifp palive
	   ifp pfacing
	  ifaction ZERO
	{
		ife CHAR 6 { sound RED_COMPBEEP ifspritepal 2 action P_ENEAR else action P_NEAR }
		ife CHAR 7 { sound RED_COMPBEEP ifspritepal 2 action P_ENEAR else action P_NEAR }
	}

	ifaction P_NEAR
	{
		ifpdistl 1024
		  ifcansee
		  ifp pfacing
		  ifp palive
		{
			switch CHAR
				case 6
				case 7
				case 16
					set player_use 0
					ife use_action_allowed 1
					{
						state setIgnoreUse

						set temp 0
						set hit_key 30

						ifn HITAGSAVED 0
						{
							ife FIREWALL_ID HITAGSAVED
								set temp 1
							else
							{
								set door_locked 0
								set key_icon FIREWALL_CHIP
								set key_pal 0
								set temp 0
							}
						}
						else
							set temp 1

						ife temp 1
						{
							ifspritepal 0
							{
								globalsound ENTER_VR
								set CONTROL_VR APLAYER
								set VR_ACTION_POINTS 10
								ifmultiplayer nullop
								else
								{
									espawn 1404
									setactorvar[RETURN].EXTRASAVED CHAR
									state SPAWN_IN_FRONT
									setactorvar[RETURN].temp9 CONTROL_VR
								}
							}
							else ifspritepal 3
							{
								globalsound ENTER_VR
								set CONTROL_VR APLAYER
								set VR_ACTION_POINTS 10
								ifmultiplayer nullop
								else
								{
									espawn 1404
									setactorvar[RETURN].EXTRASAVED CHAR
									state SPAWN_IN_FRONT
									setactorvar[RETURN].temp9 CONTROL_VR
								}
							}
							action ONE
						}
					}
					break
				default
					break
			endswitch
		}
		else ifpdistg 2048
			action ZERO
	}

	ifaction P_ENEAR
	{
		ifpdistl 1024
		  ifcansee
		  ifp pfacing
		  ifp palive
		{
			switch CHAR
				case 6
				case 7
				case 16
					set player_use 0
					ife use_action_allowed 1
					{
						state setIgnoreUse

						set hit_key 30
						set screen_pal 0
						setgamepalette 0
						set VR_ACTION_POINTS 0
						globalsound EXIT_VR
						set CONTROL_VR -1
						seta[temp9].xrepeat 0
						ifn EXTRASAVED -1
						{
							operateactivators EXTRASAVED THISACTOR
							operaterespawns EXTRASAVED
							operatemasterswitches EXTRASAVED
						}
						action ONE
					}
					break
				default
					break
			endswitch
		}
		else
		ifpdistg 2048 action ZERO
	}

	ifaction ONE
	{
		state fade_out_white
		add INTERNALCOUNT 1
		geta[].sectnum upd_sect
		updatesectorz ENDPOINTX ENDPOINTY ENDPOINTZ upd_sect
		 ifn upd_sect -1
		  ifg INTERNALCOUNT 26
		{
			setp[].cursectnum upd_sect
			setp[].posx ENDPOINTX
			setp[].posy ENDPOINTY
			setp[].posz ENDPOINTZ
			setp[].ang temp7
			set INTERNALCOUNT 0
			ifspritepal 3 killit
			action ZERO
		}
		else
		 ife upd_sect -1
		{
			quote 513
			killit
		}
	}

enda

useractor notenemy VR_SWITCH

ifaction 0
{
seta[].blend 255
cstator 273
action ZERO
}

ifaction ZERO
{
ifspritepal 36 // If it's a virus door
			{
			ife HACK_UNLOCK HITAGSAVED
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				resetcount
				set HACK_UNLOCK -1
				}
			}
else
ifspritepal 49 // If it's a download door
			{
			ife HACK_UNLOCK HITAGSAVED
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				resetcount
				set HACK_UNLOCK -1
				}
			}
else
ifpdistl 1024
 ifp pfacing
  ifp palive
   ifcount 13
    ifcansee
	{
	set player_use 0
	 ifhitspace
		{
		set hit_key 30
		ifspritepal 0 // If it's blue, it's a free door
			{
			action ONE
			soundonce VR_HACK
			cstat 18
			resetcount
			}
		else ifspritepal 11 // If it's green, it needs one point
			{
			ifg VR_ACTION_POINTS 0
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				sub VR_ACTION_POINTS 1
				resetcount
				}
				else
				{
				soundonce VR_FAIL
				resetcount
				}
			}
		else ifspritepal 23 // If it's yellow, it needs two points
			{
			ifg VR_ACTION_POINTS 1
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				sub VR_ACTION_POINTS 2
				resetcount
				}
				else
				{
				soundonce VR_FAIL
				resetcount
				}
			}
		else ifspritepal 21 // If it's red, it needs three points
			{
			ifg VR_ACTION_POINTS 2
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				sub VR_ACTION_POINTS 3
				resetcount
				}
				else
				{
				soundonce VR_FAIL
				resetcount
				}
			}
		else ifspritepal 13 // If it's black, it's free but can short out and subtract points
			{
			ifrnd 128
				{
				action ONE
				soundonce VR_HACK
				cstat 18
				resetcount
				}
				else
				{
				sub VR_ACTION_POINTS 4
				ifl VR_ACTION_POINTS 0 set VR_ACTION_POINTS 0
				wackplayer
				soundonce THUNDERBALL_HIT
				palfrom 40 0 0 40
				soundonce VR_FAIL
				action ONE
				cstat 18
				resetcount
				}
			}
		}
	}
}

ifaction ONE
{
ife CONTROL_VR -1
	{
	seta[].cstat CSTATSAVED
	action ZERO
	}
}

enda

useractor notenemy 13867 // FIREWALL
ifaction 0
 ifn LOTAGSAVED 0
  action ZERO

ifaction ZERO
{
sleeptime 0
ife FIREWALL_ID LOTAGSAVED { cstat 32768 action FORM }
}

ifaction FORM
{
sleeptime 0
ife FIREWALL_ID LOTAGSAVED nullop
else
	{
	seta[].cstat CSTATSAVED
	action ZERO
	}
}

enda

useractor notenemy FIREWALL_SLOT

ifpdistl 1536
 ifp pfacing
  ifp palive
   ifcount 13
    ifcansee
	{
	ifaction 0
	 ife GOT_FIREWALL 1
		{
		set player_use 0
		setp[].inven_icon 12
		 ifhitspace
			{
			set hit_key 30
			setp[].inven_icon 0
			set GOT_FIREWALL 0
			action ONE
			sound PLUGPUTIN
			set FIREWALL_ID HITAGSAVED
			resetcount
			}
		}
	else
	 ifaction ONE
	  ife GOT_FIREWALL 0
		{
		set player_use 0
		 ifhitspace
			{
			set hit_key 30
			set GOT_FIREWALL 1
			action 0
			sound PLUGPULLOUT
			set FIREWALL_ID 0
			resetcount
			}
		}
	}
else
ifpdistg 1536
 ife player[].inven_icon 12 setp[].inven_icon 0

enda


action USED 0
action HACKED 0
action HACKED_2 0

useractor notenemy 12533 // CHARGE/DOWNLOADING STATIONS

ifaction 0
{
cstator 16
action ZERO
}

ifaction ZERO
{
ifpdistl 1024
 ifp pfacing
  ifp palive
   ifcount 13
    ifcansee
	{
	set player_use 0
	 ifhitspace
		{
		set hit_key 30
		ifspritepal 0
			{
			action USED
			soundonce VR_CHARGE
			set VR_ACTION_POINTS 10
			resetcount
			}
		ifspritepal 49
			{
			quote 750
			action HACKED
			soundonce VR_CHARGE
			set HACKING_PROGRESS 130
			resetcount
			}
		ifspritepal 36
			{
			quote 751
			action HACKED
			soundonce VR_CHARGE
			set HACKING_PROGRESS 130
			resetcount
			}
		}
	}
}

ifaction USED
{
spritepal 12
ife CONTROL_VR -1
	{
	spritepal 0
	action ZERO
	}
}

ifaction HACKED
{
ife HACKING_PROGRESS 0
	{
	ifspritepal 49 quote 752
	else ifspritepal 36 { setgamepalette 5 quote 753 }
	soundonce VR_DOWNLOAD
	set HACK_UNLOCK HITAGSAVED
	ifn LOTAGSAVED 0
		{
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		}
	spritepal 27
	action HACKED_2
	}
}

enda

// *************************************************
// SKIP PORTAL
// *************************************************

useractor notenemy 11775
ifspritepal 54 nullop
else
	{
	ife VOLUME 1
	 ife LEVEL 0
		{
		readarrayfromfile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
		ifn BEAT_EPISODES[2] 0 action ZERO
		else killit
		}

	ife VOLUME 2
	 ife LEVEL 0
		{
		readarrayfromfile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
		ifspritepal 64 // base two skip?
			{
			ifand BEAT_EPISODES[3] 64 action ZERO
			else killit
			}
			else
			{
			ifn BEAT_EPISODES[3] 0 action ZERO
			else killit
			}
		}

	ife VOLUME 4
	 ife LEVEL 0
		{
		readarrayfromfile BEAT_EPISODES F_BEAT_EPISODES_AMCTC
		ifn BEAT_EPISODES[4] 0 action ZERO
		else killit
		}

	state check_player_prox
	ife is_player_close 1
	  ifn HITAGSAVED 0
	  {
	  ife player[].fta 0
		{
		ifspritepal 64 quote 1811
		else quote 1810
		}
		   set player_use 0
			ifcount 13
		   ifhitspace
			{
			set hit_key 30
			globalsound USE_TIMEPORTAL
			ifspritepal 64 startlevel 2 26
			else
				{
				ife VOLUME 1 startlevel 1 1
				ife VOLUME 2 startlevel 2 1
				ife VOLUME 4 startlevel 4 1
				}
			set CHAR -1
			set CHARSELECT 1
			killit
			}
	  }
	}

enda

// *************************************************
// TIME PORTAL
// *************************************************

spritenopal 11772
spritenoshade 11772

useractor notenemy 11772

ifaction 0
{
sizeat 32 32
seta[].mdflags 16
seta[].blend 255
cstat 2
set temp 0
whilevarn temp 16384
{
  ifn sprite[temp].statnum 1024
  {
    ife sprite[temp].picnum DOORTRANSPORTEXIT
    {
     getav[temp].HITAGSAVED temp3
     ife LOTAGSAVED temp3
		{
		geta[temp].x ENDPOINTX
		geta[temp].y ENDPOINTY
		geta[temp].z ENDPOINTZ
		geta[temp].ang temp7
		set temp 16383
		}
    }
  }
  add temp 1
}
action ZERO
}

state check_player_prox
ife is_player_close 1
  ifn LOTAGSAVED 0
  {
	ife CHAR 0
	{
	ifand NOISE_SAID 8 nullop else { set PLAYER_VOICEOVER 14 xorvar NOISE_SAID 8 }
	   set player_use 0
		ifcount 13
	   ifhitspace
		{
		set hit_key 30
		globalsound USE_TIMEPORTAL
		spawn 8433
		action USED
		resetcount
		}
	}
  }

ifaction USED
	{
	state fade_out_white
	getp[].cursectnum upd_sect
	updatesectorz ENDPOINTX ENDPOINTY ENDPOINTZ upd_sect
	 ifn upd_sect -1
		{
		setp[].cursectnum upd_sect
		setp[].posx ENDPOINTX
		setp[].posy ENDPOINTY
		setp[].posz ENDPOINTZ
		setp[].ang temp7
		action ZERO
		}
	else
	 ife upd_sect -1
		{
		addlogvar ENDPOINTX
		addlogvar ENDPOINTY
		addlogvar ENDPOINTZ
		addlogvar upd_sect
		quote 513
		killit
		}
	}

enda

// *************************************************
// SHADOW GATE
// *************************************************

spritenopal 27112
spritenoshade 27112

useractor notenemy 27112

ifaction 0
{
sizeat 64 64
seta[].mdflags 16
seta[].blend 255
cstat 2
set temp 0
whilevarn temp 16384
{
  ifn sprite[temp].statnum 1024
  {
    ife sprite[temp].picnum DOORTRANSPORTEXIT
    {
     getav[temp].HITAGSAVED temp3
     ife LOTAGSAVED temp3
		{
		geta[temp].x ENDPOINTX
		geta[temp].y ENDPOINTY
		geta[temp].z ENDPOINTZ
		geta[temp].ang temp7
		set temp 16383
		}
    }
  }
  add temp 1
}
action ZERO
}

state check_player_prox
ife is_player_close 1
  ifn LOTAGSAVED 0
  {
	ife CHAR 17
	{
	ifand NOISE_SAID 8 nullop else { set PLAYER_VOICEOVER 14 xorvar NOISE_SAID 8 }
	   set player_use 0
		ifcount 13
	   ifhitspace
		{
		set hit_key 30
		globalsound USE_PORTAL
		spawn 8433
		action USED
		resetcount
		}
	}
  }

ifaction USED
	{
	palfrom 60 60 60 60
	getp[].cursectnum upd_sect
	updatesectorz ENDPOINTX ENDPOINTY ENDPOINTZ upd_sect
	 ifn upd_sect -1
		{
		setp[].cursectnum upd_sect
		setp[].posx ENDPOINTX
		setp[].posy ENDPOINTY
		setp[].posz ENDPOINTZ
		setp[].ang temp7
		action ZERO
		}
	else
	 ife upd_sect -1
		{
		addlogvar ENDPOINTX
		addlogvar ENDPOINTY
		addlogvar ENDPOINTZ
		addlogvar upd_sect
		quote 513
		killit
		}
	}

enda


// *************************************************
// SUPPORT RADIO STUFF
// *************************************************

useractor notenemy 13262

ifpdistl 1500
 ife CHAR 3
  ifp pfacing
   ifp palive
   ifaction 0
	{
	set player_use 0
	 ifhitspace
		{
		set hit_key 30
	    ifpinventory GET_HOLODUKE 0
		{
		set PLAYER_VOICEOVER 15
		soundonce RADIO
		quote 263
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		setp[].holoduke_amount 0
		setp[].inven_icon 0
		action ONE
		}
		else
			{
			set PLAYER_VOICEOVER 14
			quote 262
			}
		}
	}

enda

// *************************************************
// TIME PORTAL STUFF
// *************************************************


useractor notenemy 10672 // Property sign
ifcount 26
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 killit
	resetcount
	}
enda

useractor notenemy 12401 // chainlink fence
ifcount 26
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 killit
	resetcount
	}
enda


useractor notenemy MAKE_SWITCH // Make a switch!
cstat 32768

	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		cstat 16
		seta[].picnum ZVELSAVED
		}

enda

// *************************************************
// BARB_WIRE
// *************************************************

useractor notenemy 10729
ifaction 0
{
cstator 1
action ZERO
}
enda

// *************************************************
// TIME ZONE STUFF
// *************************************************

gamevar HUB_MAP 0 0

useractor notenemy 11493
cstat 32768

ifspritepal 0 nullop // For future stuff....
else
ifspritepal 1 { set TIMEZONE 1 ifl max_ally 4 set max_ally 4 killit } // Highwire Revolution period
else
ifspritepal 2 { set HUB_MAP 1 } // base
else
ifspritepal 3 { set TIMEZONE 2 killit } // Micky Begins
else
ifspritepal 4 { set TIMEZONE 3 killit } // turns snow into fire particles
else
ifspritepal 5 { set TIMEZONE 5 killit } // Snowfall future
else
ifspritepal 6 { set TIMEZONE 6 set GOT_KNIFE 0 killit  } // Snowfall VR stuff
else
ifspritepal 8 { set TIMEZONE 8 killit } // VR stuff (also used in one of the old EDF missions...)
else
ifspritepal 9 { set TIMEZONE 9 killit } // cutscene
else
ifspritepal 10 { set TIMEZONE 10 killit } // Ringworld
else
ifspritepal 11 // Ringworld town hub
	{
	set TIMEZONE 10
	ifand ELYSION_QUEST 4096 { ifand CHAR_APP 64 nullop else xor CHAR_APP 64 } // James quest done?
	ifand ELYSION_QUEST 131072 { ifand CHAR_APP 4 nullop else xor CHAR_APP 4 } // Merlijn quest done?
	ifand ELYSION_QUEST 512 { ifand CHAR_APP 32 nullop else xor CHAR_APP 32 } // Rusty quest done?
	ifand ELYSION_QUEST 64 { ifand CHAR_APP 128 nullop else xor CHAR_APP 128 }  // Micky quest done?
	}
else
ifspritepal 12 { set TIMEZONE 11 killit } // Ringworld
else
ifspritepal 20 { set TIMEZONE 20 killit } // Post-Ringworld
else
ifspritepal 21 { ifpdistl 2048 set TIMEZONE 21 }
else
ifspritepal 100 { ifpdistl 2048 set TIMEZONE 0 }
enda

// *************************************************
// DIGIT
// *************************************************

move DIG_MOVE 32 -32

spritenoshade 6142

useractor notenemy 6142
ifaction 0
{
sizeat 32 32
spritepal 8
cstat 2050
seta[].shade 0
seta[].blend 255
ifrnd 128 action ONE else action ZERO
move DIG_MOVE getv spin randomangle
resetcount
}
else
{
geta[].shade temp
add temp 1
seta[].shade temp
sizeto 64 64
ifg temp 40 killit
}

enda

// *************************************************
// MB SUIT
// *************************************************

useractor notenemy 16414

ifaction 0 action FIVE_ANG

ifaction FIVE_ANG
{
ifpdistl 1024
 ifcansee
  ifp pfacing
  {
  set player_use 0
   ifhitspace
	{
	set hit_key 30
	screensound PUT_ON_EQUIP
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	killit
	}
   }
}

enda

// *************************************************
// HOLO SWITCH
// *************************************************

useractor notenemy 22321

ifaction 0
{
ifpdistl 1024
 ifcansee
  ifp pfacing
  {
  set player_use 0
   ifhitspace
	{
	set hit_key 30
	state lower_weapon
	ifspritepal 0 { set HOLO_ACCESS 1 sound HOLO_SWITCH action EIGHT }
	else
		{
		ife CHAR 0 xorvar HOLO_ACCESS 2
		ife CHAR 1 xorvar HOLO_ACCESS 4
		ife CHAR 2 xorvar HOLO_ACCESS 8
		ife CHAR 3 xorvar HOLO_ACCESS 16
		ife CHAR 4 xorvar HOLO_ACCESS 32
		ife CHAR 5 xorvar HOLO_ACCESS 64
		ife CHAR 6 xorvar HOLO_ACCESS 128
		ife CHAR 7 xorvar HOLO_ACCESS 256
		ife CHAR 10 xorvar HOLO_ACCESS 512
		ife CHAR 11  xorvar HOLO_ACCESS 1024
		ife CHAR 12 xorvar HOLO_ACCESS 2048
		ife CHAR 13 xorvar HOLO_ACCESS 4096
		ife CHAR 14 xorvar HOLO_ACCESS 8192
		ife CHAR 15 { sound HOLO_SW_ERROR ife PLAYER_VOICEOVER 0 set PLAYER_VOICEOVER 14 break }
		ife CHAR 17 xorvar HOLO_ACCESS 32768

		sound HOLO_SWITCH
		action EIGHT
		}
	}
  }
}

enda

useractor notenemy 22332

ifaction 0
{
ifpdistl 1024
 ifcansee
  ifp pfacing
  {
  ifn HOLO_ACCESS 0
	  {
	  set player_use 0
	   ifhitspace
		{
		set hit_key 30
		state lower_weapon
		sound HOLO_SWITCH
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		action FIVE
		resetactioncount
		}
	  }
	 else
	  ifhitspace
		{
		set hit_key 30
		action MINUSONE
		sound HOLO_SW_ERROR
		}
  }
}

ifaction MINUSONE ifactioncount 8 action 0

ifaction FIVE ifactioncount 2 { action SIX resetactioncount }
ifaction SIX ifactioncount 2 { action SEVEN resetactioncount }
ifaction SEVEN ifactioncount 2 { action EIGHT resetactioncount }
ifaction EIGHT ifactioncount 2 { action NINE resetactioncount }

enda

useractor notenemy 22342

ifaction 0
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	sound TYPING_SOUND
	action ONE
	}
}

ifaction ONE
{
soundonce TYPING_SOUND
checkactivatormotion HITAGSAVED
ifn RETURN 1
	{
	stopsound TYPING_SOUND
	action 0
	}
}

enda


// *************************************************
// HITSCAN END
// *************************************************

spriteflags 5064 33554432 // will wake up any enemies it has line of sight to

useractor notenemy 5064
ifcount 1 killit
enda

// *************************************************
// LASER SIGHT
// *************************************************

spriteflags 3023 16

spritenopal 3023
spritenoshade 3023

useractor notenemy 3023
seta[].blend 255
cstat 1154
iffloordistl 4 { ifand sector[].floorstat 1 killit else cstator 32 }
else ifceilingdistl 4 { ifand sector[].floorstat 1 killit else cstator 32 }
sizeat 6 6
seta[].shade -127
ifcount 1 killit
enda

// *************************************************
// AIRDROP MARKERS
// *************************************************

spriteflags 3023 16

spritenopal 3023
spritenoshade 3023

defstate airdrop_marker_sprite
seta[].blend 255
ifactor 21122 cstat 2
else ifactor 21123 cstat 2
else ifactor 21124 cstat 2
else
	{
	cstat 1154
	iffloordistl 4 { ifand sector[].floorstat 1 killit else cstator 32 }
	}
ifceilingdistl 4 killit
sizeat 24 24
seta[].shade -127
ifcount 1 killit
ends

useractor notenemy 21120 // base circle
state airdrop_marker_sprite
ifl SUPPLY_LEVEL 30 spritepal 21
else ife scan_mode 1 spritepal 11
else spritepal 23
ifand sector[].ceilingstat 1 nullop
else spritepal 21
enda

useractor notenemy 21121 // error
state airdrop_marker_sprite
enda

useractor notenemy 21122 // supply drop
state airdrop_marker_sprite
enda

useractor notenemy 21123 // airstrike
state airdrop_marker_sprite
enda

useractor notenemy 21124 // not enough supplies
state airdrop_marker_sprite
enda

// *************************************************
// Sprite sci-fi doors
// *************************************************

action DCLOSED 0 1 1 1 0
action DOPEN   0 6 1 1 5
action DOPENED 5 1 1 1 0
action DCLOSE  5 6 1 -1 5

defstate seb_sprite_door
ifaction 0
	{
	ifactor 17676
		{
		espawn 17677
		seta[RETURN].x sprite[].x
		seta[RETURN].y sprite[].y
		seta[RETURN].z sprite[].z
		seta[RETURN].xrepeat sprite[].xrepeat
		seta[RETURN].yrepeat sprite[].yrepeat
		seta[RETURN].cstat sprite[].cstat
		seta[RETURN].ang sprite[].ang
		}
	ifactor 17677 cstat 275 else
	cstat 273
	set INTERNALCOUNT 0
	action DCLOSED
	}

ifaction DCLOSED
 ifpdistl 1024
  ifp pfacing
   ifp palive
  {
  set player_use 0
	ifhitspace
	{
	set hit_key 30
	ifactor 17677 nullop else
	sound SOPEN
	ifg HITAGSAVED 0 setarray LIGHT_OPEN[HITAGSAVED] 2
	action FORM
	resetactioncount
	}
  }

ifaction FORM
{
			geta[].ang temp6
			sub temp6 512
			cos temp7 temp6
			sin temp8 temp6
			set temp3 96
			divvarvar temp7 temp3
			divvarvar temp8 temp3
			movesprite THISACTOR temp7 temp8 0 4294901808 temp
ifactioncount 6 action ZERO
}

ifaction ZERO
	{
	ifactor 17677 cstat 18 else
	cstat 16

	ifcount 1
	{
	add INTERNALCOUNT 1
	clamp INTERNALCOUNT 0 50
	resetcount
	}

	ife INTERNALCOUNT 50
	 ifpdistg 2048
		{
		ifactor 17677 nullop else
		sound SCLOSE
		ifg HITAGSAVED 0 setarray LIGHT_OPEN[HITAGSAVED] 2
		action FORMED
		resetactioncount
		set INTERNALCOUNT 0
		}
	}
ifaction FORMED
	{
				geta[].ang temp6
				add temp6 512
				cos temp7 temp6
				sin temp8 temp6
				set temp3 96
				divvarvar temp7 temp3
				divvarvar temp8 temp3
				movesprite THISACTOR temp7 temp8 0 4294901808 temp
	ifactioncount 6
		{
		action DCLOSED
		ifactor 17677 cstat 275 else
		cstat 273
		}
	}
ends

useractor notenemy 17672
state seb_sprite_door
enda

useractor notenemy 17673
state seb_sprite_door
enda

useractor notenemy 17674
state seb_sprite_door
enda

useractor notenemy 17675
state seb_sprite_door
enda

useractor notenemy 17676
state seb_sprite_door
enda

useractor notenemy 17677
state seb_sprite_door

ifhitweapon
	{
	lotsofglass 30
	sound GLASS_BREAK3
	killit
	}

enda


// *************************************************
// IW SPRITE DOOR
// *************************************************

useractor notenemy 13050
cstat 273

ifaction 0
{
set INTERNALCOUNT 0
action DCLOSED
break
}

ifaction DCLOSED
 ifpdistl 1024
  ifp pfacing
   ifp palive
  {
  set player_use 0
	ifhitspace
	{
	set hit_key 30
	sound SOPEN
	action DOPEN
	}
  }

ifaction DOPEN
ifactioncount 6
{
action DOPENED
}

ifaction DOPENED
{
cstat 16

ifcount 1
{
add INTERNALCOUNT 1
resetcount
}

ife INTERNALCOUNT 50
	{
	sound SCLOSE
	action DCLOSE
	set INTERNALCOUNT 0
	}
}


ifaction DCLOSE
{
	ifactioncount 6
	{
	action DCLOSED
	cstat 273
	}
}

enda

// *************************************************
// SPRITE DOOR 2
// *************************************************


action D2CLOSED 0 1 1 1 0
action D2OPEN   0 7 1 1 5
action D2OPENED 6 1 1 1 0
action D2CLOSE  6 7 1 -1 5

useractor notenemy 18201

ifaction 0
{
cstat 273
set INTERNALCOUNT 0
action D2CLOSED
break
}

ifaction D2CLOSED
{
ifn HITAGSAVED 0
	{
	sleeptime 0
	ifpdistl 1536 ifp pfacing { set player_use 0 ifhitspace { set hit_key 30 set door_locked 0 set key_icon 3587 soundonce LOCKED_HITECH2 } }
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		sound SOPEN
		action D2OPEN
		}
	}
	else
 ifpdistl 1536
  ifp pfacing
   ifp palive
	{
	  set player_use 0
		ifhitspace
		{
		set hit_key 30
		sound SOPEN
		action D2OPEN
		}
	}
 }

ifaction D2OPEN
ifactioncount 7
{
action D2OPENED
}

ifaction D2OPENED
{
cstat 16
ifn HITAGSAVED 0 nullop else
	{
	ifcount 1
		{
		add INTERNALCOUNT 1
		resetcount
		}
	ife INTERNALCOUNT 128
		{
		sound SCLOSE
		action D2CLOSE
		set INTERNALCOUNT 0
		}
	}
}


ifaction D2CLOSE
{
	ifactioncount 7
	{
	action D2CLOSED
	cstat 273
	}
}

enda

// *************************************************
// RUSTY GLASS
// *************************************************

useractor notenemy 401
ifspritepal 5
{
cstator 257
ifhitweapon
	{
	sound HITGLASS
	lotsofglass 5
	strength 1
	cactor 505
	}
}
enda

useractor notenemy 505
cstator 257
ifhitweapon
	{
	sound GLASS_BREAK3
	lotsofglass 25
	killit
	}
	else
	ifpdistl 1024
	ifp prunning
	{
	sound GLASS_BREAK3
	wackplayer
	ifrnd 32 { addphealth -10 palfrom 20 20 0 0 }
	lotsofglass 25
	killit
	}
enda




// **************************************************************************************************
// AMC BASE STUFF
// **************************************************************************************************

useractor notenemy 10563

ifspritepal 3
	{
	ife VOLUME 0 killit
	}

enda

// *************************************************
// RANDOM GUNFIRE
// *************************************************

spritenoshade 13214
spritenopal 13214
useractor notenemy 13214

ifmultiplayer killit

ife STOP_EFFECTS 1 killit

ifaction 0
{
cstat 2
seta[].blend 255
seta[].shade 40
seta[].mdflags 16
sleeptime 0
ife HITAGSAVED 0 action ZERO
else
	{
	checkactivatormotion HITAGSAVED
	 ife RETURN 1
	  action ZERO
	}
}

ifaction ZERO
{
cstat 2
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	 ife RETURN 1
	  action 0
	}

	ifrnd 8
	ifg framerate 15
	ifn gotpic[13214] 0
	{
	setarray gotpic[13214] 0
	randvar temp3 2048
	ifrnd 128 mulvar temp3 -1
	ifspritepal 0 zshoot temp3 FAKE_BULLET
	else ifspritepal 1 { setprojectile[FAKE_BULLET].pal 1 zshoot temp3 FAKE_BULLET setprojectile[FAKE_BULLET].pal 0 }
	else ifspritepal 2 spawn GROUNDEXPLOSION
	else ifspritepal 3 spawn EXPLOSION2
	else ifspritepal 6
		{ ifrnd 8 sound RPG_EXPLODE else ifrnd 8 sound EXPLOSION_BIG
		shoot SPARK shoot SPARK spawn BIG_EXPLOSION }
	else ifspritepal 8 spawn 8433
	}
}

enda

// *************************************************
// PROJECTILE FLARE
// *************************************************

spritenopal PROJECTILE_FLARE
spritenoshade PROJECTILE_FLARE

useractor notenemy PROJECTILE_FLARE
seta[].blend 255
ifaction 0
{
	ifspawnedby REDM_BLAST
	{
	seta[].shade -127
	sizeat 76 76
	spritepal 2
	cstat 642
	}
	ifspawnedby 30081
	{
	seta[].shade -127
	sizeat 76 76
	spritepal 2
	cstat 642
	}
	ifspawnedby BLOOD_BLAST
	{
	seta[].shade -127
	sizeat 48 48
	spritepal 2
	cstat 642
	}
	ifspawnedby HADESBLAST
	{
	seta[].shade -127
	sizeat 16 16
	spritepal 2
	cstat 642
	}
	ifspawnedby QUIETUS_BLAST
	{
	seta[].shade -127
	sizeat 16 16
	spritepal 8
	cstat 642
	}
	ifspawnedby LAVABALL
	{
	seta[].shade -127
	sizeat 24 24
	spritepal 2
	cstat 642
	}
	ifspawnedby 6221 // Aludran purple blast
		{
		seta[].shade -127
		sizeat 40 40
		spritepal 30
		cstat 642
		}
	ifspawnedby 9115
	{
	seta[].shade -127
	sizeat 24 24
	spritepal 2
	cstat 642
	}
	ifspawnedby 12712 // Anubis magic blast
		{
		seta[].shade -127
		sizeat 32 32
		spritepal 1
		cstat 642
		}
	ifspawnedby 12717 // SIREN Curse blast
		{
		seta[].shade -127
		sizeat 32 32
		spritepal 2
		cstat 642
		}
	ifspawnedby CYCLOID_PLASMA
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 8
	cstat 642
	}
	ifspawnedby RUBYBLAST
	{
	seta[].shade -127
	sizeat 24 24
	spritepal 2
	cstat 642
	}
	ifspawnedby HADESBLASTH
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 2
	cstat 642
	}
	ifspawnedby BRUISERFIREBALL
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 2
	cstat 642
	}
	ifspawnedby 15175
	{
	seta[].shade -127
	sizeat 40 40
	spritepal 2
	cstat 642
	}
	ifspawnedby 7163
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 2
	cstat 642
	}
	ifspawnedby SPELL2
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 1
	cstat 642
	}
	ifspawnedby GOLD_SPELL2
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 7
	cstat 642
	}
	ifspawnedby HEAT_SINK
	{
	seta[].blend 255
	seta[].shade -12
	sizeat 12 12
	spritepal 2
	cstat 642
	}
	ifspawnedby 6198
	{
	seta[].shade -127
	sizeat 12 12
	spritepal 1
	cstat 642
	}
	ifspawnedby 7160
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 1
	cstat 642
	}
	ifspawnedby ICE_BLAST
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 1
	cstat 642
	}
	ifspawnedby 9376
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 19
	cstat 642
	}
	ifspawnedby SHRINKSPARK
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 8
	cstat 642
	}
	ifspawnedby SPELL4
	{
	seta[].shade -127
	sizeat 32 32
	spritepal 2
	cstat 642
	}
	ifspawnedby BRUISER
	{
	seta[].shade -127
	sizeat 64 64
	spritepal 2
	cstat 1666
	}
	ifspawnedby SPELL5
	{
	seta[].shade -127
	sizeat 16 16
	spritepal 2
	cstat 642
	}
	ifspawnedby SPELL14
	{
	seta[].shade -127
	sizeat 16 16
	spritepal 2
	cstat 642
	}
	ifspawnedby SPELL8
	{
	seta[].shade -127
	sizeat 20 20
	spritepal 49
	cstat 642
	}
	ifspawnedby 7047
	{
	seta[].shade -127
	sizeat 48 48
	spritepal 2
	cstat 642
	}
	ifspawnedby 22828
	{
	seta[].shade -127
	sizeat 48 48
	spritepal 2
	cstat 642
	}
	ifspawnedby PRIEST_BLAST
	{
	seta[].shade -127
	sizeat 24 24
	spritepal 19
	cstat 642
	}
	action ZERO
}
ifcount 1 killit
enda


// *************************************************
// MECH TUBE
// *************************************************

useractor notenemy 10637

ifspritepal 3
 ifpdistl 4096
  ifn player_using_mech -1
   killit

enda

// *************************************************
// TRIGGER STUFF
// ACTIVATE STUFF IF INVENTORY INVEN ACTIVE
// ACTIVATE IF NEAR
// Set hitag to -1 to activate regardless of player position (use to rig effects together I guess)
// *************************************************

spritenopal 3347
useractor notenemy 3347
cstat 32768

ife HITAGSAVED 0
	set HITAGSAVED 1024

ifn XVELSAVED 0
{
	action ZERO

	checkactivatormotion XVELSAVED
	set MASTERSWITCH_SUBSCRIBE XVELSAVED

	ife RETURN 1 { action 0 set XVELSAVED 0 }
	else ife MASTERSWITCH_RETURN 1
	{
		action 0
		set XVELSAVED 0
		set MASTERSWITCH_RETURN 0
	}
}

ifaction 0
{
	ife HITAGSAVED -1
	{
		set temp 1
	}
	else
	ifspritepal 60
	{
		ifcansee ifcanseetarget { spritepal 0 set temp 1 }
	}
	else
	ifspritepal 6
	{
	ife CHARSELECT 1 nullop else ifpdistl 1024 set temp 1
	}
	else
	ife sprite[].ang 512
	{
	ife sprite[].sectnum player[].cursectnum // same sector
	 ifg player[].posz sprite[].z // player is lower than the actor?
	  set temp 1
	}
	else
	{
		dist temp6 THISACTOR player[].i

		ifl temp6 HITAGSAVED
			set temp 1
	}

	// either no distance check needed, or player is closer than given distance
	ife temp 1
	{
		ifg EXTRASAVED 0
			screensound EXTRASAVED

		ifspritepal 0
		{
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
		}

		ifspritepal 100
		 ife player_in_vehicle 0
		{
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
		}

		ifspritepal 1
		{
			ifg GOT_EXPLOSIVE 0
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}
		ifspritepal 2
		{
			ifg GAS_MASK 0
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}
		ifspritepal 3
		{
			ifg TOOLBOX 0
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}

		ifspritepal 4
		{
			ifg JUMP_BOOTS 0
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}

		ifspritepal 7
		{
			ifcansee
			 ifp pfacing
				{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
				}
		}

		ifspritepal 8
		{
			ifn player_using_mech -1
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}

		ifspritepal 10
		{
			ife temple_key 4
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}

			ifand temple_key 1
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}
		ifspritepal 15
		{
			ifand OLDKEYS 1
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}

		ifspritepal 23
		{
			ifand player[].got_access 23
			{
				operateactivators LOTAGSAVED THISACTOR
				operaterespawns LOTAGSAVED
				operatemasterswitches LOTAGSAVED
				killit
			}
		}
	}
}

enda

// *************************************************
// RADIATION SCREEN
// *************************************************

useractor notenemy 14034

ifaction 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		action TWO
		}
	}

enda

// *************************************************
// WELD POINT
// *************************************************

action MELTED -7

useractor notenemy 6582

ifaction 0
	{
	cstat 257
	action ONE
	}

ifaction ONE
	{
	strength 100
		ifhitweapon
			{
				ife sprite[].htpicnum MELTA_BEAM
					{
					getav[myowner].temp6 temp6
					add temp6 1
					setactorvar[myowner].temp6 temp6
					sound COOKINGDEEPFRIER
					shoot SPARK
					sizeto 32 32
					action MELTED
					resetcount
					}
				strength 1
			}
	}

	ifaction MELTED
		{
		seta[].blend 255
		cstat 146
		ifcount 26 sizeto 8 8
		ifcount 32 spritepal 4
		ifcount 52 killit
		}


enda

action MELTED_2 -6

useractor notenemy 6581 // master weld point

ifaction 0
	{
	cstat 32768
	set temp4 0
	set temp 0
		whilevarn temp 16384
		{
		  ifn sprite[temp].statnum 1024
		  {
			ife sprite[temp].picnum 6582
				{
				getav[temp].HITAGSAVED temp8
				ife temp8 HITAGSAVED
					{
					add temp4 1
					setactorvar[temp].myowner THISACTOR
					}
				}
		  }
		  add temp 1
		}
	sub temp4 1
	action ZERO
	}

ifaction ZERO
	{
		ifg temp6 temp4
			{
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
			}
	}

enda

useractor notenemy 14038 // slashed grate

ifaction 0 { cstator 257 strength 100 action ZERO }

ifaction ZERO
	{
	ifp pfacing
	 ifpdistl 2048
	 ife FIRE_SUIT 1
		{
		set player_use 0
		 ifhitspace
			{
			set hit_key 30
			sound COOKINGDEEPFRIER
			shoot SPARK
			shoot SPARK
			shoot SPARK
			shoot SPARK
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			action ONE
			}
		}
		else
		ifn sprite[].htextra 0
			{
				ife sprite[].htpicnum MELTA_BEAM
					{
					sound COOKINGDEEPFRIER
					shoot SPARK
					shoot SPARK
					shoot SPARK
					shoot SPARK
					operateactivators LOTAGSAVED THISACTOR
					operaterespawns LOTAGSAVED
					operatemasterswitches LOTAGSAVED
					action ONE
					}
				strength 1
			}
	}



enda

// *************************************************
// TIME DELAY
// *************************************************

useractor notenemy 3592
cstat 32768


ifaction 0
	{
	checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			action ZERO
			set INTERNALCOUNT 0
			ifspritepal 1 set END_LEVEL 90
			}
	}

ifaction ZERO
	{
	ifspritepal 1
		{
		 ife END_LEVEL 0
			{
			ifn XVELSAVED 0 globalsoundvar XVELSAVED
			ifn YVELSAVED 0 set destruct_trigger YVELSAVED
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
			}
		}
	else
		{
		add INTERNALCOUNT 1
		ifspritepal 3 addlogvar INTERNALCOUNT
		 ife INTERNALCOUNT EXTRASAVED
			{
			ifn XVELSAVED 0 globalsoundvar XVELSAVED
			ifn YVELSAVED 0 set destruct_trigger YVELSAVED
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
			}
		}

	}

enda

// *************************************************
// WORLD TRIGGER
// *************************************************

useractor notenemy 22771
ifaction 0 { cstat 32768 action ZERO }

ifspritepal 0 // 'write' mode
	{
	ifvarvarand WORLD_TRIGGER LOTAGSAVED killit
	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			xorvarvar WORLD_TRIGGER LOTAGSAVED
			killit
			}
		}
	else
	ife sprite[].sectnum player[].cursectnum
		{
		xorvarvar WORLD_TRIGGER LOTAGSAVED
		killit
		}
	}
else
ifspritepal 2 // 'read' mode
	{
	ifand WORLD_TRIGGER HITAGSAVED
		{
		operateactivators LOTAGSAVED THISACTOR
		operatemasterswitches LOTAGSAVED
		operaterespawns LOTAGSAVED
		killit
		}
	}
enda

// ****************************************************************************************************************************************************************************************************
// SPRITE DETECTOR
// ****************************************************************************************************************************************************************************************************

useractor notenemy SPRITE_DETECT
ifaction 0
	{
	cstat 32768
	ife HITAGSAVED 0 set HITAGSAVED 1024
	action ZERO
	}
sleeptime 0
ifcount 30
	{
	ifspritepal 0 findnearsprite3dvar BATTERY_SPRITE HITAGSAVED temp
	ifspritepal 2 findnearsprite3dvar PIG HITAGSAVED temp
	ifn temp -1
		{
		operateactivators LOTAGSAVED THISACTOR
		operatemasterswitches LOTAGSAVED
		operaterespawns LOTAGSAVED
		ifspritepal 2
			{
			spawn SKULLEXPLOSION
			globalsound CULTIST_HAUNT
			quake 13
			seta[temp].htextra 100
			}
		killit
		}
	resetcount
	}

enda

// ****************************************************************************************************************************************************************************************************
// SPECIAL EFFECTS
// This is my go-to actor for stuff that is very case specific, there is general purpose stuff here but it's all undocumented
// ****************************************************************************************************************************************************************************************************

defstate PATCOS_SMASH
operateactivators LOTAGSAVED THISACTOR
set PATCOS_CHARGE 120
ifpdistl 1536 { palfrom 60 60 0 0 addphealth -60 }
killit
ends

useractor notenemy 3586
cstat 32768

ifspritepal 1 // plays a teleport sound when hitag is activated
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	palfrom 64 0 64 0
	globalsound TELEPORTER
	killit
	}
}
ifspritepal 3 // plays the scary shock sound when either hitag is activated or if player is close by
{
ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		state fade_out_white
		wackplayer
		stopallsounds
		globalsound MIND_ATTACK
		killit
		}
	}
	else
	ifpdistl 1024
	 ifcansee
	  ifp palive
		{
		state fade_out_white
		wackplayer
		stopallsounds
		globalsound MIND_ATTACK
		killit
		}
}
ifspritepal 4 // stops any effects that check for STOP_EFFECTS when hitag activated
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	set STOP_EFFECTS 1
	stopallsounds
	killit
	}
}
ifspritepal 5 // sets the skybox_tile, decrepated due to the improvements by Sang
{
ifn HITAGSAVED 0
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	set skybox_tile LOTAGSAVED
	}
}
else
ifn EXTRASAVED -1
{
ifpdistl 1024
	{
	ife EXTRASAVED 0 set skybox_tile -1
	else set skybox_tile EXTRASAVED
	}
}
else
	{
	set skybox_tile LOTAGSAVED
	killit
	}
}
ifspritepal 6 // clears all NPC followers when player is close by
{
ifpdistl 1024 state hard_removefollower
}
ifspritepal 7 // an ugly hack to temporarily fix the glitches in shadow realm until hopefully the E32 team can fix them
{
ifaction 0
	{
	gets[].floorpicnum temp
	gets[].ceilingpicnum temp2
	ife EXTRASAVED -1 set EXTRASAVED 4608
	ife EXTRASAVED 0 set EXTRASAVED 4608
	action ZERO
	}
ifaction ZERO
	{
	sleeptime 0
	ifpdistg 81920
		{
		ife temp3 0
			{
			sets[].floorpicnum EXTRASAVED
			sets[].ceilingpicnum EXTRASAVED
			set temp3 1
			}
		}
		else
		{
		ife temp3 1
			{
			sets[].floorpicnum temp
			sets[].ceilingpicnum temp2
			set temp3 0
			}
		}
	}
}
ifspritepal 8 // set civilians to panic when hitag is activated
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	ifn LOTAGSAVED 0 set CIVS_PANIC LOTAGSAVED else
	set CIVS_PANIC 1
	killit
	}
}
ifspritepal 9 // Digging spot for combat shovel
{
ife GOT_KNIFE 3
	{
	ifpdistl 1536
	 ifp palive
		{
		set player_use 0
		 ifhitspace
			{
			set hit_key 30
			action ZERO
			}
		}
	}
ifaction ZERO
	{
	state lower_weapon
		add INTERNALCOUNT 1
		ife INTERNALCOUNT 2 { sound DIG_UP state fade_in_black }
		else ifg INTERNALCOUNT 12 ifl INTERNALCOUNT 240
			{
			set cus_fade_duration 240
			ifrnd 2 set PLAYER_VOICEOVER 2
			}
		else ife INTERNALCOUNT 240
			{
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			}
		else ifg INTERNALCOUNT 240
			{
			state fade_out_black
			}
		ifg INTERNALCOUNT 300 killit
	}
}
ifspritepal 10 // TANK RUN OVER
{
ifpdistl 6100
 {
 getav[PLAYER_IDENTITY].ANGLE temp
 ifn temp 0
	 {
	 operateactivators HITAGSAVED THISACTOR
	 operaterespawns HITAGSAVED
	 operatemasterswitches HITAGSAVED
	 lotsofglass 15
	 quake 20
	 sound GLASS_BREAKING
	 spawn BIG_SMOKE2 spawn BIG_SMOKE2 spawn BIG_SMOKE2
	 debris SCRAP1 20
	 debris SCRAP2 20
	 soundonce CARSMASH
	 killit
	 }
 }
else // check for enemy tank
	{
	ifcount 5
		 {
		 findnearactor3d 13608 4000 temp3
		 ifn temp3 -1
			{
			 operateactivators HITAGSAVED THISACTOR
			 operaterespawns HITAGSAVED
			 operatemasterswitches HITAGSAVED
			 lotsofglass 15
			 quake 20
			 sound GLASS_BREAKING
			 spawn BIG_SMOKE2 spawn BIG_SMOKE2 spawn BIG_SMOKE2
			 debris SCRAP1 20
			 debris SCRAP2 20
			 soundonce CARSMASH
			 killit
			}
		resetcount
		}
	}
}
ifspritepal 11 // deactivates hard-coded forcefield on trigger
{
ifaction 0
	{
	set temp7 0
	whilevarvarn temp7 NUMWALLS
		{
			ife wall[temp7].picnum 663
			 ife wall[temp7].hitag HITAGSAVED
				{
				set temp5 temp7
				action ZERO
				}
		add temp7 1
		}
	action ZERO
	}
ifaction ZERO
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		setwall[temp5].overpicnum 0
		setwall[temp5].cstat 0
		getwall[temp5].nextwall temp6
		setwall[temp6].overpicnum 0
		setwall[temp6].cstat 0
		}
	}

}
ifspritepal 12 // flashes the screen blue
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	wackplayer
	palfrom 64 0 0 64
	killit
	}
}
ifspritepal 13 // simple floor change actor
{
sleeptime 0
			checkactivatormotion HITAGSAVED
			ife RETURN 1
			{
			sets[].floorpicnum LOTAGSAVED
			}
}
ifspritepal 14 // activate this on level start
{
operateactivators HITAGSAVED THISACTOR
operaterespawns HITAGSAVED
operatemasterswitches HITAGSAVED
killit
}
ifspritepal 15 // quick and dirty floor + ceiling pic changer
{
		ifcount 13
			ife sector[].floorshade sprite[].shade
			{
			sets[].floorpicnum LOTAGSAVED
			sets[].ceilingpicnum HITAGSAVED
			resetcount
			}
}
ifspritepal 16 // sets it to play water wading sounds, for sprite water stuff
{
	gets[].floorstat temp
	xorvar temp 65536
	sets[].floorstat temp
killit
}
ifspritepal 17 // PATCOS ATTACK POINT
{
ifaction 0
	{
	ifg PATCOS_CHARGE 0 action ZERO
	}
ifaction ZERO
	{
	ifpdistl 1024
	 ifp palive
	 ife PATCOS_CHARGE 0
		{
		sound CARSTRAIN
		screensound CARSTRAIN
		quake 26
		action FORM
		}
	}
ifaction FORM
	{
	add INTERNALCOUNT 1
	ife SKILL_LEVEL 1 ifg INTERNALCOUNT 120 state PATCOS_SMASH
	else ife SKILL_LEVEL 2 ifg INTERNALCOUNT 90 state PATCOS_SMASH
	else ife SKILL_LEVEL 3 ifg INTERNALCOUNT 60 state PATCOS_SMASH
	else ife SKILL_LEVEL 4 ifg INTERNALCOUNT 30 state PATCOS_SMASH
	else ife SKILL_LEVEL 5 ifg INTERNALCOUNT 20 state PATCOS_SMASH
	}
}
ifspritepal 18 // hack for Energeia Sang walking across bridge
{
sleeptime 0
findnearactor3d 1404 512 temp
ifn temp -1
	{
	setactorvar[temp].ZVELSAVED 1
	setactorvar[temp].ZPOS_SAVED sprite[THISACTOR].z
	killit
	}
}
ifspritepal 19 // Set the 'injured' effect on various conditions
{
ife HITAGSAVED 0
ife LOTAGSAVED 0
	{
	set riot_shield 0
	set injured EXTRASAVED
	ifcount 30 killit
	}
else
ifn LOTAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set riot_shield 0
		set injured EXTRASAVED killit
		}
	}
else
ifn HITAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set riot_shield 0
		set injured 0 killit
		}
	}
}
ifspritepal 20 // hides the player weapon until lotag is activated
{
sleeptime 0
state lower_weapon
set hide_xhair 1
checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set hide_xhair 0
		killit
		}
}
ifspritepal 21 // Vacation stuff
{
set INVEN_REPLACE 1
killit
}
ifspritepal 22 // electric floor
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		gets[].floorstat temp
		xorvar temp 16384
		sets[].floorstat temp
		killit
		}
	}
ifspritepal 23 // plays a sound when the floor is below the sprite
{
ifcansee
 ifpdistl 16384
	{
	ifg sector[].floorz sprite[].z
		{
		ifactorsound THISACTOR LOTAGSAVED nullop else soundvar LOTAGSAVED
		}
	}
}
ifspritepal 24 // play sound until activated
	{
	ifpdistl 16384 { ifactorsound THISACTOR LOTAGSAVED nullop else soundvar LOTAGSAVED }
	checkactivatormotion HITAGSAVED ife RETURN 1 killit
	}
ifspritepal 25 // low gravity
	{
	gets[].ceilingstat temp
	ifand temp 16384 nullop else xorvar temp 16384
	sets[].ceilingstat temp
	killit
	}
ifspritepal 26 // zero gravity
	{
	gets[].ceilingstat temp
	ifand temp 32768 nullop else xorvar temp 32768
	sets[].ceilingstat temp
	killit
	}
ifspritepal 27 // activate this
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	killit
	}
}
ifspritepal 28 // sets global trigger to hitag when lotag is activated
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	set global_trigger HITAGSAVED
	killit
	}

}
ifspritepal 29 // water bubbles
{
checkactivatormotion LOTAGSAVED
ifpdistl 16384
ife RETURN 1
	{
	spawn WATERBUBBLE spawn WATERBUBBLE spawn WATERBUBBLE spawn WATERBUBBLE spawn WATERBUBBLE
	killit
	}
}
ifspritepal 30 // water push
{
ifpdistl 1024
	{
	ife water_screen 30 set water_screen 0
	set screen_shake 12
	set shake_strength 6
	palfrom 20 0 0 20
	}
}
ifspritepal 31 // play sound when lotag is acitve
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	ifpdistl 16384 { ifactorsound THISACTOR EXTRASAVED nullop else soundoncevar EXTRASAVED }
	action ZERO
	}
else
ifaction ZERO
	{
	stopsoundvar EXTRASAVED
	soundoncevar HITAGSAVED
	action 0
	}
}
ifspritepal 32 // curtain (used for walking through masked walls)
{
ifpdistl 1024
 ifaction 0
	{
	sound CURTAIN_PULLB
	action ZERO
	}
ifaction ZERO ifpdistg 1024 action 0
}
ifspritepal 33 // air current
{
ife HITAGSAVED 0 set HITAGSAVED 256
getp[].i PLAYER_IDENTITY
	ife sprite[PLAYER_IDENTITY].sectnum sprite[].sectnum
	{
	ifp phigher nullop
		else
			{
			ife player[].jumping_toggle 0
				{
				setp[].jumping_counter 361
				setp[].jumping_toggle 1
				}
			setp[].falling_counter 0
			getp[].poszv temp
			sub temp HITAGSAVED
			setp[].poszv temp
			getp[].posz temp2
			sub temp2 8
			setp[].posz temp2
			}
	}
}
ifspritepal 34 // force floorz
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	sets[].floorz sprite[].z
	killit
	}
}
ifspritepal 35 // kill all in sector
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	geta[].sectnum mysector

	headspritesect spriteid mysector
	whilevarn spriteid -1
		{
		addlogvar spriteid
			ife sprite[spriteid].hitag HITAGSAVED
				{
				seta[spriteid].htextra 140
				seta[spriteid].htpicnum RADIUSEXPLOSION
				seta[spriteid].htowner spriteid
				seta[spriteid].xrepeat 0
				seta[spriteid].yrepeat 0
				}
			nextspritesect spriteid spriteid
		}
	killit
	}
}
ifspritepal 36 // teleport break
{
ifpdistl 1024
	{
	spawn TRANSPORTERBEAM
	globalsound SOMETHINGHITFORCE
	globalsound SHORT_CIRCUIT
	shoot SPARK shoot SPARK shoot SPARK shoot SPARK
	palfrom 16 64 64 64
	killit
	}
}
ifspritepal 37 // testing purposes
{
ifpdistl 1024
	{
	globalsound RESEARCH_DATA
	set DISP_RES -240
			set DISP_RES_PAL 1
			set DISP_RES_PIC 13331
	killit
	}
}
ifspritepal 38 // remove gravity protection from 1404
{
sleeptime 0
findnearactor3d 1404 512 temp
ifn temp -1
	{
	setactorvar[temp].ZVELSAVED 0
	killit
	}
}
ifspritepal 39 // sector that player can't reach, use to stop plants spawning targetting stuff .etc perf reasons
	{
	gets[].ceilingstat temp
	xorvar temp 8192
	sets[].ceilingstat temp
	killit
	}
ifspritepal 40 // fix floor shades
	{
	ifn sprite[].shade sector[].floorshade
		{
		headspritesect spriteid sprite[].sectnum
		whilevarn spriteid -1
			{
			geta[spriteid].shade temp
			ifn temp sector[].floorshade
			 ifg sprite[spriteid].picnum 10 // don't shade built in effectors
			  ifn sprite[spriteid].picnum 17971
				{
				ifand sprite[spriteid].htflags 4 nullop else
				seta[spriteid].shade sector[].floorshade
				}
			nextspritesect spriteid spriteid
			}
		}
	}
ifspritepal 41 // kill all target actors
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
		set temp 0
		whilevarn temp 16384
		{
		  ifn sprite[temp].statnum 1024
		  {
			geta[temp].picnum temp2
			ife temp2 SHOOTME
			{
			seta[temp].xrepeat 0
			seta[temp].yrepeat 0
			}
		  }
		  add temp 1
		}
		killit
	}
}
ifspritepal 42 // maeph beam
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
		shoot MAEPH_BEAM
		screensound MAEPH_FIRE
		shoot SPARK
		shoot SPARK
		shoot SPARK
		state fade_out_white
		killit
	}
}
ifspritepal 43 // micky begins skipper
{
ifand BEAT_EPISODES[3] 16
	{
	operateactivators 20500 THISACTOR
	}
killit
}
ifspritepal 44 // AMC base tutorial
{
ifpdistl 2048
 ife VOLUME 0
 ife CHARSELECT 0
	{
	readgamevar base_tutorial
	ifand base_tutorial LOTAGSAVED nullop else
		{
		operateactivators HITAGSAVED THISACTOR
		xor base_tutorial LOTAGSAVED
		savegamevar base_tutorial
		}
	killit
	}
}
ifspritepal 45 // Dark area
{
ifpdistl 4096
	{
	ifand NOISE_SAID 256 nullop else { xorvar NOISE_SAID 256 set PLAYER_VOICEOVER 8 }
	ifand spells_cast 1 nullop else ifand spells_cast 2 nullop else { xorvar spells_cast 1 }
	}
}
ifspritepal 46 // Out of Dark area
{
ifpdistl 4096
	{
	ifand spells_cast 1 xorvar spells_cast 1
	ifand spells_cast 2 xorvar spells_cast 2
	}
}
ifspritepal 47 // Organ
{
ifpdistl 1024
 ifp pfacing
	{
	set player_use 0
	 ifhitspace
	  ifcount 13
		{
		ife WORLD_TRIGGER 1 // has sheet music?
			{
			screensound ORGAN_P
			operateactivators LOTAGSAVED THISACTOR
			state lower_weapon
			set hit_key 30
			set WORLD_TRIGGER 0
			}
		else
		switch CHAR
		case 2
		case 3
		case 11
		case 12
		case 17
			soundonce ORGAN
			state lower_weapon
			set hit_key 30
			break
		default
			userquote 410
			set hit_key 30
			break
		endswitch
		resetcount
		}
	}
}
ifspritepal 48
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
		stopallsounds
		screensound LIGHTNING_SLAP
		globalsound WOOD_DEBRIS
		set screen_shake 90
		set shake_strength 30
		state fade_out_white
		killit
	}
}
ifspritepal 49
{
ifpdistl 1536
 ifp pfacing
	{
	set player_use 0
	 ifhitspace
	  ifcount 13
		{
		set hit_key 30
		ife WORLD_TRIGGER 64 // has mystic powder?
			{
			operateactivators LOTAGSAVED THISACTOR
			state lower_weapon
			set WORLD_TRIGGER 0
			quake 130
			killit
			}
		else
			quote 6906
		resetcount
		}
	}
}
ifspritepal 50
	{
	set INEBRIATION 900
	killit
	}
ifspritepal 51
	{
	// museum unlocker
	ifand BEAT_EPISODES[4] 32768 operateactivators LOTAGSAVED THISACTOR
	killit
	}
ifspritepal 53
	{
	ifpdistl 1024
		{
		randvar temp 100
		ife SKILL_LEVEL 1 clamp temp 25 100
		else ife SKILL_LEVEL 2 clamp temp 50 100
		else ife SKILL_LEVEL 3 clamp temp 75 100
		else ifge SKILL_LEVEL 4 set temp 100
		add haunted_factor temp
		clamp haunted_factor 0 800
		killit
		}
	}
ifspritepal 54
	{
	ifle EXTRASAVED 0
		{
		ife sprite[].ang 512 gets[].ceilingpicnum EXTRASAVED
		else gets[].floorpicnum EXTRASAVED
		}
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		ife sprite[].ang 512 setsector[].ceilingpicnum LOTAGSAVED else
		setsector[].floorpicnum LOTAGSAVED
		}
	else
		{
		ife sprite[].ang 512 { ifn sector[].ceilingpicnum EXTRASAVED setsector[].ceilingpicnum EXTRASAVED }
		else { ifn sector[].floorpicnum EXTRASAVED setsector[].floorpicnum EXTRASAVED }
		}
	}
ifspritepal 55
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		shoot NINEMM_SHELL
		killit
		}
	}
enda

// #################################################
// Upside down camera
// #################################################

spriteshadow 1411

useractor notenemy 1411 ifaction 0 action FIVE_ANG enda

// #################################################
// Space Suit stuff
// #################################################

useractor notenemy 22447
cstat 32768
ifspritepal 2
{
	ifcansee
	ifcanseetarget
	 ifpdistl 2048
	 ife inspace 0
		{
		add temp 1
		ifg temp 4
			{
			ife CHAR 12 soundonce SPACESUIT_ON
			ife CHAR 13 soundonce SPACESUIT_ON
			ife CHAR 14 soundonce SPACESUIT_ON
			ife SPACE_SUIT 1 soundonce SPACESUIT_ON
			set inspace 1
			set temp 0
			}
		}
}
ifspritepal 3 // exit door
{
	ifcansee
	ifcanseetarget
	 ife inspace 1
		{
		add temp 1
		ifg temp 4
			{
			setp[].sound_pitch 0
			ife CHAR 12 soundonce SPACESUIT_OFF
			ife CHAR 13 soundonce SPACESUIT_OFF
			ife CHAR 14 soundonce SPACESUIT_OFF
			ife SPACE_SUIT 1 soundonce SPACESUIT_OFF
			set inspace 0
			set temp 0
			}
		}
}
ifspritepal 4
{
ife CHARSELECT 0
 ife playerwait 0
	{
	ifn CHAR 12
	 ifn CHAR 13
	  ifn CHAR 14
	   set SPACE_SUIT 1
	set just_changed 1
	soundonce SPACESUIT_ON
	set LIFE_SUPPORT 14400
	set inspace 1
	killit
	}
}
ifspritepal 5
{
	getp[].i PLAYER_IDENTITY
		ife sprite[PLAYER_IDENTITY].sectnum sprite[].sectnum
		 ife inspace 0
			{
			ife CHAR 12 soundonce SPACESUIT_ON
			ife CHAR 13 soundonce SPACESUIT_ON
			ife CHAR 14 soundonce SPACESUIT_ON
			ife SPACE_SUIT 1 soundonce SPACESUIT_ON
			set inspace 1
			set temp 0
			}
}
ifspritepal 6
{
	getp[].i PLAYER_IDENTITY
		ife sprite[PLAYER_IDENTITY].sectnum sprite[].sectnum
		 ife inspace 1
			{
			setp[].sound_pitch 0
			ife CHAR 12 soundonce SPACESUIT_OFF
			ife CHAR 13 soundonce SPACESUIT_OFF
			ife CHAR 14 soundonce SPACESUIT_OFF
			ife SPACE_SUIT 1 soundonce SPACESUIT_OFF
			set inspace 0
			set temp 0
			}
}
ifspritepal 8
{
			setp[].sound_pitch 0
			soundonce SPACESUIT_OFF
			set inspace 0
			set SPACE_SUIT 0
			set temp 0
			killit
}
ifspritepal 10
{
ifaction 0
ife CHARSELECT 0
	{
	ifn CHAR 12
	 ifn CHAR 13
	  ifn CHAR 14
	   set SPACE_SUIT 1
	globalsound SPACESUIT_ON
	set LIFE_SUPPORT 14400
	set inspace 1
	action ZERO
	}
ifaction ZERO
	{
	checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			globalsound SPACESUIT_OFF
			set inspace 0
			quake 52
			set SPACE_SUIT 0
			set temp 0
			killit
			}
	}
}
enda

// *************************************************
// Bang head here
// *************************************************

useractor notenemy 11964

ifaction 0
ifpdistl 1024
 ifp pfacing
  ifp palive
	{
	set player_use 0
	 ifhitspace
		{
		setplayer[].weapon_pos 0
		wackplayer
		quake 13
		stopsound EARTHQUAKE
		ife CHAR 0 sound SPUNCHHIT else
		ife CHAR 1 sound SPUNCHHIT else
		ife CHAR 3 sound SPUNCHHIT else
		ife CHAR 4 sound SPUNCHHIT else
		ife CHAR 5 sound SPUNCHHIT else
		ife CHAR 9 sound SPUNCHHIT else
		ife CHAR 10 sound SPUNCHHIT else
		sound PAN_IMPACT
		addphealth 10
		action ZERO
		}
	}

enda


// *************************************************
// Dirt splotch
// *************************************************

spritenoshade 13243
useractor notenemy 13243
ifn HITAGSAVED 0
	{
	ifaction 0
		{
		cstat 32768
		action ZERO
		}
	}

ifaction ZERO
	{
	checkactivatormotion HITAGSAVED
		ife RETURN 1 { seta[].cstat CSTATSAVED action FORM }
	}

enda

// *************************************************
// PRECOG BRAIN ie how many people will read the the signs?
// *************************************************

useractor notenemy 11050

ifaction 0
{
cstat 257
action ZERO
}

ifpdistl 1524
 ifaction ZERO
  ifp pfacing
   ifp palive
    ifcansee
	{
	set player_use 0
		ifhitspace
		 ifcount 13
			{
			set hit_key 30
			stopallsounds
			globalsound MIND_ATTACK
			set player_spooked 98
			state fade_out_white
			wackplayer
			state setknockback
			resetcount
			}
	}

ifhitweapon
	{
	spawn BLOOD
	guts JIBS6 8
	stopallsounds
	globalsound MIND_ATTACK
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	else
	{
	set player_spooked 98
	addphealth -10
	}
	state fade_out_white
	wackplayer
	state setknockback
	action ONE
	}

enda


// ***************************************************************************************************************************************************
// DANCE DANCE BYATCH
// ***************************************************************************************************************************************************


// breakdancin' Micky

spriteshadow 17610

useractor notenemy 17610
ifaction 0 action FIVE_ANG
ifaction FIVE_ANG
	{
	geta[].ang temp
	add temp 128
	seta[].ang temp
	}
enda

// Spinnin' Sang

spriteshadow 17605

useractor notenemy 17605
ifaction 0 action FIVE_ANG
ifaction FIVE_ANG
	{
	geta[].ang temp
	add temp 128
	seta[].ang temp
	}
enda


// ***************************************************************************************************************************************************
// BULLET PENETRATION ===============================================================================
// ***************************************************************************************************************************************************

useractor notenemy 5131
cstat 32768
sizeat 32 32
ifaction 0
{
geta[].extra temp2
divvar temp2 2
setprojectile[NINEMM_CALIBRE].extra temp2
shoot NINEMM_CALIBRE
setprojectile[NINEMM_CALIBRE].extra 8
killit
}
enda

// ***************************************************************************************************************************************************
// SPOTLIGHT
// ***************************************************************************************************************************************************

spritenoshade 3070

defstate SHADE_CLOSER
ifaction 0
	{
	sleeptime 0
	dist xydist THISACTOR player[].i
	shiftvarr xydist 8
	ifl xydist 0 set xydist 0
	// sub xydist 127
	set temp4 96
	sub temp4 xydist
	ifl temp4 -127 set temp4 -127
	ifg temp4 127 set temp4 127
	seta[].shade temp4
	}
ends

useractor notenemy 3069 state SHADE_CLOSER enda
useractor notenemy 3070 state SHADE_CLOSER enda

// ***************************************************************************************************************************************************
// SPEECH BUBBLE (sizing code by DeeperThought)
// ***************************************************************************************************************************************************

useractor notenemy 12263
ifaction 0
{
  seta[].mdflags 16
  set temp4 0
  cstat 128
  action ZERO
 }

ifaction ZERO
{
sleeptime 0
dist xydist THISACTOR player[].i
shiftvarr xydist 8
ifl xydist 0 set xydist 0
add xydist 32
ifg xydist 255 set xydist 255
ifpdistl 2548
	{
	// addlogvar alpha
	ifl temp4 254 add temp4 8
	}
	else
	ifpdistg 2547
	{
	ifg temp4 0 sub temp4 8
	}
set alpha temp4
state SPRITE_FADE
seta[].xrepeat xydist
seta[].yrepeat xydist
geta[].owner temp
ife sprite[temp].statnum 1024 killit
ifl sprite[temp].extra 1 killit
}
enda

// ***************************************************************************************************************************************************
// REACTOR
// ***************************************************************************************************************************************************

useractor notenemy 1091
ifpdistl 2048
 ifrnd 6
	{
	soundonce SHORT_CIRCUIT
	palfrom 10 10
	addphealth -4
	}

ifhitweapon
	{
	soundonce CHOPPER_EXPLODE
	hitradius 4096 5 15 20 40
	spawn EXPLOSION2
	quake 26
	shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
	debris SCRAP1 10
	debris SCRAP2 8
	debris SCRAP3 8
	debris SCRAP4 8
	debris SCRAP5 8
	operateactivators LOTAGSAVED THISACTOR
	operaterespawns LOTAGSAVED
	operatemasterswitches LOTAGSAVED
	cactor REACTORBURNT
	}

enda

useractor notenemy 1095

ifaction 0 { seta[].blend 255 action ZERO }

checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
	shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
	killit
	}


enda

// ***************************************************************************************************************************************************
// GLINT (code and idea by DeeperThought aka Dan Gaskill)
// ***************************************************************************************************************************************************

spritenoshade GLINT
spritenopal GLINT
useractor notenemy GLINT 0

ifspawnedby PARTICLE_SPAWNER
	{
	fall
	state getfloordist
	seta[].z z
	}

ifcount 26
{
	sizeto 0 0
}
else
{
	cstator 1154
	seta[].blend 255
	sizeto 16 16
}

enda

// ***************************************************************************************************************************************************
// GRASS CODE BY DeeperThought aka Dan Gaskill
// ***************************************************************************************************************************************************


defstate FIND_CENTER_OF_SECTOR
// also calculates area of sector

whilevarvarn currwall sector[sect].wallnum
{
	ife currwall 0
	{
		set SMALLX wall[wallpoint].x set BIG_X wall[wallpoint].x
		set SMALLY wall[wallpoint].y set BIGY wall[wallpoint].y
	}
	else
	{
		ifl wall[wallpoint].x SMALLX set SMALLX wall[wallpoint].x else
		ifg wall[wallpoint].x BIG_X set BIG_X wall[wallpoint].x

		ifl wall[wallpoint].y  SMALLY set SMALLY wall[wallpoint].y  else
		ifg wall[wallpoint].y  BIGY set BIGY wall[wallpoint].y
	}

	add wallpoint 1
	add currwall 1
}

sub BIG_X SMALLX
sub BIGY SMALLY
ifl BIG_X 0 mulvar BIG_X -1
ifl BIGY 0 mulvar BIGY -1
set x BIG_X
shiftvarr x 3
set y BIGY
shiftvarr y 3
set area x
mulvarvar area y
shiftvarr area 11
ifg area 320 set area 320

ends

defstate spawngrass
	gets[sect].wallptr wallpoint // get the firstwall of the sector
	// ife wallpoint -1 break
	set currwall 0
	state FIND_CENTER_OF_SECTOR
	// mx, my is the center point
	// BIG_X is the x length of sector
	// BIGY is the y length of sector

	set temp5 0
	//add attempts area
	whilevarvarn temp5 area
	{
		randvarvar temp2 BIG_X
		randvarvar temp3 BIGY
		set x SMALLX
		set y SMALLY
		add x temp2
		add y temp3
		geta[].sectnum upd_sect
		updatesector x y upd_sect
		ife sect upd_sect
		ifrnd 128
		{
			add temp5 1
			add grasscount 1
			ifspritepal 8 // xuglop
				{
				ifrnd 128 espawn 5421 else
				ifrnd 128 espawn 5420 else
				espawn 8065
				}
			else
			ifspritepal 1 // woods
				{
				ifrnd 32 espawn 23650 else
				ifrnd 128 espawn 23651 else
				espawn 8065
				}
			else
				{
				ifrnd 128 espawn 5421 else
				ifrnd 128 espawn 5420 else
				espawn 5422
				}
			ifn sector[sect].floorslope 0
				{
					getflorzofslope sect x y z
					ifrnd 128
					seta[RETURN].cstat 2 else
					seta[RETURN].cstat 6
				}
			else
			{
				gets[sect].floorz z
				seta[RETURN].cstat 16
			}
			seta[RETURN].pal sector[sect].floorpal
			setsprite RETURN x y z
			changespritestat RETURN 1
			seta[RETURN].xrepeat 12
			randvar temp3 4 add temp3 12
			seta[RETURN].yrepeat temp3
			seta[RETURN].shade sector[sect].floorshade
		}
		ifl temp5 area
		add temp5 1
		// ifg grasscount 7000 set temp5 area
	}

ends

useractor notenemy 5430
cstat 32768

ifaction 0
{
set grasscount 0
set sect 0
whilevarvarn sect NUMSECTORS
	{
		ife sector[sect].floorpicnum sector[].floorpicnum ife sector[sect].lotag 0
			{
			ifand sector[sect].floorstat 1 nullop else
			ifand sector[sect].floorstat 1024 nullop else
			state spawngrass
			}
		add sect 1
		ifg grasscount 6000 set sect NUMSECTORS // failsafe
	}
killit
}

enda

// *************************************************
// MAGIC CIRCLES
// *************************************************

defstate MAGIC_CIRCLES
ife LOTAGSAVED 1 nullop
else ife LOTAGSAVED 2
{
ife temp5 1
	{
	geta[].shade temp6
	add temp6 1
	seta[].shade temp6
	ifg temp6 24 set temp5 0
	}
else
ife temp5 0
	{
	geta[].shade temp6
	sub temp6 1
	seta[].shade temp6
	ifl temp6 -6 set temp5 1
	}

checkactivatormotion HITAGSAVED
ife RETURN 1 killit

}
else
{
ifaction 0
{
ifspawnedby SORCERER
	{
	seta[].blend 255
	palfrom 20 0 0 20
	cstat 130
	}
else
ifspawnedby OBLIVION_STAFF_SENTRY
	{
	seta[].blend 255
	spritepal 27
	fall
	sizeto 32 32
	cstat 34
	}
else
ifspawnedby 18085 // Nemesis Staff
	{
	seta[].blend 255
	spritepal 27
	fall
	sizeto 32 32
	cstat 34
	}
else
	{
	seta[].z sector[].floorz
	seta[].blend 255
	sizeat 16 16
	cstat 34
	}
soundonce COOKINGDEEPFRIER
set INTERNALCOUNT 0
action ZERO
}



ifaction ZERO
{
add INTERNALCOUNT 1
geta[].ang temp4
sub temp4 4
seta[].ang temp4

ifspawnedby SORCERER
	{
	sizeto 48 48
	ifg sprite[].xrepeat 47 killit else
	ifg sprite[].xrepeat 40 cstat 642 else
	ifg sprite[].xrepeat 34 cstat 130
	}

ifg INTERNALCOUNT 256
	{
	geta[].shade temp6
	add temp6 1
	seta[].shade temp6
	ifg temp6 40 killit
	}

}

}
ends


spritenoshade 13949
spritenoshade 13950
spritenoshade 13951
spritenoshade 13952

useractor notenemy 13949 state MAGIC_CIRCLES enda
useractor notenemy 13950 state MAGIC_CIRCLES enda
useractor notenemy 13951 state MAGIC_CIRCLES enda
useractor notenemy 13952 state MAGIC_CIRCLES enda

useractor notenemy 3587 // distance actor
cstat 32768
killit
enda

// *************************************************
// TELEPORTER
// *************************************************

action CHARGE_1 1
action CHARGE_2 2
action CHARGE_3 3
action CHARGE_4 4
action CHARGE_5 5
action CHARGED 6

useractor notenemy 14289

ifaction 0
	{
	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		 ife RETURN 1
			{
			globalsound MS_MECH_BLIP
			set INTERNALCOUNT 0
			action CHARGE_1
			set CHARGE_UP 1
			}
		}
	}

ifaction CHARGE_1
{
soundonce TELEP_AP
add INTERNALCOUNT 1
 ifg INTERNALCOUNT 300
	{
	globalsound MS_MECH_BLIP
	set INTERNALCOUNT 0
	add HITAGSAVED 1
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	stopsound TELEP_AP
	action CHARGE_2
	set CHARGE_UP 2
	}
}

ifaction CHARGE_2
{
	soundonce TELEP_AP
	setactorsoundpitch THISACTOR TELEP_AP 256
add INTERNALCOUNT 1
 ifg INTERNALCOUNT 300
	{
	globalsound MS_MECH_BLIP
	set INTERNALCOUNT 0
	add HITAGSAVED 1
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	stopsound TELEP_AP
	action CHARGE_3
	set CHARGE_UP 3
	}
}

ifaction CHARGE_3
{
soundonce TELEP_AP
setactorsoundpitch THISACTOR TELEP_AP 512
add INTERNALCOUNT 1
 ifg INTERNALCOUNT 300
	{
	globalsound MS_MECH_BLIP
	set INTERNALCOUNT 0
	add HITAGSAVED 1
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	stopsound TELEP_AP
	action CHARGE_4
	set CHARGE_UP 4
	}
}

ifaction CHARGE_4
{
soundonce TELEP_AP
setactorsoundpitch THISACTOR TELEP_AP 768
add INTERNALCOUNT 1
 ifg INTERNALCOUNT 300
	{
	globalsound MS_MECH_BLIP
	set INTERNALCOUNT 0
	add HITAGSAVED 1
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	stopsound TELEP_AP
	action CHARGE_5
	set CHARGE_UP 5
	}
}

ifaction CHARGE_5
{
soundonce TELEP_AP
setactorsoundpitch THISACTOR TELEP_AP 1024
add INTERNALCOUNT 1
 ifg INTERNALCOUNT 300
	{
	globalsound MS_MECH_BLIP
	quake 38
	set INTERNALCOUNT 0
	add HITAGSAVED 1
	operateactivators HITAGSAVED THISACTOR
	operaterespawns HITAGSAVED
	operatemasterswitches HITAGSAVED
	stopsound TELEP_AP
	action CHARGED
	set CHARGE_UP 6
	}
}

enda

useractor notenemy 14296

ifaction 0
	{
	ifspritepal 8 cstator 1024
	seta[].blend 255
	ifn HITAGSAVED 0
		{
		cstat 32768
		action ZERO
		}
	else ifn LOTAGSAVED 0
		{
		checkactivatormotion LOTAGSAVED
		ife RETURN 1
			{
			palfrom 10 50 0 0
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			killit
			}
		}
	}

ifaction ZERO
	{
	checkactivatormotion HITAGSAVED
	 ife RETURN 1
		{
		ifspritepal 0
			{
			palfrom 10 0 50 0
			shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
			soundonce TELEP_CH
			action FORM
			}
		else
		ifspritepal 23
			{
			palfrom 10 50 0 0
			shoot SPARK shoot SPARK shoot SPARK shoot SPARK
			soundonce TELEP_CH
			action FORM
			}
		seta[].cstat CSTATSAVED
		}
	}

enda

// *************************************************
// BIG FAN
// *************************************************

useractor notenemy 16152

ifaction 0
	{
	seta[].mdflags 16
	action ZERO
	}

ifaction ZERO
	{
	ifpdistl 16384 soundonce FAN
	set command 512

	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		set command 0
		ifg temp9 0 killit
		ifspritepal 3 killit
		stopactorsound THISACTOR FAN
		set temp2 64
		action BREAK
		}

	}

ifaction BREAK
	{
	ifg temp2 0
		{
		geta[].ang temp
		add temp temp2
		seta[].ang temp
		sub temp2 1
		}
	}

enda

// *************************************************
// SKY TOGGLE
// *************************************************

useractor notenemy 16154
cstat 32768

ifspritepal 1
 ifpdistl 2048
  ifcansee
	{
	set skybox 1
	}

ifspritepal 2
 ifpdistl 2048
  ifcansee
	{
	set skybox 2
	}

ifspritepal 2
 ifpdistl 2048
  ifcansee
	{
	set skybox 3
	}

ifspritepal 0
 ifpdistl 2048
  ifcansee
	{
	set skybox 0
	}


enda

// *************************************************
// CODEC FACE
// *************************************************

defstate CODEC_STUFF
ifaction 0
	{
	ifn HITAGSAVED 0
		{
		sizeat 48 2
		seta[].shade 35
		spritepal 27
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			action ZERO
			resetcount
			}
		}
	}

ifaction ZERO
	{
	geta[].yrepeat temp
	ifl temp 48 { add temp 2 seta[].yrepeat temp }
	else ifg temp 48 seta[].yrepeat 48
	geta[].shade temp2
	ifg temp2 0 { sub temp2 1 seta[].shade temp2 }
	else ife temp2 0 spritepal 0
	}


ends

useractor notenemy 3589 state CODEC_STUFF enda
useractor notenemy 3598 state CODEC_STUFF enda
useractor notenemy 5368 state CODEC_STUFF enda

// *************************************************
// GONG
// *************************************************

useractor notenemy 16343

ifaction 0
{
ife LOTAGSAVED 0 set LOTAGSAVED 120
ifhitweapon
	{
	globalsound GONG_HIT
	action ONE
	set INTERNALCOUNT 0
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	seta[].htextra -1
	}
}

ifaction ONE
{
seta[].htextra -1
add INTERNALCOUNT 1
ifg INTERNALCOUNT LOTAGSAVED
	{
	strength 1
	action 0
	}
}


enda

// *************************************************
// MAGIC PLATFORM
// *************************************************

spriteflags 18432 16384

spritenoshade 18161

useractor notenemy 18161

ifaction 0
{
cstat 290
seta[].shade 30

checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	action ZERO
	set INTERNALCOUNT LOTAGSAVED
	}
}

ifaction ZERO
{
add INTERNALCOUNT 1
ifg INTERNALCOUNT 150
	{
	cstat 291
	geta[].shade temp
	ifg temp 0 sub temp 1
	seta[].shade temp
	ife temp 0 { globalsound USE_TIMEPORTAL set INTERNALCOUNT_2 0 action FORMED }
	}
}

ifaction FORMED
{
add INTERNALCOUNT_2 1
ifg INTERNALCOUNT_2 350
	{
	set INTERNALCOUNT LOTAGSAVED action FORM
	}
}

ifaction FORM
{
add INTERNALCOUNT 1
ifg INTERNALCOUNT 300
	{
	geta[].shade temp
	ifl temp 30 add temp 1
	seta[].shade temp
	ife temp 30 { globalsound USE_TIMEPORTAL set INTERNALCOUNT 0 set INTERNALCOUNT_2 0 action 0 }
	}
}


enda

// =================================================
// CLOUD
// ==================================================

defstate cloud_rot

ife cloud_rot_allowed -1 // -1, dunno yet
{
	// find pal 1 cloud sprite 18165
	// if exists in sector, disable cloud rot
	geta[].sectnum temp2

	for temp3 spritesofsector temp2
	{
		geta[temp3].picnum temp4
		ife temp4 18165
		{
			geta[temp3].pal temp4

			ife temp4 1
				set cloud_rot_allowed 0
		}
	}
}

ife cloud_rot_allowed -1 // still -1 so not disabled
	set cloud_rot_allowed 1

ife cloud_rot_allowed 1
{
	ifaction 0
	 ifn cloud_centre 0
		{
		set temp9 -1
		spriteflags 141312
		// kinda sucks, cloud centre can change, but that scenario doesn't work atm
		ldist xydist THISACTOR cloud_centre
		geta[cloud_centre].picnum temp
		seta[].htlastvx sprite[cloud_centre].x
		seta[].htlastvy sprite[cloud_centre].y
		getangletotarget temp4 // hack to use this command to calculate the angle for us
		action ZERO
		}

	ifaction ZERO
		{

		set temp3 skybox_x
		add temp3 xydist
		ifn HITAGSAVED 0
			{
			add INTERNALCOUNT 1
			ifg INTERNALCOUNT HITAGSAVED
				{
				add temp4 1
				set INTERNALCOUNT 0
				}
			}
		else
		ifcount 30 {  add temp4 1 resetcount }
		ifg temp4 2048 set temp4 0

		// reduce frequency of update
		ifn temp4 temp9
			{
			rotatepoint skybox_x skybox_y temp3 skybox_y temp4 temp5 temp6

			seta[].x temp5
			seta[].y temp6

			setsprite THISACTOR temp5 temp6 sprite[].z

			set mx skybox_x
			set my skybox_y
			sub mx sprite[].x
			sub my sprite[].y
			getangle angvar mx my
			seta[].ang angvar // tell the cloud to face the skybox, so flat sprites are always parallel to it
			set temp9 temp4
			}
		}
}
ends


// NOTE: temp9 used to track previous angle for these actors. Do not overwrite!
useractor notenemy 18165
	geta[].pal PALSAVED
	ife PALSAVED 1 // pal 1 cloud disables cloud rot
		cstat 32768
	state cloud_rot
enda
useractor notenemy 18166 state cloud_rot enda
useractor notenemy 18167 state cloud_rot enda

// *************************************************
// CLOUD SPAWNER/WAVE SPAWNER
// Spawns clouds or waves depending on spritepal.
// If a hitag is set, wait until hitag is triggered before spawning.
// If a lotag is set, kill the spawner when the lotag is triggered.
// *************************************************

useractor notenemy 14307
	ifaction 0
	{
		cstat 32768
		set SPEC_ACT 1
		ifn HITAGSAVED 0
		{
			sleeptime 0
			checkactivatormotion HITAGSAVED
			ife RETURN 1 action ZERO
		}
		else action ZERO
	}

	ifaction ZERO
	{
		ifn LOTAGSAVED 0
		{
			checkactivatormotion LOTAGSAVED
			ife RETURN 1 killit
		}

		ifspritepal 1 // vertical cloud spawner
		{
			ifrnd 48
			{
				espawn 14302
				seta[RETURN].pal 3
				add INTERNALCOUNT 1

				set temp3 sprite[].x
				randvar temp8 3072
				add temp3 temp8

				randvar temp4 2048

				rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

				seta[RETURN].x temp5
				seta[RETURN].y temp6
				seta[RETURN].ang sprite[].ang
				geta[].z temp4
				randvar temp9 8192
				ifrnd 128 sub temp9 8192
				sub temp4 temp9
				seta[RETURN].z temp4
				resetcount
			}
		}
		else ifspritepal 2 // wave spawner
		{
			add INTERNALCOUNT 1
			ifg INTERNALCOUNT 160
			{
				spawn 17079
				set INTERNALCOUNT 0
			}
		}
		else // horizontal cloud spawner
		{
			ifg INTERNALCOUNT 19
			{
				add INTERNALCOUNT_2 1
				ifg INTERNALCOUNT_2 520 { set INTERNALCOUNT 0 set INTERNALCOUNT_2 0 }
			}

			ifrnd 48
			  ifl INTERNALCOUNT 20
			{
				add INTERNALCOUNT 1

				set temp3 sprite[].x
				randvar temp8 3072
				add temp3 temp8

				randvar temp4 2048

				rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
				updatesectorz temp5 temp6 sprite[].z mysector

				ifn mysector -1
				{
					espawn 14302
					ifspritepal 3 seta[RETURN].pal 3
					else seta[RETURN].pal sprite[].pal
					seta[RETURN].x temp5
					seta[RETURN].y temp6
					seta[RETURN].ang sprite[].ang
					geta[].z temp4
					randvar temp9 8192
					ifrnd 128 sub temp9 8192
					sub temp4 temp9
					seta[RETURN].z temp4
				}

				resetcount
			}
		}
	}
enda

spritenoshade 14302
spritenopal 14302

move CLOUD_MOVE 512
move CLOUD_MOVEV 15 -4096

useractor notenemy 14302 // Cloud

ifspawnedby 14307
	{
	ifspritepal 3 { seta[].shade -127 move CLOUD_MOVEV geth getv ifnotmoving killit ifceilingdistl 64 killit } else
	ifg sprite[].pal 127 { seta[].shade sector[].floorshade move CLOUD_MOVE geth ifnotmoving killit }
	else { seta[].shade -32 move CLOUD_MOVE geth ifnotmoving killit }
	}

ifaction 0
{
seta[].mdflags 16
  ifrnd 128
    cstat 4
  else
    cstat 0
randvar g_temp 64
add g_temp 96
ifspritepal 3 add g_temp 64
seta[].xrepeat g_temp
seta[].yrepeat g_temp
action ZERO
}


enda

spritenopal 17079

useractor notenemy 17079 // Wave

	ifaction 0
	{
		seta[].blend 255
		set SPEC_ACT 1
		seta[].pal sector[].floorpal
		cstator 131
		randvar g_temp 64
		add g_temp 96
		seta[].xrepeat g_temp
		seta[].yrepeat g_temp
		action ZERO
	}

	ifaction ZERO
	{
		ifpdistl 1536
		 ifcanseetarget
		{
			state setknockback
			wackplayer

			ife boss_stage 1
			{
				addphealth -15

				set screen_shake 15
				set shake_strength 30

				stopsound WATER_RUSH
				sound WAVECRASH
				spawn 18138
				spawn WATERBUBBLE
				spawn WATERBUBBLE
				spawn WATERBUBBLE
				spawn WATERBUBBLE

				killit
			}
		}

		move CLOUD_MOVE geth
		ifnotmoving
		{
			ifpdistl 8096
			{
				set screen_shake 15
				set shake_strength 30
			}
			stopsound WATER_RUSH
			sound WAVECRASH
			spawn 18138
			spawn WATERBUBBLE
			spawn WATERBUBBLE
			spawn WATERBUBBLE
			spawn WATERBUBBLE
			cstat 32768
			set INTERNALCOUNT 0
			action LOCKED
		}

		ifpdistl 16384
		 ifn VOLUME 3 // ugly hack lol
		{
			ifrnd 16
			{
				ifactorsound THISACTOR WATER_RUSH nullop
				else sound WATER_RUSH
			}
		}
	}

	ifaction LOCKED
	{
		add INTERNALCOUNT 1
		espawn 18138
		set temp3 sprite[].x
		add temp3 512
		randvar temp4 2048
		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		set temp6 0
		ifg INTERNALCOUNT 10 killit
	}

enda


// *************************************************
// SECRET AREA
// *************************************************

useractor notenemy 3832

ifaction 0
	{
	cstat 32768
	getp[].max_secret_rooms temp
	add temp 1
	setp[].max_secret_rooms temp
	action ZERO
	}

ifaction ZERO
	{
	ifspritepal 0
	 ifpdistl 1524
	  ifcansee
	   ifp palive
		{
		quote 9
		getp[].secret_rooms temp
		add temp 1
		setp[].secret_rooms temp
		killit
		}
	}

enda

// *************************************************
// CITIES
// *************************************************

defstate CITY_HITLIGHTS
seta[].z sector[].floorz
ifaction 0
	{
	ife amc_base 1
	 ifn DAYNIGHT 2
		{
		spritepal 13
		seta[].shade 20
		action ZERO
		}
	checkactivatormotion HITAGSAVED
		ife RETURN 1
		{
		spritepal 13
		seta[].shade 20
		action ZERO
		}
	}
ends

useractor notenemy 8536 state CITY_HITLIGHTS enda
useractor notenemy 8537 state CITY_HITLIGHTS enda
useractor notenemy 18266 state CITY_HITLIGHTS enda
useractor notenemy 18267 state CITY_HITLIGHTS enda
useractor notenemy 18268 state CITY_HITLIGHTS enda
useractor notenemy 18269 state CITY_HITLIGHTS enda

useractor notenemy 8573
ife HITAGSAVED 0
{
seta[].z sector[].floorz
}
enda

useractor notenemy 8575
ife HITAGSAVED 0
{
seta[].z sector[].floorz
}
enda

useractor notenemy 10673
ife HITAGSAVED 0
{
seta[].z sector[].floorz
}
enda

// *************************************************
// BASE SPOTLIGHT
// *************************************************

useractor notenemy 21616
ifaction 0
	{
	seta[].blend 255
	ife DAYNIGHT 1 killit
	action ZERO
	}
enda

// *************************************************
// LIGHTNING
// *************************************************

spritenoshade 17128
spritenopal 17128

spritenoshade 17131
spritenopal 17131

useractor notenemy 17132 // lightning controller

ifaction 0
	{

	ife LOTAGSAVED 0 set LOTAGSAVED 64
	cstat 32768
	ifn HITAGSAVED 0 ife LOTAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			set HITAGSAVED 0
			set LOTAGSAVED 256
			action ZERO
			}
		}
	else
	action ZERO
	}

ifaction ZERO
ifn speaker 1
	{
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 300
		{
		ifrnd 128 globalsound THUNDER
		set INTERNALCOUNT 0
		}
	// will not play if a character is set to talking
	randvarvar LIGHTNING_STRIKE LOTAGSAVED
	}

ifn HITAGSAVED 0
 ifg LOTAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 killit
	}

enda

useractor notenemy 17128 // bolt
	ifspawnedby JEDRIK
	{
		seta[].blend 255
		sizeat 16 16
		cstat 514
		flash
		ifaction 0
		{
			ifrnd 96 action ZERO
			else ifrnd 96 action ONE
			else action TWO
		}
	}
	else ifaction 0
	{
		seta[].blend 255
		cstat 32768
		ifn LOTAGSAVED 0
		{
			checkactivatormotion LOTAGSAVED
			 ife RETURN 1
				{
				set LIGHTNING_STRIKE LOTAGSAVED
				cstat 514
				ifspritepal 2 globalsound BOLT_SUM
				else ifspritepal 3 flash
				else ifspritepal 9 palfrom 40 64 64 64
				ifaction 0
					{
					action ZERO
					}
				resetcount
				}
			}
			else
			ife LIGHTNING_STRIKE HITAGSAVED
				{
				cstat 514
				ifspritepal 3 flash
				else ifspritepal 9 palfrom 40 64 64 64
				ifaction 0
					{
					ifrnd 96 action ZERO
					else ifrnd 96 action ONE
					else action TWO
					}
				resetcount
				}
	}

	ifaction 0
		nullop
	else
	{
		ifcount 13
		{
			ifrnd 128 globalsound LIGHTNING_SLAP2 else globalsound LIGHTNING_SLAP
			set LIGHTNING_STRIKE 0
			action 0
			cstat 32768
			ifn LOTAGSAVED 0 killit
			ifspawnedby JEDRIK killit
		}
	}

enda

spritenoshade 17131

useractor notenemy 17131 // light effect

ifaction 0
{
ife LOTAGSAVED 1 break
seta[].blend 255
cstat 32768
ife LIGHTNING_STRIKE HITAGSAVED
	{
	cstat 34
	action ZERO
	seta[].shade -13
	resetcount
	}
}
else
ifaction ZERO
{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 25
		{
		cstat 32768
		action 0
		resetcount
		}
}

enda

// *************************************************
// COMPUTER
// *************************************************

action MINUSTHREE -3

useractor notenemy 17014
ifaction 0
{
cstator 257
action ZERO
}

ifhitweapon
	{
	debris SCRAP1 2
	debris SCRAP2 2
	sound VENT_BUST
	cactor 17018
	}

enda

useractor notenemy 17015 // PC tower

ifaction 0
{
cstator 257
ifspritepal 3 action BREAK else
action ZERO
}

ifaction ZERO
 ifpdistl 1024
  ifp pfacing
	{
	set player_use 0
	ifhitspace
		{
		set hit_key 30
		action MINUSONE
		sound LIGHT_SWITCH
		set INTERNALCOUNT -512
		set PC_TURNED_ON LOTAGSAVED
		}
	}


ifaction MINUSONE
{
ifpdistl 8192 soundonce PC_IDLE
ifl INTERNALCOUNT 0
{
add INTERNALCOUNT 16
setactorsoundpitch THISACTOR PC_IDLE INTERNALCOUNT
}
 ifpdistl 1024
  ifp pfacing
   ife PC_TURNED_ON HITAGSAVED
	{
	set player_use 0
	ifhitspace
	 ifcount 13
		{
		set hit_key 30
		ifspritepal 9
		 ifand FLOPPY_DISK 2
			{
			state lower_weapon
			xorvar FLOPPY_DISK 2
			sound PC_LOAD_FLOPPY
			set PC_TURNED_ON XVELSAVED
			}
		else
		ifand FLOPPY_DISK 1
			{
			state lower_weapon
			xorvar FLOPPY_DISK 1
			sound PC_LOAD_FLOPPY
			set PC_TURNED_ON XVELSAVED
			}
		else
			{
			quote 288
			}
		resetcount
		}
	}
}

ifhitweapon
	{
	debris SCRAP1 2
	debris SCRAP2 2
	sound VENT_BUST
	cactor 17018
	}

enda

defstate PC_MONITOR_CODE

ifaction 0
{
cstator 257
set INTERNALCOUNT 0
ifspritepal 3 nullop else
action ZERO
}

ifaction ZERO
{
ife PC_TURNED_ON LOTAGSAVED
	{
	geta[].shade temp
	add INTERNALCOUNT 1
	ife INTERNALCOUNT 3 sub temp 1
	ife INTERNALCOUNT 6 sub temp 1
	ife INTERNALCOUNT 9 sub temp 1
	ife INTERNALCOUNT 12 sub temp 1
	ife INTERNALCOUNT 15 sub temp 1
	ife INTERNALCOUNT 18 sub temp 1
	seta[].shade temp
	ife INTERNALCOUNT 26 action MINUSTWO
	}
}

ifaction MINUSTWO
{
ife PC_TURNED_ON HITAGSAVED
	{
	set INTERNALCOUNT 0
	action BREAK
	}
}

ifaction MINUSONE
{
ife PC_TURNED_ON YVELSAVED
	{
	add INTERNALCOUNT 1
	ife INTERNALCOUNT 300
		{
		sound PC_NOTIFY
		action MINUSTHREE
		set PC_TURNED_ON ZVELSAVED
		}
	}
}

ifaction BREAK
{
add INTERNALCOUNT 1
ife INTERNALCOUNT 26
	{
	action MINUSONE
	ifactor 17346 soundonce PC_WINXP_LOAD else
	soundonce PC_WINDOWS_LOAD
	set PC_TURNED_ON XVELSAVED
	set INTERNALCOUNT 0
	}
}

ifhitweapon
	{
	debris SCRAP1 2
	debris SCRAP2 2
	sound VENT_BUST
    lotsofglass 5
    sound GLASS_BREAKING
	cactor 17017
	}

ends

useractor notenemy 17012 // PC Monitor
state PC_MONITOR_CODE
enda

useractor notenemy 17346 // PC Monitor 2
state PC_MONITOR_CODE
enda


useractor notenemy 17016 // PC Keyboard

ifaction 0
{
cstator 257
ifspritepal 3 action ZERO else
ifspritepal 9 action ZERO else
action FORM
}

ifaction FORM
 ifpdistl 1024
  ifp pfacing
   ifhitspace
    ifcansee
	{
	set hit_key 30
	soundonce MASH
	state lower_weapon
	}

ifaction ZERO
 ifpdistl 1024
  ifp pfacing
  {
   ife PC_TURNED_ON LOTAGSAVED
	{
	set player_use 0
	ifhitspace
	 ifcount 13
		{
		set hit_key 30
		state lower_weapon
		soundonce TYPING_SOUND
		ifspritepal 9
		ife GOT_PASSWORD 3
			{
			set PC_TURNED_ON HITAGSAVED
			resetcount
			}
			else
		ife GOT_PASSWORD 1
			{
			set PC_TURNED_ON HITAGSAVED
			resetcount
			}
			else
			{
			quote 287
			soundonce PC_ERROR
			}
		}
	}
	else
   ife PC_TURNED_ON XVELSAVED
	{
	set player_use 0
	ifhitspace
	 ifcount 13
		{
		set hit_key 30
		state lower_weapon
		soundonce TYPING_SOUND
		set PC_TURNED_ON YVELSAVED
		resetcount
		}
	}
   }

ifhitweapon
	{
	debris SCRAP1 2
	debris SCRAP2 2
	sound VENT_BUST
	cactor 17019
	}

enda

useractor notenemy 17032

ifaction 0
ifn LOTAGSAVED 0
  ife PC_TURNED_ON LOTAGSAVED
	{
	action ZERO
	set INTERNALCOUNT 0
	}

ifaction ZERO
	{
	add INTERNALCOUNT 1
	ife INTERNALCOUNT 13
		{
		sound MS_MECH_BLIP
		setactorsoundpitch THISACTOR MS_MECH_BLIP 512
		geta[].z temp
		add temp 512
		seta[].z temp
		add INTERNALCOUNT_2 1
		set INTERNALCOUNT 0
		}
	ife INTERNALCOUNT_2 10
		{
		set PC_TURNED_ON HITAGSAVED
		sound MS_MECH_SERV2
		setactorsoundpitch THISACTOR MS_MECH_SERV1 1024
		killit
		}
	}

enda

move PRINT 16 0

useractor notenemy 17033

ifaction 0
{
cstat 32768
ife PC_TURNED_ON LOTAGSAVED
	{
	cstat 32
	action ZERO
	set INTERNALCOUNT 0
	}
}

ifaction ZERO
	{
	soundonce MS_MECH_BLIP
	setactorsoundpitch THISACTOR MS_MECH_BLIP 256
	move PRINT geth
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 52 { move STOP sound PC_DONE action BREAK resetcount }
	}

ifaction BREAK
	{
	ifpdistl 1024
	 ifp pfacing
		{
		set player_use 0
		 ifhitspace
		  ifcount 13
			{
			set hit_key 30
			ifn DOCUMENT -1
				{
					readgamevar EVIDENCE_EP2
					ifand EVIDENCE_EP2 2 { set DOCUMENT -1 resetcount }
					else
						{
						xorvar EVIDENCE_EP2 2
						savegamevar EVIDENCE_EP2
						globalsound PROMOTION
						add TREASURE_FOUND 5
						state getfactornormalizedframerate
						set DISP_KEY -150
						mul DISP_KEY NORMALIZED_FACTOR
						set DISP_KEY_PAL 12
						set DISP_KEY_PIC 20376
						userquote 289
						set DOCUMENT -1
						killit
						}
				}
			else
			  ife DOCUMENT -1
			  {
				set temp9 1
				soundonce READ_DOCUMENT
				set DOCUMENT 17034
				lockplayer 13
				resetcount
			   }
			}
		}
	}

enda

useractor notenemy 17404

ifaction 0
{
cstat 32768
ife PC_TURNED_ON LOTAGSAVED
	{
	cstat 32
	action ZERO
	set INTERNALCOUNT 0
	}
}

ifaction ZERO
	{
	soundonce MS_MECH_BLIP
	setactorsoundpitch THISACTOR MS_MECH_BLIP 256
	move PRINT geth
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 30 { move STOP sound PC_DONE action BREAK resetcount }
	}

ifaction BREAK
	{
	ifpdistl 1024
	 ifp pfacing
		{
		set player_use 0
		 ifhitspace
		  ifcount 13
			{
			set hit_key 30
			ifn DOCUMENT -1
				{
					readgamevar EVIDENCE_EP2
					ifand EVIDENCE_EP2 256 { set DOCUMENT -1 resetcount }
					else
						{
						xorvar EVIDENCE_EP2 256
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 5
						globalsound PROMOTION
						state getfactornormalizedframerate
						set DISP_KEY -150
						mul DISP_KEY NORMALIZED_FACTOR
						set DISP_KEY_PAL 12
						set DISP_KEY_PIC 20376
						userquote 289
						set DOCUMENT -1
						killit
						}
				}
			else
			  ife DOCUMENT -1
			  {
				set temp9 1
				soundonce READ_DOCUMENT
				set DOCUMENT 17403
				lockplayer 13
				resetcount
			   }
			}
		}
	}

enda

// ===============================================
// EAF COMPUTER
// ===============================================

useractor notenemy 18381
ifspritepal 3
{
	ifaction 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 action ZERO
	}

	ifmove STOP nullop
	else
	ifaction ZERO
		{
		ifpdistl 4096
		ife GOT_PASSWORD 2
			{
			set player_use 0
			ifhitspace
				{
				set hit_key 30
				soundonce TYPING_SOUND
				operateactivators HITAGSAVED THISACTOR
				move STOP
				}
			}
		}
}
enda

useractor notenemy 18391

ifaction 0
{
checkactivatormotion HITAGSAVED
 ife RETURN 1
	{
	action ONE
	}
}

ifaction ONE
{
ife GOT_PASSWORD 2
	{
	checkactivatormotion LOTAGSAVED
	 ifn RETURN 0
		{
		action TWO
		set INTERNALCOUNT 0
		}
	}
}

ifaction TWO
{
add INTERNALCOUNT 1
ifg INTERNALCOUNT 30
	{
	soundonce PC_NOTIFY
	action THREE
	}
}

ifaction THREE
{
ifp pfacing
ifcansee
ifpdistl 4096
ifp palive
	{
	readgamevar EVIDENCE_EP2
	  ifand EVIDENCE_EP2 8 nullop
					else
						{
						xorvar EVIDENCE_EP2 8
						savegamevar EVIDENCE_EP2
						add TREASURE_FOUND 2
						globalsound PROMOTION
						state getfactornormalizedframerate
						set DISP_KEY -150
						mul DISP_KEY NORMALIZED_FACTOR
						set DISP_KEY_PAL 12
						set DISP_KEY_PIC 20376
						userquote 289
						}
	}
}

enda

// ===============================================
// LIGHTS
// ===============================================

gamearray LIGHT_SMASHED 16384

useractor notenemy 5862
ifaction 0
{
ife HITAGSAVED 0 action ZERO
else
ifhitweapon
	{
	sound HITGLASS
	sound VENT_BUST
	lotsofglass 2
	debris SCRAP1 8
	shoot SPARK shoot SPARK shoot SPARK
	ife LIGHT_SMASHED[HITAGSAVED] 0 setarray LIGHT_SMASHED[HITAGSAVED] 1
	killit
	}
}
enda

useractor notenemy WALLLIGHT4
ifaction 0
{
ife HITAGSAVED 0 action ZERO
else
ifhitweapon
	{
	sound HITGLASS
	sound VENT_BUST
	sound SHORT_CIRCUIT
	lotsofglass 8
	debris SCRAP1 8
	shoot SPARK shoot SPARK shoot SPARK
	ife LIGHT_SMASHED[HITAGSAVED] 0 setarray LIGHT_SMASHED[HITAGSAVED] 1
	action ONE
	}
}
enda


useractor notenemy WALLLIGHT3
ifaction 0
{
ife HITAGSAVED 0 action ZERO
else
ifhitweapon
	{
	sound HITGLASS
	sound VENT_BUST
	sound SHORT_CIRCUIT
	lotsofglass 8
	debris SCRAP1 8
	shoot SPARK shoot SPARK shoot SPARK
	ife LIGHT_SMASHED[HITAGSAVED] 0 setarray LIGHT_SMASHED[HITAGSAVED] 1
	ifrnd 16 set temp2 1 else set temp2 0
	action ONE
	}
}

ifaction ONE
{
ife temp2 1
 ifrnd 1
	{
	sound SHORT_CIRCUIT shoot SPARK shoot SPARK shoot SPARK spawn 8433
	}
}

enda

// chinese lanterns

useractor notenemy 3887
ifaction 0
{
cstator 1024
ife HITAGSAVED 0 action ZERO
else
ifhitweapon
	{
	sound GLASS_HEAVYBREAK
	sound VENT_BUST
	spritepal 2
	lotsofglass 8
	debris SCRAP1 8
	ife LIGHT_SMASHED[HITAGSAVED] 0 setarray LIGHT_SMASHED[HITAGSAVED] 1
	killit
	}
}
enda

spritenoshade 8515
useractor notenemy 8515
ifaction 0
{
ife HITAGSAVED 0 action ZERO
else
ifhitweapon
	{
	sound GLASS_HEAVYBREAK
	sound VENT_BUST
	sound ENGULF_FIRE
	spawn 8433
	spritepal 19
	lotsofglass 8
	debris SCRAP1 8
	shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK shoot SPARK
	ifrnd 32 spawn BURNING
	ife LIGHT_SMASHED[HITAGSAVED] 0 setarray LIGHT_SMASHED[HITAGSAVED] 1
	ifg LOTAGSAVED 0
		{
		operateactivators LOTAGSAVED THISACTOR
		operatemasterswitches LOTAGSAVED
		operaterespawns LOTAGSAVED
		}
	killit
	}
}
enda

defstate lightbeam_smash
ifaction 0 { geta[].shade SHADESAVED action ZERO }
ife sprite[].ang 0
	{
	ifg sector[].floorshade 5 setactor[].shade 30
	else setactor[].shade SHADESAVED
	}
ifn HITAGSAVED 0
	{
	sleeptime 0
	ife LIGHT_SMASHED[HITAGSAVED] 1 action BREAK
	}
ifaction BREAK
	{
	geta[].shade temp
	add temp 4
	ifg temp 32 killit
	seta[].shade temp
	}
ends

spritenopal 8623
spritenoshade 8623
useractor 4 8623 state lightbeam_smash enda

useractor notenemy 18225 state lightbeam_smash enda

useractor notenemy 18224 state lightbeam_smash enda


// =============================================================================================================================================
// RISE PARTICLES
// =============================================================================================================================================

spriteflags 18574 16

spritenoshade 18574
spritenopal 18574

useractor notenemy 18574
ifaction 0
	{
	sizeat 32 32
	seta[].blend 255
	seta[].cstat 130
	seta[].shade -16
	action ZERO
	}

ifaction ZERO
{
move FLYUP getv
geta[].shade temp
add temp 1
ifg temp 30 killit
seta[].shade temp
}
enda

defstate spawn_curse_particles
ifdead nullop
else
	{
	set temp3 sprite[].x
	randvarvar temp4 768
	add temp3 temp4
	randvar temp4 2048

	rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6

	espawn 30080

	seta[RETURN].x temp5
	seta[RETURN].y temp6
	geta[].z temp7
	sub temp7 16384
	seta[RETURN].z temp7

	seta[RETURN].pal 30
	}
ends

spriteflags 30080 16

spritenoshade 30080
spritenopal 30080

useractor notenemy 30080
ifaction 0
	{
	sizeat 16 16
	seta[].blend 255
	seta[].cstat 130
	seta[].shade -127
	action ZERO
	}

ifaction ZERO
{
sizeto 8 8
move FLYDOWN getv
geta[].shade temp
add temp 4
ifge temp 30 killit
seta[].shade temp
}
enda

// ============================================================================================================================================================================================
// flat rise particles
// =============================================================================================================================================

spriteflags 30070 16

spritenoshade 30070
spritenopal 30070

spritenoshade 30071
spritenopal 30071

defstate flat_rise_particle
ifaction 0
	{
	sizeat 16 16
	seta[].blend 255
	seta[].cstat 50
				set temp2 2048
                setactor[].xoffset temp2
                set temp2 2048
                shiftvarr temp2 8
                setactor[].yoffset temp2
	seta[].shade -16
	action ZERO
	}
ifaction ZERO
	{
	move FLYUP getv
	geta[].ang temp
	add temp 32
	seta[].ang temp
	geta[].shade temp
	add temp 1
	ifge temp 30 killit
	seta[].shade temp
	}
ends


useractor notenemy 30070 state flat_rise_particle enda
useractor notenemy 30071 state flat_rise_particle enda

// ===============================================
// ALIEN SKY BLAST
// ===============================================

move UPWARDS 0 -800

spritenoshade 18271
spritenopal 18271
spritenoshade 18273
spritenopal 18273

useractor notenemy 18273
ifaction 0
{
seta[].blend 255
seta[].shade -127
sizeat 128 128
cstat 514
spritepal 8
move UPWARDS getv
action ZERO
}

ifaction ZERO
{
sizeto 1 1

ifceilingdistl 16 killit
ifcount 128 killit

}

enda

useractor notenemy 18271

ifaction 0
{
seta[].shade -127
ifspritepal 3 cstat 32768 else
	{
	globalsound BIG_LASER_FIRE
	spritepal 8
	}
sizeat 200 200
ifp pfacing palfrom 20 20 20 20
quake 52
action ZERO
}

ifaction ZERO
{
		ifspritepal 8 spawn 18273
		sound RPG_EXPLODE
		set temp3 sprite[].x
		add temp3 2048
		randvar temp4 2048
		rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
		espawn BIG_EXPLOSION
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		espawn BIG_SMOKE2
		seta[RETURN].x temp5
		seta[RETURN].y temp6
		set temp6 0

ifcount 26
	{
	flash
	spawn NUCLEAR_EXPLOSION
	globalsound EXPLOSION_HUGE
	operateactivators LOTAGSAVED THISACTOR
	operatemasterswitches LOTAGSAVED
	operaterespawns LOTAGSAVED
	killit
	}
}

enda


// GORO RING OF FIRE===============================================================================================================

spritenoshade 17361
spritenopal 17361

useractor notenemy 17361

ifaction 0
{
	seta[].shade -64
	seta[].blend 255
	cstator 130

	ifspawnedby APLAYER
		sizeat 8 8
	// make svedr's fire ring a bit more easily visible
	// unfortunately we can't mess with blend/transparency so just make it bigger
	else ifspawnedby SVEDR
		sizeat 16 16
}

ifspawnedby APLAYER
{
	getp[].posx temp
	getp[].posy temp2
}
else ifspawnedby SVEDR
{
	set temp my_x
	set temp2 my_y
}

add INTERNALCOUNT_2 1
ifg INTERNALCOUNT_2 300 { spawn GORO_FLAME_EX soundonce EXPLOSION_SMALL killit }


// This sets the diameter of the ring.
// If spawned by player it pulsates
// If spawned by Svedr, after a few tics it starts to shrink until it collides with the player (or until singularity)
// temp3 = base radius, temp7 = radius modifier value
//======
ifspawnedby APLAYER
{
	set temp3 temp
	add temp3 256

	ife INTERNALCOUNT 0
	{
		add temp7 16
		ifg temp7 512
		set INTERNALCOUNT 1
	}
	else ife INTERNALCOUNT 1
	{
		sub temp7 16
		ifl temp7 16
		set INTERNALCOUNT 0
	}
}
else ifspawnedby SVEDR
{
	set temp3 temp
	add temp3 1800

	ifg temp7 -1800
	{
		ifl INTERNALCOUNT 40
			add INTERNALCOUNT 1
		else
			sub temp7 90
	}
	else
	{
		spawn GORO_FLAME_EX soundonce EXPLOSION_SMALL killit
	}
}
add temp3 temp7
//======

rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

add temp4 32

ifg temp4 2048 sub temp4 2048

seta[].x temp5
seta[].y temp6
ifspawnedby APLAYER
{
	getp[].posz temp8
	add temp8 512
	seta[].z temp8

	findnearactor3d SHOOTME 768 target ifn target -1 { action ZERO resetcount }

	ifaction ZERO
	{
		ife target -1 action 0
		getav[target].myowner temp8
		seta[temp8].htextra 3
		setactorvar[temp8].fire_damage 10
		soundonce EXPLOSION_SMALL
		spawn GORO_FLAME_EX
		action 0
	}
}
else ifspawnedby SVEDR
{
	set temp8 my_z
	add temp8 512
	seta[].z temp8

	dist temp THISACTOR my_target

	ifl temp 700
	{
		// player can jump over it
		geta[my_target].z mz

		ifge mz temp8
		{
			action ZERO
			resetcount
		}
	}

	ifaction ZERO
	{
		seta[my_target].htextra 3
		setav[my_target].fire_damage 10
		soundonce EXPLOSION_SMALL
		spawn GORO_FLAME_EX
		action 0
		killit
	}
}
enda

// *************************************************
// STARDRIVE
// *************************************************

spritenoshade 15331
spritenopal 15331

useractor notenemy 15331

ifaction 0
	{
	seta[].blend 255
	seta[].shade -120
	sizeat 2 2
	cstat 32768
	ife LOTAGSAVED 0
		{
		// ugly case specific code to stop it interupting radio in micky begins
		ife VOLUME 2 ife LEVEL 42 nullop
		else stopallsounds
		palfrom 32 0 0 32
		globalsound STARDRIVE_WARP
		cstat 1154
		action ZERO
		resetcount
		}
		else
		{
		checkactivatormotion LOTAGSAVED
		 ife RETURN 1
			{
			palfrom 32 0 0 32
			globalsound STARDRIVE_WARP
			cstat 1154
			action ZERO
			resetcount
			}
		}
	}

ifaction ZERO
	{
	ifspritepal 3 sizeto 12 12 else
	sizeto 64 64
	ifcount 105
		{
		spawn 8433
		ife HITAGSAVED 15331
			{
			set global_trigger 15330
			}
			else
		ife HITAGSAVED 21695
			{
			set global_trigger 21694
			}
			else
		ifn HITAGSAVED 0 espawnvar HITAGSAVED
		ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
		action FORM
		resetcount
		}
	}

ifaction FORM
	{
	geta[].shade temp
	add temp 5
	ifg temp 40 killit
	seta[].shade temp
	}


enda

spritenoshade 15330

useractor notenemy 15330 // Cycloid ship
	ifspawnedby 15331
	 ifaction 0
		{
		seta[].blend 255
		seta[].shade 30
		ife VOLUME 2 ife LEVEL 42 sizeat 128 128 else
		sizeat 32 32
		cstat 130
		action ZERO
		}

ifaction ZERO
	{
	geta[].shade temp
	sub temp 2
	ife temp 0 { cstat 128 action FORMED }
	seta[].shade temp
	}

ifaction FORMED
	{
	checkactivatormotion 15331
	 ife RETURN 1
		{
		espawn 15331
		setactorvar[RETURN].HITAGSAVED 15331
		action UNFORM
		}
	}

ifaction UNFORM
	{
	ife global_trigger 15330
		{
		cstat 130
		action FORM
		set global_trigger 0
		}
	}

ifaction FORM
	{
	geta[].shade temp
	add temp 2
	ife temp 30 killit
	seta[].shade temp
	}

enda

spritenoshade 21694

useractor notenemy 21694 // EAF ship
ifaction 0
	{
	seta[].blend 255
	ifspawnedby 15331
		{
		seta[].shade 30
		ifspritepal 3 sizeat 8 8 else
		sizeat 32 32
		cstat 130
		action ZERO
		}
		else action FORMED
	}


ifaction ZERO
	{
	geta[].shade temp
	sub temp 2
	ife temp 0 { cstat 128 action FORMED }
	seta[].shade temp
	}

ifaction FORMED
	{
		checkactivatormotion 21694
		 ife RETURN 1
			{
			espawn 15331
			setactorvar[RETURN].HITAGSAVED 21695
			action UNFORM
			}
	}

ifaction UNFORM
	{
	ife global_trigger 21694
		{
		cstat 130
		action FORM
		// set global_trigger 0
		}
	}

ifaction FORM
	{
	geta[].shade temp
	add temp 2
	ife temp 30 killit
	seta[].shade temp
	}

enda

// *************************************************
// NEON/MISC ADDITIVE
// *************************************************

defstate ADD_BLEND
ifaction 0 { seta[].blend 255 action ZERO }
ends

useractor notenemy 13035 state ADD_BLEND enda
useractor notenemy 13036 state ADD_BLEND enda
useractor notenemy 13037 state ADD_BLEND enda
useractor notenemy 13038 state ADD_BLEND enda
useractor notenemy 13039 state ADD_BLEND enda
useractor notenemy 18654 state ADD_BLEND enda

useractor notenemy 18333
state ADD_BLEND
enda

useractor notenemy 16292
state ADD_BLEND
enda

useractor notenemy 18473
state ADD_BLEND
enda



spritenoshade 18414
spritenopal 18414

useractor notenemy 18432
state ADD_BLEND
enda

spritenopal 10613
spritenoshade 10613
useractor 4 10613
state ADD_BLEND
enda

useractor notenemy 11686
state ADD_BLEND
enda

useractor notenemy 18428
state ADD_BLEND
enda

useractor notenemy 18429
state ADD_BLEND
enda

spritenoshade 18431
spritenopal 18431
useractor notenemy 18431
state ADD_BLEND
enda

spritenopal 18427
spritenoshade 18427

useractor notenemy 18427
ifaction 0
	{
	seta[].blend 255
	cstat 34
	sizeat 64 64
	seta[].mdflags 16
	set command 256
	action ZERO
	}

ifaction ZERO
	{
	ife sprite[myowner].statnum 1024
		{
		action FORM
		}
	}

ifaction FORM
	{
	geta[].shade temp2
	add temp2 2
	ifg temp2 35 killit
	seta[].shade temp2
	}

enda

spriteflags 15967 84

useractor notenemy 15967
sleeptime 0
seta[].mdflags 16

ifn HITAGSAVED 0
	{
	seta[].blend 255
	action ZERO
	checkactivatormotion HITAGSAVED
	ife RETURN 1 killit
	}
else
ifn LOTAGSAVED 0
	{
	cstat 32768
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		seta[].cstat CSTATSAVED
		action ZERO
		}
	}
else
ifaction 0
	{
	seta[].blend 255
	ifspritepal 3 action FORMED else action ZERO
	}

ifaction ZERO set command 256

ifaction FORMED
	{
	seta[].pal sector[].floorpal
	geta[].ang temp
	ifspritepal 2 sub temp 8 else
	add temp 8
	seta[].ang temp
	}

enda

spritenoshade 3375

useractor notenemy 3375

 ifaction 0
	{
	ifn LOTAGSAVED 0
		{
		sizeat 2 2
		seta[].shade 30
		checkactivatormotion LOTAGSAVED
		 ife RETURN 1
		   {
		   sound FFIELD_ON
		   action FORM
		   }
		}
	}

ifaction FORM
	{
	geta[].ang temp3
	add temp3 256
	seta[].ang temp3
	geta[].xrepeat temp2
	add temp2 2
	seta[].xrepeat temp2
	seta[].yrepeat temp2
	ifg temp2 128
		{
		sizeat 128 128
		seta[].ang temp8
		sound SOMETHINGHITFORCE
		setactorsoundpitch THISACTOR SOMETHINGHITFORCE 1024
		action FORMED
		}

	geta[].shade temp
	ifg temp -120
		{
		sub temp 2
		seta[].shade temp
		}
	}

ifaction FORMED
	{
	ifpdistl 16384 soundonce FFIELD_IDLE
		checkactivatormotion HITAGSAVED
		 ife RETURN 1
		   {
		   killit
		   }
	}



enda


useractor notenemy 9175 state ADD_BLEND enda
useractor notenemy 16290 state ADD_BLEND enda

spritenoshade 10037
spritenopal 10037
useractor notenemy 10037 state ADD_BLEND enda

useractor notenemy 16327 state ADD_BLEND enda

// *************************************************
// MOUNTAIN CODE
// *************************************************

useractor notenemy 18136
cstat 32768

ifcount 600
{
sleeptime 0
espawn 18137

	geta[].x temp
	geta[].y temp2

	set temp3 temp
	randvar temp8 1536
	add temp3 temp8

	randvar temp4 2048

	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	seta[RETURN].x temp5
	seta[RETURN].y temp6
	geta[].ang temp7
	seta[RETURN].ang temp7
	resetcount
}

enda

useractor notenemy 18137
fall
ifspawnedby 18136
	{
	sizeat 90 90
	fall
	move TREESPEEDUP geth
	ifnotmoving killit
	}
enda

// *************************************************
// GHOST SHIP STUFF
// *************************************************

defstate FADE_AWAY
ifcansee
	{
	getp[].i target
	dist xydist THISACTOR target
	shiftvarr xydist 8
	ifl xydist 0 set xydist 0
	add xydist 32
	ifg xydist 255 set xydist 255
	ifpdistl 2548
		{
		soundonce SHIP_LAUGH
		ifl temp4 254 add temp4 8
		else killit
		}
	set alpha temp4

	state SPRITE_FADE
	}
ends

defstate FADE_IN
ifcansee
	{
	getp[].i target
	dist xydist THISACTOR target
	shiftvarr xydist 8
	ifl xydist 0 set xydist 0
	add xydist 32
	ifg xydist 255 set xydist 255
	ifpdistl 2548
		{
		soundonce SHIP_LAUGH
		ifg temp4 0 sub temp4 8
		else spritepal 0
		}
	set alpha temp4

	state SPRITE_FADE
	}
ends

useractor notenemy 7260
ifspritepal 3
	{
	ifaction 0 { cstator 1 set temp4 254 action ZERO }
	state FADE_IN
	}
enda

action GHOST_STRIPPER -15282
action GHOST_STRIPPER2 -15277
action GHOST_STRIPPER3 -15269
action GHOST_STRIPPER4 -15199
action GHOST_STRIPPER5 -15199

useractor notenemy 16594
ifspritepal 0 action GHOST_STRIPPER
ifspritepal 3 action GHOST_STRIPPER2
ifspritepal 5 action GHOST_STRIPPER3
ifspritepal 9 action GHOST_STRIPPER4

ifcansee
	{
	ifpdistl 4096
		{
		soundonce SHIP_LAUGH
		ifl temp4 254 add temp4 8
		else { spawn SKULLEXPLOSION spawn SHADE killit }
		}
	set alpha temp4

	state SPRITE_FADE
	}

enda

useractor notenemy 16611 state FADE_AWAY enda
useractor notenemy 16612 state FADE_AWAY enda

// KEYPAD #################################################################


// This is defined once and re-used for each of the buttons. 'param' is the digit to enter
defstate enterdigit
{
	ifn pad_id -1 ifl pnum 9999 //hitags can only reach 65536, so we limit entered codes to 5 digits.
	{
		sound KEY_PRESS
		set RETURN -1 //Keep in mind that the buttons we are using are serving double-duty as weapon selection. We only disbale that when we are entering a passcode.
		mulvar pnum 10 // This shifts the passcode over a place value to make room for the new digit
		add pnum param
	}
}
ends

// The actual buttons. Any one should do as long as you set param to the digit and call 'enterdigit'.
appendevent EVENT_WEAPKEY10
	set param 0
	state enterdigit
endevent
appendevent EVENT_WEAPKEY1
	set param 1
	state enterdigit
endevent
appendevent EVENT_WEAPKEY2
	set param 2
	state enterdigit
endevent
appendevent EVENT_WEAPKEY3
	set param 3
	state enterdigit
endevent
appendevent EVENT_WEAPKEY4
	set param 4
	state enterdigit
endevent
appendevent EVENT_WEAPKEY5
	set param 5
	state enterdigit
endevent
appendevent EVENT_WEAPKEY6
	set param 6
	state enterdigit
endevent
appendevent EVENT_WEAPKEY7
	set param 7
	state enterdigit
endevent
appendevent EVENT_WEAPKEY8
	set param 8
	state enterdigit
endevent
appendevent EVENT_WEAPKEY9
	set param 9
	state enterdigit
endevent

// this makes reseting the code entry easier.
defstate clearpad
{
	sound KEY_PRESS
	set pad_id -1
	set pnum 0
}
ends

// Additional Section by Lord Misfit: How to incorporate a "backspace" key to the keypad
appendevent EVENT_TURNAROUND
        ifn pad_id -1
        {
         ife pnum 0 // If you hit backspace when the code is 0, you cancel out of the keypad
         {
          state clearpad
         }
         else
         ifg pnum 0 // Else if it's above 0, you divide by 10 to emulate a backspace key
         {
          divvar pnum 10
          ifl pnum 0 { set pnum 0 }
         }
        }
endevent

// Additional Section By Lord Misfit: Displaying the code on the screen as you type it.
// You'll probably need vars for the following...

appendevent EVENT_DISPLAYREST
ifn pad_id -1
{
 ifn pnum -1
 {
  digitalnumber 2837 160 32 pnum 0 0 27 0 0 xdim ydim
 }
}
endevent

// The hitag of the keypad actor is the passcode, and the lotag should match that of the activator or activator-locked.

useractor notenemy 3609
ifaction 0 { cstat 32768 action ZERO }

ifg INTERNALCOUNT 0 sub INTERNALCOUNT 1
clamp INTERNALCOUNT 0 10

	ifpdistl 1024 ifp pfacing ifhitspace ifcansee ife INTERNALCOUNT 0 // There are lot of conditons on this line. The last one is there to ensure proper timing.
	{
		ife pad_id -1 // enter passcode entering mode
		{
			soundonce COMP_SELECT
			set pad_id THISACTOR
		}
		else ife pad_id THISACTOR
		{
			ife pnum HITAGSAVED // passcode accepted
			{
				operateactivators LOTAGSAVED THISACTOR
				soundonce KEY_ACCEPT
				state clearpad
				killit
			}
			else // passcode rejected (The 'soundonce' and 'quote' statements are optional, and the quote numbers wil likely be different for your mod. )
			{
				soundonce COMP_INVALID
				state clearpad
			}
		}
		set INTERNALCOUNT 10 // This sets a delay between presses of space so that everything doesn't happen at once.
	}
	ife pad_id THISACTOR // this block disengages the player from the keypad when he moves or looks away from it.
	{
		ifpdistg 1024
			state clearpad
		ifp pfacing nullop else
			state clearpad
		ifcansee nullop else
			state clearpad
	}

enda

// *************************************************
// UNDERWORLD STATUE
// *************************************************

spritenoshade 18133
spritenopal 18133
spriteflags 18133 131072

useractor notenemy 18133
ifaction 0
{
cstator 514
seta[].blend 255
action ZERO
}

ifspawnedby 18134 // statues to be destroyed
{
sizeat 4 4
spawn FRAMEEFFECT1
	ifaction ZERO
	{
	geta[myowner].x temp
	geta[myowner].y temp2

	set temp3 temp
	add temp3 128


				set INTERNALCOUNT player[].player_par
				set INTERNALCOUNT_2 THISACTOR
				ifand INTERNALCOUNT_2 1 add INTERNALCOUNT_2 4096
				add INTERNALCOUNT INTERNALCOUNT_2
				shiftvarl INTERNALCOUNT 6
				sin INTERNALCOUNT_2 INTERNALCOUNT
				shiftvarr INTERNALCOUNT_2 4
				sub temp3 INTERNALCOUNT_2


	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	ifg temp4 2048 sub temp4 2048

	seta[].x temp5
	seta[].y temp6
	geta[myowner].z temp8
	sub temp8 16384
	seta[].z temp8

	ifcount 1 { add temp4 32 resetcount }
	ife temp9 1 { spawn 8433 sound SPIRIT_RELEASE set INTERNALCOUNT 0 action BREAK }
	}

	ifaction BREAK
	{
	move FLYUP getv
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 60
		{
		spawn 8433
		addphealth 50
		sound SHADOWRM_UNL
		killit
		}
	}
}
else
ifspawnedby 18132 // main lock
{
sizeat 8 8
	ifaction ZERO
	{
	geta[myowner].x temp
	geta[myowner].y temp2

	set temp3 temp
	add temp3 128

				set INTERNALCOUNT player[].player_par
				set INTERNALCOUNT_2 THISACTOR
				ifand INTERNALCOUNT_2 1 add INTERNALCOUNT_2 4096
				add INTERNALCOUNT INTERNALCOUNT_2
				shiftvarl INTERNALCOUNT 6
				sin INTERNALCOUNT_2 INTERNALCOUNT
				shiftvarr INTERNALCOUNT_2 4
				sub temp3 INTERNALCOUNT_2


	rotatepoint temp temp2 temp3 temp2 temp4 temp5 temp6

	ifg temp4 2048 sub temp4 2048

	seta[].x temp5
	seta[].y temp6
	geta[myowner].z temp8
	sub temp8 18384
	seta[].z temp8

	ifcount 1 { add temp4 16 resetcount }
	checkactivatormotion temp9
	ife RETURN 1 { spawn 8433 sound SPIRIT_RELEASE set INTERNALCOUNT 0 action BREAK }
	}

	ifaction BREAK
	{
	move FLYUP getv
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 60
		{
		spawn 8433
		getav[myowner].INTERNALCOUNT temp
		add temp 1
		setactorvar[myowner].INTERNALCOUNT temp
		sound USE_PORTAL
		killit
		}
	}
}

enda

spriteflags 18134 131072

useractor notenemy 18134
ifaction 0
 ifn HITAGSAVED 0
	{
	geta[].pal temp
	strength 100
	espawn 18133
	set temp7 RETURN // store id of first sphere
	seta[RETURN].pal temp
	setactorvar[RETURN].myowner THISACTOR
	espawn 18133
	set temp8 RETURN // store id of second sphere
	seta[RETURN].pal temp
	setactorvar[RETURN].myowner THISACTOR
	setactorvar[RETURN].temp4 682
	espawn 18133
	set temp9 RETURN // store id of third sphere
	seta[RETURN].pal temp
	setactorvar[RETURN].myowner THISACTOR
	setactorvar[RETURN].temp4 1365
	action ZERO
	}

ifaction ZERO
	{
	ifpdistl 16384 { ifactorsound THISACTOR SMALLST_CHANT nullop else sound SMALLST_CHANT }
	ifhitweapon
		{
		sound HITSTONE
		spawn DUSTSMOKE
		ifdead
			{
			stopactorsound THISACTOR SMALLST_CHANT
			sound BUST_CONCRETE
				quake 26
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1 shoot ROCKDEB_1
			spawn BIG_SMOKE2 spawn BIG_SMOKE2
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			setactorvar[temp7].temp9 1
			setactorvar[temp8].temp9 1
			setactorvar[temp9].temp9 1
			killit
			}
		}
	}


enda

spriteflags 18132 131072

useractor notenemy 18132
ifaction 0
 ifn LOTAGSAVED 0
	{
	set INTERNALCOUNT 0
	espawn 18133
	set temp7 RETURN // store id of blue sphere
	seta[RETURN].pal 0
	setactorvar[RETURN].myowner THISACTOR
	setactorvar[RETURN].temp9 LOTAGSAVED
	espawn 18133
	set temp8 RETURN // store id of red sphere
	seta[RETURN].pal 21
	setactorvar[RETURN].myowner THISACTOR
	setactorvar[RETURN].temp4 682
	setactorvar[RETURN].temp9 HITAGSAVED
	espawn 18133
	set temp9 RETURN // store id of green sphere
	seta[RETURN].pal 11
	setactorvar[RETURN].myowner THISACTOR
	setactorvar[RETURN].temp4 1365
	setactorvar[RETURN].temp9 EXTRASAVED
	action ZERO
	}

ifaction ZERO
	{
	ifpdistl 16384 { ifactorsound THISACTOR LARGEST_CHANT nullop else sound LARGEST_CHANT }
	ife INTERNALCOUNT 3
		{
		stopactorsound THISACTOR LARGEST_CHANT
		globalsound SHADOWRM_UNL
		ifn XVELSAVED 0
			{
			operateactivators XVELSAVED THISACTOR
			operaterespawns XVELSAVED
			operatemasterswitches XVELSAVED
			}
		spawn BIG_SMOKE2 spawn BIG_SMOKE2
		action MINUSONE
		}
	}

enda

// *************************************************
// MAGIC FIELD
// *************************************************

spritenoshade 18463
spritenopal 18463

useractor notenemy 18463

ifaction 0
{
ife sprite[].shade 30 action UNFORM else action FORM
}

ifaction FORM
{
checkactivatormotion HITAGSAVED
ife RETURN 1 { seta[].shade 0 globalsound MAGIC_FIELD_DOWN action FORMED }
}

ifaction FORMED
{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30 killit
}

ifaction UNFORM
{
cstat 32768
checkactivatormotion LOTAGSAVED
ife RETURN 1 { globalsound MAGIC_FIELD_UP seta[].cstat CSTATSAVED action ZERO }
}

ifaction ZERO
{
geta[].shade temp
sub temp 1
seta[].shade temp
ife temp 0 { seta[].shade -127 action FORM }
}

enda

// *************************************************
// thruster
// *************************************************

spritenoshade 16212

useractor notenemy 16212

ifn HITAGSAVED 0
{
seta[].blend 255
ifaction 0 seta[].shade 30
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	action ZERO
	set HITAGSAVED 0
	}
}

ifaction ZERO
{
geta[].shade temp
sub temp 1
ifl temp -50 action FORM
seta[].shade temp
}

enda

// *************************************************
// FLAME
// *************************************************

spritenoshade 8694

useractor notenemy 8694
ifspritepal 3
{
	ifaction 0
	{
	sleeptime 0
	set INTERNALCOUNT 0
	action ZERO
	}

	ifaction ZERO
	{
	sleeptime 0
	hitradius 2048 1 8 32 128
	ifpdistl 4096
	 ifcansee
		{
		soundonce FLESH_BURNING
		getp[].i temp4
		getav[temp4].fire_damage temp5
		ifl temp5 1 sound ENGULF_FIRE
		add temp5 5
		setactorvar[temp4].fire_damage temp5
		palfrom 16 16
		}
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 140
		{
		stopsound MC_JETPAKIDLE
		soundonce MC_JETPAKOFF
		geta[].shade temp
		add temp 4
		seta[].shade temp
		ife temp 40 { shoot SPARK shoot SPARK shoot SPARK set INTERNALCOUNT 0 action BREAK }
		}
		else
		{ ifpdistl 16384 soundonce MC_JETPAKIDLE seta[].shade -16 }
	}

	ifaction BREAK
		{
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 140
			{
			soundonce MC_JETPAKON
			geta[].shade temp
			sub temp 4
			seta[].shade temp
			ife temp -16 { set INTERNALCOUNT 0 action ZERO }
			}
			else
			seta[].shade 40
		}
}
enda

// *************************************************
// JET TRAIL
// *************************************************

spritenoshade 16234

useractor notenemy 16234
ifspritepal 3 break
ifaction 0
{
sleeptime 0
set INTERNALCOUNT 0
action ZERO
}

ifaction ZERO
{
sleeptime 0
hitradius 2048 1 8 32 128
ifpdistl 4096
 ifcansee
	{
	soundonce FLESH_BURNING
	getp[].i temp4
	getav[temp4].fire_damage temp5
	ifl temp5 1 sound ENGULF_FIRE
	add temp5 5
	setactorvar[temp4].fire_damage temp5
    palfrom 16 16
	}
add INTERNALCOUNT 1
ifg INTERNALCOUNT 140
	{
	stopsound MC_JETPAKIDLE
	soundonce MC_JETPAKOFF
	geta[].shade temp
	add temp 4
	seta[].shade temp
	ife temp 40 { shoot SPARK shoot SPARK shoot SPARK set INTERNALCOUNT 0 action BREAK }
	}
	else
	{ ifpdistl 16384 soundonce MC_JETPAKIDLE seta[].shade -16 }
}

ifaction BREAK
{
add INTERNALCOUNT 1
ifg INTERNALCOUNT 140
	{
	soundonce MC_JETPAKON
	geta[].shade temp
	sub temp 4
	seta[].shade temp
	ife temp -16 { set INTERNALCOUNT 0 action ZERO }
	}
	else
	seta[].shade 40
}

enda

// *************************************************
// CRACK
// *************************************************

useractor notenemy 4805

ifn HITAGSAVED 0
{
ifaction 0 cstat 32768
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	seta[].cstat CSTATSAVED
	action ZERO
	set HITAGSAVED 0
	}
}
else
ifspritepal 3
{
ifaction 0 cstat 32768
ifhitweapon
 ifwasweapon RADIUSEXPLOSION
	{
	seta[].cstat CSTATSAVED
	action ZERO
	}
}

enda

// *************************************************
// WOOD PILE
// *************************************************

useractor notenemy 19704

ifn HITAGSAVED 0
{
ifaction 0 cstat 32768
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	seta[].cstat CSTATSAVED
	action ZERO
	set HITAGSAVED 0
	}
}

enda

// *************************************************
// IDLE MECHSUITS
// *************************************************

// spriteshadow 19181

useractor notenemy 19181

ifaction 0
	{
	seta[].htflags 1
	sizeat 32 32
	strength 2000
	action FIVE_ANG
	}

ifaction FIVE_ANG
	{
	ifn LOTAGSAVED 0
		{
		checkactivatormotion LOTAGSAVED
		ife RETURN 1 cactor HPOWERSUIT
		}
	ifn HITAGSAVED 0
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			ife temp9 0 { resetcount set temp9 1 }
			soundonce BLOWUP
			sound RPG_EXPLODE
			set temp3 sprite[].x
			add temp3 256
			randvar temp4 2048
			rotatepoint sprite[].x sprite[].y temp3 sprite[].y temp4 temp5 temp6
			espawn BIG_EXPLOSION
			seta[RETURN].x temp5
			seta[RETURN].y temp6
			espawn BIG_SMOKE2
			seta[RETURN].x temp5
			seta[RETURN].y temp6
			set temp6 0
			readarrayfromfile PLOT_POINTS F_DATA_ARRAYS_AMCTC
			ifn PLOT_POINTS[LOTAGSAVED] 1
				{
				setarray PLOT_POINTS[LOTAGSAVED] 1
				writearraytofile PLOT_POINTS F_DATA_ARRAYS_AMCTC
				}
			ifcount 13
				{
				debris SCRAP1 40
				debris SCRAP2 32
				debris SCRAP3 32
				debris SCRAP4 32
				debris SCRAP5 32
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19177
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				ezshoot 4096 19172
				randvar temp 2048
				seta[RETURN].ang temp
				killit
				}
			}
		}
	}

enda

// BIPOD ACTOR =====================================================

useractor notenemy 3630
ifpdistl 1536
	{
	ifp pjumping
		{
		state lower_weapon
		xorvar gun_firemode 512
		soundonce PUT_ON_EQUIP
		killit
		}
	ifand gun_firemode 512 nullop else killit

	geta[].x mx
	geta[].y my
	getp[].posx x
	getp[].posy y
	sub mx x
	sub my y
	getangle angvar mx my
	setp[].ang angvar
	}
ifpdistg 1536
	{
	ifand gun_firemode 512
		{
		state lower_weapon
		xorvar gun_firemode 512
		soundonce PUT_ON_EQUIP
		}
	killit
	}
enda

// LOCKED DOOR HOLOGRAMS =====================================================

gamevar X_RATIO 0 2
gamevar Y_RATIO 0 2

gamevar XREPEAT 0 2
gamevar YREPEAT 0 2

defstate HOLO_FLICKER
	espawn 5627
	seta[RETURN].blend 255
	seta[RETURN].shade 30
	seta[RETURN].pal temp2
	seta[RETURN].cstat sprite[].cstat
	seta[RETURN].ang sprite[].ang
	geta[].xrepeat temp7
	mulvarvar temp7 X_RATIO
	divvar temp7 100
	seta[RETURN].xrepeat temp7

	geta[].yrepeat temp8
	mulvarvar temp8 Y_RATIO
	divvar temp8 100
	seta[RETURN].yrepeat temp8
	resetcount
ends

defstate HOLOGRAM_FLICKER
ifrnd 4 state HOLO_FLICKER
ifpdistl 1024 ifrnd 32 state HOLO_FLICKER
ends

useractor notenemy 19622

ifaction 0 { set temp2 2 set X_RATIO 54 set Y_RATIO 54 action ZERO } state HOLOGRAM_FLICKER

ifaction ZERO
{
ifn HITAGSAVED 0
	{
	sleeptime 0
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action MINUSTWO
	}
}

enda

useractor notenemy 19621

ifaction 0 { set temp2 7 set X_RATIO 54 set Y_RATIO 54 action ZERO } state HOLOGRAM_FLICKER

ifaction ZERO
{
ifn HITAGSAVED 0
	{
	sleeptime 0
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action MINUSONE
	}
}

enda

useractor notenemy 19626 // ladies holographic sign

ifaction 0
{
spritepal 8
ifpdistl 1536
 ifcansee
  ifp palive
	{
	ifn CHAR 12
	ifn CHAR 10
	ifn CHAR 17
	ifn CHAR 20
		{
		spritepal 2
		soundonce COMP_INVALID
		action ZERO
		resetcount
		}
	}
}

ifaction ZERO
{
ifcount 30
	{
	action 0
	resetcount
	}
}


enda

useractor notenemy 19627 // mens holographic sign

ifaction 0
{
spritepal 8
ifpdistl 1536
 ifcansee
  ifp palive
	{
	ife CHAR 10
		{
		spritepal 2
		soundonce COMP_INVALID
		action ZERO
		resetcount
		}
	ife CHAR 12
		{
		spritepal 2
		soundonce COMP_INVALID
		action ZERO
		resetcount
		}
	ife CHAR 17
		{
		spritepal 2
		soundonce COMP_INVALID
		action ZERO
		resetcount
		}
	ife CHAR 20
		{
		spritepal 2
		soundonce COMP_INVALID
		action ZERO
		resetcount
		}
	}
}

ifaction ZERO
{
ifcount 30
	{
	action 0
	resetcount
	}
}

enda

useractor notenemy 20111 // subway ticket sign

ifaction 0
{
spritepal 1
ifpdistl 1536
 ifcansee
  ifp palive
	{
	spritepal 2
	soundonce TICKET_PUNCH
	action ZERO
	resetcount
	}
}

ifaction ZERO
{
ifpdistg 1536
	{
	action 0
	resetcount
	}
}

enda


// LASER BEAM SPAWNER =====================================================

spritenoshade 19568
spritenopal 19568

useractor notenemy 19568

ifaction 0
	{
	seta[].blend 255
	ifn EXTRASAVED -1
		{
		ifl SKILL_LEVEL EXTRASAVED killit
		}
	action ZERO
	}

ifaction ZERO
	{
	ifn LOTAGSAVED 0
		{
		checkactivatormotion LOTAGSAVED
		ife RETURN 1 killit
		}
	}

enda

spritenoshade 19569
spritenopal 19569

useractor notenemy 19569

ifaction 0
	{
	sleeptime 0
	seta[].blend 255
	ifn EXTRASAVED -1
		{
		ifl SKILL_LEVEL EXTRASAVED killit
		}
	ifn LOTAGSAVED 0
		{
		checkactivatormotion LOTAGSAVED
		ife RETURN 1 killit
		}
	ifcount 3
	 ifpdistl 32767
		{
		eshoot 19570 geta[RETURN].z temp add temp 1024 seta[RETURN].z temp
		resetcount
		}
	}

enda

// INSTANT RESPAWN =====================================================

useractor notenemy INSTANT_RESPAWN
cstat 32768
sleeptime 0

ifaction 0
{
checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	ifn XVELSAVED 0
		{
		set INTERNALCOUNT 0
		action ONE
		}
	else
		{
		ifn HITAGSAVED 0 espawnvar HITAGSAVED
		ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
		ife EXTRASAVED 1 { globalsound SOMETHINGHITFORCE spawn TRANSPORTERBEAM }
		killit
		}
	}
}
else
ifaction ONE
{
add INTERNALCOUNT 1
ifge INTERNALCOUNT XVELSAVED
	{
	ifn HITAGSAVED 0 espawnvar HITAGSAVED
	ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
	ife EXTRASAVED 1 { globalsound SOMETHINGHITFORCE spawn TRANSPORTERBEAM }
	killit
	}
}

enda

useractor notenemy RESPAWN_SKILL3
ifl SKILL_LEVEL 3 killit
cstat 32768
sleeptime 0

checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	ifn HITAGSAVED 0 espawnvar HITAGSAVED
	ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
	ife EXTRASAVED 1 { globalsound SOMETHINGHITFORCE spawn TRANSPORTERBEAM }
	killit
	}

enda

useractor notenemy RESPAWN_SKILL4
ifl SKILL_LEVEL 4 killit
cstat 32768
sleeptime 0

checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	ifn HITAGSAVED 0 espawnvar HITAGSAVED
	ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
	ife EXTRASAVED 1 { globalsound SOMETHINGHITFORCE spawn TRANSPORTERBEAM }
	killit
	}

enda

useractor notenemy RESPAWN_SKILL5
ifl SKILL_LEVEL 5 killit
cstat 32768
sleeptime 0

checkactivatormotion LOTAGSAVED
ife RETURN 1
	{
	ifn HITAGSAVED 0 espawnvar HITAGSAVED
	ifn sprite[].pal 0 seta[RETURN].pal sprite[].pal
	ife EXTRASAVED 1 { globalsound SOMETHINGHITFORCE spawn TRANSPORTERBEAM }
	killit
	}

enda


// *************************************************
// SYSTEM ON OFF TEXT
// *************************************************

useractor notenemy 19575
ifaction 0
	{
	ifn HITAGSAVED 0 action ZERO
	}

ifaction ZERO
	{
	sleeptime 0
	checkactivatormotion HITAGSAVED
	 ife RETURN 1
		{
		soundonce RED_COMPBEEP
		action MINUSONE
		}
	}

enda

useractor notenemy 19574
ifaction 0
	{
	ifn HITAGSAVED 0 action ZERO
	}

ifaction ZERO
	{
	sleeptime 0
	checkactivatormotion HITAGSAVED
	 ife RETURN 1
		{
		soundonce RED_COMPBEEP
		action ONE
		}
	}

enda

// *************************************************
// RED CIVILIAN
// *************************************************

useractor notenemy 15455
ifaction 0
	{
	sleeptime 0
	checkactivatormotion HITAGSAVED
	ife RETURN 1 killit
	}
enda


// *************************************************
// COUNTER
// *************************************************

useractor notenemy 17636
ifaction 0
{
sleeptime 0
cstat 32768
ifn LOTAGSAVED 0 xorvar INTERNALCOUNT 1
ifn HITAGSAVED 0 xorvar INTERNALCOUNT 2
ifn XVELSAVED 0 xorvar INTERNALCOUNT 4
ifn YVELSAVED 0 xorvar INTERNALCOUNT 8
ifn ZVELSAVED 0 xorvar INTERNALCOUNT 16
ifspritepal 3 addlogvar INTERNALCOUNT
action ZERO
}

ifaction ZERO
{
sleeptime 0
ifand INTERNALCOUNT 1
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 { ifand INTERNALCOUNT_2 1 nullop else xorvar INTERNALCOUNT_2 1 }
	}
ifand INTERNALCOUNT 2
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 { ifand INTERNALCOUNT_2 2 nullop else xorvar INTERNALCOUNT_2 2 }
	}
ifand INTERNALCOUNT 4
	{
	checkactivatormotion XVELSAVED
	ife RETURN 1 { ifand INTERNALCOUNT_2 4 nullop else xorvar INTERNALCOUNT_2 4 }
	}
ifand INTERNALCOUNT 8
	{
	checkactivatormotion YVELSAVED
	ife RETURN 1 { ifand INTERNALCOUNT_2 8 nullop else xorvar INTERNALCOUNT_2 8 }
	}
ifand INTERNALCOUNT 16
	{
	checkactivatormotion ZVELSAVED
	ife RETURN 1 { ifand INTERNALCOUNT_2 16 nullop else xorvar INTERNALCOUNT_2 16 }
	}
ife INTERNALCOUNT_2 INTERNALCOUNT
	{
	operateactivators EXTRASAVED THISACTOR
	operaterespawns EXTRASAVED
	operatemasterswitches EXTRASAVED
	killit
	}
}

enda

// RAW CRYSTAL

spritenopal 3468
spritenoshade 3468
useractor notenemy 3468
	state MAKO_RING_ARTIFACT

cstator 1024

ifspritepal 11
	{
	ifpdistl 8192
		set near_crystal 30
	}

enda

// CRYSTAL

useractor notenemy 13875
	state MAKO_RING_ARTIFACT

cstator 1024

ifspritepal 11
	{
	ifpdistl 8192
		set near_crystal 30
	}

enda

// ===========================================================================
// SCI-FI FIGHTER JET
// ===========================================================================

useractor notenemy 21591

sleeptime 0
ifaction 0 { ifpdistg 32768 action ONE }
ifaction ONE { ifpdistl 32768 action 0 }

enda

// ===========================================================================
// COUCH
// ===========================================================================

useractor notenemy 21599

sleeptime 0
ifaction 0 { cstat 257 action ZERO }
ifaction ZERO { ifpdistg 8192 action FOUR }
ifaction FOUR { ifpdistl 8192 action ZERO }

enda


// ===========================================================================
// FLOOR SHOCK
// ===========================================================================

spritenoshade 18919

useractor notenemy 18919

ifaction 0
	{
	seta[].blend 255
	cstat 32768
	ifn HITAGSAVED 0 action BREAK
	else action ZERO
	}
ifaction BREAK
	{
	checkactivatormotion HITAGSAVED
	ife RETURN 1 action ZERO
	}
ifaction ZERO
	{
	ifrnd 1
		{
		seta[].cstat CSTATSAVED
		sound SPELL2FIRE
		shoot SPARK2
		shoot SPARK2
		shoot SPARK2
		shoot SPARK2
		shoot SPARK2
		action FORM
		resetcount
		}
	}
ifaction FORM
	{
	ifcount 4
		{
		cstat 32768
		action ZERO
		}
	}


enda

// *************************************************
// EGYPT STUFF
// *************************************************

useractor notenemy 16864

ifaction 0
{
seta[].blend 255
seta[].shade 30
ifand EGYPT_KEYS 1
	{
	seta[].shade -127
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	action ZERO
	}
}

enda

useractor notenemy 16866

ifaction 0
{
seta[].blend 255
seta[].shade 30
ifand EGYPT_KEYS 2
	{
	seta[].shade -127
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	action ZERO
	}
}

enda

useractor notenemy 16868

ifaction 0
{
seta[].blend 255
seta[].shade 30
ifand EGYPT_KEYS 4
	{
	seta[].shade -127
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	action ZERO
	}
}

enda

useractor notenemy 16870

ifaction 0
{
seta[].blend 255
seta[].shade 30
ifand EGYPT_KEYS 8
	{
	seta[].shade -127
	ifn HITAGSAVED 0
		{
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		}
	action ZERO
	}
}

enda

spritenoshade 16862

useractor notenemy 16862

ifaction 0
{
seta[].shade 10
ifand EGYPT_ARTIFACTS 4
	{
	action ZERO
	seta[].shade -20
	}
else
ifpdistl 1536
 ifp pfacing
  ifcansee
	{
	ifhitspace quote 6905
	}
}

ifaction ZERO
{
ifpdistl 1536
 ifp pfacing
  ifcansee
	{
	set player_use 0
		ifhitspace
			{
			set hit_key 30
			set PLAYER_VOICEOVER 2
			ifspritepal 3 sound CDOOR28
			operateactivators HITAGSAVED THISACTOR
			operaterespawns HITAGSAVED
			operatemasterswitches HITAGSAVED
			killit
			}
	}
}

enda

// *************************************************
// LIGHT BEAM
// *************************************************


useractor notenemy 7933

ifaction 0
	{
	seta[].mdflags 16
	ifrnd 128
		{
		ifrnd 128 set command 1024 else
		set command 2048
		}
	else
		{
		ifrnd 128 set command 128 else
		set command 256
		}
	action ZERO
	}

enda

// *************************************************
// MANDALA
// *************************************************


useractor notenemy 21223

ifaction 0
	{
	seta[].mdflags 16
	set command 128
	action ZERO
	}

enda

// *************************************************
// BUGATTI
// *************************************************

useractor notenemy 22452

ifspritepal 0
{
ifaction 0
	{
	ifand BASE_COSMETICS 1 action ZERO else killit
	}
}

enda

// *************************************************
// JEDRIK STATUE
// *************************************************

spriteshadow 29320

useractor notenemy 29320 action FIVE_ANG enda

// *************************************************
// GLOBE STATUE
// *************************************************

useractor notenemy 29306

ifspritepal 0
{
ifaction 0
	{
	ifand BASE_COSMETICS 4 action ZERO else killit
	}
}

enda

// *************************************************
// ALCHEMY TABLE
// *************************************************

useractor notenemy 29308
ifspritepal 3 // only appear in base if bought
	{
	ifaction 0
		{
		ife BASE_RESEARCH[8] 2 action ZERO else cstat 32768
		}
	}
else ifspritepal 9 // shop version
	{
	ifpdistl 1024
	 ifp pfacing
	  ifhitspace
	   ifcount 90
		{
		ifrnd 128 globalsound DSK_CHAT3
		else globalsound DSK_CHAT4
		resetcount
		}
	}
enda


// *************************************************
// SPIKES
// *************************************************

useractor notenemy 22490

ifaction 0
{
cstator 257
strength 100
action ZERO
}

ifpdistl 1024
{
ifp prunning
	{
	addphealth -3
	soundonce STAB_01
	palfrom 20 20
	getp[].ang temp5
	sub temp5 1024
	set knockbackang temp5
	set knockback 1
	}
}

ifhitweapon
 {
 ifwasweapon RADIUSEXPLOSION
	{
	debris SCRAP1 3
	debris SCRAP2 3
	debris SCRAP3 3
	sound BUST_METAL
	killit
	}
 }

enda

// *************************************************
// UNIVERSITY ADVERT
// *************************************************

spritenoshade 21617

defstate fade_to_black
ifl INTERNALCOUNT 40
	{
	geta[].shade temp
	ifg temp 0 sub temp 1
	seta[].shade temp
	}
ends



useractor notenemy 21617

ifhitweapon
	{
	shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
	sound SHORT_CIRCUIT sound GLASS_BREAKING
	stopsound UNI_ADEL
	soundonce STORE_MUSIC_BROKE
	ife CHAR 13
	 ife NOISE_SAID 0
		{
		setvar PLAYER_VOICEOVER 47
		set NOISE_SAID 1
		}
	lotsofglass 32
	ifrnd 128 cstator 4
	ifrnd 128 cstator 8
	action EIGHTEEN
	}

ifaction 0
{
ifspritepal 3 nullop else
ifpdistl 16384 soundonce UNI_ADEL
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 120
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action ONE
		}
	}
}
ifaction ONE
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action TWO
		}
	}
}
ifaction TWO
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action THREE
		}
	}
}
ifaction THREE
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 120
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action FOUR
		}
	}
}
ifaction FOUR
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action FIVE
		}
	}
}
ifaction FIVE
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action SIX
		}
	}
}
ifaction SIX
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action SEVEN
		}
	}
}
ifaction SEVEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 90
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action EIGHT
		}
	}
}
ifaction EIGHT
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 45
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action NINE
		}
	}
}
ifaction NINE
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 45
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action TEN
		}
	}
}
ifaction TEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 45
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action ELEVEN
		}
	}
}
ifaction ELEVEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 90
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action TWELVE
		}
	}
}
ifaction TWELVE
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action THIRTEEN
		}
	}
}
ifaction THIRTEEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 60
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action FOURTEEN
		}
	}
}
ifaction FOURTEEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 135
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action FIFTEEN
		}
	}
}
ifaction FIFTEEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 90
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action SIXTEEN
		}
	}
}
ifaction SIXTEEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 90
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		set INTERNALCOUNT 0
		action SEVENTEEN
		}
	}
}
ifaction SEVENTEEN
{
add INTERNALCOUNT 1
state fade_to_black
ifg INTERNALCOUNT 90
	{
	geta[].shade temp
	add temp 1
	seta[].shade temp
	ifg temp 30
		{
		stopsound UNI_ADEL
		set INTERNALCOUNT 0
		action 0
		}
	}
}


enda

// ======================================================================================================================
// LIGHT BUG - CODE By Dan 'DeeperThought' Gaskil
// ======================================================================================================================

spritenoshade 23731
spritenopal 23731

move BUGMOVE1 16 -16
move BUGMOVE2 16 16

useractor notenemy 23731 0

// if spawned as dragon projectile trail, stop existing after a few seconds
ifspawnedby DRAGON_REGEN_PROJECTILE
{
	add INTERNALCOUNT 1
	ifg INTERNALCOUNT 270
		killit
}


ifmove 0
{
	seta[].blend 255
	seta[].shade -127
	ife LOTAGSAVED 2
	{
		geta[].sectnum mysector
		seta[].htactorstayput mysector
	}
	action ZERO
	cstat 2
	sizeat 32 32
	getactor[THISACTOR].x initx
	getactor[THISACTOR].y inity
	getactor[THISACTOR].z initz
	move BUGMOVE1 geth getv
}

addvar INTERNALCOUNT 1
ifvarg INTERNALCOUNT 300 // will reset to its starting position if it's been longer than 10 seconds and the player cannot see it
{
	setvar INTERNALCOUNT 0
	ifcansee break
	setsprite THISACTOR initx inity initz
}


sleeptime 0

ifcount 52 ifrnd 64
{
	ifpdistl 1560 move BUGMOVE1 geth getv furthestdir
	else
	iffloordistl 8 move BUGMOVE1 geth getv randomangle else
	ifceilingdistl 16 move BUGMOVE2 geth getv randomangle
	else
	ifrnd 128 move BUGMOVE1 geth getv randomangle
	else
	move BUGMOVE2 geth getv randomangle
	ifrnd 128 set mtype 1 else set mtype 0
}

geta[].ang temp
ife mtype 0 add temp 16 else sub temp 16
seta[].ang temp
enda

// ===================================================================================================
// CYCLOID EGG SPAWNER
// ======================================================================================================================

useractor notenemy 23447

ifaction 0
{
sizeat 20 20
cstat 257
ifrnd 160 { strength 5 action ONE }
else { strength TOUGH action ZERO }
}

move FLYSLOW geth
ifnotmoving killit
// ifg sprite[].z sector[].floorz killit

    ifhitweapon
    {
	guts JIBS6 1
	ifdead
		{
		state squish_sounds
		spritepal 7
        state standard_jibs
        killit
		}
    }

enda

useractor notenemy 23448

ifaction 0
	{
	cstat 32768
	action ONE
	resetcount
	}

ifaction ONE
	{
	ifcount 70
		{
		ifrnd 128 spawn 23447
		resetcount
		}
	}

enda

// =================================================================================================================================
// egg scanner stuff
// =================================================================================================================================

action ERROR -3818

gamearray EGG_SCANNER 8

useractor notenemy 23449
ifaction 0
{
ifspritepal 64 break
cstat 32768
ife EGG_SCANNER[LOTAGSAVED] 1 { sound RED_COMPBEEP setactorsoundpitch THISACTOR RED_COMPBEEP 512 action ZERO }
else ife EGG_SCANNER[LOTAGSAVED] 2 { sound RED_COMPBEEP action ONE }
else ife EGG_SCANNER[LOTAGSAVED] 3 { sound RED_COMPBEEP action ERROR }
}

ifaction ZERO
{
spritepal 0
cstat 514
ife EGG_SCANNER[LOTAGSAVED] 0 action 0
}

ifaction ONE
{
spritepal 2
cstat 514
ife EGG_SCANNER[LOTAGSAVED] 0 action 0
}

ifaction ERROR
{
spritepal 2
cstat 514
ife EGG_SCANNER[LOTAGSAVED] 0 action 0
}

enda

useractor notenemy 23450

ifaction 0
{
cstat 32768
action ZERO
}

ifaction ZERO
{
ifcount 8
	{
	findnearactor3d 23447 512 temp2
	ifn temp2 -1
		{
		ife sprite[temp2].htdispicnum 23447
			{
			ifspritepal 0 setarray EGG_SCANNER[0] 1
			ifspritepal 1 setarray EGG_SCANNER[1] 1
			ifspritepal 2 setarray EGG_SCANNER[2] 1
			ifspritepal 3 setarray EGG_SCANNER[3] 1
			}
		else ife sprite[temp2].htdispicnum 23448
			{
			ifspritepal 0 setarray EGG_SCANNER[0] 2
			ifspritepal 1 setarray EGG_SCANNER[1] 2
			ifspritepal 2 setarray EGG_SCANNER[2] 2
			ifspritepal 3 setarray EGG_SCANNER[3] 2
			}
		}
	else ife temp2 -1
			{
			ifpdistl 512
				{
				ifspritepal 0 setarray EGG_SCANNER[0] 3
				ifspritepal 1 setarray EGG_SCANNER[1] 3
				ifspritepal 2 setarray EGG_SCANNER[2] 3
				ifspritepal 3 setarray EGG_SCANNER[3] 3
				}
			else
				{
				ifspritepal 0 setarray EGG_SCANNER[0] 0
				ifspritepal 1 setarray EGG_SCANNER[1] 0
				ifspritepal 2 setarray EGG_SCANNER[2] 0
				ifspritepal 3 setarray EGG_SCANNER[3] 0
				}
			}
	}
}

enda

// =================================================================================================================================
// Egypt key scanner
// =================================================================================================================================

spritenoshade 23738

useractor notenemy 23738

ifaction 0
{
seta[].blend 255
seta[].shade 30
ife EGYPT_KEYS 15
	{
	seta[].shade -127
	action ZERO
	}
}
ifaction ZERO
{
geta[].ang temp2
ifspritepal 9 sub temp2 2 else add temp2 2
seta[].ang temp2
ifpdistl 2048
ifspritepal 3
 ifn global_trigger 1
	{
	operateactivators 988 THISACTOR
	state fade_out_white
	quake 52
	globalsound USE_PORTAL
	stopallmusic
	set global_trigger 1
	}
}

enda

spritenoshade 23742

useractor notenemy 23742

ifaction 0
{
seta[].blend 255
seta[].shade 30
ife global_trigger 1
	{
	seta[].shade -127
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	shoot SPARK2
	action ZERO
	}
}
ifaction ZERO
{
ifpdistl 16384 soundonce PORTAL_AMB2
ifpdistl 2048
 ifspritepal 3
	{
	operateactivators 989 THISACTOR
	globalsound LONGD_TELEPORT
	killit
	}
}

enda

// ======================================================================================================================
// REAPER DRONE
// ======================================================================================================================

useractor notenemy 23780

ifaction 0
{
ifspritepal 3 action OK else
	{
	geta[].x temp
	geta[].y temp2
	angoff 512
	action ZERO
	}
}

ifaction ZERO
{

	// geta[].ang temp9
	set temp3 temp
	add temp3 2048

	rotatepoint temp temp2 temp3 temp2 temp9 temp5 temp6

	// add temp9 512
	ifg temp9 2048 sub temp9 2048
	seta[].ang temp9
	seta[].x temp5
	seta[].y temp6


	add temp9 2
}

enda

// *************************************************
// METAL BARRIER
// *************************************************

useractor notenemy 3481

ifaction 0
{
cstator 257
strength 300
action ZERO
}

ifaction ZERO
ifspritepal 3
{
findnearactor MADRAX 1024 temp
ifn temp -1
	{
	debris SCRAP1 12
	debris SCRAP2 12
	sound BUST_METAL
	sound VENT_BUST
	killit
	}
}

ifhitweapon
{
ifdead
	{
	debris SCRAP1 12
	debris SCRAP2 12
	sound BUST_METAL
	sound VENT_BUST
	cactor 3482
	}
}

enda

useractor notenemy 3482

ifaction 0
{
ifspritepal 5 break
cstator 257
strength 300
action ZERO
}

ifaction ZERO
ifspritepal 3
{
findnearactor MADRAX 1024 temp
ifn temp -1
	{
	debris SCRAP1 12
	debris SCRAP2 12
	sound BUST_METAL
	sound VENT_BUST
	killit
	}
}

ifhitweapon
{
ifdead
	{
	debris SCRAP1 12
	debris SCRAP2 12
	sound BUST_METAL
	sound VENT_BUST
	killit
	}
}

enda

// *************************************************
// TRAIN
// *************************************************

useractor notenemy 15
ifaction 0
{
setsector[].extra 0
spriteflags 163840
ife LOTAGSAVED 0 set LOTAGSAVED 5
cstat 32768
action ZERO
}

ifaction ZERO
{

	getsector[].extra temp
	ifn temp 0
	{
	ifpdistl 16384 { ifactorsound THISACTOR SUBWAY_S nullop else sound SUBWAY_S }
	setactorsoundpitch THISACTOR SUBWAY_S temp
	ifg temp 0 sub temp 2
	ifl temp 0 add temp 2
	ife temp 30 { stopsound SUBWAY_S soundonce SUBTRAIN_STOP }
	setsector[].extra temp
	}

ifpdistl 2048
 ifp palive
  ife player_using_train -1
	{
	set player_use 0
	 ifhitspace
	  ifcount 13
		{
		sound TRAIN_ACTIVATE
		set hit_key 30
		set hc_gear 0
		set player_using_train 0
		action FORM
		resetcount
		}
	}
}

ifaction FORM
{
		setp[].cursectnum sprite[].sectnum
		setp[].posx sprite[].x
		setp[].posy sprite[].y

ife MISSION_UP 0
ifand EXTBITS_PRESS 1
 ifcount 13
  ifl hc_gear LOTAGSAVED
	{
	sound TRAIN_GEAR
	add hc_gear 1
	resetcount
	}

ife MISSION_UP 0
ifand EXTBITS_PRESS 2
 ifcount 13
  ifg hc_gear -1
	{
	sound TRAIN_GEAR
	sub hc_gear 1
	resetcount
	}

getsector[].extra temp

ifn hc_gear 0
{
ifactorsound THISACTOR SUBWAY_S nullop else sound SUBWAY_S
setactorsoundpitch THISACTOR SUBWAY_S temp
}

ife hc_gear -1
	{
	ifg temp -64 sub temp 2
	ife temp 32 { stopsound SUBWAY_S soundonce SUBTRAIN_STOP }
	sets[].extra temp
	}
ife hc_gear 0
	{
	ifg temp 0 sub temp 2
	ifl temp 0 add temp 2
	ife temp 30 { stopsound SUBWAY_S soundonce SUBTRAIN_STOP }
	setsector[].extra temp
	}
else ife hc_gear 1
	{
	ifl temp 32 add temp 2
	ifl temp 32 { stopsound SUBWAY_S soundonce SUBTRAIN_START }
	ifg temp 32 sub temp 2
	sets[].extra temp
	}
else ife hc_gear 2
	{
	ifl temp 64 add temp 2
	ifg temp 64 sub temp 2
	sets[].extra temp
	}
else ife hc_gear 3
	{
	ifl temp 128 add temp 2
	ifg temp 128 sub temp 2
	sets[].extra temp
	}
else ife hc_gear 4
	{
	ifl temp 256 add temp 2
	ifg temp 256 sub temp 2
	sets[].extra temp
	}
else ife hc_gear 5
	{
	ifl temp 512 add temp 2
	sets[].extra temp
	}

clamp temp -64 512

clamp hc_gear -1 5


ifpdistl 2048
 ifp palive
  ife player_using_train 0
	{
	 ifhitspace
	  ifcount 13
		{
		set hc_gear 0
		sound TRAIN_ACTIVATE
		set player_using_train -1
		action ZERO
		resetcount
		}
	}
}

enda

// *************************************************
// CHAOSPHERE
// *************************************************

useractor notenemy 21213

ifaction 0
	{
	ifspritepal 3 nullop
	else
		{
		ifand BEAT_EPISODES[3] 262144 nullop else
		killit
		}
	action ZERO
	}

enda

// *************************************************
// AIRSTRIKE STUFF
// *************************************************

useractor notenemy REAPER_CONTROLBOX
ifaction 0
{
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	action ZERO
	}
}

ifaction ZERO
{
ifpdistl 1536
 ifp pfacing
	  {
	  set player_use 0
	   ifhitspace
			{
			set hit_key 30
			globalsound RED_COMPBEEP
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			action FORMED
			}
	  }
}

enda

useractor notenemy ASTRIKE_PLACE_HERE

ifaction 0
	{
	sizeat 30 30
	cstat 32768
	action MINUSONE
	}

ifaction MINUSONE
	{
	ifpdistl 8192
	 ifg AIRSTRIKE_MARKER 0
	 {
	 cstat 514
	 spritepal 2
	 }
	 else
	 cstat 32768

	ifpdistl 1536
	 ifp pfacing
		 {
		 set player_use 0
		  ifhitspace
		   ifcount 13
			  {
			  set hit_key 30
			  ife AIRSTRIKE_MARKER 0 quote 185
			  else ifg AIRSTRIKE_MARKER 0
					{
					state lower_weapon
					set temp2 0
					sizeat 24 24
					globalsound C4_ATTACH
					set PLAYER_VOICEOVER 24
					sub AIRSTRIKE_MARKER 1
					cstat 0
					spritepal 0
					action FORMED
					ifn HITAGSAVED 0
						{
						operateactivators HITAGSAVED THISACTOR
						operaterespawns HITAGSAVED
						operatemasterswitches HITAGSAVED
						}
					}
			  resetcount
			  }
		}
	}

ifaction FORMED
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 killit
	}


enda

// *************************************************
// HIDE WHEN SKYBOX IS ON
// *************************************************

useractor notenemy 22722

ife skybox 0 { seta[].cstat CSTATSAVED } else cstat 32768

enda

// *************************************************
// VELVET ROPE
// *************************************************

useractor notenemy 18349
ifaction 0
	{
	ife LOTAGSAVED 0
		{
		ifpdistl 1024
		 ifp prunning
			{
			sound VELV_SNAP
			cstat 32
			randvar temp 2048
			setactor[].z sector[].floorz
			seta[].ang temp
			action ZERO
			}
		ifcount 30
			{
			findnearactor SHOOTME2 1536 temp
			ifn temp -1
				{
				sound VELV_SNAP
				cstat 32
				randvar temp 2048
				setactor[].z sector[].floorz
				seta[].ang temp
				action ZERO
				}
			resetcount
			}
		}
	}
enda

// *************************************************
// RADIO CONTACT - will expand over time
// *************************************************

useractor notenemy 3246
cstat 32768
ifspritepal 0 set RADIO_CONTACT 1 else ifspritepal 2 { set RADIO_CONTACT 0 set airdrop_available 0 }
ife LOTAGSAVED 1
	{
	ife FACILITIES_OWNED[10] 2 set airdrop_available 1
	else set airdrop_available 0
	}
ife LOTAGSAVED 2
	{
	ife FACILITIES_OWNED[11] 2 set airdrop_available 1
	else set airdrop_available 0
	}
ife LOTAGSAVED 3
	{
	ife FACILITIES_OWNED[14] 2 set airdrop_available 1
	else set airdrop_available 0
	}
ife HITAGSAVED 1 { ife BASE_RESEARCH[7] 2 nullop else set airdrop_available 0 }
enda

// *************************************************
// Shrink grate
// *************************************************

useractor notenemy 23558

ifaction 0
	{
	ifspritepal 4 break
	ifand sprite[].cstat 32
		{
		ifp pshrunk cstat 32 else cstat 33
		}
		else
		action ZERO
	}

enda

// *************************************************
// Claw marks
// *************************************************

spritenoshade 27333
spritenopal 27333

spritenoshade 27334
spritenopal 27334

defstate claw_marks
ifaction 0
{
geta[].pal PALSAVED
geta[].shade SHADESAVED
action ZERO
}

ifaction ZERO
{
ife CHAR 1 nullop else
ife CHAR 17 nullop else
break

ifp pfacing
 ifpdistl 4096
	{
	spritepal 107
	seta[].shade -127
	}
	else
	{
	seta[].pal PALSAVED
	seta[].shade SHADESAVED
	}
}

ends

useractor notenemy 27333 state claw_marks enda
useractor notenemy 27334 state claw_marks enda

// ================================================================================================
// DUMMIES FOR AIR SHIP
// ================================================================================================

action DUMMY_PIG_WALK -25 4 5 1 20

defstate PRISON_ENEMIES_STUFF
ifaction 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		action ZERO
		resetcount
		}
	}
else
ifaction ZERO
{
ifcount 180 killit
}
ends

useractor notenemy 1685

ifaction 0 action FIVE_ANG

ifaction FIVE_ANG
	{
	sleeptime 0
	ifpdistl 4096 ifrnd 2 sound PRED_ROAM
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		action ZERO
		resetcount
		}
	}
else
ifaction ZERO
{
ifcount 180 killit
}

enda

useractor notenemy 1749

ifaction 0 action FIVE_ANG

ifaction FIVE_ANG
	{
	sleeptime 0
	ifpdistl 4096 ifrnd 2 sound PRED_ROAM2
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		action ZERO
		resetcount
		}
	}
else
ifaction ZERO
{
ifcount 180 killit
}

enda

useractor notenemy 1760 state PRISON_ENEMIES_STUFF enda
useractor notenemy 1935 state PRISON_ENEMIES_STUFF enda
useractor notenemy 1942 state PRISON_ENEMIES_STUFF enda
useractor notenemy 2049 state PRISON_ENEMIES_STUFF enda

spriteshadow 2025

move PIG2WALKVELS 72

useractor notenemy 2025
ifaction 0 action FIVE_ANG

ifaction FIVE_ANG
	{
	sleeptime 0
	ifpdistl 4096 ifrnd 2 soundonce PIG_ROAM3
	checkactivatormotion LOTAGSAVED

		ife RETURN 1
			{
			globalsound PIG_RECOG
			action DUMMY_PIG_WALK
			set INTERNALCOUNT 0
			}

	ifg INTERNALCOUNT 70
		{
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 160 { action 0 spawn PIGCOP killit }
		}

	}

ifaction DUMMY_PIG_WALK
	{
	add INTERNALCOUNT 1
	move PIG2WALKVELS geth
	ifg INTERNALCOUNT 74
		{
		globalsound PIG_ROAM4
		action FIVE_ANG
		move STOP
		}
	}

enda

spriteshadow 7492

action DUMMY_SHADE_FIRE    0 3 5 1 25

useractor notenemy 7492

ifaction 0
	{
	cstator 257
	sound SHADE_ALERT
	spritepal 0
	strength 50
	sizeat 24 24
	geta[].ang temp6
	action FIVE_ANG
	set INTERNALCOUNT 0
	}

ifaction FIVE_ANG
	{
	ife INTERNALCOUNT 0 action DUMMY_SHADE_FIRE
	else ifg INTERNALCOUNT 0
		{
		ife camerasprite -1
			{
			add INTERNALCOUNT 1
			ifg INTERNALCOUNT 10 { action 0 cactor SHADE }
			}
		}

	}

ifaction DUMMY_SHADE_FIRE
{
cstat 257
	ifactioncount 3
	{
		sound DEMON_THROWFIRE
		eshoot 7526
		seta[RETURN].ang temp6
		action FIVE_ANG
		set INTERNALCOUNT 1
	}
}


enda

// HAYSTACK

useractor notenemy 25344
ifspritepal 3 nullop
else
{
cstat 0
ifp phigher
 ifpdistl 2048
	{
	wackplayer
	sound LAND_LEAF
	setp[].falling_counter 0
	setp[].poszv 512
	espawn 25283
	seta[RETURN].cstat 32
	randvar temp 2048
	seta[RETURN].ang temp
	seta[RETURN].xrepeat 32
	seta[RETURN].yrepeat 32
	killit
	}
}

enda

useractor notenemy 18653 cstator 1024 enda
useractor notenemy 18654 cstator 1024 enda

// SAIL ====================================================================

useractor notenemy 16311
ifn LOTAGSAVED 0
{
ifaction 0 { geta[].cstat CSTATSAVED cstat 32768 action ZERO }
ifaction ZERO
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1
		{
		seta[].cstat CSTATSAVED
		seta[].z sector[].floorz
		action FORM
		}
	}
}

ifspritepal 9 seta[].z sector[].floorz

enda


// NECROMANCER NAALDIR SPOOK ====================================================================

spritenoshade 25497
spritenopal 25497

defstate necro_curse
	sound P_CURSED
	palfrom 20 40 0 40
	ife MYSTICAL_RESEARCH[4] 2 // Siren Ward?
		{
		ifl SKILL_LEVEL 2 set CURSED 120
		else ife SKILL_LEVEL 2 set CURSED 210
		else ife SKILL_LEVEL 3 set CURSED 252
		else ife SKILL_LEVEL 4 set CURSED 336
		else ife SKILL_LEVEL 5 set CURSED 420
		}
	else
		{
		ifl SKILL_LEVEL 2 set CURSED 180
		else ife SKILL_LEVEL 2 set CURSED 300
		else ife SKILL_LEVEL 3 set CURSED 360
		else ife SKILL_LEVEL 4 set CURSED 480
		else ife SKILL_LEVEL 5 set CURSED 600
		}
	ife CHAR 4 ifg PARMOUR 0 div CURSED 2
	// necromancer resurrection charges
	ifl SKILL_LEVEL 2 set necro_ress 3
	else ife SKILL_LEVEL 2 set necro_ress 5
	else ife SKILL_LEVEL 3 set necro_ress 7
	else ife SKILL_LEVEL 4 set necro_ress 9
	else ife SKILL_LEVEL 5 set necro_ress 12
ends

defstate naaldir_spirit
ifn LOTAGSAVED 0
 ife HITAGSAVED 0
	{
	checkactivatormotion LOTAGSAVED
	ife RETURN 1 killit
	}
ifn HITAGSAVED 0
	{
	cstat 32768
	checkactivatormotion HITAGSAVED
	ife RETURN 1
		{
		seta[].cstat CSTATSAVED
		ifspritepal 5
			{
			quake 52
			flash
			globalsound MIND_ATTACK
			set necro_ress -1
			}
		set HITAGSAVED 0
		}
	}
ifspritepal 3
 ife HITAGSAVED 0
	{
	ifaction 0
		{
		cstat 32768
		ifpdistl 4096
		 ifcansee
			{
			quake 6
			flash
			globalsound MIND_ATTACK
			seta[].cstat CSTATSAVED
			action FORM
			}
		}
	else
	ifaction FORM
		{
		add INTERNALCOUNT 1
		ifg INTERNALCOUNT 20
			{
			quake 26
			state necro_curse
			globalsound NMIND_LAUGH
			spawn SKULLEXPLOSION
			shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
			shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2 shoot SPARK2
			state detectcombat
			ife RETURN 0 spawn LOST else
			spawn VOID_TOTEM
			killit
			}
		}
	}
ends

useractor notenemy 25489
state naaldir_spirit
enda

useractor notenemy 25497
state naaldir_spirit
enda

// PELINAL SWORD ====================================================================

useractor notenemy 29696
ifpdistl 1024
 ifp palive
  ifp pfacing
	{
	set player_use 0
		ifhitspace
		{
		ifspritepal 0
			{
			ife rendmode 0
				{
				setp[].weapon_pos 10
				sound DRAW_SWORD2
				}
			else
			startcutscene 1325
			}
		operateactivators LOTAGSAVED THISACTOR
		operaterespawns LOTAGSAVED
		operatemasterswitches LOTAGSAVED
		killit
		}
	}
enda

// MUSIC SUPPRESS ====================================================================

useractor notenemy 11457
cstat 32768

ifaction 0
	{
	sleeptime 0
	ifpdistl 32767
		{
		dist xydist THISACTOR player[].i
		div xydist 256
		// sub xydist 127
		clamp xydist 0 100
		setmusicvolume xydist
		}
	}

enda

// BERSERKER SUIT ====================================================================

useractor notenemy 23905

ifaction 0 action FIVE_ANG

ifaction FIVE_ANG
ifpdistl 1536
 ife CHAR 2
  ifp palive
   ifp pfacing
	{
	set player_use 0
		ifhitspace
			{
			setp[].shield_amount 100
			state fade_out_black
			sound PUT_ON_EQUIP
			set just_changed 1
			orvar CHAR_APP 4
			startcutscene 1326
			action FORM
			}
	}

ifaction FORM
{
cstat 32768
setplayer[].weapon_pos 10
add INTERNALCOUNT 1
ifg INTERNALCOUNT 20
	{
	operateactivators 535 THISACTOR
	operaterespawns 535
	starttrackslot 2 60
	set char_sel_anim 30
	killit
	}
}

enda

// EDF cargo mule ====================================================================

spriteshadow 29505

useractor notenemy 29505
ifaction 0
	{
	cstat 257
	sizeat 40 40
	action FIVE_ANG
	}


ifaction FIVE_ANG
	{
	ifg OWNERSAVED 0
		{
		sleeptime 0
		checkactivatormotion OWNERSAVED
		ife RETURN 1
			{
			sound SWIPE_UNLOCK
			set OWNERSAVED 0
			}
		}

	   ifp palive
		 ifpdistl RETRIEVEDISTANCE // quote check ok
		  ifp pfacing
		   ife MOUSEUP 0
		    ife hit_key 0
			  {
			set player_use 0
			ifhitspace
				{
				set hit_key 30
				ifg OWNERSAVED 0
					{
					set door_locked 0
					set key_icon 12527
					soundonce RED_COMPBEEP
					}
				else
					{
					ifrnd 64
						{
						ifand NOISE_SAID 32 nullop else
							{
							set PLAYER_VOICEOVER 48
							xorvar NOISE_SAID 32
							}
						}
					soundonce PALACE_DOOR1
					set MOUSEUP 1
					getp[].ang PANG
					getp[].horiz PHORIZ
					set CHECKBOX THISACTOR
					geta[].pal temp5
					set CBOXSLOT1PAL temp5
					ifn LOTAGSAVED 0 set CBOXSLOT1 LOTAGSAVED
					ifn HITAGSAVED 0 set CBOXSLOT2 HITAGSAVED
					ifn EXTRASAVED 0 ifn EXTRASAVED -1 set CBOXSLOT3 EXTRASAVED
					ifn XVELSAVED 0 set CBOXSLOT4 XVELSAVED
					ifn YVELSAVED 0 set CBOXSLOT5 YVELSAVED
					ifn ZVELSAVED 0 set CBOXSLOT6 ZVELSAVED
					}
				}
			}
	}


ife CHECKBOX THISACTOR
{
ife MISSION_UP 1 state reset_PDA
set temp9 1

ifg MOUSEX 232
 ifl MOUSEX 243
  ifg MOUSEY 28
   ifl MOUSEY 41
	{
	ifand BITS_PRESS 4
		{
		soundonce PALACE_DOOR2
		set MOUSEUP 0
		set CHECKBOX -1
		}
	}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 46
	   ifl MOUSEY 90
		{
		ifand BITS_PRESS 4
		 ifn LOTAGSAVED 0
			set SELSLOT 1
		}


	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 46
	   ifl MOUSEY 90
		{
		ifand BITS_PRESS 4
		 ifn HITAGSAVED 0
			set SELSLOT 2
		}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 80
	   ifl MOUSEY 116
		{
		ifand BITS_PRESS 4
		 ifn EXTRASAVED 0
			set SELSLOT 3
		}

	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 80
	   ifl MOUSEY 116
		{
		ifand BITS_PRESS 4
		 ifn XVELSAVED 0
			set SELSLOT 4
		}

	ifg MOUSEX 156
	 ifl MOUSEX 190
	  ifg MOUSEY 117
	   ifl MOUSEY 152
		{
		ifand BITS_PRESS 4
		 ifn YVELSAVED 0
			set SELSLOT 5
		}

	ifg MOUSEX 194
	 ifl MOUSEX 228
	  ifg MOUSEY 117
	   ifl MOUSEY 152
		{
		ifand BITS_PRESS 4
		 ifn ZVELSAVED 0
			set SELSLOT 6

		}

	ife SELSLOT 1
	{
	ifn LOTAGSAVED 0 { espawnvar LOTAGSAVED state grab_item_sound }

	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set LOTAGSAVED 0
	set CBOXSLOT1 -1
	set SELSLOT 0
	}

	ife SELSLOT 2
	{
	ifn HITAGSAVED 0 { espawnvar HITAGSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set HITAGSAVED 0
	set CBOXSLOT2 -1
	set SELSLOT 0
	}

	ife SELSLOT 3
	{
	ifn EXTRASAVED 0 ifn EXTRASAVED -1 { espawnvar EXTRASAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set EXTRASAVED 0
	set CBOXSLOT3 -1
	set SELSLOT 0
	}

	ife SELSLOT 4
	{
	ifn XVELSAVED 0 { espawnvar XVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set XVELSAVED 0
	set CBOXSLOT4 -1
	set SELSLOT 0
	}

	ife SELSLOT 5
	{
	ifn YVELSAVED 0 { espawnvar YVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set YVELSAVED 0
	set CBOXSLOT5 -1
	set SELSLOT 0
	}

	ife SELSLOT 6
	{
	ifn ZVELSAVED 0 { espawnvar ZVELSAVED state grab_item_sound }
	geta[RETURN].z temp
	sub temp -1024
	seta[RETURN].z temp
	seta[RETURN].x player[].posx
	seta[RETURN].y player[].posy
	set ZVELSAVED 0
	set CBOXSLOT6 -1
	set SELSLOT 0
	}

	   ifp palive
		 ifpdistl 4096
		  ife hit_key 0
		   ife CHECKBOX THISACTOR
		   ifhitspace
			{
			set hit_key 30
			soundonce PALACE_DOOR2
			set MOUSEUP 0
			set CHECKBOX -1
			}

	ifp pjumping
	{
	soundonce PALACE_DOOR2
	set MOUSEUP 0
	set CHECKBOX -1
	}

	ifpdistg RETRIEVEDISTANCE
	{
	soundonce PALACE_DOOR2
	set MOUSEUP 0
	set CHECKBOX -1
	}
}

enda

// WALL TRAP ====================================================================

action FACE_TRAP_OPEN 0 4 1 1 20
action FACE_TRAP_OPENED 3


useractor notenemy 16282

ifaction 0
	{
	sizeat 36 36
	geta[].shade SHADESAVED
	geta[].pal PALSAVED
	action ZERO
	}

ifaction ZERO
	{
	seta[].shade SHADESAVED
	seta[].pal PALSAVED
	ifcount 60 action FACE_TRAP_OPEN
	}

ifaction FACE_TRAP_OPEN
	{
	ifactioncount 4 action FACE_TRAP_OPENED
	}

ifaction FACE_TRAP_OPENED
	{
	ifactioncount 3
		{
		sound GUARDIAN_FLAME
		setprojectile[GORO_FLAME].workslike 36866
		setprojectile[GORO_FLAME].OFFSET 14354
		ezshoot temp3 GORO_FLAME geta[RETURN].z temp add temp 6144 seta[RETURN].z temp
		setprojectile[GORO_FLAME].OFFSET 224
		setprojectile[GORO_FLAME].workslike 32770
		add ally_mag 1
		resetactioncount
		}
	ifge ally_mag 5
		{
		action ZERO
		set ally_mag 0
		resetcount
		}
	}

enda

// CAMP CONTROLLER ====================================================================

useractor notenemy 25531
cstat 32768
ifaction 0
	{
	addlogvar ELYSION_QUEST
	ifand ELYSION_QUEST 64 // Micky quest - done
		{
		operateactivators 20 THISACTOR
		operaterespawns 20
		operatemasterswitches 20
		}
	ifand ELYSION_QUEST 256 // Sang quest - done
		{
		operateactivators 10 THISACTOR
		operaterespawns 10
		operatemasterswitches 10
		}
	ifand ELYSION_QUEST 512 // Rusty quest - done
		{
		operateactivators 30 THISACTOR
		operaterespawns 30
		operatemasterswitches 30
		}
	ifand ELYSION_QUEST 2048 // Snowfall quest - done
		{
		operateactivators 40 THISACTOR
		operaterespawns 40
		operatemasterswitches 40
		}
	ifand ELYSION_QUEST 4096 // James quest - done
		{
		operateactivators 50 THISACTOR
		operaterespawns 50
		operatemasterswitches 50
		}
	ifand ELYSION_QUEST 16384 // Kagura Quest - done
		{
		operateactivators 60 THISACTOR
		operaterespawns 60
		operatemasterswitches 60
		}
	ifand ELYSION_QUEST 65536 // Zaxtor Quest - done
		{
		operateactivators 70 THISACTOR
		operaterespawns 70
		operatemasterswitches 70
		}
	ifand ELYSION_QUEST 131072 // Merlijn Quest - done
		{
		operateactivators 80 THISACTOR
		operaterespawns 80
		operatemasterswitches 80
		}
	ifand ELYSION_QUEST 524288 // Mikko quest - done
		{
		operateactivators 90 THISACTOR
		operaterespawns 90
		operatemasterswitches 90
		}
	killit
	}
enda

// ENEMY TARGET ==============================================================================================================================================

useractor notenemy ENEMY_TARGET

ifaction 0
	{
	cstat 32768
	ifg EXTRASAVED 0 seta[].extra EXTRASAVED else strength 500
	ife LOTAGSAVED 0 set LOTAGSAVED 99
	spawn SHOOTME2
	action ONE
	}


ifaction ONE
	{
	cstat 257
	ifhitweapon
		{
		ifspritepal 1
			{
			shoot SWOODSCRAP
			sound HITWOOD
			}
		}

	ifdead
		{
		ifspritepal 1
			{
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			shoot WOODSCRAP
			quake 26
			sound WOOD_DEBRIS
			}
		operateactivators HITAGSAVED THISACTOR
		operaterespawns HITAGSAVED
		operatemasterswitches HITAGSAVED
		killit
		}

	}

enda

// Ringworld false moon

spritenopal 26023
useractor 4 26023

ifspritepal 3
	{
	ifand ELYSION_QUEST 64 cstat 2048 else cstat 32768
	}
else ifn HITAGSAVED 0
	{
	ifaction 0 { geta[].cstat CSTATSAVED cstat 32768 action ZERO }
	ifaction ZERO
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			seta[].cstat CSTATSAVED
			set HITAGSAVED 0
			}
		}
	}

enda

useractor 4 17924

 ifn HITAGSAVED 0
	{
	ifaction 0 { geta[].cstat CSTATSAVED cstat 32768 action ZERO }
	ifaction ZERO
		{
		checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			seta[].cstat CSTATSAVED
			set HITAGSAVED 0
			}
		}
	}

enda

// hologram stuff

useractor 4 29333 enda
useractor 4 29334 enda

useractor 4 23745 enda

// Ringworld travel dots

defstate travel_dot
ifaction 0
{
cstat 32768
sizeat 2 2
checkactivatormotion HITAGSAVED
ife RETURN 1
	{
	set INTERNALCOUNT 1
	action ZERO
	}
}
ifaction ZERO
{
add INTERNALCOUNT 1
ifge INTERNALCOUNT LOTAGSAVED
	{
	cstat 128
	action FORM
	}
}
ifaction FORM
	{
	ifactor 29241 sizeto 32 32
	else sizeto 8 8
	}
ends

useractor notenemy 29240 state travel_dot enda
useractor notenemy 29241 state travel_dot enda

// dazed stars =============================================

defstate spawn_dazed
	espawn 30139
	setav[RETURN].temp4 0

	espawn 30139
	setav[RETURN].temp4 512

	espawn 30139
	setav[RETURN].temp4 1024

	espawn 30139
	setav[RETURN].temp4 1536
ends

spritenoshade 30139
spritenopal 30139

useractor notenemy 30139

ifaction 0
	{
	sizeat 8 8
	seta[].shade -127
	seta[].blend 255
	cstat 514
	spritepal 7
	// pivot
	geta[myowner].x x
	geta[myowner].y y
	geta[myowner].z z
	ifspawnedby APLAYER set dazed_count p_dazed_count else
	getactorvar[myowner].dazed_count dazed_count

	// me
	set temp2 x
	ifspawnedby MONGUR add temp2 1024 else
	add temp2 512

	rotatepoint x y temp2 y temp4 mx my

	add temp4 32

	ifg temp4 2048
		sub temp4 2048

	seta[].x mx
	seta[].y my
	ifspawnedby MONGUR sub z 32768
	else ifspawnedby APLAYER sub z 8192
	else sub z 16384
	seta[].z z

	geta[myowner].sectnum mysector
	updatesectorz mx my z mysector
	ifn mysector -1 seta[].sectnum mysector

	ifl dazed_count 30 seta[].shade disp_pulse

	ife dazed_count 0 killit
	}

enda

// *************************************************
// GLOW
// *************************************************

useractor notenemy 10214

ifaction 0
	{
	randvar temp 127
	ifrnd 128 inv temp
	seta[].shade temp
	action ZERO
	}

ifaction ZERO
	{
	geta[].shade temp
	add temp 2
	seta[].shade temp
	ifge temp 40 action FORM
	}

ifaction FORM
	{
	geta[].shade temp
	sub temp 2
	seta[].shade temp
	ifle temp -127 action ZERO
	}

enda

// *************************************************
// MAP TRIGGERS
// *************************************************

gamearray MAP_TRIGGERS 16384

useractor notenemy 26098
cstat 32768

ifn HITAGSAVED 0
	{
	checkactivatormotion HITAGSAVED
		ife RETURN 1
			{
			setarray MAP_TRIGGERS[HITAGSAVED] 1
			al MAP_TRIGGERS[HITAGSAVED]
			killit
			}
	}
ifn LOTAGSAVED 0
	{
		ife MAP_TRIGGERS[LOTAGSAVED] 1
			{
			al MAP_TRIGGERS[LOTAGSAVED]
			operateactivators LOTAGSAVED THISACTOR
			operaterespawns LOTAGSAVED
			operatemasterswitches LOTAGSAVED
			killit
			}
	}

enda



// ***************************************************************************************************************************************************
// WARNING GLINT
// ***************************************************************************************************************************************************

spritenoshade WARN_GLINT
spritenopal WARN_GLINT

action GLINT_FLASH 26 3 1 1 8

useractor notenemy WARN_GLINT 0

ifaction 0
	{
	spritepal 21
	sizeat 128 128
	seta[].shade 30
	seta[].blend 255
	geta[myowner].z temp2
	ifspawnedby CYBERTOUR
		{
		ife sprite[myowner].pal 24 sub temp2 12288 else
		sub temp2 8192
		}
	ifspawnedby LIZ_SNIPER sub temp2 4096

	seta[].z temp2
	cstat 1154
	action ZERO
	}
else
ifaction ZERO
	{
	sizeto 2 2
	sizeto 2 2
	sizeto 2 2
	sizeto 2 2
	sizeto 2 2
	geta[].shade temp
	ifg temp -30 sub temp 1
	seta[].shade temp
	ife sprite[].xrepeat 2 action GLINT_FLASH
	}
else
ifaction GLINT_FLASH
	{
	sizeto 128 128
	sizeto 128 128
	ifactioncount 3 killit
	}
enda

// SKYCUBE ================================

spriteflags 3810 8192

spritenopal 3810
spritenoshade 3810

useractor notenemy 3810
ifaction 0
	{
	ife EXTRASAVED -1 set EXTRASAVED 0
	sleeptime 0
	seta[].mdflags 16
	ifand EXTRASAVED 1 set command 64
	ifand EXTRASAVED 2
		{
		seta[].mdzoff -11500
		geta[].pitch temp
		add temp 2
		seta[].pitch temp
		}
	ifand EXTRASAVED 4
		{
		seta[].mdzoff -11500
		geta[].roll temp
		add temp 2
		seta[].roll temp
		}
	}
enda

// LIGHT FIX

useractor notenemy TECHLIGHT4
ife EXTRASAVED 1 seta[].z sector[].ceilingz
enda

// 3D PLANET =================================================================

spritenopal 33697
spritenoshade 33697

spritenopal 33698
spritenoshade 33698

useractor notenemy 33697
seta[].mdflags 16
set command 128
ifspritepal 27
	{
	ife show_system SYS_MOON seta[].shade 40
	else ife show_system SYS_MARS seta[].shade 40
	else ife show_system SYS_GANYMEDE seta[].shade 40
	else ife show_system SYS_IO seta[].shade 40
	else ife show_system SYS_XUGLOP seta[].shade 40
	else ife show_system SYS_RACTONIA seta[].shade 40
	else ife show_system SYS_UMBRIEL seta[].shade 40
	else ife show_system SYS_CERES seta[].shade 40
	else ife show_system SYS_HAUMEA seta[].shade 40
	else ife show_system SYS_MAKEMAKE seta[].shade 40
	else ife show_system SYS_ERIS seta[].shade 40
	else ife show_system SYS_VENUS seta[].shade 40
	else seta[].shade 10
	}
else
	{
	ife show_system SYS_EARTH ifge VOLUME 4 spritepal 21
	else ife VOLUME 3 ife LEVEL 60 spritepal 21
	else ife show_system SYS_MOON spritepal 12
	else ife show_system SYS_MARS spritepal 15
	else ife show_system SYS_GANYMEDE spritepal 5
	else ife show_system SYS_IO spritepal 23
	else ife show_system SYS_XUGLOP spritepal 11
	else ife show_system SYS_RACTONIA spritepal 10
	else ife show_system SYS_UMBRIEL spritepal 13
	else ife show_system SYS_CERES spritepal 40
	else ife show_system SYS_HAUMEA spritepal 41
	else ife show_system SYS_MAKEMAKE spritepal 42
	else ife show_system SYS_ERIS spritepal 43
	else ife show_system SYS_VENUS spritepal 70
	else ifge VOLUME 4 spritepal 21
	else spritepal 3
	}
enda

useractor notenemy 33698 // glow
ife show_system SYS_EARTH ifge VOLUME 4 spritepal 2
else ife show_system SYS_MOON spritepal 3
else ife show_system SYS_MARS spritepal 50
else ife show_system SYS_GANYMEDE spritepal 52
else ife show_system SYS_IO spritepal 51
else ife show_system SYS_XUGLOP spritepal 8
else ife show_system SYS_RACTONIA spritepal 2
else ife show_system SYS_UMBRIEL spritepal 3
else ife show_system SYS_CERES spritepal 51
else ife show_system SYS_HAUMEA spritepal 3
else ife show_system SYS_MAKEMAKE spritepal 52
else ife show_system SYS_ERIS spritepal 3
else ife show_system SYS_VENUS spritepal 29
else ifge VOLUME 4 spritepal 2
else spritepal 1
enda
